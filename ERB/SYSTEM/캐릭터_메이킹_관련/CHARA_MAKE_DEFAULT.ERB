
;------------------------------------------------------------------------------------------------------------------
;캐릭터 메이킹 화면을 연 시점에서 설정되는 개별 캐릭터 요소
;------------------------------------------------------------------------------------------------------------------
@CHARA_MAKE_INITIALIZE(SELECT=0)
#DIM SELECT

	;종족과 피트가 랜덤이라면 자동 설정
	CALL SYUZOKU_CHECK,"GET_SYUZOKU_VALUE",SELECT
	IF RESULT == 0
		;종족 자동 설정
		IF FLAG:823 > 0
			IF FLAG:823 == 1
				TALENT:SELECT:인간 = 1
			ELSEIF FLAG:823 == 2
				TALENT:SELECT:이능력자 = 1
			ELSEIF FLAG:823 == 3
				TALENT:SELECT:마법사 = 1
			ELSEIF FLAG:823 == 4
				TALENT:SELECT:인조인간 = 1
			ELSEIF FLAG:823 == 5
				TALENT:SELECT:천사 = 1
			ELSEIF FLAG:823 == 6
				TALENT:SELECT:마족 = 1
			ELSEIF FLAG:823 == 7
				TALENT:SELECT:오니 = 1
			ELSEIF FLAG:823 == 8
				TALENT:SELECT:요정 = 1
			ELSEIF FLAG:823 == 9
				TALENT:SELECT:짐승족 = 1
			ELSEIF FLAG:823 == 10
				TALENT:SELECT:뱀파이어 = 1
				TALENT:SELECT:밤악마의귀족 = 1
			ELSEIF FLAG:823 == 11
				TALENT:SELECT:폴리니안 = 1
			ENDIF
		ELSE
			LOCAL = RAND:24
			LOCAL += 201
			IF LOCAL == 201
				TALENT:SELECT:인간 = 1
			ELSEIF LOCAL == 202
				TALENT:SELECT:이능력자 = 1
			ELSEIF LOCAL == 203
				TALENT:SELECT:마법사 = 1
			ELSEIF LOCAL == 204
				TALENT:SELECT:인조인간 = 1
			ELSEIF LOCAL == 205
				TALENT:SELECT:천사 = 1
			ELSEIF LOCAL == 206
				TALENT:SELECT:마족 = 1
			ELSEIF LOCAL == 207
				TALENT:SELECT:오니 = 1
			ELSEIF LOCAL == 208
				TALENT:SELECT:요정 = 1
			ELSEIF LOCAL == 209
				TALENT:SELECT:짐승족 = 1
			ELSEIF LOCAL == 210 && RAND:2 == 0
				TALENT:SELECT:뱀파이어 = 1
				TALENT:SELECT:밤악마의귀족 = 1
			ELSEIF LOCAL == 211
				TALENT:SELECT:폴리니안 = 1
			;인간이 잘 나오도록
			ELSE
				TALENT:SELECT:인간 = 1
			ENDIF
		ENDIF
		;FEAT 자동 설정
		CALL SYUZOKU_CHECK,"GET_SYUZOKU_VALUE",SELECT
		SIF FLAG:824 == 1
			CALL SET_FEAT_DEFAULT, SELECT, RESULT
	ENDIF

	;성격이 랜덤이라면 결정
	IF SEIKAKU_CHECK_F(SELECT) == 0
		local:1=1
		WHILE local:1<=100
			LOCAL = RAND:18
			LOCAL += 10
			IF LOCAL == 10
				TALENT:SELECT:겁쟁이 = 1
			ELSEIF LOCAL == 11
				TALENT:SELECT:부끄럼쟁이 = 1
			ELSEIF LOCAL == 12
				TALENT:SELECT:오기 = 1
			ELSEIF LOCAL == 13
				TALENT:SELECT:활발함 = 1
			ELSEIF LOCAL == 14
				TALENT:SELECT:대범함 = 1
			ELSEIF LOCAL == 15
				TALENT:SELECT:게으름 = 1
			ELSEIF LOCAL == 16
				TALENT:SELECT:진지함 = 1
			ELSEIF LOCAL == 17
				TALENT:SELECT:낙천적 = 1
			ELSEIF LOCAL == 18
				TALENT:SELECT:비관적 = 1
			ELSEIF LOCAL == 19
				TALENT:SELECT:순종적 = 1
			ELSEIF LOCAL == 20
				TALENT:SELECT:츤데레 = 1
			ELSEIF LOCAL == 21
				TALENT:SELECT:얀데레 = 1
			ELSEIF LOCAL == 22
				TALENT:SELECT:과묵함 = 1
			ELSEIF LOCAL == 23
				TALENT:SELECT:수다쟁이 = 1
			ELSEIF LOCAL == 24
				TALENT:SELECT:고풍적 = 1
			ELSEIF LOCAL == 25
				TALENT:SELECT:경박함 = 1
			ELSEIF LOCAL == 26
				TALENT:SELECT:고집쟁이 = 1
			ELSEIF LOCAL == 27
				TALENT:SELECT:사나움 = 1
			ENDIF
			;다른 캐릭터와의 중복은 피하고자 한다
			FOR local:2,1,SELECT
				IF SEIKAKU_CHECK_F(local:2) == SEIKAKU_CHECK_F(SELECT)
					local:1++
					CONTINUE
				ENDIF
			NEXT

			;구상이 없는 것까지 포함한 경우
			IF FLAG:825 == 0
				BREAK
			ELSE
				;구상이 있는 것만 한정한 경우
				TRYCCALLFORM KOJO_0_COLOR_{LOCAL}
					;구상이 있다
					RESETCOLOR
					BREAK
				CATCH
					;구상이 없다(가챠 속행)
					local:1 ++
				ENDCATCH
			ENDIF
		WEND

		;체력 등의 BASE값 계산
		CALL SEIKAKU_CHECK,"GET_TALENT_VALUE",SELECT
		LOCAL = RESULT
		LOCAL:1 = 0
		LOCAL:2 = 0
		FOR LOCAL:3, 0, 7
			LOCAL:1 = LOCAL:3
			SIF LOCAL:3 > 2
				LOCAL:1 = LOCAL:3 + 7
			LOCAL:2 = BASE:SELECT:(LOCAL:1)
			;성격 보정
			BASE:SELECT:(LOCAL:1) = SEIKAKU_HOSEI_F(LOCAL, LOCAL:1, LOCAL:2)
			MAXBASE:SELECT:(LOCAL:1) = BASE:SELECT:(LOCAL:1)
		NEXT
		;성격에 따른 일인칭 설정
		;「저(私, わたし, わたくし)」이외로 설정되어 있는 경우에는 변경하지 않는다
		IF CFLAG:SELECT:8
		;고풍적:「이 몸(わらわ)」
		ELSEIF TALENT:SELECT:고풍적
			CFLAG:SELECT:8 = 20
		;경박함:「나(あたし)」
		ELSEIF TALENT:SELECT:경박함
			CFLAG:SELECT:8 = 11
		ELSE
			CFLAG:SELECT:8 = 0
		ENDIF
	ENDIF
	;신체 데이터 생성
	CALL CHARA_MAKE_BASE_PROFILE(SELECT)

	;공통 설정에서 변신명 로드　※범용 캐릭터 이외에의 반영
	IF CALLNAME:SELECT != "범용 캐릭터" && TALENT:SELECT:변신능력 == 1
		IF CSTR:SELECT:0 == ""
			;공통 윗구 ("매지컬 나이트" 할 때 매지컬 부분)
			LOCALS=%SAVESTR:11%
			;아랫구 장르 ("매지컬 나이트" 할 때 나이트 부분)
			LOCALS:1=%CALLNAME:SELECT%
			LOCAL:10=0
			$NAMEFROMGENRE
			IF FLAG:820>0
				CALL RANDOMNAMING_FROMGENRE(FLAG:820)
				LOCALS:1=%RESULTS%
			ENDIF
			IF LOCALS+LOCALS:1!=""
				CSTR:SELECT:0 = %LOCALS%%LOCALS:1%
				;장르 지정으로부터의 생성이라면 다른 캐릭터와의 중복은 피하고자 한다
				IF FLAG:820>0
					FOR LOCAL, 1, CHARANUM
						SIF LOCAL==SELECT
							CONTINUE
						IF CSTR:SELECT:0==CSTR:LOCAL:0 && LOCAL:10<10
							LOCAL:10++
							GOTO NAMEFROMGENRE
						ENDIF
					NEXT
				ENDIF
				CFLAG:SELECT:2 = 1
				IF CSTR:SELECT:3==""
					CALL SEIKAKU_CHECK, "GET_TALENT_VALUE", SELECT
					CSTR:SELECT:3=%SAVESTR:10%%CSTR:SELECT:0%%FIRSTSETTING_CHANGINGCALL_DETAIL(SELECT,RESULT)%
					CFLAG:SELECT:5 = 1
				ENDIF

			ENDIF
		ENDIF
		IF CSTR:SELECT:1 == "" && CSTR:SELECT:0 != ""
			CSTR:SELECT:1 = %CSTR:SELECT:0%
			CFLAG:SELECT:3 = 1
		ENDIF
		IF CSTR:SELECT:2 == "" && SAVESTR:12 != ""
			CSTR:SELECT:2 = %SAVESTR:12%
			CFLAG:SELECT:4 = 1
		ENDIF
	ENDIF




;------------------------------------------------------------------------------------------------------------------
;캐릭터 메이킹 완료 후 모든 캐릭터 공통으로 진행하는 초기 처리 ＋ 미설정 항목을 보충하는 처리
;------------------------------------------------------------------------------------------------------------------
;ARG 개별 설정하는 경우
@CHARA_MAKE_FINALIZE, ARG = 0
#DIM SELECT

;캐릭터의 내력(素性)을 랜덤으로 설정
FOR SELECT,1,CHARANUM

	;캐릭터 지정이 있는 경우, 다른 것은 스킵한다
	SIF ARG != 0 && SELECT != ARG
		CONTINUE

	;미초기화 항목(범용 캐릭터 상태)의 요소에 대한 보완
	CALL CHARA_MAKE_INITIALIZE(SELECT)


	;CSV 기술 오류 회피
	SIF MAXBASE:SELECT:사정 < 1
		MAXBASE:SELECT:사정 = 10000
	SIF MAXBASE:SELECT:모유 < 1
		MAXBASE:SELECT:모유 = 10000

	;번호 등록
	SIF CFLAG:SELECT:240 == 0
		CFLAG:SELECT:240 = SELECT

	;미사용 보너스치를 반영
	IF JUEL:SELECT:수련P >= 10
		FOR LOCAL,0,JUEL:SELECT:수련P / 10
			CFLAG:SELECT:(50 + RAND:7) += 1
			JUEL:SELECT:수련P -= 10
		NEXT
	ENDIF

	;점프 성능
	IF TALENT:SELECT:변신능력 == -1
		MAXBASE:SELECT:공중대시 = LIMIT(CSVBASE_F(NO:SELECT,22), 0, 8)
	ELSE
		MAXBASE:SELECT:공중대시 = LIMIT(CSVBASE_F(NO:SELECT,22), 0, 8)
	ENDIF

	SIF TALENT:SELECT:공중전특기
		MAXBASE:SELECT:공중대시 = MIN(MAXBASE:SELECT:공중대시 + TALENT:SELECT:공중전특기, 8)
	SIF TALENT:SELECT:공중전서툼
		MAXBASE:SELECT:공중대시 = MAX(MAXBASE:SELECT:공중대시 - TALENT:SELECT:공중전서툼, 0)

	;소질 기본값 보존
	;통상 성별
	IF CHARATALENT_F(SELECT, 0, "남자") > 0
		IF CHARATALENT_F(SELECT, 0, "오토코노코") > 0
			BASE:SELECT:성별기본값 = 3
		ELSE
			BASE:SELECT:성별기본값 = 1
		ENDIF
	ELSEIF CHARATALENT_F(SELECT, 0, "후타나리") > 0
		BASE:SELECT:성별기본값 = 2
	ELSE
		BASE:SELECT:성별기본값 = 0
	ENDIF
	;통상 체격
	IF CHARATALENT_F(SELECT, 0, "왜소함") > 0
		BASE:SELECT:체격기본값 = -1
	ELSEIF CHARATALENT_F(SELECT, 0, "장신") > 0
		BASE:SELECT:체격기본값 = 1
	ELSE
		BASE:SELECT:체격기본값 = 0
	ENDIF
	;통상 가슴 사이즈
	IF CHARATALENT_F(SELECT, 0, "절벽") > 0
		BASE:SELECT:가슴사이즈기본값 = -2
	ELSEIF CHARATALENT_F(SELECT, 0, "빈유") > 0
		BASE:SELECT:가슴사이즈기본값 = -1
	ELSEIF CHARATALENT_F(SELECT, 0, "기유") > 0
		BASE:SELECT:가슴사이즈기본값 = 5
	ELSEIF CHARATALENT_F(SELECT, 0, "마유") > 0
		BASE:SELECT:가슴사이즈기본값 = 4
	ELSEIF CHARATALENT_F(SELECT, 0, "초유") > 0
		BASE:SELECT:가슴사이즈기본값 = 3
	ELSEIF CHARATALENT_F(SELECT, 0, "폭유") > 0
		BASE:SELECT:가슴사이즈기본값 = 2
	ELSEIF CHARATALENT_F(SELECT, 0, "거유") > 0
		BASE:SELECT:가슴사이즈기본값 = 1
	ELSE
		BASE:SELECT:가슴사이즈기본값 = 0
	ENDIF
	;통상 외관
	IF CHARATALENT_F(SELECT, 0, "순산형") > 0
		BASE:SELECT:외관기본값 = 1
	ELSEIF CHARATALENT_F(SELECT, 0, "포동포동") > 0
		BASE:SELECT:외관기본값 = 2
	ELSEIF CHARATALENT_F(SELECT, 0, "유아배") > 0
		BASE:SELECT:외관기본값 = 3
	ELSEIF CHARATALENT_F(SELECT, 0, "날씬함") > 0
		BASE:SELECT:외관기본값 = 4
	ELSEIF CHARATALENT_F(SELECT, 0, "빵뎅이") > 0
		BASE:SELECT:외관기본값 = 5
	ELSEIF CHARATALENT_F(SELECT, 0, "왕뎅이") > 0
		BASE:SELECT:외관기본값 = 6
	ELSE
		BASE:SELECT:외관기본값 = 0
	ENDIF
	;변신 시 성별
	IF CHARATALENT_F(SELECT, 1, "남자") > 0
		IF CHARATALENT_F(SELECT, 1, "오토코노코") > 0
			MAXBASE:SELECT:성별기본값 = 3
		ELSE
			MAXBASE:SELECT:성별기본값 = 1
		ENDIF
	ELSEIF CHARATALENT_F(SELECT, 1, "후타나리") > 0
		MAXBASE:SELECT:성별기본값 = 2
	ELSE
		MAXBASE:SELECT:성별기본값 = 0
	ENDIF
	;변신 시 체격
	MAXBASE:SELECT:체격기본값 = TALENT:SELECT:변신시체격변동
	;변신 시 가슴 사이즈
	MAXBASE:SELECT:가슴사이즈기본값 = TALENT:SELECT:변신시가슴사이즈변동
	;변신 시 외관
	IF CHARATALENT_F(SELECT, 1, "순산형") > 0
		MAXBASE:SELECT:외관기본값 = 1
	ELSEIF CHARATALENT_F(SELECT, 1, "포동포동") > 0
		MAXBASE:SELECT:외관기본값 = 2
	ELSEIF CHARATALENT_F(SELECT, 1, "유아배") > 0
		MAXBASE:SELECT:외관기본값 = 3
	ELSEIF CHARATALENT_F(SELECT, 1, "날씬함") > 0
		MAXBASE:SELECT:외관기본값 = 4
	ELSEIF CHARATALENT_F(SELECT, 1, "빵뎅이") > 0
		MAXBASE:SELECT:외관기본값 = 5
	ELSEIF CHARATALENT_F(SELECT, 1, "왕뎅이") > 0
		MAXBASE:SELECT:외관기본값 = 6
	ELSE
		MAXBASE:SELECT:외관기본값 = 0
	ENDIF




	;캐릭터의 보너스치를 실스테이터스에 반영
	BASE:SELECT:체력 += CFLAG:SELECT:50 * 10
	BASE:SELECT:기력 += CFLAG:SELECT:51 * 10
	BASE:SELECT:성내성 += CFLAG:SELECT:52

	BASE:SELECT:공격 += CFLAG:SELECT:53
	BASE:SELECT:방어 += CFLAG:SELECT:54
	BASE:SELECT:민첩 += CFLAG:SELECT:55
	BASE:SELECT:지성 += CFLAG:SELECT:56

	FOR LOCAL:3, 0, 7
		LOCAL:1 = LOCAL:3
		SIF LOCAL:3 > 2
			LOCAL:1 = LOCAL:3 + 7
		MAXBASE:SELECT:(LOCAL:1) = BASE:SELECT:(LOCAL:1)
	NEXT

	;1레벨 미만인 캐릭터는 초기 레벨을 1로
	SIF ABL:SELECT:레벨 < 1
		ABL:SELECT:레벨 = 1

	;캐릭터의 스테이터스를 1레벨 기준으로 보정한다
	BASE:SELECT:기초체력 = BASE:SELECT:체력
	BASE:SELECT:기초기력 = BASE:SELECT:기력
	BASE:SELECT:기초성내성 = BASE:SELECT:성내성
	CALL LEVELSTATUS, SELECT

	BASE:SELECT:체력 = MAXBASE:SELECT:체력
	BASE:SELECT:기력 = MAXBASE:SELECT:기력
	BASE:SELECT:성내성 = MAXBASE:SELECT:성내성

	;의상이 미설정이라면 사복, -1(장비 없음 지정)이라면 0으로 해 둔다
	SIF CFLAG:SELECT:40 == 0
		CFLAG:SELECT:40 = 100
	SIF CFLAG:SELECT:40 == -1
		CFLAG:SELECT:40 = 0
	;변신능력이 있을 때, 의상이 미설정이라면 오리지널 슈트
	SIF TALENT:SELECT:변신능력 == 1 && CFLAG:SELECT:41 == 0
		CFLAG:SELECT:41 = 200
	;없음으로 지정되어 있으면 없음
	SIF CFLAG:SELECT:41 == -1
		CFLAG:SELECT:41 = 0
	;의상이 미설정이라면 평범한 속옷, 하지만 겉옷이(변신능력이 있다면 변신 후 겉옷도)속옷겸용이라면 추가하지 않는다
	IF CFLAG:SELECT:42 == 0
		LOCAL = 0
		CALL CLOTH_NO_INNER, SELECT
		SIF RESULT
			LOCAL += 1
		IF TALENT:SELECT:변신능력 == 1
			CFLAG:SELECT:1 = 1
			CALL CLOTH_NO_INNER, SELECT
			SIF RESULT
				LOCAL += 1
			CFLAG:SELECT:1 = 0
		ENDIF
		SIF LOCAL == 0
			CFLAG:SELECT:42 = 300
	ENDIF
	SIF CFLAG:SELECT:42 == -1
		CFLAG:SELECT:42 = 0
	;잘못 설정했을 경우에 대비하여 CFLAG:43도 0으로
	SIF CFLAG:SELECT:43 == -1
		CFLAG:SELECT:43 = 0

	;변신 전과 ＳＰ변신 후의 능력
	SIF CFLAG:SELECT:10 == 0
		CFLAG:SELECT:10 = 25
	SIF CFLAG:SELECT:11 == 0
		CFLAG:SELECT:11 = 110


	;압축 무기 데이터 압축 해제
	SIF CSTR:SELECT:15 != ""
		CALL DECODE_WEAPON_DATA, SELECT, 1
	SIF CSTR:SELECT:16 != ""
		CALL DECODE_WEAPON_DATA, SELECT, 2
	SIF CSTR:SELECT:17 != ""
		CALL DECODE_WEAPON_DATA, SELECT, 3
	;압축 무기 데이터 클리어
	CSTR:SELECT:15 =
	CSTR:SELECT:16 =
	CSTR:SELECT:17 =

	;인맥 반영
	SIF TALENT:SELECT:소문을좋아하는친구
		CFLAG:SELECT:120 = 1
	SIF TALENT:SELECT:경찰관계자 == 1 && CFLAG:SELECT:121 < 2
		CFLAG:SELECT:121 = 1
	SIF TALENT:SELECT:경찰관계자 == 2
		CFLAG:SELECT:121 = 2
	IF TALENT:SELECT:정보원 == 1 && CFLAG:SELECT:122 == 0
		CFLAG:SELECT:122 = 1
		CFLAG:SELECT:122 |= 2
		;조금 살이 찐 남자
		IF RAND:7 == 0
			SETBIT CFLAG:SELECT:122, 2
		;고상한 남자
		ELSEIF RAND:6 == 0
			SETBIT CFLAG:SELECT:122, 3
		;탐정풍의 남자
		ELSEIF RAND:5 == 0
			SETBIT CFLAG:SELECT:122, 4
		;천박한 놈
		ELSEIF RAND:4 == 0
			SETBIT CFLAG:SELECT:122, 2
			SETBIT CFLAG:SELECT:122, 3
		;시티헌터적인 녀석(경박한 남자)
		ELSEIF RAND:3 == 0
			SETBIT CFLAG:SELECT:122, 2
			SETBIT CFLAG:SELECT:122, 4
		;노인
		ELSEIF RAND:2 == 0
			SETBIT CFLAG:SELECT:122, 3
			SETBIT CFLAG:SELECT:122, 4
		ENDIF
	ENDIF
	IF TALENT:SELECT:유복한친정 == 1 || CFLAG:SELECT:123 > 0
		CFLAG:SELECT:123 = 1
		MONEY += 2500
	ENDIF




	;처음부터 비처녀라면 Ｖ경험이 들어간다
	IF EXP:SELECT:Ｖ경험 > 0
	ELSEIF TALENT:SELECT:처녀 < 1 && ISFEMALE(SELECT)
		EXP:SELECT:Ｖ경험 = 1+RAND:4
	ELSE
		EXP:SELECT:Ｖ경험 = 0
	ENDIF


NEXT



@CHARA_MAKE_BASE_PROFILE(SELECT)
#DIM SELECT,1
	;●csv캐릭터 or 설정 화면을１번이라도 연 캐릭터일 경우
	IF CALLNAME:SELECT != "범용 캐릭터"
		;상세 프로필 미설정 시
		IF CFLAG:SELECT:34 == 0 && NO:SELECT == 0
			;나이나 쓰리 사이즈 등 설정
			CALL GENERATE_BODYLINE, SELECT
			CALL CHARA_MAKE_AGE_SETTING, SELECT
			CALL CHARA_SIZE_DEFAULT, SELECT
		ENDIF
		;여기서부터 아래의 처리를 날린다 ------------------------------------------------------------------------------------------------------------------------
		RETURN
	;●미초기화 캐릭터일 경우
	ELSE
		;여성이라면 처녀와 청순파를 지니도록 한다
		IF ISFEMALE(SELECT)
			TALENT:SELECT:처녀 = 1
			TALENT:SELECT:청순파 = 1
		ENDIF
		;우선 나이 설정
		CALL GENERATE_BODYLINE, SELECT
		CALL CHARA_MAKE_AGE_SETTING, SELECT
			;나이 설정에 근거하여 소질 결정
			CALL CHARA_MAKE_STATUS_TALENT, SELECT
			CALL CHARA_MAKE_STATUS_TALENT_FLAVOR, SELECT
		;쓰리 사이즈 등 설정
		CALL CHARA_SIZE_DEFAULT, SELECT

			;●초기화 완료되지 않은 캐릭터 틀(キャラ枠)의 경우, 이 자리에서 캐릭터 메이킹에 상당하는 데이터를 랜덤 생성한다

		;헤어 스타일 자동 설정
		CSTR:SELECT:12 = %STR:(30000 + RAND:9)%
		CSTR:SELECT:13 = %STR:(30100 + RAND:15)%
		CSTR:SELECT:14 = %CSTR:SELECT:13%
		SIF RAND:2 == 0
			CSTR:SELECT:14 = %STR:(30100 + RAND:15)%
		;변신능력 설정
		;종족：인간에는 반드시 변신을 설정
		;기타 종족에 대해서는 보류
		SIF TALENT:SELECT:인간 == 1
			TALENT:SELECT:변신능력 = 1

	ENDIF


	;이 타이밍에서 변신능력이 설정된 경우를 대비하여 다시 한 번 더 의상 설정
	;의상이 미설정이라면 오리지널 슈트
	SIF TALENT:SELECT:변신능력 == 1 && CFLAG:SELECT:41 == 0
		CFLAG:SELECT:41 = 200



	;캐릭터명 랜덤 생성
	;캐릭터 메이킹의 함수 처리와 동일
	LOCAL:0 = 0
	LOCAL:1 = 0
	;３/４ 확률로 일본어 성씨
	IF FLAG:821==1 || (FLAG:821==0 && RAND:4 != 0)
		;성씨
		LOCAL:0 = RAND:500 + 3000
		WHILE STR:(LOCAL:0) == ""
			LOCAL:0 = RAND:500 + 3000
		WEND
		LOCALS:0 = %STR:(LOCAL:0)%
		;이름
		LOCALS:1'=""
		WHILE LOCALS:1 == ""
			SELECTCASE FLAG:822
			;Japanese
			CASE 1
				LOCAL:1 = RAND:500 + 12000
			;English
			CASE 2
				LOCAL:1 = RAND:1000 + 12500
			;French
			CASE 3
				LOCAL:1 = RAND:500 + 13500
			;German
			CASE 4
				LOCAL:1 = RAND:1000 + 14000
			;Italy
			CASE 5
				LOCAL:1 = RAND:500 + 15000
			;Russian
			CASE 6
				LOCAL:1 = RAND:500 + 15500
			;Chinese
			CASE 7
				LOCAL:1 = RAND:1000 + 16500
			;Japanese(no-gender)
			CASE 8
				LOCAL:1 = RAND:500 + 18000
			;no setting=>Japanese
			; *Random heroines who have JP family-name also have JP first name.
			;  자동 생성으로 언어를 섞으면 에로겜으로서 이상한 음향의 이름이 되어 버리기 쉽기 때문.
			CASEELSE
				LOCAL:1 = RAND:500 + 12000
			ENDSELECT
			LOCALS:1'=STR:(LOCAL:1)

			;중국어는 한자조합(두 번째 글자, 二文字目)일 가능성이 있다
			IF FLAG:822 == 7 && RAND:2
				LOCAL:1 = RAND:1000 + 16500
				WHILE STR:(LOCAL:1) == ""
					LOCAL:1 = RAND:1000 + 16500
				WEND
				LOCALS:1+=STR:(LOCAL:1)
			ENDIF

		WEND


		;일본어명일 경우, 일본인풍 외관
		IF RAND:8 == 0
			IF RAND:3 == 0
				CSTR:SELECT:30 = 빨강
				CSTR:SELECT:31 = 빨강
			ELSEIF RAND:2 == 0
				CSTR:SELECT:30 = 초록
				CSTR:SELECT:31 = 초록
			ELSE
				CSTR:SELECT:30 = 파랑
				CSTR:SELECT:31 = 파랑
			ENDIF
		ELSEIF RAND:3 != 0
			CSTR:SELECT:30 = 다색
			CSTR:SELECT:31 = 다색
		ELSE
			CSTR:SELECT:30 = 검정
			CSTR:SELECT:31 = 검정
		ENDIF
		CSTR:SELECT:32 = 검정
		CSTR:SELECT:33 = 검정
		CSTR:SELECT:34 = 검정
		CSTR:SELECT:35 = 검정
		IF RAND:4 == 0
			CSTR:SELECT:36 = 갈색
			CSTR:SELECT:37 = 갈색
		ELSE
			CSTR:SELECT:36 = 살구색
			CSTR:SELECT:37 = 살구색
		ENDIF
		;특수 외관
		IF RAND:15 == 0
			;알비노
			CSTR:SELECT:30 = 은색
			CSTR:SELECT:31 = 은색
			CSTR:SELECT:32 = 빨강
			CSTR:SELECT:33 = 빨강
			CSTR:SELECT:34 = 빨강
			CSTR:SELECT:35 = 빨강
			CSTR:SELECT:36 = 백색
			CSTR:SELECT:37 = 백색
		ENDIF
	;디폴트로는 １／４확률로 외국 성씨
	ELSE
		;성씨
		LOCALS:0'=""
		WHILE LOCALS:0 == ""
			SELECTCASE FLAG:821
			;English
			CASE 2
				LOCAL:0 = RAND:2000 + 3500
			;French
			CASE 3
				LOCAL:0 = RAND:2000 + 5500
			;German
			CASE 4
				LOCAL:0 = RAND:1500 + 7500
			;Italy
			CASE 5
				LOCAL:0 = RAND:2000 + 9000
			;Russian
			CASE 6
				LOCAL:0 = RAND:1000 + 11000
			;Chinese
			CASE 7
				LOCAL:0 = RAND:500 + 16000
			;random
			CASEELSE
				LOCAL:0 = RAND:8500 + 3500
			ENDSELECT
			LOCALS:0'=STR:(LOCAL:0)
		WEND
		;이름
		LOCALS:1'=""
		WHILE LOCALS:1 == ""
			SELECTCASE FLAG:822==0 ? FLAG:821 # FLAG:822
			;Japanese
			CASE 1
				LOCAL:1 = RAND:500 + 12000
			;English
			CASE 2
				LOCAL:1 = RAND:1000 + 12500
			;French
			CASE 3
				LOCAL:1 = RAND:500 + 13500
			;German
			CASE 4
				LOCAL:1 = RAND:1000 + 14000
			;Italy
			CASE 5
				LOCAL:1 = RAND:500 + 15000
			;Russian
			CASE 6
				LOCAL:1 = RAND:500 + 15500
			;Chinese
			CASE 7
				LOCAL:1 = RAND:1000 + 16500
			;Japanese(no-gender)
			CASE 8
				LOCAL:1 = RAND:500 + 18000
			;random(without Japanese)
			; *In most case, [JP first name]+[Foreign family name] sounds so strange in Japan.
			;  xenophobia? 배외성(排外性)？
			;  this sense may change in some future...
			;  하지만 여기는 관공서나 논문이 아니라 eraGVT이므로 하반신의 인도에 따를게
			CASEELSE
				LOCAL:1 = RAND:3500 + 12500
			ENDSELECT
			LOCALS:1'=STR:(LOCAL:1)

			;중국어는 한자조합(두 번째 글자, 二文字目)일 가능성이 있다
			IF FLAG:822 == 7 && RAND:2
				LOCAL:1 = RAND:1000 + 16500
				WHILE STR:(LOCAL:1) == ""
					LOCAL:1 = RAND:1000 + 16500
				WEND
				LOCALS:1+=STR:(LOCAL:1)
			ENDIF
		WEND

		;서양 이름일 경우, 서양풍 외관
		IF RAND:6 == 0
			CSTR:SELECT:30 = 은색
			CSTR:SELECT:31 = 은색
		ELSEIF RAND:4 == 0
			CSTR:SELECT:30 = 빨강
			CSTR:SELECT:31 = 빨강
		ELSEIF RAND:14 == 0
			CSTR:SELECT:30 = 초록
			CSTR:SELECT:31 = 초록
		ELSEIF RAND:12 == 0
			CSTR:SELECT:30 = 파랑
			CSTR:SELECT:31 = 파랑
		ELSEIF RAND:2 == 0
			CSTR:SELECT:30 = 다색
			CSTR:SELECT:31 = 다색
		ELSE
			CSTR:SELECT:30 = 금색
			CSTR:SELECT:31 = 금색
		ENDIF
		IF RAND:12 == 0
			CSTR:SELECT:32 = 빨강
			CSTR:SELECT:33 = 빨강
			CSTR:SELECT:34 = 빨강
			CSTR:SELECT:35 = 빨강
		ELSEIF RAND:8 == 0
			CSTR:SELECT:32 = 보라
			CSTR:SELECT:33 = 보라
			CSTR:SELECT:34 = 보라
			CSTR:SELECT:35 = 보라
		ELSEIF RAND:4 == 0
			CSTR:SELECT:32 = 파랑
			CSTR:SELECT:33 = 파랑
			CSTR:SELECT:34 = 파랑
			CSTR:SELECT:35 = 파랑
		ELSE
			CSTR:SELECT:32 = 다색
			CSTR:SELECT:33 = 다색
			CSTR:SELECT:34 = 다색
			CSTR:SELECT:35 = 다색
		ENDIF
		IF RAND:20 == 0
			CSTR:SELECT:36 = 은색
			CSTR:SELECT:37 = 은색
		ELSEIF RAND:20 == 0
			CSTR:SELECT:36 = 검정
			CSTR:SELECT:37 = 검정
		ELSEIF RAND:4 == 0
			CSTR:SELECT:36 = 갈색
			CSTR:SELECT:37 = 갈색
		ELSEIF RAND:10 == 0
			CSTR:SELECT:36 = 살구색
			CSTR:SELECT:37 = 살구색
		ELSE
			CSTR:SELECT:36 = 백색
			CSTR:SELECT:37 = 백색
		ENDIF

		;특수 외관
		IF RAND:6 == 0
			;갈색 소녀(茶色娘)
			CSTR:SELECT:30 = 은색
			CSTR:SELECT:31 = 은색
			CSTR:SELECT:36 = 갈색
			CSTR:SELECT:37 = 갈색
		ELSEIF RAND:5 == 0
			;금발 벽안
			CSTR:SELECT:30 = 금색
			CSTR:SELECT:31 = 금색
			CSTR:SELECT:32 = 파랑
			CSTR:SELECT:33 = 파랑
			CSTR:SELECT:34 = 파랑
			CSTR:SELECT:35 = 파랑
		ELSEIF RAND:10 == 0
			;알비노
			CSTR:SELECT:30 = 은색
			CSTR:SELECT:31 = 은색
			CSTR:SELECT:32 = 빨강
			CSTR:SELECT:33 = 빨강
			CSTR:SELECT:34 = 빨강
			CSTR:SELECT:35 = 빨강
			CSTR:SELECT:36 = 백색
			CSTR:SELECT:37 = 백색
		ENDIF
	ENDIF

	;변신 후의 머리색을 랜덤으로 바꾼다
	IF RAND:4 == 0
		IF RAND:8 == 0
			CSTR:SELECT:31 = 빨강
		ELSEIF RAND:7 == 0
			CSTR:SELECT:31 = 초록
		ELSEIF RAND:6 == 0
			CSTR:SELECT:31 = 파랑
		ELSEIF RAND:5 == 0
			CSTR:SELECT:31 = 황색
		ELSEIF RAND:4 == 0
			CSTR:SELECT:31 = 보라
		ELSEIF RAND:3 == 0
			CSTR:SELECT:31 = 주황
		ELSEIF RAND:2 == 0
			CSTR:SELECT:31 = 분홍
		ENDIF
	ENDIF

	;변신 후의 눈동자 색을 랜덤으로 바꾼다
	IF RAND:50 == 0
		;오드아이
		IF RAND:8 == 0
			CSTR:SELECT:(34 + RAND:2) = 빨강
		ELSEIF RAND:7 == 0
			CSTR:SELECT:(34 + RAND:2) = 초록
		ELSEIF RAND:6 == 0
			CSTR:SELECT:(34 + RAND:2) = 파랑
		ELSEIF RAND:5 == 0
			CSTR:SELECT:(34 + RAND:2) = 황색
		ELSEIF RAND:4 == 0
			CSTR:SELECT:(34 + RAND:2) = 보라
		ELSEIF RAND:3 == 0
			CSTR:SELECT:(34 + RAND:2) = 주황
		ELSEIF RAND:2 == 0
			CSTR:SELECT:(34 + RAND:2) = 분홍
		ENDIF
	ELSEIF RAND:4 == 0
		IF RAND:8 == 0
			CSTR:SELECT:34 = 빨강
			CSTR:SELECT:35 = 빨강
		ELSEIF RAND:7 == 0
			CSTR:SELECT:34 = 초록
			CSTR:SELECT:35 = 초록
		ELSEIF RAND:6 == 0
			CSTR:SELECT:34 = 파랑
			CSTR:SELECT:35 = 파랑
		ELSEIF RAND:5 == 0
			CSTR:SELECT:34 = 황색
			CSTR:SELECT:35 = 황색
		ELSEIF RAND:4 == 0
			CSTR:SELECT:34 = 보라
			CSTR:SELECT:35 = 보라
		ELSEIF RAND:3 == 0
			CSTR:SELECT:34 = 주황
			CSTR:SELECT:35 = 주황
		ELSEIF RAND:2 == 0
			CSTR:SELECT:34 = 분홍
			CSTR:SELECT:35 = 분홍
		ENDIF
	ENDIF


	;일본 or 중국일 경우
	IF LOCAL:0 < 3500 || (LOCAL:0 >= 16000 && LOCAL:0 < 16500)
		LOCALS:2 = %LOCALS:0% %LOCALS:1%
	ELSE
		LOCALS:2 = %LOCALS:1%・%LOCALS:0%
	ENDIF
	NAME:SELECT = %LOCALS:2%
	CALLNAME:SELECT = %LOCALS:1%
	CSTR:SELECT:10 = %LOCALS:0%

	SIF CALLNAME:SELECT != ""
		CSTR:SELECT:200 = %CALLNAME:SELECT%


	;공통 설정으로부터의 로드, 범용 캐릭터용
	IF CSTR:SELECT:0 == "" && TALENT:SELECT:변신능력 == 1
		;공통 윗구 ("매지컬 나이트" 할 때 매지컬 부분)
		LOCALS=%SAVESTR:11%
		;아랫구 장르 ("매지컬 나이트" 할 때 나이트 부분)
		LOCALS:1=%CALLNAME:SELECT%
		LOCAL:10=0
		$NAMEFROMGENRE_1
		IF FLAG:820>0
			CALL RANDOMNAMING_FROMGENRE(FLAG:820)
			LOCALS:1=%RESULTS%
		ENDIF
		IF LOCALS+LOCALS:1!=""
			CSTR:SELECT:0 = %LOCALS%%LOCALS:1%

			;장르 지정으로부터의 생성이라면 다른 캐릭터와의 중복은 피하고자 한다
			IF FLAG:820>0
				FOR LOCAL, 1, CHARANUM
					SIF LOCAL==SELECT
						CONTINUE
					IF CSTR:SELECT:0==CSTR:LOCAL:0 && LOCAL:10<10
						LOCAL:10++
						GOTO NAMEFROMGENRE_1
					ENDIF
				NEXT
			ENDIF

			CFLAG:SELECT:2 = 1
			IF CSTR:SELECT:3==""
				CALL SEIKAKU_CHECK, "GET_TALENT_VALUE", SELECT
				CSTR:SELECT:3=%SAVESTR:10%%CSTR:SELECT:0%%FIRSTSETTING_CHANGINGCALL_DETAIL(SELECT,RESULT)%
				CFLAG:SELECT:5 = 1
			ENDIF
		ENDIF
	ENDIF

	;일인칭을 낮은 확률로 변경
	IF RAND:20 == 0 || ISMALE(SELECT)
		IF RAND:2 == 0
			CFLAG:SELECT:8 = 25 + RAND:3
		ELSE
			CFLAG:SELECT:8 = 30 + RAND:3
		ENDIF
	ENDIF


	IF CSTR:SELECT:1 == "" && CSTR:SELECT:0 != ""
		CSTR:SELECT:1 = %CSTR:SELECT:0%
		CFLAG:SELECT:3 = 1
	ENDIF
	IF CSTR:SELECT:2 == "" && SAVESTR:12 != ""
		CSTR:SELECT:2 = %SAVESTR:12%
		CFLAG:SELECT:4 = 1
	ENDIF
	CFLAG:SELECT:34 = 1

;------------------------------------------------------------------------------------------------------------------
;소질 랜덤 설정
;------------------------------------------------------------------------------------------------------------------
@CHARA_MAKE_STATUS_TALENT, ARG
;처녀？ 80% 확률로 처녀
IF ISFEMALE(ARG) && EXP:ARG:Ｖ경험 == 0
	LOCAL = RAND:100
	IF LOCAL < 20
		TALENT:ARG:처녀 = 0
		EXP:ARG:Ｖ경험 = 1+RAND:4
	ELSE
		TALENT:ARG:처녀 = 1
	ENDIF
ENDIF

;청순파？　통상 20%, 처녀라면 확률 +50%
LOCAL = RAND:100
SIF LOCAL < 20 + TALENT:ARG:처녀 * 50
	TALENT:ARG:청순파 = 1

;백보지？　20% 확률로 백보지
LOCAL = RAND:100
SIF LOCAL < 20
	TALENT:ARG:백보지 = 1

;민감, 둔감 설정　각각 3% 확률로 붙는다
;C
LOCAL = RAND:100
IF LOCAL < 3
	TALENT:ARG:Ｃ민감 = 1
ELSEIF LOCAL > 96
	TALENT:ARG:Ｃ둔감 = 1
ENDIF
;V
LOCAL = RAND:100
IF LOCAL < 3 && ISFEMALE(ARG)
	TALENT:ARG:Ｖ민감 = 1
ELSEIF LOCAL > 96 && ISFEMALE(ARG)
	TALENT:ARG:Ｖ둔감 = 1
ENDIF
;A
LOCAL = RAND:100
IF LOCAL < 3
	TALENT:ARG:Ａ민감 = 1
ELSEIF LOCAL > 96
	TALENT:ARG:Ａ둔감 = 1
ENDIF
;B
LOCAL = RAND:100
IF LOCAL < 3
	TALENT:ARG:Ｂ민감 = 1
ELSEIF LOCAL > 96
	TALENT:ARG:Ｂ둔감 = 1
ENDIF

;신체적 특징　빈유 거유 왜소함 장신은 20%확률로, 나머지는 5% 확률로
;젖가슴
LOCAL = RAND:100
IF LOCAL < 20 && ISFEMALE(ARG)
	TALENT:ARG:빈유 = 1
ELSEIF LOCAL > 79 && ISFEMALE(ARG)
	TALENT:ARG:거유 = 1
ENDIF
;체형
LOCAL = RAND:100
IF LOCAL < 20
	TALENT:ARG:왜소함 = 1
ELSEIF LOCAL > 79
	TALENT:ARG:장신 = 1
ENDIF
;젖기 쉬움/어려움
LOCAL = RAND:100
IF LOCAL < 10 && ISFEMALE(ARG)
	TALENT:ARG:젖기쉬움 = 1
ELSEIF LOCAL > 89 && ISFEMALE(ARG)
	TALENT:ARG:젖기어려움 = 1
ENDIF

;------------------------------------------------------------------------------------------------------------------
;플레이버 소질 설정
;------------------------------------------------------------------------------------------------------------------
@CHARA_MAKE_STATUS_TALENT_FLAVOR, ARG
;기본 확률
LOCAL:1 = 4
LOCAL:2 = 4
LOCAL:3 = 2
LOCAL:4 = 6
LOCAL:5 = 1
;LOCAL:101 = 4
;LOCAL:102 = 4
;LOCAL:103 = 4
;LOCAL:104 = 4
LOCAL:201 = 2
LOCAL:202 = 2

;나이 6~12세는 초등학생
IF BASE:ARG:나이 >= 6 && BASE:ARG:나이 <= 12
	TALENT:ARG:학생 = 1
;나이 13~15세는 중학생
ELSEIF BASE:ARG:나이 >= 13 && BASE:ARG:나이 <= 15
	TALENT:ARG:학생 = 2
;나이 16~18세는 고등학생
ELSEIF BASE:ARG:나이 >= 16 && BASE:ARG:나이 <= 18
	TALENT:ARG:학생 = 3
;나이 19~22세는 대학생
ELSEIF BASE:ARG:나이 >= 19 && BASE:ARG:나이 <= 22
	TALENT:ARG:학생 = 4	
ENDIF


;;나이 6~12세는 초등학생+50%
;IF BASE:ARG:나이 >= 6 && BASE:ARG:나이 <= 12
;	LOCAL:101 += 50
;ENDIF
;;나이 13~15세는 중학생+35%
;IF BASE:ARG:나이 >= 13 && BASE:ARG:나이 <= 15
;	LOCAL:102 += 35
;ENDIF
;;나이 16~18세는 고등학생+25%
;IF BASE:ARG:나이 >= 16 && BASE:ARG:나이 <= 18
;	LOCAL:103 += 25
;ENDIF
;;나이 19~22세는 대학생+10%
;IF BASE:ARG:나이 >= 19 && BASE:ARG:나이 <= 22
;	LOCAL:104 += 10
;ENDIF
;
;;처녀가 아니라면 남친있음 +3%, 유부녀+10%, 과부+5%, 초등학생-10%, 중학생-5%, 고등학생+3%, 대학생+10%
;IF TALENT:ARG:처녀 == 0
;	LOCAL:2 += 3
;	LOCAL:4 += 10
;	LOCAL:5 += 5
;	LOCAL:101 -= 10
;	LOCAL:102 -= 5
;	LOCAL:103 += 3
;	LOCAL:104 += 10
;ENDIF
;;백보지라면 유부녀, 과부, 고등학생, 대학생의 확률 반감
;IF TALENT:ARG:백보지 > 0
;	LOCAL:4 /= 2
;	LOCAL:5 /= 2
;	LOCAL:103 /= 2
;	LOCAL:104 /= 2
;ENDIF
;;왜소함이라면 초등학생+16%
;IF TALENT:ARG:왜소함 > 0
;	LOCAL:101 += 16
;ENDIF
;;장신이라면 초등학생-8%
;IF TALENT:ARG:장신 > 0
;	LOCAL:101 -= 8
;ENDIF
;;빈유라면 중학생 이하 +8%
;IF TALENT:ARG:빈유 > 0
;	LOCAL:101 += 8
;	LOCAL:102 += 8
;ENDIF
;
;교제상대
LOCAL = RAND:100
IF LOCAL < LOCAL:1
	TALENT:ARG:교제상대 = 1
ELSEIF LOCAL < LOCAL:1 + LOCAL:2 && ISFEMALE(ARG)
	TALENT:ARG:교제상대 = 2
ELSEIF LOCAL < LOCAL:1 + LOCAL:2 + LOCAL:3
	TALENT:ARG:교제상대 = 3
ELSEIF LOCAL < LOCAL:1 + LOCAL:2 + LOCAL:3 + LOCAL:4 && ISFEMALE(ARG)
	TALENT:ARG:교제상대 = 4
ELSEIF LOCAL <  LOCAL:1 + LOCAL:2 + LOCAL:3 + LOCAL:4 + LOCAL:5 && ISFEMALE(ARG)
	TALENT:ARG:교제상대 = 5
ENDIF
;;약혼 이상이라면 중학생 이하의 확률을 0으로
;IF TALENT:ARG:교제상대 >= 3
;	LOCAL:101 = 0
;	LOCAL:102 = 0
;ENDIF
;;유부녀라면 고등학생, 대학생의 확률을 절반으로
;IF TALENT:ARG:교제상대 == 4
;	LOCAL:103 /= 2
;	LOCAL:104 /= 2
;ENDIF
;;과부라면 고등학생, 대학생의 확률을 0으로
;IF TALENT:ARG:교제상대 == 5
;	LOCAL:103 = 0
;	LOCAL:104 = 0
;ENDIF
;
;;학생
;LOCAL = RAND:100
;IF LOCAL < LOCAL:101
;	TALENT:ARG:학생 = 1
;ELSEIF LOCAL < LOCAL:101 + LOCAL:102
;	TALENT:ARG:학생 = 2
;ELSEIF LOCAL < LOCAL:101 + LOCAL:102 + LOCAL:103
;	TALENT:ARG:학생 = 3
;ELSEIF LOCAL < LOCAL:101 + LOCAL:102 + LOCAL:103 + LOCAL:104
;	TALENT:ARG:학생 = 4
;ENDIF


;가족관계
LOCAL = RAND:100
IF LOCAL < LOCAL:201
	TALENT:ARG:가족관계 = 1
ELSEIF LOCAL < LOCAL:201 + LOCAL:202
	TALENT:ARG:가족관계 = 2
ENDIF
;악세서리
LOCAL = RAND:100
IF ISFEMALE(ARG)
	IF LOCAL < 6
		TALENT:ARG:악세서리 = 1
	ELSEIF LOCAL < 12 && ISFEMALE(ARG)
		TALENT:ARG:악세서리 = 2
	ELSEIF LOCAL < 18 && ISFEMALE(ARG)
		TALENT:ARG:악세서리 = 3
	ENDIF
ENDIF
;외관
LOCAL = RAND:100
IF ISFEMALE(ARG)
	IF LOCAL < 4
		TALENT:ARG:외관 = 1
		TALENT:ARG:변신시외관 = 1
	ELSEIF LOCAL < 8
		TALENT:ARG:외관 = 2
		TALENT:ARG:변신시외관 = 2
	ELSEIF LOCAL < 12
		TALENT:ARG:외관 = 3
		TALENT:ARG:변신시외관 = 3
	ELSEIF LOCAL < 16
		TALENT:ARG:외관 = 4
		TALENT:ARG:변신시외관 = 4
	ENDIF
ENDIF

;초등학생이라면 12% 확률로 유아배로, 75% 확률로 백보지로 변경
LOCAL = RAND:100
;장신이라면 유아배로는 되지 않는다
SIF TALENT:ARG:장신
	LOCAL += 12
;왜소함이라면 확률 2배
SIF TALENT:ARG:왜소함
	LOCAL /= 2
IF LOCAL < 12 && TALENT:ARG:학생 == 1 && ISFEMALE(ARG)
	TALENT:ARG:외관 = 3
	TALENT:ARG:변신시외관 = 3
ENDIF
LOCAL = RAND:100
SIF LOCAL < 75 && TALENT:ARG:백보지 == 0
	TALENT:ARG:백보지 = 1


;------------------------------------------------------------------------------------------------------------------
;정신 소질의 랜덤 설정
;------------------------------------------------------------------------------------------------------------------
;ARG   = 캐릭터 번호
;ARG:1 = 부여할 소질의 수
@CHARA_MAKE_STATUS_TALENT_SEIKAKU, ARG, ARG:1
#DIM HAVE_TALENT
#DIM SELECT
SIF ARG:1 < 1 || TALENT:ARG:감정결여 > 0
	RETURN
SIF ARG:1 >= 10
	ARG:1 = 10

CALL CLEARRANDCHOOSE

;내성적/사교적
IF RAND:2 == 0
	CALL ADDRANDCHOOSE, 600
ELSE
	CALL ADDRANDCHOOSE, 601
ENDIF
;제멋대로굼/헌신적
IF RAND:2 == 0
	CALL ADDRANDCHOOSE, 602
ELSE
	CALL ADDRANDCHOOSE, 603
ENDIF
;소심함/자존심높음
IF RAND:2 == 0
	CALL ADDRANDCHOOSE, 604
ELSE
	CALL ADDRANDCHOOSE, 605
ENDIF
;보수적/호기심
IF RAND:2 == 0
	CALL ADDRANDCHOOSE, 606
ELSE
	CALL ADDRANDCHOOSE, 607
ENDIF
;새드기질/마조기질
IF RAND:2 == 0
	CALL ADDRANDCHOOSE, 608
ELSE
	CALL ADDRANDCHOOSE, 609
ENDIF
;쾌감에솔직/쾌감을부정
IF RAND:2 == 0
	CALL ADDRANDCHOOSE, 610
ELSE
	CALL ADDRANDCHOOSE, 611
ENDIF
;전투거부/싸움꾼
IF RAND:2 == 0
	CALL ADDRANDCHOOSE, 612
ELSE
	CALL ADDRANDCHOOSE, 613
ENDIF
;수줍음/배짱
IF RAND:2 == 0
	CALL ADDRANDCHOOSE, 614
ELSE
	CALL ADDRANDCHOOSE, 615
ENDIF
;인간불신/양도/남성기피/여성기피
IF RAND:4 == 0
	CALL ADDRANDCHOOSE, 616
ELSEIF RAND:3 == 0
	CALL ADDRANDCHOOSE, 617
ELSEIF RAND:2 == 0
	CALL ADDRANDCHOOSE, 618
ELSE
	CALL ADDRANDCHOOSE, 619
ENDIF
;모성적/소악마/눈에띄고싶어함/고상함/울보/결벽증
IF RAND:6 == 0
	CALL ADDRANDCHOOSE, 620
ELSEIF RAND:5 == 0
	CALL ADDRANDCHOOSE, 621
ELSEIF RAND:4 == 0
	CALL ADDRANDCHOOSE, 622
ELSEIF RAND:3 == 0
	CALL ADDRANDCHOOSE, 623
ELSEIF RAND:2 == 0
	CALL ADDRANDCHOOSE, 624
ELSE
	CALL ADDRANDCHOOSE, 625
ENDIF
$LOOP
SELECT = RANDCHOOSE_F()
TALENT:ARG:SELECT = 1
;후보에서 뽑힌 소질을 제외
CALL CLEARSPECIFICCHOOSE, SELECT

HAVE_TALENT = 0
FOR LOCAL, 600, 700
	SIF TALENT:ARG:LOCAL > 0
		HAVE_TALENT ++
NEXT
SIF HAVE_TALENT < ARG:1
	GOTO LOOP

;------------------------------------------------------------------------------------------------------------------
;나이의 초기값 설정
;------------------------------------------------------------------------------------------------------------------
@CHARA_MAKE_AGE_SETTING, ARG
#DIM AGE
#DIM REAL_AGE
#DIM SET_AGE, 3

;나이의 기본값은 랜덤으로 결정
BASE:ARG:나이 = RAND:11 + 10
BASE:ARG:실제나이 = BASE:ARG:나이
;변신하면 어른스러워지는 패턴
IF TALENT:ARG:변신능력 > 0 && MAXBASE:ARG:나이 <= 18 && RAND:4 == 0
	MAXBASE:ARG:나이 = BASE:ARG:나이 + RAND:6
;변신해도 외관나이에 영향을 주지 않는 패턴
ELSEIF TALENT:ARG:변신능력 > 0
	MAXBASE:ARG:나이 = BASE:ARG:나이
ENDIF

IF CALLNAME:ARG == "범용 캐릭터"
	
ENDIF

;여기서부터는 소질에 따른 나이 범위 자동 설정
AGE = BASE:ARG:나이
;초등학생이라면 나이 폭 6~12
IF TALENT:ARG:학생 == 1
	AGE = RAND:7 + 6
;중학생이라면 나이 폭 12~16
ELSEIF TALENT:ARG:학생 == 2
	AGE = RAND:5 + 12
;고등학생이라면 나이 폭 15~19
ELSEIF TALENT:ARG:학생 == 3
	AGE = RAND:5 + 15
;대학생이라면 나이 폭 18~25
ELSEIF TALENT:ARG:학생 == 4
	AGE = RAND:6 + 18
	;SIF AGE >= 22 && RAND:10 == 0
	;	AGE = AGE + RAND:5
ENDIF

;유부녀 또는 과부라면 나이 16세 이상
IF TALENT:ARG:교제상대 >= 4 && AGE < 25
	;고등학생일 경우
	IF TALENT:ARG:학생 == 3
		AGE = 16 + RAND:3
	;대학생일 경우
	ELSEIF TALENT:ARG:학생 == 4
		AGE = 19 + RAND:4
	ELSE
		AGE = TALENT:ARG:교제상대 * 5 + RAND:11
	ENDIF
ENDIF

;실제나이 처리
REAL_AGE = AGE

;인조인간이라면 실제나이 0~5
SIF TALENT:ARG:인조인간 > 0
	REAL_AGE = RAND:6
;요정이라면 노령으로
SIF TALENT:ARG:요정 > 0
	REAL_AGE += RAND:100
;불로장수라면 노령으로
SIF TALENT:ARG:불로장수 > 0
	REAL_AGE += RAND:500
;천사, 마족, 뱀파이어라면 50% 확률로 노령, 5% 확률로 초노령
SIF (TALENT:ARG:천사 > 0 || TALENT:ARG:마족 > 0 || TALENT:ARG:뱀파이어 > 0) && RAND:2 == 0
	REAL_AGE += RAND:1000
SIF (TALENT:ARG:천사 > 0 || TALENT:ARG:마족 > 0 || TALENT:ARG:뱀파이어 > 0) && RAND:10 == 0
	REAL_AGE += RAND:10000

BASE:ARG:나이 = AGE
MAXBASE:ARG:나이 = AGE
BASE:ARG:실제나이 = REAL_AGE

;나이 지정 있음
IF CSTR:ARG:204 != ""
	IF CSTR:ARG:204 == "0"
		BASE:ARG:실제나이 = 0
	ELSEIF TOINT(CSTR:ARG:204) > 0
		BASE:ARG:실제나이 = TOINT(CSTR:ARG:204)
	ELSE
		BASE:ARG:실제나이 = RANDOM_AGE_F(CSTR:ARG:204)
	ENDIF
ENDIF
IF CSTR:ARG:205 != ""
	IF CSTR:ARG:205 == "0"
		BASE:ARG:나이 = 0
	ELSEIF TOINT(CSTR:ARG:205) > 0
		BASE:ARG:나이 = TOINT(CSTR:ARG:205)
	ELSE
		BASE:ARG:나이 = RANDOM_AGE_F(CSTR:ARG:205)
	ENDIF
ENDIF
IF CSTR:ARG:206 != ""
	IF CSTR:ARG:206 == "0"
		MAXBASE:ARG:나이 = 0
	ELSEIF TOINT(CSTR:ARG:206) > 0
		MAXBASE:ARG:나이 = TOINT(CSTR:ARG:206)
	ELSE
		MAXBASE:ARG:나이 = RANDOM_AGE_F(CSTR:ARG:206)
	ENDIF
ENDIF

SIF CSTR:ARG:204 == "나이에 맞춘다"
	BASE:ARG:실제나이 = BASE:ARG:나이
SIF CSTR:ARG:205 == "실제 나이에 맞춘다"
	BASE:ARG:나이 = BASE:ARG:실제나이
SIF CSTR:ARG:206 == "나이에 맞춘다"
	MAXBASE:ARG:나이 = BASE:ARG:나이
SIF CSTR:ARG:206 == "실제 나이에 맞춘다"
	MAXBASE:ARG:나이 = BASE:ARG:실제나이


;나이 지정에서 음수가 지정된 경우 랜덤 범위에서 값을 반환한다
@RANDOM_AGE_F(ARGS)
#FUNCTION
;아기　범위 0~3
IF ARGS == "갓난아기"
	LOCAL = RAND:4
;유아/유치원생　범위 3~5
ELSEIF ARGS == "유아" || ARGS == "유치원생"
	LOCAL = RAND:3 + 3
;아동/유녀　범위 4~9
ELSEIF ARGS == "아동" || ARGS == "유녀"
	LOCAL = RAND:6 + 4
;아이/소녀/소년　범위 7~15
ELSEIF ARGS == "아이" || ARGS == "소녀" || ARGS == "소년"
	LOCAL = RAND:9 + 7
;청소년　범위 13~19
ELSEIF ARGS == "청소년"
	LOCAL = RAND:7 + 13
;젊은이　범위 15~25
ELSEIF ARGS == "젊은이"
	LOCAL = RAND:11 + 15
;어른/청년　범위 20~30
ELSEIF ARGS == "어른" || ARGS == "청년"
	LOCAL = RAND:11 + 20
;방년　범위 25~35
ELSEIF ARGS == "방년"
	LOCAL = RAND:11 + 25
;중년　범위 30~45
ELSEIF ARGS == "중년"
	LOCAL = RAND:16 + 30
;장년　범위 35~60
ELSEIF ARGS == "장년"
	LOCAL = RAND:26 + 35
;초등학생　범위 5~12
ELSEIF ARGS == "초등학생"
	LOCAL = RAND:8 + 5
;중학생　범위 13~15
ELSEIF ARGS == "중학생"
	LOCAL = RAND:4 + 13
;고등학생　범위 16~18
ELSEIF ARGS == "고등학생"
	LOCAL = RAND:3 + 16
;대학생　범위 19~22
ELSEIF ARGS == "대학생"
	LOCAL = RAND:4 + 19
ELSE
	;오류 표시의 대신
	LOCAL = -99
ENDIF
RETURNF LOCAL


;ARG번째 캐릭터에게 ARG:1 종족의 FEAT를 랜덤으로 설정
@SET_FEAT_DEFAULT, ARG, ARG:1
#DIM RACE
#DIM FEAT_NUM
RACE = ARG:1
;틀(枠)의 수를 결정
IF RAND:8 == 0
	FEAT_NUM = 1
ELSEIF RAND:4 == 0
	FEAT_NUM = 2
ELSE
	FEAT_NUM = 3
ENDIF
;뱀파이어는 고유 FEAT "밤악마의귀족" 이 있으므로 1개 제외
SIF TALENTNAME:(ARG:1) == "뱀파이어"
	FEAT_NUM -= 1

;７５％ 확률로 프리셋에서 선택
IF RAND:4 != 0
	IF TALENTNAME:RACE == "인간"
		;고유 FEAT만 Ａ타입
		IF RAND:4 == 0
			TALENT:ARG:평범 = 1
			FEAT_NUM -= 1
		;고유 FEAT만 Ｂ타입
		ELSEIF RAND:3 == 0
			TALENT:ARG:말려드는체질 = 1
			FEAT_NUM -= 1
		;숨겨진 힘
		ELSEIF RAND:2 == 0
			TALENT:ARG:숨겨진힘 = 1
			TALENT:ARG:불굴 = 1
			FEAT_NUM -= 2
		;축복, 불굴 세트
		ELSE
			TALENT:ARG:축복 = 1
			TALENT:ARG:불굴 = 1
			FEAT_NUM -= 2
		ENDIF
		;FEAT가 너무 많으면 다루기 힘들어지므로 확률로 줄인다
		SIF RAND:2 == 0
			FEAT_NUM -= 1
	ELSEIF TALENTNAME:RACE == "이능력자"
		;고유 FEAT만
		IF RAND:4 == 0
			TALENT:ARG:공세구축 = 1
			FEAT_NUM -= 1
		;숨겨진 힘
		ELSEIF RAND:3 == 0
			TALENT:ARG:숨겨진힘 = 1
			TALENT:ARG:심안 = 1
			FEAT_NUM -= 2
		;고고한 헌터형
		ELSEIF RAND:2 == 0
			TALENT:ARG:타고난전사 = 1
			TALENT:ARG:사냥꾼의직감 = 1
			FEAT_NUM -= 2
		;직격률 중시
		ELSE
			TALENT:ARG:공세구축 = 1
			TALENT:ARG:심안 = 1
			FEAT_NUM -= 2
		ENDIF
	ELSEIF TALENTNAME:RACE == "마법사"
		;고유 FEAT만
		IF RAND:5 == 0
			TALENT:ARG:마력저장 = 1
			FEAT_NUM -= 1
		;불로장수만
		ELSEIF RAND:4 == 0
			TALENT:ARG:불로장수 = 1
			FEAT_NUM -= 1
		;후방지원형
		ELSEIF RAND:3 == 0
			TALENT:ARG:행운아 = 1
			TALENT:ARG:지혜의극치 = 1
			FEAT_NUM -= 2
		;고화력형
		ELSEIF RAND:2 == 0
			TALENT:ARG:마력저장 = 1
			TALENT:ARG:풀버스트 = 1
			FEAT_NUM -= 2
		;공중전형
		ELSE
			TALENT:ARG:공중부유 = 1
			TALENT:ARG:불로장수 = 1
			FEAT_NUM -= 2
		ENDIF
	ELSEIF TALENTNAME:RACE == "인조인간"
		;고유 FEAT만
		IF RAND:2 == 0
			TALENT:ARG:고속회피 = 1
			FEAT_NUM -= 1
		;스태미나 축(軸)
		ELSEIF RAND:2 == 0
			TALENT:ARG:스태미나 = 1
			IF RAND:3 == 0
				TALENT:ARG:근력강화 = 1
			ELSEIF RAND:3 == 0
				TALENT:ARG:공중부유 = 1
			ELSEIF RAND:2 == 0
				TALENT:ARG:핫스타트 = 1
			ELSE
				TALENT:ARG:풀버스트 = 1
			ENDIF
			FEAT_NUM -= 2
		;속공 화력형
		ELSE
			TALENT:ARG:핫스타트 = 1
			TALENT:ARG:풀버스트 = 1
			FEAT_NUM -= 2
		ENDIF
		;FEAT가 너무 많으면 다루기 힘들어지므로 확률로 줄인다
		SIF RAND:3 == 0
			FEAT_NUM -= 1
	ELSEIF TALENTNAME:RACE == "천사"
		;고유 FEAT만
		IF RAND:4 != 0
			TALENT:ARG:신기소유자 = 1
			FEAT_NUM -= 1
		;후방지원형
		ELSEIF RAND:3 == 0
			TALENT:ARG:축복 = 1
			TALENT:ARG:지혜의극치 = 1
			FEAT_NUM -= 2
		;공중전형
		ELSEIF RAND:2 == 0
			TALENT:ARG:날개 = 1
			TALENT:ARG:공중부유 = 1
			FEAT_NUM -= 2
		;자유 틀(枠)
		ELSE
			FEAT_NUM = 3
		ENDIF
	ELSEIF TALENTNAME:RACE == "마족"
		;고유 FEAT만
		IF RAND:4 != 0
			TALENT:ARG:투쟁본능 = 1
			FEAT_NUM -= 1
		;탱커형
		ELSEIF RAND:3 == 0
			TALENT:ARG:투쟁본능 = 1
			TALENT:ARG:불굴 = 1
			FEAT_NUM -= 2
		;고고한 헌터형
		ELSEIF RAND:2 == 0
			TALENT:ARG:타고난전사 = 1
			TALENT:ARG:사냥꾼의직감 = 1
			FEAT_NUM -= 2
		;자유 틀(枠)
		ELSE
			FEAT_NUM = 3
		ENDIF
	ELSEIF TALENTNAME:RACE == "오니"
		;고유 FEAT만
		IF RAND:4 != 0
			TALENT:ARG:넘치는생명력 = 1
			FEAT_NUM -= 1
		;탱커형
		ELSEIF RAND:3 == 0
			TALENT:ARG:불굴 = 1
			TALENT:ARG:스태미나 = 1
			FEAT_NUM -= 2
		;순수 파워형
		ELSEIF RAND:2 == 0
			TALENT:ARG:타고난전사 = 1
			TALENT:ARG:근력강화 = 1
			FEAT_NUM -= 1
		;불로장수만
		ELSE
			TALENT:ARG:불로장수 = 1
			FEAT_NUM -= 1
		ENDIF
	ELSEIF TALENTNAME:RACE == "요정"
		;고유 FEAT만
		IF RAND:4 != 0
			TALENT:ARG:정령교신 = 1
			FEAT_NUM -= 1
		;페어리형
		ELSEIF RAND:2 == 0
			TALENT:ARG:소형체구 = 1
			IF RAND:2 == 0
				TALENT:ARG:남다른미모 = 1
			ELSE
				TALENT:ARG:에어마스터 = 1
			ENDIF
			FEAT_NUM -= 2
		;엘프형
		ELSE
			TALENT:ARG:지혜의극치 = 1
			IF RAND:2 == 0
				TALENT:ARG:남다른미모 = 1
			ELSE
				TALENT:ARG:불로장수 = 1
			ENDIF
			FEAT_NUM -= 2
		ENDIF
	ELSEIF TALENTNAME:RACE == "짐승족"
		;고유 FEAT만
		IF RAND:4 != 0
			TALENT:ARG:야수의후손 = 1
			FEAT_NUM -= 1
		;공중전형
		ELSEIF RAND:3 == 0
			TALENT:ARG:소형체구 = 1
			TALENT:ARG:에어마스터 = 1
			FEAT_NUM -= 2
		;치명타형
		ELSEIF RAND:2 == 0
			TALENT:ARG:야수의후손 = 1
			TALENT:ARG:심안 = 1
			FEAT_NUM -= 2
		;스태미나만
		ELSE
			TALENT:ARG:스태미나 = 1
			FEAT_NUM -= 1
		ENDIF
	ELSEIF TALENTNAME:RACE == "뱀파이어"
		;파워형
		IF RAND:4 == 0
			TALENT:ARG:근력강화 = 1
			TALENT:ARG:날개 = 1
			FEAT_NUM -= 2
		;농락형
		ELSEIF RAND:3 == 0
			TALENT:ARG:남다른미모 = 1
			TALENT:ARG:배덕의낙인 = 1
			FEAT_NUM -= 2
		;자유 틀(枠)
		ELSE
			FEAT_NUM = 2
		ENDIF
	ELSEIF TALENTNAME:RACE == "폴리니안"
		;고유 FEAT만
		IF RAND:2 == 0
			TALENT:ARG:고속회피 = 1
			FEAT_NUM -= 1
		;스태미나 축(軸)
		ELSEIF RAND:2 == 0
			TALENT:ARG:스태미나 = 1
			IF RAND:3 == 0
				TALENT:ARG:근력강화 = 1
			ELSEIF RAND:3 == 0
				TALENT:ARG:공중부유 = 1
			ELSEIF RAND:2 == 0
				TALENT:ARG:핫스타트 = 1
			ELSE
				TALENT:ARG:풀버스트 = 1
			ENDIF
			FEAT_NUM -= 2
		;속공 화력형
		ELSE
			TALENT:ARG:핫스타트 = 1
			TALENT:ARG:풀버스트 = 1
			FEAT_NUM -= 2
		ENDIF
		;FEAT가 너무 많으면 다루기 힘들어지므로 확률로 줄인다
		SIF RAND:3 == 0
			FEAT_NUM -= 1
	ENDIF
ENDIF
;틀(枠)이 남아 있으면 랜덤 설정
IF FEAT_NUM > 0
	CALL CLEARRANDCHOOSE
	FOR LOCAL, 1100, 1300
		;취득 가능한 FEAT를 후보에 추가한다, 다만 취득이 끝난 것은 제외한다
		SIF FEAT_ABLE_F(RACE, LOCAL) > 0 && TALENT:ARG:LOCAL == 0
			CALL ADDRANDCHOOSE, LOCAL
	NEXT
	FOR LOCAL, 0, FEAT_NUM
		LOCAL:1 = RANDCHOOSE_F()
		TALENT:ARG:(LOCAL:1) = 1
		CALL CLEARSPECIFICCHOOSE(LOCAL:1)
	NEXT
ENDIF
;프로필에 반영
CALL SET_PROFILE, ARG

RETURN 0


