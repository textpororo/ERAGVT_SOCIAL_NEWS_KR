;**********************************************************
;캐릭터 메이킹 처리
;ARG = 수련P 주회 보너스치
;----------------------------------------------------------
@CHARA_MAKE_MAIN, ARG = 0
#DIM CCOUNT

;공통 설정을 글로벌에서 읽어둔다(글로벌로 설정한다는 것은 자주 사용하고 싶다는 것이므로)
	LOADGLOBAL
	FLAG:5 = GLOBAL:5
	FLAG:6 = GLOBAL:6
	FLAG:7 = GLOBAL:7
	FLAG:820 = GLOBAL:8
	FLAG:821 = GLOBAL:9
	FLAG:822 = GLOBAL:20
	FLAG:823 = GLOBAL:21
	FLAG:824 = GLOBAL:22
	FLAG:825 = GLOBAL:23
	SAVESTR:10 = %GLOBALS:15%
	SAVESTR:11 = %GLOBALS:16%
	SAVESTR:12 = %GLOBALS:17%

;설정 선택
VARSET LOCAL
$LOOP_SETTING_START
DRAWLINE


FONTBOLD
SETCOLORBYNAME CYAN
PRINTL ◆디폴트 공통 설정
FONTREGULAR
RESETCOLOR
	PRINTFORML [1001] 유닛의 주제 ・・・・・・\@(FLAG:5 == 1) ?『%SAVESTR:10%』#없음\@
	PRINTFORML [1002] 변신명（윗구）・・・・・\@(FLAG:6 == 1) ?『%SAVESTR:11%』#없음\@
	PRINTFORML [1003] 변신명（아랫구）장르・ \@(FLAG:820 > 0) ?『%transnamegenre:(FLAG:820-1)%』#없음\@
	PRINTFORML [1004] 변신 시 구호・・・・・・\@(FLAG:7 == 1) ?『%SAVESTR:12%』#없음\@
	PRINTFORML [1005] 성씨의 언어 ・・・・・・\@(FLAG:821 > 0) ?%namelang:(FLAG:821-1)%#디폴트\@
	PRINTFORML [1006] 이름의 언어 ・・・・・・\@(FLAG:822 > 0) ?%namelang:(FLAG:822-1)%#디폴트\@
	PRINTFORML [1007] 종족 ・・・・・・・・・・\@(FLAG:823 > 0) ?%racegenre:(FLAG:823-1)%#랜덤\@
	PRINTFORML [1008] FEAT 자동 할당・・・・・\@(FLAG:824 == 1) ?있음#없음\@
	PRINTFORML [1009] 성격 ・・・・・・・・・・\@(FLAG:825 == 1) ?구상 있음만#완전 랜덤\@

	PRINTFORM [170] 글로벌에 보존　　　　　　　
	PRINTFORM [180] 글로벌에서 불러오기　　　　
	PRINTFORML [190] 초기화　　 
	DRAWLINE
PRINTL

FONTBOLD
SETCOLORBYNAME CYAN
PRINTL ◆개별 캐릭터 메이킹
FONTREGULAR
RESETCOLOR
RESULT = 0
FOR LOCAL, 1, CHARANUM
	IF CALLNAME:LOCAL == "범용 캐릭터"
		LOCALS = 
		PRINTFORM [{LOCAL}] 캐릭터 작성%"♀", 6, LEFT%
		PRINTFORM [{LOCAL + 100}] 캐릭터 작성%"♂", 4, LEFT%
		PRINTFORM 　
		PRINTPLAIN
	ELSE
		SIF LOCAL == MASTER
			CONTINUE
		LOCALS = %CALLNAME:LOCAL%
		PRINTFORM [{LOCAL}] %LOCALS, 35, LEFT%
		PRINTPLAIN 　
	ENDIF

	IF CFLAG:LOCAL:0 == 1
		LOCALS = 유폐
	ELSEIF CFLAG:LOCAL:0 == 2
		LOCALS = 세뇌
	ELSEIF CFLAG:LOCAL:0 == 3
		LOCALS = 흑화
	ELSEIF CFLAG:LOCAL:0 == 4
		LOCALS = 감금
	ELSEIF CFLAG:LOCAL:0 == 9
		LOCALS = 선생
	ELSE
		LOCALS'=" ― "
	ENDIF
	LOCALS = [{LOCAL+500}][%LOCALS%]
	PRINTBUTTON LOCALS,LOCAL+500
	PRINTPLAIN   

	IF CALLNAME:LOCAL == "범용 캐릭터"
		PRINTL
	ELSE
		IF ISMALE(LOCAL)
			PRINT ♂ / 
		ELSE
			PRINT ♀ / 
		ENDIF
		CALL SYUZOKU_CHECK, "STRING", LOCAL
		LOCALS = %RESULTS%
		PRINTFORM [%LOCALS%] 
		CALL SEIKAKU_CHECK, "STRING", LOCAL
		LOCALS = %RESULTS%
		PRINTFORM [%LOCALS%] 
		LOCALS = 
		IF TALENT:LOCAL:변신능력 == -1
			LOCALS += "비전투원"
		ELSEIF TALENT:LOCAL:변신능력 == 0
			LOCALS += "변신 없음"
		ELSE
			LOCALS += "변신 있음"
		ENDIF
		PRINTFORM [%LOCALS%]
		PRINTL 
	ENDIF
NEXT

PRINTL
;프로필 미설정 인원 확인
RESULT=0
FOR CCOUNT, 0, CHARANUM
	SIF CALLNAME:CCOUNT == "범용 캐릭터"
		RESULT ++
NEXT

	IF !GAME_OPTION_CHECK_F(OPTION_solo)
		PRINTL [200] 프리셋을 불러온다　　　　　
	ENDIF

	;프리 플레이 시 또는 SOLO/NORMAL/HARDCORE 클리어가 완료된 동시에 SOLO 모드가 아닐 시　인원수 변경 가능
	SIF GAME_OPTION_CHECK_F(OPTION_실적엔드없음) || ((GLOBAL:100 + GLOBAL:101 + GLOBAL:102) > 0 && !GAME_OPTION_CHECK_F(OPTION_solo))
		PRINTL [300] 인원수를 변경한다
PRINTL
PRINTL
PRINTL
PRINTFORM [1000] ★캐릭터 메이킹을 완료한다%\@ RESULT ? （미설정인 캐릭터는 자동 설정） #  \@,32,LEFT%
PRINTFORML [999] 모드 선택으로 돌아간다


$INPUT_LOOP_0
INPUT
IF RESULT > 100 && RESULT < CHARANUM + 100 && CALLNAME:(RESULT - 100) == "범용 캐릭터"
	TALENT:(RESULT - 100):남자 = 1
	NAME:(RESULT - 100) = 범용 캐릭터(♂)
	CALL FIRSTSETTING_CHARA_MAIN, (RESULT - 100), ARG
	GOTO LOOP_SETTING_START

ELSEIF RESULT > 500 && RESULT < CHARANUM + 500
	ARG = RESULT - 500
	IF CFLAG:ARG:0 == 0
		IF GLOBAL:258 || GAME_OPTION_CHECK_F(OPTION_실적엔드없음)
			CFLAG:ARG:0 = 1
		ELSEIF (GLOBAL:262 || GAME_OPTION_CHECK_F(OPTION_실적엔드없음)) && !GAME_OPTION_CHECK_F(OPTION_solo)
			CFLAG:ARG:0 = 3
			CFLAG:ARG:41 = 401
			EXP:ARG:함락경험 += 1
		ELSEIF GLOBAL:278 || GAME_OPTION_CHECK_F(OPTION_실적엔드없음)
			CFLAG:ARG:0 = 4
			CFLAG:ARG:71 = 30
		ELSEIF GLOBAL:212 || GAME_OPTION_CHECK_F(OPTION_실적엔드없음)
			CFLAG:ARG:0 = 9
		ENDIF
	;유폐
	ELSEIF CFLAG:ARG:0 == 1
		IF (GLOBAL:262 || GAME_OPTION_CHECK_F(OPTION_실적엔드없음)) && !GAME_OPTION_CHECK_F(OPTION_solo)
			CFLAG:ARG:0 = 3
			CFLAG:ARG:41 = 401
			EXP:ARG:함락경험 += 1
		ELSEIF GLOBAL:278 || GAME_OPTION_CHECK_F(OPTION_실적엔드없음)
			CFLAG:ARG:0 = 4
			CFLAG:ARG:71 = 30
		ELSEIF GLOBAL:212 || GAME_OPTION_CHECK_F(OPTION_실적엔드없음)
			CFLAG:ARG:0 = 9
		ELSE
			CFLAG:ARG:0 = 0
		ENDIF
	;흑화
	ELSEIF CFLAG:ARG:0 == 3
		IF GLOBAL:278 || GAME_OPTION_CHECK_F(OPTION_실적엔드없음)
			CFLAG:ARG:0 = 4
			CFLAG:ARG:71 = 30
		ELSEIF GLOBAL:212 || GAME_OPTION_CHECK_F(OPTION_실적엔드없음)
			CFLAG:ARG:0 = 9
		ELSE
			CFLAG:ARG:0 = 0
		ENDIF
		CFLAG:ARG:41 = 401
		EXP:ARG:함락경험 -= 1
	;감금
	ELSEIF CFLAG:ARG:0 == 4
		IF GLOBAL:212 || GAME_OPTION_CHECK_F(OPTION_실적엔드없음)
			CFLAG:ARG:0 = 9
		ELSE
			CFLAG:ARG:0 = 0
		ENDIF
	ELSEIF CFLAG:ARG:0 == 9
		CFLAG:ARG:0 = 0
	ENDIF
	GOTO LOOP_SETTING_START

ELSEIF RESULT > 0 && RESULT < CHARANUM
	CALL FIRSTSETTING_CHARA_MAIN, RESULT, ARG
	GOTO LOOP_SETTING_START

;결정
ELSEIF RESULT == 1000
	PRINTL 캐릭터 메이킹을 종료합니다.
	;캐릭터 메이킹 완료 처리(전원분 모아서)
	CALL CHARA_MAKE_FINALIZE
ELSEIF RESULT == 999
	RETURN -1

ELSEIF RESULT == 1001
	CALL FIRSTSETTING_TITLE
	GOTO LOOP_SETTING_START
ELSEIF RESULT == 1002
	CALL FIRSTSETTING_CROWNNAME
	GOTO LOOP_SETTING_START
ELSEIF RESULT == 1003
	CALL FIRSTSETTING_transnamegenre
	GOTO LOOP_SETTING_START
ELSEIF RESULT == 1004
	CALL FIRSTSETTING_TRANSCALL
	GOTO LOOP_SETTING_START
ELSEIF RESULT == 1005
	CALL FIRSTSETTING_familynamelang
	GOTO LOOP_SETTING_START
ELSEIF RESULT == 1006
	CALL FIRSTSETTING_namelang
	GOTO LOOP_SETTING_START
ELSEIF RESULT == 1007
	CALL FIRSTSETTING_racegenre
	GOTO LOOP_SETTING_START
ELSEIF RESULT == 1008
	FLAG:824=!FLAG:824
	GOTO LOOP_SETTING_START
ELSEIF RESULT == 1009
	FLAG:825=!FLAG:825
	GOTO LOOP_SETTING_START
ELSEIF RESULT == 170
	PRINTFORML 현재 보존되어 있는 글로벌
	PRINTFORML 　주제・・・・\@(GLOBAL:5 == 1) ?있음 『%GLOBALS:15%』#없음\@
	PRINTFORML 　관명・・・・\@(GLOBAL:6 == 1) ?있음 『%GLOBALS:16%』#없음\@
	PRINTFORML 　변신명・・・\@(GLOBAL:8 > 0) ?있음 『%transnamegenre:(GLOBAL:8-1)%』#없음\@
	PRINTFORML 　구호・・・・\@(GLOBAL:7 == 1) ?있음 『%GLOBALS:17%』#없음\@
	PRINTFORML 　성씨 언어・\@(GLOBAL:9 > 0) ?있음 『%namelang:(GLOBAL:9-1)%』#없음\@
	PRINTFORML 　이름 언어・\@(GLOBAL:20 > 0) ?있음 『%namelang:(GLOBAL:20-1)%』#없음\@
	PRINTFORML 　종족・・・・\@(GLOBAL:21 > 0) ?있음 『%racegenre:(GLOBAL:21-1)%』#없음\@
	PRINTFORML 　FEAT・・・・\@(GLOBAL:22 > 0) ?있음#없음\@
	PRINTFORML 　성격・・\@(GLOBAL:23 > 0) ?구상 있음만#완전 랜덤\@
	PRINTL 
	PRINTL 【주의】공통 설정의 글로벌에 덮어씁니다.
	PRINTL         이전 상태로 복원할 수 없습니다만, 괜찮습니까?
	PRINTL     [0] 덮어쓰기 실행      [99] 취소하고 돌아간다
	PRINTL 
	$INPUT_LOOP_3_1
	INPUT
	IF RESULT == 0
		GLOBAL:5 = FLAG:5
		GLOBAL:6 = FLAG:6
		GLOBAL:7 = FLAG:7
		GLOBAL:8 = FLAG:820
		GLOBAL:9 = FLAG:821
		GLOBAL:20 = FLAG:822
		GLOBAL:21 = FLAG:823
		GLOBAL:22 = FLAG:824
		GLOBAL:23 = FLAG:825
		GLOBALS:15 = %SAVESTR:10%
		GLOBALS:16 = %SAVESTR:11%
		GLOBALS:17 = %SAVESTR:12%
		SAVEGLOBAL
		PRINTW 현재의 공통 설정을 글로벌에 보존했습니다.
		GOTO LOOP_SETTING_START
	ELSEIF RESULT == 99
		;PRINTW 덮어쓰기를 취소했습니다.
		GOTO LOOP_SETTING_START
	ELSE
		GOTO INPUT_LOOP_3_1
	ENDIF
ELSEIF RESULT == 180
	LOADGLOBAL
	FLAG:5 = GLOBAL:5
	FLAG:6 = GLOBAL:6
	FLAG:7 = GLOBAL:7
	FLAG:820 = GLOBAL:8
	FLAG:821 = GLOBAL:9
	FLAG:822 = GLOBAL:20
	FLAG:823 = GLOBAL:21
	FLAG:824 = GLOBAL:22
	FLAG:825 = GLOBAL:23
	SAVESTR:10 = %GLOBALS:15%
	SAVESTR:11 = %GLOBALS:16%
	SAVESTR:12 = %GLOBALS:17%
	;↓보면 알 수 있기 때문에 코멘트 아웃
	;PRINTW 글로벌에서 공통 설정을 불러왔습니다.
	GOTO LOOP_SETTING_START
ELSEIF RESULT == 190
	FLAG:5 = 0
	FLAG:6 = 0
	FLAG:7 = 0
	FLAG:820 = 0
	FLAG:821 = 0
	FLAG:822 = 0
	FLAG:823 = 0
	FLAG:824 = 0
	FLAG:825 = 0
	SAVESTR:10 = 
	SAVESTR:11 = 
	SAVESTR:12 = 
	;↓보면 알 수 있기 때문에 코멘트 아웃
	;PRINTW 공통 설정을 초기화했습니다.
	GOTO LOOP_SETTING_START

;초기 세트 불러오기
ELSEIF RESULT == 200 && !GAME_OPTION_CHECK_F(OPTION_solo)
	LOCAL=LINECOUNT
	TRYCALL CHARA_MAKE_FINALIZE_KAI
	IF RESULT < 0
		CLEARLINE LINECOUNT - LINECOUNT
	ENDIF
	GOTO LOOP_SETTING_START
ELSEIF RESULT == 300 && (GAME_OPTION_CHECK_F(OPTION_실적엔드없음) || ((GLOBAL:100 + GLOBAL:101 + GLOBAL:102) > 0 && !GAME_OPTION_CHECK_F(OPTION_solo)))
		PRINTL 
		IF (GLOBAL:100 + GLOBAL:101 + GLOBAL:102) > 0
			PRINTFORML 주회 클리어 보너스：
		ELSE
			PRINTFORML 샌드박스：
		ENDIF
		PRINTFORML 게임 시작 시의 인원수를 증감할 수 있습니다.
		PRINTFORML [ 2] ２인으로 시작
		PRINTFORML [ 3] ３인으로 시작(기본)
		PRINTFORML [ 4] ４인으로 시작
		PRINTFORML [ 5] ５인으로 시작
		PRINTFORML [ 6] ６인으로 시작
		PRINTL 
		PRINTFORML [99] 변경하지 않고 돌아간다
		$INPUT_LOOP_CHARA_NUM
		INPUT
		IF RESULT >= 2 && RESULT <= 6
			LOCAL = RESULT
			PRINTFORML {LOCAL}인으로 시작합니다.
			IF CHARANUM - 1 < LOCAL
				REPEAT LOCAL - (CHARANUM-1)
					ADDCHARA 0
					FLAG:8 ++
				REND
			ELSEIF LOCAL < CHARANUM - 1
				REPEAT (CHARANUM - 1) - LOCAL
					DELCHARA CHARANUM - 1
					FLAG:8 --
				REND
			ENDIF
			GOTO LOOP_SETTING_START
		ELSEIF RESULT == 99
			GOTO LOOP_SETTING_START
		ELSE
			GOTO INPUT_LOOP_CHARA_NUM
		ENDIF
ELSE
	GOTO INPUT_LOOP_0
ENDIF
