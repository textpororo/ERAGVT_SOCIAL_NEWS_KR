;@TURNEND
@EVENTTURNEND
;악질 시민 관련 플래그를 초기화
FLAG:70 = 0
FLAG:71 = 0
FLAG:72 = 0

;PALAM 최대값 초기화
VARSET MAX_PALAM

;행동마다 줄 바꿈을 넣는다
PRINTL 
PRINTL 
PRINTL 

;다음 캐릭터를 행동시킨다
FLAG:799 += 1

BEGIN SHOP





;--------------------------------------------------
@EVENTSHOP
#DIM CCOUNT
#DIM LCOUNT
REDRAW 0

;파티 멤버 관리
CALL SET_PARTYMEMBER


;습격 이벤트 후 스킵
;RAID_HANTEI로 판정을 통과하여 이벤트가 개시되면 $RAID_SKIP까지 건너뛰며, 도중의 이벤트를 스킵한다. (턴이 여분으로 남은 채 경과해 버리기 때문)
;전투가 끝나면 SHOP으로 돌아오므로, 원래 위치 $AFTER_RAID까지 건너뛰며 처리 완료. 여기까지 1세트
IF FLAG:45 > 0
	LOCAL = TARGET
	;구출된 캐릭터의 치료
	FOR CCOUNT, 0, CHARANUM
		SIF CCOUNT == MASTER
			CONTINUE
		SIF CCOUNT >= CHARANUM
			BREAK

		IF CFLAG:CCOUNT:0 == 상태_구출직후
			TARGET = CCOUNT
			IF CFLAG:TARGET:6 == -1
				PRINTL 
				CALL RESCUE_CHILD,TARGET
				SIF RESULT < 0
					CCOUNT -= RESULT
			ELSE
				CALL AFTER_RESCUED,TARGET
			ENDIF
		ELSE
			CALL INMON_RECOVERY,CCOUNT
		ENDIF
	NEXT
	TARGET = LOCAL
	PRINTW 

	;습격 플래그를 끈다
	FLAG:45 = 0

	;턴 종료 처리중에 발생하고 있던 전투라면 종료 처리를 재차 계속한다(＝전원 행동 완료)
	IF FLAG:48 > 0
		FLAG:48 = 0
		GOTO AFTER_RAID
	ENDIF
ENDIF


;모든 캐릭터가 행동 예정을 종료하고 있는 경우
IF DAY == 0 || FLAG:799 > CHARANUM || FLAG:64 != 0
	SIF FLAG:64 == 0
		FLAG:799 = 0
	;게임 시작 직후의 처리였던 경우
	IF DAY == 0
		DAY = 1
		TIME = 0
		FLAG:64 = 0
		FLAG:799 = 0
		TARGET = 1
		CALL LB
		;PRINTL 게임을 시작합니다.
		;PRINTW 
		GOTO RAID_SKIP
	;통상의 턴 엔드
	ELSE
		;이전 턴에서 행동 개시 전에 선택했던 캐릭터에 타겟을 맞춘다
		;턴 중에 사용이 불가할 경우, 다른 사용 가능한 캐릭터 중 가장 어린 캐릭터에 맞춘다
		IF CFLAG:(FLAG:798):0 != 상태_건강
			FOR CCOUNT, 0, CHARANUM
				SIF CCOUNT == MASTER
					CONTINUE
				
				IF CFLAG:CCOUNT:0 == 상태_건강
					TARGET = CCOUNT
					BREAK
				ENDIF
			NEXT
		ELSE
			TARGET = FLAG:798
		ENDIF
	ENDIF
;행동하지 않는 캐릭터가 있는 경우
ELSE
	;SHOP_ACTION으로 건너뛰고 다시 루프
	JUMP SHOP_ACTION
ENDIF

;엔딩 판정
CALL ENDING
;強制的にタイトルに戻る場合
IF FLAG:999 == -999
	CLEARLINE LINECOUNT
	RESETDATA
	BEGIN TITLE
ELSEIF FLAG:64 != 0
	GOTO RAID_SKIP
ENDIF

PRINTL 
;구출된 캐릭터의 치료・역대 최고 스테이터스 기록 갱신
FOR CCOUNT, 0, CHARANUM
	SIF CCOUNT == MASTER
		CONTINUE
	SIF CCOUNT >= CHARANUM
		BREAK

	;스테이터스 재계산
	CALL LEVELSTATUS,CCOUNT

	IF CFLAG:CCOUNT:0 == 상태_구출직후
		TARGET = CCOUNT
		IF CFLAG:TARGET:6 == -1
			PRINTL 
			CALL RESCUE_CHILD,TARGET
			SIF RESULT < 0
				CCOUNT -= RESULT
		ELSE
			CALL AFTER_RESCUED,TARGET
		ENDIF
		PRINTW 
	ELSE
		CALL INMON_RECOVERY,CCOUNT
	ENDIF
	;딸을 시설에 맡긴 경우의 처리
	SIF CCOUNT > CHARANUM - 1
		BREAK

	;역대 최고 기록의 갱신 판정
	IF MAXBASE:CCOUNT:체력 > GLOBAL:103
		GLOBAL:103 = MAXBASE:CCOUNT:체력
		GLOBALS:0 = %NAME:CCOUNT,40,LEFT%（Lv{ABL:CCOUNT:레벨}）　{GLOBAL:103}
	ENDIF
	IF MAXBASE:CCOUNT:기력 > GLOBAL:104
		GLOBAL:104 = MAXBASE:CCOUNT:기력
		GLOBALS:1 = %NAME:CCOUNT,40,LEFT%（Lv{ABL:CCOUNT:레벨}）　{GLOBAL:104}
	ENDIF
	IF MAXBASE:CCOUNT:성내성 > GLOBAL:105
		GLOBAL:105 = MAXBASE:CCOUNT:성내성
		GLOBALS:2 = %NAME:CCOUNT,40,LEFT%（Lv{ABL:CCOUNT:레벨}）　{GLOBAL:105}
	ENDIF
	IF MAXBASE:CCOUNT:공격 > GLOBAL:106
		GLOBAL:106 = MAXBASE:CCOUNT:공격
		GLOBALS:10 = %NAME:CCOUNT,40,LEFT%（Lv{ABL:CCOUNT:레벨}）　{GLOBAL:106}
	ENDIF
	IF MAXBASE:CCOUNT:방어 > GLOBAL:107
		GLOBAL:107 = MAXBASE:CCOUNT:방어
		GLOBALS:11 = %NAME:CCOUNT,40,LEFT%（Lv{ABL:CCOUNT:레벨}）　{GLOBAL:107}
	ENDIF
	IF MAXBASE:CCOUNT:민첩 > GLOBAL:108
		GLOBAL:108 = MAXBASE:CCOUNT:민첩
		GLOBALS:12 = %NAME:CCOUNT,40,LEFT%（Lv{ABL:CCOUNT:레벨}）　{GLOBAL:108}
	ENDIF
	IF MAXBASE:CCOUNT:지성 > GLOBAL:109
		GLOBAL:109 = MAXBASE:CCOUNT:지성
		GLOBALS:13 = %NAME:CCOUNT,40,LEFT%（Lv{ABL:CCOUNT:레벨}）　{GLOBAL:109}
	ENDIF
	IF 	EXP:CCOUNT:매료경험 > GLOBAL:110
		GLOBAL:110 = EXP:CCOUNT:매료경험
		GLOBALS:14 = %NAME:CCOUNT,40,LEFT%（Lv{ABL:CCOUNT:레벨}）　{GLOBAL:110}
	ENDIF

	;Shame
	IF 	EXP:CCOUNT:V경험 > GLOBAL:120
		GLOBAL:120 = EXP:CCOUNT:V경험
		GLOBALS:20 = %NAME:CCOUNT,40,LEFT%（Lv{ABL:CCOUNT:레벨}）　{GLOBAL:120}회
	ENDIF
	IF 	EXP:CCOUNT:A경험 > GLOBAL:121
		GLOBAL:121 = EXP:CCOUNT:A경험
		GLOBALS:21 = %NAME:CCOUNT,40,LEFT%（Lv{ABL:CCOUNT:레벨}）　{GLOBAL:121}회
	ENDIF
	IF 	EXP:CCOUNT:자위경험 > GLOBAL:122
		GLOBAL:122 = EXP:CCOUNT:자위경험
		GLOBALS:22 = %NAME:CCOUNT,40,LEFT%（Lv{ABL:CCOUNT:레벨}）　{GLOBAL:122}회
	ENDIF
	IF 	EXP:CCOUNT:펠라경험 > GLOBAL:123
		GLOBAL:123 = EXP:CCOUNT:펠라경험
		GLOBALS:23 = %NAME:CCOUNT,40,LEFT%（Lv{ABL:CCOUNT:레벨}）　{GLOBAL:123}회
	ENDIF
	IF 	EXP:CCOUNT:정액경험 > GLOBAL:124
		GLOBAL:124 = EXP:CCOUNT:정액경험
		GLOBALS:24 = %NAME:CCOUNT,40,LEFT%（Lv{ABL:CCOUNT:레벨}）　{GLOBAL:124}회
	ENDIF
	IF 	EXP:CCOUNT:절정경험 > GLOBAL:125
		GLOBAL:125 = EXP:CCOUNT:절정경험
		GLOBALS:25 = %NAME:CCOUNT,40,LEFT%（Lv{ABL:CCOUNT:레벨}）　{GLOBAL:125}회
	ENDIF
	IF 	EXP:CCOUNT:출산경험 > GLOBAL:126
		GLOBAL:126 = EXP:CCOUNT:출산경험
		GLOBALS:26 = %NAME:CCOUNT,40,LEFT%（Lv{ABL:CCOUNT:레벨}）　{GLOBAL:126}회
	ENDIF
	IF 	EXP:CCOUNT:근친교배경험 > GLOBAL:127
		GLOBAL:127 = EXP:CCOUNT:근친교배경험
		GLOBALS:27 = %NAME:CCOUNT,40,LEFT%（Lv{ABL:CCOUNT:레벨}）　{GLOBAL:127}회
	ENDIF
	IF 	EXP:CCOUNT:사정경험 > GLOBAL:128
		GLOBAL:128 = EXP:CCOUNT:사정경험
		GLOBALS:28 = %NAME:CCOUNT,40,LEFT%（Lv{ABL:CCOUNT:레벨}）　{GLOBAL:128}회
	ENDIF
	IF 	EXP:CCOUNT:모유경험 > GLOBAL:129
		GLOBAL:129 = EXP:CCOUNT:모유경험
		GLOBALS:29 = %NAME:CCOUNT,40,LEFT%（Lv{ABL:CCOUNT:레벨}）　{GLOBAL:129}회
	ENDIF
	IF 	EXP:CCOUNT:방뇨경험 > GLOBAL:130
		GLOBAL:130 = EXP:CCOUNT:방뇨경험
		GLOBALS:30 = %NAME:CCOUNT,40,LEFT%（Lv{ABL:CCOUNT:레벨}）　{GLOBAL:130}회
	ENDIF
	IF 	EXP:CCOUNT:기생경험 > GLOBAL:131
		GLOBAL:131 = EXP:CCOUNT:기생경험
		GLOBALS:31 = %NAME:CCOUNT,40,LEFT%（Lv{ABL:CCOUNT:레벨}）　{GLOBAL:131}회
	ENDIF


	SAVEGLOBAL

	
	;실적 획득 판정
	CALL GET_STATE_TROPHY,CCOUNT
NEXT


;**********************************************************
;턴 종료 후 일어나는 각종 이벤트

;턴 종료 중 플래그
FLAG:48 = 1

;보스 출현 정지 플래그를 끈다
FLAG:49 = 0

;보스 촉수의 피해 축적치가 회복된다
IF GET_LASTBOSS_PHASE_F() >= 2
	;히든 보스는 회복 없음
ELSE
	SETCOLOR 128,128,128
	SIF FLAG:999 == 1
		PRINTL DEBUG 보스 상처가 회복
	RESETCOLOR
	FOR LOCAL,1,FLAG:3 + 1
		FLAG:(300 + LOCAL) -= RAND:3000000 + 8000000
		SIF FLAG:(300 + LOCAL) < 0
			FLAG:(300 + LOCAL) = 0
	NEXT
	FOR LOCAL,1,FLAG:4 + 1
		;최종 보스 강화 시 회복량 저하
		CALL LASTBOSS_REST
		FLAG:(400 + LOCAL) -= (RAND:3000000 + 1000000) / (2 + RESULT)
		SIF FLAG:(400 + LOCAL) < 0
			FLAG:(400 + LOCAL) = 0
	NEXT
ENDIF

;구상의 턴 종료 시의 처리를 호출
FOR CCOUNT, 0, CHARANUM
	SIF CCOUNT == MASTER
		CONTINUE

	TARGET = CCOUNT
	CALL MESSAGE_TURNEND
NEXT

;TARGET 값을 피난
FLAG:797 = TARGET

;악질 시민에 의한 협박 혹은 유괴 감금
FOR CCOUNT, 1, CHARANUM
	;협박 이벤트
	IF CONFIG_CHECK_PRISON_F(10) == 1 && GROUPMATCH(CFLAG:CCOUNT:0,0,5) && (CFLAG:CCOUNT:286 + CFLAG:CCOUNT:320 + CFLAG:CCOUNT:321) > 0 ;一度でもレイプされたことがあれば発生
		TARGET = CCOUNT
		CALL INTIMIDATION_EVENT
	;감금 이벤트 (컨피그에 관계없이 캐릭터 메이킹에서 부여되었을 가능성이 있음)
	ELSEIF CFLAG:CCOUNT:0 == 상태_악질감금
		TARGET = CCOUNT
		CALL KIDNAPPING
	ENDIF
NEXT

;촉수 유폐
CALL PRISON
;출산 판정
CALL BIRTH_HANTEI
;육아 종료 판정
CALL GROW_HANTEI

;흑화 캐릭터가 날뛰는 이벤트
CALL AKUOTI_ATTACK
;イチャックス 뭔 말인지 모르겠다
CALL LOVESEX_NIGHT
;야간 자위
CALL SELF_NIGHT
;요바이
SIF CONFIG_CHECK_EVENT_F(2) > 0
	CALL YOBAI

;게임 오버 모드에서는 방어력이 0이 된다
IF CHECK_GAMEOVER_F() == 1
	FLAG:852 = 0
	FLAG:853 = LIMIT(FLAG:853, -100, 0)
ELSEIF DAY > 0
	;방위력의 자동 변동
	;①아무도「출격／거점방위」를 하지 않음(SOLO모드만「단련／정보 수집」도 출격 취급)
	;②포기 턴 ＞방위 시설 레벨
	;두 조건을 함께 채울 경우에 저하
	IF FLAG:41 == 0
		;출격 포기 중에는 치안 악화가 가속되어 간다
		;(단, SOLO 모드일 때는 유폐 리스크가 높기 때문에 악화 페이스는 1/4로 억제)
		IF FLAG:41 == 0 && (FLAG:2!=2 || (DAY % 2 ==0 && TIME == 0))
			FLAG:42 ++
		ENDIF
		DRAWLINE
		PRINTL 
		FONTBOLD
		PRINTL 방위력 변동　　 
		IF FLAG:42 < FLAG:52
			PRINTFORML 　방위 설비가 효과적으로 기능하고 있습니다.
		ELSEIF FLAG:42 < 2 + FLAG:52
			PRINTFORML 　위험도 레벨{FLAG:42 - FLAG:52}：지금은 비교적 안전합니다.
		ELSEIF FLAG:42 < 3 + FLAG:52
			PRINTFORML 　위험도 레벨{FLAG:42 - FLAG:52}：아직은 무시할 만한 수준입니다.
		ELSEIF FLAG:42 < 5 + FLAG:52
			PRINTFORML 　위험도 레벨{FLAG:42 - FLAG:52}：촉수의 활동이 점점 활발해지고 있습니다.
		ELSEIF FLAG:42 < 7 + FLAG:52
			PRINTFORML 　위험도 레벨{FLAG:42 - FLAG:52}：도시 곳곳에서 피해가 발생하고 있습니다!
		ELSE
			PRINTFORML 　위험도 레벨MAX：상당히 위험한 상태입니다!！
		ENDIF
		FONTREGULAR
		PRINTFORM 　방위력( {FLAG:852} )　
		;이 턴에 아무도 출격하지 않은 경우, 전투 포기 턴 수에 따라 방위력이 크게 저하
		LOCAL:852 = 0
		IF FLAG:41 == 0
			CALL TENTACLE_LEVEL
			IF FLAG:42 < FLAG:52
				LOCAL:852 = 0
			ELSEIF FLAG:42 < 2 + FLAG:52
				LOCAL:852 = RESULT + 5
			ELSEIF FLAG:42 == 2 + FLAG:52
				LOCAL:852 = FLAG:852 * 1 / 100 + RESULT + 5
			ELSEIF FLAG:42 == 3 + FLAG:52
				LOCAL:852 = FLAG:852 * 4 / 100 + RESULT + 10
			ELSEIF FLAG:42 == 4 + FLAG:52
				LOCAL:852 = FLAG:852 * 8 / 100 + RESULT + 20
			ELSEIF FLAG:42 == 5 + FLAG:52
				LOCAL:852 = FLAG:852 * 12 / 100 + RESULT + 40
			ELSEIF FLAG:42 == 6 + FLAG:52
				LOCAL:852 = FLAG:852 * 15 / 100 + RESULT + 80
			ELSEIF FLAG:42 == 7 + FLAG:52
				LOCAL:852 = FLAG:852 * 20 / 100 + RESULT + 160
			ELSE
				LOCAL:852 = FLAG:852 * 50 / 100 + RESULT + 320
			ENDIF
			PRINTFORM −　시간 경과( {LOCAL:852} )　
			FLAG:852 = MAX(FLAG:852 - LOCAL:852, 0)
		ELSE
			;누군가 출격한다면 포기 턴 수 리셋
			FLAG:42 = 0
		ENDIF
		LOCAL:852 = FLAG:52 * FLAG:52 * 5
		;방위 설비가 자동으로 방위
		FLAG:852 = FLAG:852 + LOCAL:852
		SIF LOCAL:852
			PRINTFORM ＋　방위 설비Lv{FLAG:52}( {LOCAL:852} )　
		SIF (FLAG:41 == 0 && !GAME_OPTION_CHECK_F(OPTION_solo)) || FLAG:52
			PRINTFORM ＝　{FLAG:852}
		PRINTL 
	;한 명이라도 출격했다면 포기 턴 수 초기화
	ELSEIF FLAG:41 > 0
		FLAG:42 = 0
	ENDIF
	;인기도 자동 변동
	FLAG:853 = LIMIT(FLAG:853, -100, 100)
	DRAWLINE
	PRINTL 
	FONTBOLD
	PRINTL 인기도 변동　　 
	FONTREGULAR
	PRINTFORM 　인기도( {FLAG:853} )　
	;시민 평판 (一般評価)
	LOCAL:853 = 0
	LOCAL:0 = FLAG:853
	IF FLAG:852 == 0
		SIF RAND:100 < 5
			LOCAL:853 -= 1
		SIF RAND:100 < 5
			LOCAL:853 -= 1
		SIF RAND:100 < 5
			LOCAL:853 -= 1
	ELSEIF FLAG:852 < 1250
		SIF RAND:100 < 5
			LOCAL:853 -= 1
		SIF RAND:100 < 5
			LOCAL:853 -= 1
	ELSEIF FLAG:852 < 2500
		SIF RAND:100 < 5
			LOCAL:853 -= 1
		SIF RAND:100 < 5
			LOCAL:853 += 1
	ELSEIF FLAG:852 < 5000
		SIF RAND:100 < 5
			LOCAL:853 += 1
	ELSEIF FLAG:852 < 7500
		SIF RAND:100 < 5
			LOCAL:853 += 1
		SIF RAND:100 < 5
			LOCAL:853 += 1
	ELSE
		SIF RAND:100 < 5
			LOCAL:853 += 1
		SIF RAND:100 < 5
			LOCAL:853 += 1
		SIF RAND:100 < 5
			LOCAL:853 += 1
	ENDIF
	IF LOCAL:853 < 0
		RESULT = ABS(LOCAL:853)
		PRINTFORM −　시민 평판( {RESULT} )　
	ENDIF
	SIF LOCAL:853 > 0
		PRINTFORM ＋ 시민 평판( {LOCAL:853} )　
	FLAG:853 = FLAG:853 + LOCAL:853
	;처녀 보너스
	LOCAL:853 = 0
	FOR CCOUNT, 0, CHARANUM
		SIF CCOUNT == MASTER
			CONTINUE
		IF TALENT:CCOUNT:처녀 > 0
			SIF RAND:100 < 5
				LOCAL:853 += 1
		ELSEIF CHARATALENT_F(CCOUNT,0,"남자") > 0 && CHARATALENT_F(CCOUNT,1,"남자") == 0 && TALENT:CCOUNT:변신시비처녀 == 0
			SIF RAND:100 < 5
				LOCAL:853 += 1
		ENDIF
	NEXT
	SIF LOCAL:853 > 0
		PRINTFORM ＋　처녀 보너스( {LOCAL:853} )　
	FLAG:853 = FLAG:853 + LOCAL:853
	;팬 인기
	LOCAL:853 = 0
	FOR CCOUNT, 0, CHARANUM
		SIF CCOUNT == MASTER
			CONTINUE
		IF EXP:CCOUNT:매료경험 >= 100
			SIF RAND:100 < 5
				LOCAL:853 += 1
			SIF EXP:CCOUNT:매료경험 >= 150 && RAND:100 < 5
				LOCAL:853 += 1
			SIF EXP:CCOUNT:매료경험 >= 200 && RAND:100 < 5
				LOCAL:853 += 1
		ENDIF
		SIF TALENT:CCOUNT:청순파 > 0 && RAND:100 < 5
			LOCAL:853 += 1
		SIF RAND:100 < MIN(CFLAG:CCOUNT:284 / 4, 10)
			LOCAL:853 += 1
		;성격 보정
		SIF TALENT:CCOUNT:모성적 > 0 && RAND:100 < 5
			LOCAL:853 += 1
		SIF TALENT:CCOUNT:소악마 > 0 && RAND:100 < 5
			LOCAL:853 += 1
		SIF TALENT:CCOUNT:눈에띄고싶어함 > 0 && RAND:100 < 5
			LOCAL:853 += 1
		SIF TALENT:CCOUNT:고상함 > 0 && RAND:100 < 5
			LOCAL:853 += 1
		SIF TALENT:CCOUNT:울보 > 0 && RAND:100 < 5
			LOCAL:853 += 1
		;FEAT에 의한 인기도 보정
		SIF TALENT:CCOUNT:야수의후손 > 0 && RAND:100 < 5
			LOCAL:853 += 1
		SIF TALENT:CCOUNT:남다른미모 > 0 && RAND:100 < 10
			LOCAL:853 += 1
	NEXT
	SIF LOCAL:853 > 0
		PRINTFORM ＋　팬 인기( {LOCAL:853} )　　 
	FLAG:853 = FLAG:853 + LOCAL:853
	;부정적인 소문
	LOCAL:853 = 0
	FOR CCOUNT, 0, CHARANUM
		SIF CCOUNT == MASTER
			CONTINUE
		SIF RAND:50 < MIN(CFLAG:825, 10)
			LOCAL:853 += 1
	NEXT
	SIF LOCAL:853 > 0
		PRINTFORM −　부정적인 소문( {LOCAL:853} )
	FLAG:853 = FLAG:853 - LOCAL:853
	FLAG:853 = LIMIT(FLAG:853, -100, 100)
	PRINTFORM ＝　{FLAG:853}　
	IF FLAG:853 < LOCAL:0
		PRINT (↓하락)
	ELSEIF FLAG:853 > LOCAL:0
		PRINT (↑상승)
	ELSE
		PRINT (변동 없음)
	ENDIF
	PRINTL 
	DRAWLINE
	PRINTL 
	SIF CONFIG_CHECK_SCREEN_F(3) == 0
		WAIT
ENDIF
LOCAL:852 = 0
LOCAL:853 = 0

;보스 촉수 내습 판정
SIF CONFIG_CHECK_EVENT_F(0) > 0
	CALL RAID_HANTEI
;내습 발생 시에는 아래를 스킵
SIF FLAG:45 > 0
	GOTO RAID_SKIP
$AFTER_RAID



;기생 관련
CALL PARASITE
;새끼 촉수 내습
SIF CONFIG_CHECK_EVENT_F(1) > 0
	CALL SMALL_TENTACLE_HANTEI
;새끼 촉수 증식 판정
CALL BIRTH_AUTO_RANDOM

;동시 출격 인원수와 전투 지원 인원수를 초기화
FLAG:41 = 0
FLAG:43 = 0
;TARGET을 원래대로 되돌린다
TARGET = FLAG:797


;**********************************************************


;밤낮의 전환
INVERTBIT TIME, 0

;시간 경과에 따라 자동적으로 체력, 기력, 성내성, 결계, 음문이 회복된다
FOR CCOUNT, 0, CHARANUM
	SIF CCOUNT == MASTER
		CONTINUE
	
	;하루가 경과한 직후 (낮) 이라면 회복량 8%
	IF TIME == 0
		LOCAL:1 = 8
		IF TALENT:CCOUNT:회복빠름 == 1
			LOCAL:1 += 4
		ELSEIF TALENT:CCOUNT:회복느림 == 1
			LOCAL:1 -= 4
		ENDIF
		;자동 회복 향상 효과
		SIF FLAG:901
			LOCAL:1 += 4
	ELSE
		LOCAL:1 = 0
		;FEAT 효과에 따른 회복량 보정
		SIF TALENT:밤악마의귀족 > 0
			LOCAL:1 += 8
	ENDIF
	;휴식 설비 레벨에 비례하여 자동 회복
	IF FLAG:51 == 1
		LOCAL:1 += 0
	ELSEIF FLAG:51 == 2
		LOCAL:1 += 2
	ELSEIF FLAG:51 == 3
		LOCAL:1 += 4
	ELSEIF FLAG:51 == 4
		LOCAL:1 += 6
	ELSEIF FLAG:51 == 5
		LOCAL:1 += 8
	ENDIF

	;피로도에 따라 회복량 저하（피로도 회복 처리 전에 계산）
	IF CFLAG:CCOUNT:99 >= 50
		LOCAL:1 = LOCAL:1 * 0 / 100
	ELSEIF CFLAG:CCOUNT:99 >= 40
		LOCAL:1 = LOCAL:1 * 5 / 100
	ELSEIF CFLAG:CCOUNT:99 >= 30
		LOCAL:1 = LOCAL:1 * 25 / 100
	ELSEIF CFLAG:CCOUNT:99 >= 20
		LOCAL:1 = LOCAL:1 * 50 / 100
	ELSEIF CFLAG:CCOUNT:99 >= 10
		LOCAL:1 = LOCAL:1 * 75 / 100
	ENDIF

	;;유폐, 세뇌, 흑화, 사망 캐릭터는 체력•기력•성내성이 0이 된다
	IF GROUPMATCH(CFLAG:CCOUNT:0, 상태_유폐, 상태_세뇌, 상태_흑화, 상태_사망)
		BASE:CCOUNT:체력 = 0
		BASE:CCOUNT:기력 = 0
		BASE:CCOUNT:성내성 = 0
	;회복 처리
	ELSE
		LOCAL = MAXBASE:CCOUNT:체력 * LOCAL:1 / 100
		BASE:CCOUNT:체력 = LIMIT(LOCAL + BASE:CCOUNT:체력, 0, MAXBASE:CCOUNT:체력)

		LOCAL = MAXBASE:CCOUNT:기력 * LOCAL:1 / 100
		BASE:CCOUNT:기력 = LIMIT(LOCAL + BASE:CCOUNT:기력, 0, MAXBASE:CCOUNT:기력)

		LOCAL = MAXBASE:CCOUNT:성내성 * LOCAL:1 / 100
		BASE:CCOUNT:성내성 = LIMIT(LOCAL + BASE:CCOUNT:성내성, 0, MAXBASE:CCOUNT:성내성)
	ENDIF

	;피로도 자동 회복 처리
	IF CFLAG:CCOUNT:99 > 0 && CFLAG:CCOUNT:0 == 상태_건강
		LOCAL = 0
		SIF (FLAG:53 & 관엽식물) && RAND:2 == 0
			LOCAL += 1
		SIF (FLAG:53 & 마음이편안해지는풍경화) && RAND:3 == 0
			LOCAL += 1
		SIF (FLAG:53 & 세련된오브제) && RAND:4 == 0
			LOCAL += 1
		SIF (FLAG:53 & 호화로운분수) && RAND:2 == 0
			LOCAL += 1
		SIF (FLAG:53 & 안마의자)
			LOCAL += 1
		SIF (FLAG:53 & 뷰티살롱)
			LOCAL += 2

		SIF CFLAG:CCOUNT:100 == 예정_단련 && (FLAG:53 & 목욕설비)
			LOCAL += 1

		;FEAT 효과에 따른 피로 자동 회복
		SIF TALENT:CCOUNT:불로장수 > 0 && TIME == 0
			LOCAL += 1 + RAND:2 + RAND:2
		SIF TALENT:CCOUNT:불로장수 > 0 && TIME == 1
			LOCAL += 1 + RAND:2

		SIF CFLAG:CCOUNT:99 <= 0
			LOCAL = 0

		;회복 실행
		CFLAG:CCOUNT:99 -= LOCAL
		SIF CFLAG:CCOUNT:99 < 0
			CFLAG:CCOUNT:99 = 0
		;과잉피로 시 상한 컷(일단 상한 200)
		;세뇌 또는 흑화 시에는 상한 50
		IF GROUPMATCH(CFLAG:CCOUNT:0, 상태_세뇌, 상태_흑화)
			SIF CFLAG:CCOUNT:99 > 50
				CFLAG:CCOUNT:99 = 50
		ELSE
			SIF CFLAG:CCOUNT:99 > 200
				CFLAG:CCOUNT:99 = 200
		ENDIF

		IF CFLAG:CCOUNT:100 != 예정_휴식 && LOCAL > 0
			IF CFLAG:CCOUNT:99 == 0
				PRINTFORML %CALLNAME:CCOUNT%의 몸에 쌓인 피로가 완전히 풀렸다!
			ELSE
				PRINTFORML %CALLNAME:CCOUNT%의 몸에 쌓인 피로가 조금 풀렸다……
			ENDIF
		ENDIF

		;이성 회복, 개율(概率)5%
		IF TALENT:CCOUNT:번식자루 == 1
			IF RAND(25-CHARANUM_ACTRESS()*4) < 1
				PRINTL 이성 회복
				PRINTFORML %CHARANUM_ACTRESS() > 0 ? "동료의 헌신적인 치료에 의해" # "그 누구도 이유는 모르지만,"% 유대와 사랑의 힘이 바램을 이루어준 걸까,
				PRINTFORML 폐인화되었던 %CALLNAME:CCOUNT%의 이성이 기적적으로 회복되었다!
				TALENT:CCOUNT:번식자루 = 0
				FORCEWAIT
			ENDIF
		ENDIF
		[SKIPSTART]
		;아래 중국판 컨텐츠는 미번인 상태이기에 작동시키지 않도록 했습니다.
		;괴멸 카운트 합계 (毁灭倒计时加总)
		;우선 어떤 이유로든 팔다리가 덜 부러지면 카운트다운이 중지됩니다.(首先如果因为任何原因不再四肢欠损，则倒计时停止)
		IF TALENT:CCOUNT:사지결손 < 1
			TALENT:CCOUNT:괴멸카운트 = 0
		ENDIF
		;카운트다운이 시작되면 라운드마다 무작위로 숫자를 늘린다. (如果倒计时开始计算，每回合随机增加数值)
		IF TALENT:CCOUNT:괴멸카운트 > 0
			TALENT:CCOUNT:괴멸카운트 += RAND(1,3)
		ENDIF
		;카운트다운이 15 혹은 그 이상으로 증가하면, 처형에 들어간다. (当倒计时增加至15或以上，进入处刑)
		IF TALENT:CCOUNT:괴멸카운트 >= 15
			TARGET = CCOUNT
			CALL AMPUTEE_EXECUTION,TARGET
		ENDIF
		[SKIPEND]
	ENDIF

	;부위 결계의 회복
	FOR LCOUNT, 0, 감각수
		LOCAL = 0
		IF CFLAG:CCOUNT:0 == 상태_건강
			LOCAL = 3
			SIF CFLAG:CCOUNT:100 == 예정_휴식
				LOCAL += 3
		ENDIF
		LOCAL = MAXBASE:CCOUNT:(LCOUNT + 30) * LOCAL / 100
		SIF BASE:CCOUNT:(LCOUNT + 30) > 0
			BASE:CCOUNT:(LCOUNT + 30) = LIMIT(BASE:CCOUNT:(LCOUNT + 30) + LOCAL, 0, MAXBASE:CCOUNT:(LCOUNT + 30))
	NEXT

	;사교적일 경우, 낮은 확률로 스토커 플래그 증가
	SIF TALENT:CCOUNT:사교적 > 0 && RAND:100 < (4 + SQRT(EXP:CCOUNT:매료경험))
		CFLAG:CCOUNT:285 += 1
	;소심함일 경우, 낮은 확률로 스토커 플래그 감소
	SIF TALENT:CCOUNT:소심함 > 0 && RAND:100 < 8 && CFLAG:CCOUNT:285 > 0
		CFLAG:CCOUNT:285 -= 1

	;피임약을 처방하면 1개 소비
	SIF CFLAG:CCOUNT:241 > 0
		CFLAG:CCOUNT:241 -= 1





	CFLAG:CCOUNT:400 = 0

	IF TIME == 0
		;날짜 변경 시 생리 주기 변수를 진행시킨다
		CALL ESTRUS_CYCLE(CCOUNT)
	ENDIF
	
NEXT

;일수
DRAWLINE
IF TIME == 0
	DAY += 1
	IF DAY > 1
		PRINTL 하루가 지나갔습니다.
		SAVESTR'=""
		IF CHECK_GAMEOVER_F() == 0
			IF DAY == 7
				PRINTW 국가로부터 원조 표명 통지가 도착했습니다.
				PRINTW 앞으로는 일주일마다 정부의 원조와 시민들의 의연금도 함께 전달됩니다.
				PRINTW 방위 성과에 따라 금액이 늘어나기 때문에, 방위력 감소에 주의합시다.
				PRINTL 
				PRINTL 첫 지원 자금이 도착했습니다.
				PRINTW 
			ELSEIF DAY == 21
				PRINTL 국가로부터의 원조금이 증액되었습니다.
				PRINTW 
			ELSEIF DAY == 42
				PRINTL 불황의 영향으로 인해, 국가로부터의 원조금이 대폭 감액되었습니다.
				PRINTW 
			ELSEIF DAY == 63
				PRINTL 불황의 영향으로 인해, 국가로부터 자금 원조가 중단되었습니다.
				PRINTW 
			ENDIF

			;자금 원조
			IF DAY % 7 == 0
				LOCAL = 1000 + ((FLAG:852 +1) / 25)
				LOCAL:1 = RAND:((FLAG:852 +1) / 10 + 50) + 10
				IF FLAG:853 <= -50
					LOCAL:2 = 0
				ELSeIF FLAG:853 <= -25
					LOCAL:2 = 200
				ELSEIF FLAG:853 < 0
					LOCAL:2 = 600
				ELSEIF FLAG:853 < 25
					LOCAL:2 = 1200
				ELSEIF FLAG:853 < 50
					LOCAL:2 = 1800
				ELSEIF FLAG:853 < 75
					LOCAL:2 = 3600
				ELSE
					LOCAL:2 = 7200
				ENDIF
				FOR CCOUNT, 0, CHARANUM
					SIF CCOUNT == MASTER
						CONTINUE
					SIF EXP:CCOUNT:매료경험 > 100
						LOCAL:2 += SQRT(MAX(EXP:CCOUNT:매료경험 - 100, 0)) * 500 + RAND:(EXP:CCOUNT:매료경험)
				NEXT
				SIF DAY >= 21
					LOCAL += 800
				SIF DAY >= 42
					LOCAL /= 4
				SIF DAY >= 63
					LOCAL = 0
				SIF DAY < 63
					PRINTFORML 정부로부터 원조금을 받아 자금 {LOCAL}$ 를 얻었다!
				PRINTFORML 시민들로부터 의연금을 받아 자금 {LOCAL:1 + LOCAL:2}$ 를 얻었다!
				MONEY += LOCAL + LOCAL:1 + LOCAL:2
			ENDIF

			;지출
			LOCAL:2 = 40 + 20 * CHARANUM_SAFE()
			SIF GAME_OPTION_CHECK_F(OPTION_hardcore)
				LOCAL:2 *= 3
			IF MONEY >= LOCAL:2
				PRINTFORML 유지비와 생활비 등으로 자금 {LOCAL:2}$ 를 지불했다.
				MONEY -= LOCAL:2
				WAIT
			ELSE
				PRINTFORML 유지비나 생활비 등을 지불할 자금이 부족합니다!
				PRINTFORML 체력과 기력을 소모해 버렸습니다……
				WAIT
				MONEY = 0
				FOR CCOUNT, 0, CHARANUM
					TIMES BASE:CCOUNT:체력 , 0.8
					TIMES BASE:CCOUNT:기력 , 0.8
				NEXT
			ENDIF
		ENDIF



	ENDIF
ELSE
	PRINTL 밤이 되었습니다.
	WAIT
ENDIF

;뉴스를 초기화
SAVESTR:20'=""
;특정 상황에서는 뉴스를 고정
;게임 오버 모드
IF CHECK_GAMEOVER_F() == 1
	FLAG:60 = 10001
;최종 보스 출현 중
ELSEIF FLAG:101 > 0
	FLAG:60 = 10000
ENDIF

;TARGET을 원래대로 되돌린다
TARGET = FLAG:798
$RAID_SKIP
;모든 캐릭터 결계 상태 체크
CALL CHECK_SHIELD_ALL

;습격이 발생하지 않았다면 턴 종료 처리를 종료
SIF FLAG:45 == 0
	FLAG:48 = 0






