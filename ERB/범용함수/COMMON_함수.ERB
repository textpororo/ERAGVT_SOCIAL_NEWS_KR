;자주 사용하는 함수군

;--------------------------------------------------
;짧은 선 생성
@SHORTLINE, ARG = 0
PRINT ――――――――――――――――――――――――――――
SIF ARG == 0
	PRINTL 
RETURN RESULT

;--------------------------------------------------
;조금 더 긴 선 생성(트로피 표시 화면에서 사용)
@SHORTLINE_2, ARG = 0
PRINT ―――――――――――――――――――――――――――――――――――――
SIF ARG == 0
	PRINTL 
RETURN RESULT

;--------------------------------------------------
;LINEBLACKOUT
;줄 바꿈을 통한 화면 전환 연출
@LB
#DIM LCOUNT
FOR LCOUNT, 0, 50
	PRINTL 
NEXT
;받은 RESULT를 그대로 돌려준다
RETURN RESULT

;--------------------------------------------------
;컬러바 @COLOR_BAR
;ARG:1 현재값
;ARG:2 최댓값
;ARG:3 게이지 길이
;ARG:4  색・R  *0xFFFFFF 방식으로 색상을 지정할 수 없으므로 주의.
;ARG:5  색・G
;ARG:6  색・B
;ARG:7 배경색의 색상차(ARGS:1 과 ARGS:2의 색차). 수치가 클수록 배경의 색이 옅어진다
;ARG:8 그라데이션 변화 • R가산치
;ARG:9  그라데이션 변화 • G가산치
;ARG:10 그라데이션 변화 • B가산치
;ARG:11 그라데이션 변화의 빈도. 수치가 클수록 변화가 완만해진다
;ARGS:1 게이지에 사용하는 문자
;ARGS:2 게이지 배경 문자
@COLOR_BAR(ARG:1,ARG:2,ARG:3=20,ARG:4=160,ARG:5=160,ARG:6=160,ARG:7=-80,ARG:8,ARG:9,ARG:10,ARG:11=1,ARGS:1=UNICODE(0x2582),ARGS:2=UNICODE(0x2581))
VARSET LOCAL
VARSET LOCALS
LOCAL:1 = ARG:1 * ARG:3 / ARG:2
LOCAL:2 = ARG:3 - LOCAL:1
LOCAL:4 = ARG:4 + ARG:7
LOCAL:5 = ARG:5 + ARG:7
LOCAL:6 = ARG:6 + ARG:7
SIF LOCAL:4 < 0
	LOCAL:4 = 0
SIF LOCAL:4 > 255
	LOCAL:4 = 255
SIF LOCAL:5 < 0
	LOCAL:5 = 0
SIF LOCAL:5 > 255
	LOCAL:5 = 255
SIF LOCAL:6 < 0
	LOCAL:6 = 0
SIF LOCAL:6 > 255
	LOCAL:6 = 255
FOR LOCAL,0,ARG:3
	IF LOCAL:1 > LOCAL
		IF LOCAL:100 < ARG:11
			SETCOLOR ARG:4,ARG:5,ARG:6
			LOCAL:100 += 1
		ELSE
			ARG:4 += ARG:8
			ARG:5 += ARG:9
			ARG:6 += ARG:10
			SIF ARG:4 < 0
				ARG:4 = 0
			SIF ARG:4 > 255
				ARG:4 = 255
			SIF ARG:5 < 0
				ARG:5 = 0
			SIF ARG:5 > 255
				ARG:5 = 255
			SIF ARG:6 < 0
				ARG:6 = 0
			SIF ARG:6 > 255
				ARG:6 = 255
			SETCOLOR ARG:4,ARG:5,ARG:6
			LOCAL:100 = 0
		ENDIF
		PRINTFORM %ARGS:1%
	ELSE
		IF !LOCAL:101
			SETCOLOR LOCAL:4,LOCAL:5,LOCAL:6
			LOCAL:101 = 1
		ENDIF
		PRINTFORM %ARGS:2%
	ENDIF
NEXT
RESETCOLOR
VARSET LOCAL
RETURN 1


;--------------------------------------------------
;색을 표시하는 문장 - 캐릭터의 체력바 등
@COLORSENTENCE_BAR, ARGS, ARG:0, ARG:1, ARG:2, ARG:3
SIF ARGS != "사정" && ARGS != "모유"
	CALL COLORSENTENCE_BARCOLOR, ARG:1, ARG:2

PRINTFORM %ARGS,ARG:0,LEFT%　  
;CALL PRINT_COLORBAR(ARG:1,ARG:2,ARG:3, UNICODE(0x2584), UNICODE(0x2584))
;BAR ARG:1,ARG:2,ARG:3
IF ARGS == "체력"
	ARG:4 = 215
	ARG:5 = 105
	ARG:6 = 30
ELSEIF ARGS == "기력"
	ARG:4 = 60
	ARG:5 = 75
	ARG:6 = 215
ELSEIF ARGS == "성내성"
	ARG:4 = 255
	ARG:5 = 90
	ARG:6 = 120
ELSEIF ARGS == "사정"
	ARG:4 = 215
	ARG:5 = 180
	ARG:6 = 105
ELSEIF ARGS == "모유"
	ARG:4 = 255
	ARG:5 = 220
	ARG:6 = 205
ENDIF
PRINTFORM %UNICODE(0x208D)%
CALL COLOR_BAR(ARG:1,ARG:2,ARG:3,ARG:4,ARG:5,ARG:6,-160,10,18,6,2,UNICODE(0x25AE),UNICODE(0x25AE))
SIF ARGS != "사정" && ARGS != "모유"
	CALL COLORSENTENCE_BARCOLOR, ARG:1, ARG:2
PRINTFORM %UNICODE(0x208E)%
PRINTFORM （{ARG:1, 5}/{ARG:2, 5}）
RESETCOLOR


;--------------------------------------------------
;색을 표시하는 문장 - 캐릭터의 체력바 등(SHOP 화면의 비조작 캐릭터용)
@COLORSENTENCE_MINIBAR, ARGS, ARG:0, ARG:1, ARG:2, ARGS:1, ARG:3
CALL COLORSENTENCE_BARCOLOR, ARG:0, ARG:1

PRINTFORM %ARGS%
;CALL PRINT_COLORBAR(ARG:0,ARG:1,ARG:2, UNICODE(0x2584), UNICODE(0x2584))
;BAR ARG:0,ARG:1,ARG:2
IF ARGS == "체력"
	ARG:4 = 245
	ARG:5 = 135
	ARG:6 = 60
ELSEIF ARGS == "기력"
	ARG:4 = 90
	ARG:5 = 105
	ARG:6 = 245
ELSEIF ARGS == "성내성"
	ARG:4 = 255
	ARG:5 = 120
	ARG:6 = 150
ENDIF
PRINTFORM %UNICODE(0x208D)%
CALL COLOR_BAR(ARG:0,ARG:1,ARG:2,ARG:4,ARG:5,ARG:6,-160,10,18,6,1,UNICODE(0x2582),UNICODE(0x2581))
CALL COLORSENTENCE_BARCOLOR, ARG:1, ARG:2
PRINTFORM %UNICODE(0x208E)%
PRINTFORM ({ARG:0}/{ARG:1})%ARGS:1, ARG:3%
RESETCOLOR

;--------------------------------------------------
;색을 표시하는 문장 - 적의 체력바 등
@COLORSENTENCE_ENEMYBAR, ARGS, ARG:0, ARG:1, ARG:2

SIF ARGS == "적 체력"
	CALL COLORSENTENCE_BARCOLOR, ARG:1, ARG:2
IF FLAG:73 > 0 && ARGS == "적 체력"
	RESETCOLOR
	PRINTFORM 인원수
ELSEIF ENEMY_TYPE_CHECK_F("AKUOTI") == 0 && ARGS == "적 체력"
	PRINTFORM %STR:2500% 체력
ELSEIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1 && ARGS == "적 체력"
	PRINTFORM 적 체력
ENDIF

;CALL PRINT_COLORBAR(ARG:0,ARG:1,ARG:2, UNICODE(0x2584), UNICODE(0x2584))
;BAR ARG:0,ARG:1,ARG:2
IF ARGS == "적 체력"
	ARG:4 = 215
	ARG:5 = 105
	ARG:6 = 30
ELSEIF ARGS == "적 사정"
	ARG:4 = 215
	ARG:5 = 180
	ARG:6 = 105
ELSEIF ARGS == "적 절정"
	ARG:4 = 250
	ARG:5 = 120
	ARG:6 = 250
ENDIF
PRINTFORM %UNICODE(0x208D)%
CALL COLOR_BAR(ARG:0,ARG:1,ARG:2,ARG:4,ARG:5,ARG:6,-160,10,18,6,2,UNICODE(0x25AE),UNICODE(0x25AE))
SIF ARGS == "촉수 체력"
	CALL COLORSENTENCE_BARCOLOR, ARG:1, ARG:2
PRINTFORM %UNICODE(0x208E)%
;해석도가 부족한 경우에는 수치가 표시되지 않는다
IF FLAG:999 == 1 || FLAG:20 >= 50 || ENEMY_TYPE_CHECK_F("AKUOTI") == 1
	PRINTFORM （{ARG:0, 5}/
ELSE
	PRINTFORM （？？？/
ENDIF
IF FLAG:999 == 1 || FLAG:20 >= 25 || ENEMY_TYPE_CHECK_F("AKUOTI") == 1
	PRINTFORM {ARG:1, 5}）
ELSE
	PRINTFORM ？？？）
ENDIF
RESETCOLOR

;--------------------------------------------------
;색을 표시하는 문장 - 색상 판정
@COLORSENTENCE_BARCOLOR, ARG:0 ,ARG:1
IF ARG:0 == 0
	SETCOLOR 255, 0, 0
ELSE
	LOCAL = PERCENT_CAL_F(ARG:0, ARG:1)
	IF LOCAL < 25
		SETCOLOR 255, 123, 0
	ELSEIF LOCAL < 50
		SETCOLOR 255, 255, 0
	ENDIF
ENDIF

;--------------------------------------------------
;수치 열의 문자 수를 반환하는 식중 함수
@INTLEN, ARG
#FUNCTION
LOCALS = %TOSTR(ARG)%
LOCAL = STRLENSU(LOCALS)
RETURNF LOCAL

;--------------------------------------------------
;퍼센트 계산, 백분율을 반환한다
@PERCENT_CAL, ARG:0, ARG:1
SIF ARG:0 == 0 || ARG:1 == 0
	RETURN 0
LOCAL = ARG:0*100 / ARG:1
RETURN LOCAL

;--------------------------------------------------
;퍼센트 계산의 식중함수 버전
@PERCENT_CAL_F, ARG:0, ARG:1
#FUNCTION
SIF ARG:0 == 0 || ARG:1 == 0
	RETURNF 0 
LOCAL = ARG:0*100 / ARG:1
RETURNF LOCAL

;--------------------------------------------------
;문자열을 초기화하는 식중 함수
;「LOCALS =」와 같은 역할
@STRCLEAR
#FUNCTIONS
RETURNF LOCALS

;--------------------------------------------------
;건강한 캐릭터의 총수를 반환하는 식중 함수
;대기 멤버 포함 계산
;※「건강하다」란 인류사회의 비호 하에 자기결정권 아래 생존해 있는 상태를 나타낸다
;원문 "「無事」とは人類社会の庇護下で自己決定権のもと生存している状態を示す"
;　구체적으로는 조작 가능 or 입원 중 캐릭터 수
@CHARANUM_SAFE
#FUNCTION
LOCAL = CHARANUM-1
FOR LOCAL:1, 1, CHARANUM
	SIF CFLAG:(LOCAL:1):0 != 0 && CFLAG:(LOCAL:1):0 != 10 && CFLAG:(LOCAL:1):0 != 11
		LOCAL -= 1
NEXT
RETURNF LOCAL

;무사하고 파티 편성이 가능한 캐릭터 총수
@CHARANUM_PLAYABLE
#FUNCTION
LOCAL = CHARANUM-1
FOR LOCAL:1, 1, CHARANUM
	SIF CFLAG:(LOCAL:1):0 != 0
		LOCAL -= 1
NEXT
RETURNF LOCAL

;자발적으로 행동 가능한 캐릭터 수 (SAFE에서 사지결손과 번식자루 제외)
@CHARANUM_ACTRESS
#FUNCTION
LOCAL = CHARANUM-1
FOR LOCAL:1, 1, CHARANUM
	SIF CFLAG:(LOCAL:1):0 != 0 || TALENT:(LOCAL:1):사지결손 || TALENT:(LOCAL:1):번식자루
		LOCAL -= 1
NEXT
RETURNF LOCAL


;무사하지 못한 캐릭터 총수( CHARANUM-CHARANUM_SAFE() )을 반환하는 식중함수
@CHARANUM_LOST
#FUNCTION
LOCAL = CHARANUM-1
FOR LOCAL:1, 1, CHARANUM
	SIF CFLAG:(LOCAL:1):0 != 1 && CFLAG:(LOCAL:1):0 != 2 && CFLAG:(LOCAL:1):0 != 3 && CFLAG:(LOCAL:1):0 != 4 && CFLAG:(LOCAL:1):0 != 9
		LOCAL -= 1
NEXT
RETURNF LOCAL

;현재 유폐 중 or 세뇌 흑화중인 캐릭터 수를 반환하는 식중함수 (거두어진 사망 캐릭터를 제외하고 센다)
@CHARANUM_PRISON
#FUNCTION
LOCAL = CHARANUM-1
FOR LOCAL:1, 1, CHARANUM
	SIF CFLAG:(LOCAL:1):0 != 1 && CFLAG:(LOCAL:1):0 != 2 && CFLAG:(LOCAL:1):0 != 3
		LOCAL -= 1
NEXT
RETURNF LOCAL

;현재 세뇌 흑화 중인 캐릭터 수를 반환하는 식중함수
@CHARANUM_DARKEN
#FUNCTION
LOCAL = CHARANUM-1
FOR LOCAL:1, 1, CHARANUM
	SIF CFLAG:(LOCAL:1):0 != 2 && CFLAG:(LOCAL:1):0 != 3
		LOCAL -= 1
NEXT
RETURNF LOCAL

;악질 시민에게 감금・성노예화되어 있는 캐릭터 수를 반환한다
@CHARANUM_ENSLAVED
#FUNCTION
LOCAL = 0
FOR LOCAL:1, 1, CHARANUM
	SIF CFLAG:(LOCAL:1):0 == 4
		LOCAL++
NEXT
RETURNF LOCAL

;지금도 인류의 희망인 히로인의 총수를 반환한다
;(「건강」하거나「유폐 중/감금 중이지만 흑화 또는 로스트에는 이르지 않은」캐릭터 수를 반환한다)
@CHARANUM_HOPE
#FUNCTION
LOCAL = CHARANUM-1
FOR LOCAL:1, 1, CHARANUM
	SIF CFLAG:(LOCAL:1):0 == 2 || CFLAG:(LOCAL:1):0 == 3 || CFLAG:(LOCAL:1):0 == 9
		LOCAL -= 1
NEXT
RETURNF LOCAL

;여성 취급을 받는 캐릭터의 수
@CHARANUM_GIRLY()
#FUNCTION
LOCAL = 0
FOR LOCAL:1, 1, CHARANUM
	SIF ISGIRLY(LOCAL:1)
		LOCAL++
NEXT
RETURNF LOCAL
;남성 취급을 받는 캐릭터의 수
@CHARANUM_MANLY()
#FUNCTION
LOCAL = 0
FOR LOCAL:1, 1, CHARANUM
	SIF ISMANLY(LOCAL:1)
		LOCAL++
NEXT
RETURNF LOCAL



;--------------------------------------------------
;생존 중이면서 관리 상태(PT 편성 중)가 해당하는 캐릭터의 총수를 반환하는 식중함수
;ARG는 인계 플래그용
@CHARANUM_SAFE_PARTYCHECK, ARG
#FUNCTION
LOCAL = CHARANUM-1
FOR LOCAL:1, 1, CHARANUM
	SIF CFLAG:(LOCAL:1):0 == 1 || CFLAG:(LOCAL:1):0 == 2 || CFLAG:(LOCAL:1):0 == 3 || CFLAG:(LOCAL:1):0 == 4 || CFLAG:(LOCAL:1):0 == 9 || CFLAG:(LOCAL:1):999 == 0
		LOCAL -= 1
NEXT
RETURNF LOCAL
;--------------------------------------------------
;조작 중(파티 편성에 참가하고 있거나, 건강한 상태인) 캐릭터의 총수를 반환하는 식중 함수
@CHARANUM_ACTIVE
#FUNCTION
LOCAL = CHARANUM-1
FOR LOCAL:1, 1, CHARANUM
	SIF CFLAG:(LOCAL:1):0 != 0 || CFLAG:(LOCAL:1):999 == 0
		LOCAL -= 1
NEXT
RETURNF LOCAL

;--------------------------------------------------
;안부를 불문하고, 게임 내에 배치되어 있는(대기하고 있지 않은) 캐릭터의 총수를 반환하는 식중 함수
@CHARANUM_PARTYCHECK, ARG
#FUNCTION
LOCAL = CHARANUM-1
FOR LOCAL:1, 1, CHARANUM
	SIF CFLAG:(LOCAL:1):999 == 0
		LOCAL -= 1
NEXT
RETURNF LOCAL

;--------------------------------------------------
;변신 중이라면 변신 후 이름, 아니라면 풀네임을 반환하는 식중 함수
@PRINT_TRANSNAME, ARG
#FUNCTIONS
IF CFLAG:ARG:3 == 1 && CFLAG:ARG:1 != 0
	LOCALS = %CSTR:ARG:0%
ELSE
	LOCALS = %NAME:ARG%
ENDIF
RETURNF LOCALS

;--------------------------------------------------
;변신 중이라면 변신 후 호칭, 아니라면 CALLNAME을 반환하는 식중 함수
@PRINT_TRANSCALLNAME, ARG
#FUNCTIONS
;당신(あなた) 구상 선택 시
IF CFLAG:ARG:6 == 3
	LOCALS = 당신
ELSEIF CFLAG:ARG:3 == 1 && CFLAG:ARG:1 != 0
	LOCALS = %CSTR:ARG:1%
ELSE
	LOCALS = %CALLNAME:ARG%
ENDIF
RETURNF LOCALS

;※변신 상태에 관계없이 변신 후 이름을 취득하고 싶다면 %CSTR:1% 을 직접 사용

;--------------------------------------------------
;당신(あなた) 구상에 대응하는 호칭을 반환하는 식중 함수
@PRINT_CALLNAME, ARG, ARG:1 = 0
#FUNCTIONS
;지문용
IF ARG:1 == 0
	;당신(あなた) 구상選択時
	IF CFLAG:ARG:6 == 3
		LOCALS = 당신
	ELSE
		LOCALS = %CALLNAME:ARG%
	ENDIF
;시스템 표시용
ELSE
	LOCALS = %CALLNAME:ARG%
	;당신(あなた) 구상 선택 시
	SIF CFLAG:ARG:6 == 3
		LOCALS += "(당신)"
ENDIF
RETURNF LOCALS



;주제 취득 래퍼(wrapper) 식중함수(보통은 SAVESTR:10 으로 공통이지만, 전락 캐릭터만 뿔뿔이 흩어진다(バラバラになる))
@PRINT_THEME(ARG)
#FUNCTIONS
SIF ARG==0
	ARG=TARGET
RETURNF CSTR:ARG:60 != "" ? CSTR:ARG:60 # SAVESTR:10


;--------------------------------------------------
;캐릭터 목록 표시
;ARG=1이라면 CFLAG:0=0인 캐릭터(동료 상태로 살아 있는 캐릭터)에서 밖에 선택할 수 없다. ARG=2라면 모든 캐릭터를 표시한다. ARG=3이라면 일시 이탈한 캐릭터만 표시한다.
;ARG:1은 표시 설정
;ARG:1=1	생존 상태
;ARG:1=2	수련P
;ARG:2=1	취소 불가능
@CHARA_LIST, ARG, ARG:1, ARG:2 = 0
#DIM LCOUNT
PRINTL 누구를 선택하시겠습니까?
FOR LCOUNT, 0, CHARANUM
	SIF LCOUNT == MASTER
		CONTINUE
	SIF ARG != 2 && ARG != 3 && CFLAG:LCOUNT:999 == 0
		CONTINUE
	SIF ARG == 3 && CFLAG:LCOUNT:0 != 1 && CFLAG:LCOUNT:0 != 2 && CFLAG:LCOUNT:0 != 3 && CFLAG:LCOUNT:0 != 4
		CONTINUE

	IF (ARG == 0 && CFLAG:LCOUNT:0 != 9) || CFLAG:LCOUNT:0 == 0 || ARG == 2 || ARG == 3
		PRINTFORM [{LCOUNT}] %NAME:LCOUNT,28,LEFT%
		IF ARG:1 == 1
			SIF CFLAG:LCOUNT:0 == 1
				PRINT （유폐 중）
			SIF CFLAG:LCOUNT:0 == 2
				PRINT （세뇌）
			SIF CFLAG:LCOUNT:0 == 3
				PRINT （흑화）
			SIF CFLAG:LCOUNT:0 == 4
				PRINT （유괴 감금 중）
			;미실장, 대충 멘탈 다운된 플래그
			;SIF CFLAG:LCOUNT:0 == 5
			;	PRINT （히키코모리 중）
			SIF CFLAG:LCOUNT:0 == 9
				PRINT （거두어짐）
			SIF CFLAG:LCOUNT:0 == 10
				PRINT （출산에 대비하여 입원 중）
			SIF CFLAG:LCOUNT:0 == 11
				PRINT （육아 중）
		ENDIF
		SIF ARG:1 == 2
			PRINTFORM （수련P：{JUEL:LCOUNT:수련P}）
		PRINTL 
	ENDIF
NEXT
SIF ARG:2 == 0
	PRINTFORML [999] 캐릭터 선택을 그만둔다
$INPUT_LOOP_CHARA_LIST
INPUT
IF RESULT == 999 && ARG:2 == 0
	RETURN 999
ELSEIF RESULT < 1 || RESULT >= CHARANUM || ((ARG == 0 && CFLAG:RESULT:0 == 9) || (ARG == 1 && CFLAG:RESULT:0 != 0)) || (ARG != 2 && ARG != 3 && CFLAG:RESULT:999 == 0) || (ARG == 3 && CFLAG:RESULT:0 != 1 && CFLAG:RESULT:0 != 2 && CFLAG:RESULT:0 != 3 && CFLAG:RESULT:0 != 4)
	PRINTL 올바른 값을 입력해 주십시오.
	GOTO INPUT_LOOP_CHARA_LIST
ELSE
	RETURN RESULT
ENDIF
PRINTL 

;--------------------------------------------------
;캐릭터 이름 변경
@NAME_CHANGE ,ARG
PRINTL 
PRINTL 어떤 이름으로 하시겠습니까?
$INPUT_LOOP_NAME_CHANGE
INPUTS
IF RESULTS == ""
	GOTO INPUT_LOOP_NAME_CHANGE
ELSE
	CALLNAME:ARG = %RESULTS%
	PRINTFORML 「%RESULTS%」%조사만처리(RESULTS,"로")% 변경하였습니다.
ENDIF

;--------------------------------------------------
;전투 경험치算出
@GET_EXP_BATTLE
;잡몹적 싸움
IF (SAVESTR:13 == "MOB") || FLAG:73 > 0
	CALL TENTACLE_LEVEL
	LOCAL = 8 * (RESULT + 2)/ABL:레벨 + RAND(15)

	SIF LOCAL < 1
		LOCAL = 1

	;###공중 촬영 드론에 의한 상승
	SIF CFLAG:43 == 505
		TIMES LOCAL, 1.10

	CALL GET_EXP,min(LOCAL,75)
;보스전
ELSEIF (SAVESTR:13 == "BOSS")
	CALL TENTACLE_LEVEL
	LOCAL = 50 * (RESULT + 2)/ABL:레벨 + RAND(15) + 25

	SIF LOCAL < 10
		LOCAL = 10

	;###공중 촬영 드론에 의한 상승
	SIF CFLAG:43 == 505
		TIMES LOCAL, 1.10

	CALL GET_EXP,min(LOCAL,150)
;그 어느 것도 아닐 때
ELSE
	PRINTW ERROR NO EXP DATA
ENDIF

;--------------------------------------------------
;경험치 취득
@GET_EXP, ARG
;내성적
SIF TALENT:내성적 > 0
	TIMES ARG, 1.10
;제멋대로굼
SIF TALENT:제멋대로굼 > 0
	TIMES ARG, 1.10
JUEL:경험치 += ARG
PRINTFORML %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 경험치를 {ARG}％ 얻었다.
;레벨업 여부를 확인
CALL CHECK_LEVELUP

;--------------------------------------------------
;수련P 취득
@GET_SYUREN, ARG
JUEL:수련P += ARG
PRINTFORML %PALAMNAME:20%를 {ARG}P 획득하였다.

;--------------------------------------------------
;자금 취득
@GET_MONEY, ARG
MONEY += ARG
PRINTFORML {ARG}＄를 손에 넣었다.

;--------------------------------------------------
;촉수의 조각 취득
@GET_KAKERA, ARG
FLAG:200 += ARG
PRINTFORML 촉수의 조각 {ARG}개를 손에 넣었다.

;--------------------------------------------------
;처녀 상실 
;！！！！이 기능은 현재 전투 중에만 호출되고 있다　필수 통합
@LOSTVIRGIN, ARG:0 = 0
SIF TALENT:처녀 == -1
	RETURN
TALENT:처녀 = -1
SIF ARG:0 != 0
	ARG:0 = 1
CFLAG:206 = 2 + ARG:0
;지문: 전투 중 처녀 상실
CALL MESSAGE_SEX_LOSTVIRGIN
SIF (MESSAGE_BRANCH_F(TARGET) & 타락) == 0
	CALL SHINKYOU_CHANGE, "TOUSAKU"

;성(聖)처녀 상태(or처녀를 잃지 않는 옵션 ON)의 판정을 반환하는 식중함수, 처녀 상실하지 않은 상태라면 1
;ARG 캐릭터 번호, 생략 시 TARGET
@CHECK_HOLYVIRGIN_F,ARG=-1
#FUNCTION
SIF ARG==-1
	ARG=TARGET

IF TALENT:ARG:처녀 >=2 || (CONFIG_CHECK_OTHER_F(5) == 1 && TALENT:ARG:처녀 > 0)
	RETURNF 1
ELSE
	RETURNF 0
ENDIF

;--------------------------------------------------
;BASE값 산출
;ARGS는 계산 모드, ARG:0은 대상, ARG:1은 원가
@BASEUP, ARGS, ARG:0, ARG:1 

CALL SEIKAKU_CHECK, "GET_TALENT_VALUE", ARG:0
IF RESULT != 0
	SELECTCASE ARGS
		CASE "ALL"
			;성내성, 공격, 방어, 민첩의 원래 값은 1/10
			LOCAL:0= ARG:1 / 10

			;BASE 값의 계산 대상, 원래 값, 성격
			;체력 계산
			;상승값에 랜덤성 부여
			LOCAL:2 = BASEUP_CAL_RANDAM(ARG:1)
			CALL BASEUP_CAL_TAIRYOKU, ARG:0, LOCAL:2, RESULT
			;기력 계산
			LOCAL:3 = BASEUP_CAL_RANDAM(ARG:1)
			CALL BASEUP_CAL_KIRYOKU, ARG:0, LOCAL:3, RESULT
			;성내성 계산
			LOCAL:4 = BASEUP_CAL_RANDAM(ARG:1)
			CALL BASEUP_CAL_SEITAISEI, ARG:0, LOCAL:4, RESULT
			;공격 계산
			LOCAL:5 = BASEUP_CAL_RANDAM(ARG:1)
			CALL BASEUP_CAL_KOUGEKI, ARG:0, LOCAL:5, RESULT
			;방어 계산
			LOCAL:6 = BASEUP_CAL_RANDAM(ARG:1)
			CALL BASEUP_CAL_BOUGYO, ARG:0, LOCAL:6, RESULT
			;민첩 계산
			LOCAL:7 = BASEUP_CAL_RANDAM(ARG:1)
			CALL BASEUP_CAL_BINSYOU, ARG:0, LOCAL:7, RESULT
			;지성 계산
			LOCAL:8 = BASEUP_CAL_RANDAM(ARG:1)
			CALL BASEUP_CAL_CHISEI, ARG:0, LOCAL:8, RESULT

		CASE "TAIRYOKU"
			ARG:1 = BASEUP_CAL_RANDAM(ARG:1)
			;체력 계산
			CALL BASEUP_CAL_TAIRYOKU, ARG:0, ARG:1, RESULT
			SIF RESULT > 0
				PRINTFORML %BASENAME:0%의 기초값이 {RESULT} 올랐다.

		CASE "KIRYOKU"
			ARG:1 = BASEUP_CAL_RANDAM(ARG:1)
			;기력
			CALL BASEUP_CAL_KIRYOKU, ARG:0, ARG:1, RESULT
			SIF RESULT > 0
				PRINTFORML %BASENAME:1%의 기초값이 {RESULT} 올랐다.
			
		CASE "SEITAISEI"
			ARG:1 = BASEUP_CAL_RANDAM(ARG:1)
			;성내성 계산
			CALL BASEUP_CAL_SEITAISEI, ARG:0, ARG:1, RESULT
			SIF RESULT > 0
				PRINTFORML %BASENAME:2%의 기초값이 {RESULT} 올랐다.
			
		CASE "KOUGEKI"
			ARG:1 = BASEUP_CAL_RANDAM(ARG:1)
			;공격 계산
			CALL BASEUP_CAL_KOUGEKI, ARG:0, ARG:1, RESULT
			SIF RESULT > 0
				PRINTFORML %BASENAME:10%의 기초값이 {RESULT} 올랐다.
			
		CASE "BOUGYO"
			ARG:1 = BASEUP_CAL_RANDAM(ARG:1)
			;방어 계산
			CALL BASEUP_CAL_BOUGYO, ARG:0, ARG:1, RESULT
			SIF RESULT > 0
				PRINTFORML %BASENAME:11%의 기초값이 {RESULT} 올랐다.
			
		CASE "BINSYOU"
			ARG:1 = BASEUP_CAL_RANDAM(ARG:1)
			;민첩 계산
			CALL BASEUP_CAL_BINSYOU, ARG:0, ARG:1, RESULT
			SIF RESULT > 0
				PRINTFORML %BASENAME:12%의 기초값이 {RESULT} 올랐다.
		CASE "CHISEI"
			ARG:1 = BASEUP_CAL_RANDAM(ARG:1)
			;지성 계산
			CALL BASEUP_CAL_CHISEI, ARG:0, ARG:1, RESULT
			SIF RESULT > 0
				PRINTFORML %BASENAME:13%의 기초값이 {RESULT} 올랐다.
			
	ENDSELECT
	
	;스테이터스 재계산
	CALL LEVELSTATUS, ARG:0
	;부위 결계 설정
	CALL BASEUP_CAL_SHIELD, ARG:0
ENDIF

;--------------------------------------------------
;BASE값에 대한 랜덤성 부여에 관한 식중 함수
@BASEUP_CAL_RANDAM, ARG
#FUNCTION
LOCAL = RAND:100 + FLAG:50 * 5
SIF LOCAL < FLAG:50 * 10
	LOCAL = FLAG:50 * 10
IF LOCAL < 20
	LOCAL:1 = 70
ELSEIF LOCAL < 30
	LOCAL:1 = 75
ELSEIF LOCAL < 40
	LOCAL:1 = 80
ELSEIF LOCAL < 50
	LOCAL:1 = 85
ELSEIF LOCAL < 60
	LOCAL:1 = 90
ELSEIF LOCAL < 70
	LOCAL:1 = 95
ELSEIF LOCAL < 80
	LOCAL:1 = 100
ELSEIF LOCAL < 95
	LOCAL:1 = 105
ELSE
	LOCAL:1 = 110
ENDIF
SIF TALENT:회복빠름 > 0
	LOCAL:1 -= 5
SIF TALENT:회복느림 > 0
	LOCAL:1 += 5
ARG = ARG * LOCAL:1 / 100
RETURNF ARG

;--------------------------------------------------
;BASE 값 계산 상세 - 체력
;ARG:0은 대상, ARG:1은 원가, ARG:2는 성격
@BASEUP_CAL_TAIRYOKU, ARG:0, ARG:1, ARG:2
BASE:(ARG:0):기초체력 += SEIKAKU_HOSEI_F(ARG:2, 0, ARG:1)
RETURN SEIKAKU_HOSEI_F(ARG:2, 0, ARG:1)

;--------------------------------------------------
;BASE 값 계산 상세 - 기력
;ARG:0은 대상, ARG:1은 원가, ARG:2는 성격
@BASEUP_CAL_KIRYOKU, ARG:0, ARG:1, ARG:2
BASE:(ARG:0):기초기력 += SEIKAKU_HOSEI_F(ARG:2, 1, ARG:1)
RETURN SEIKAKU_HOSEI_F(ARG:2, 1, ARG:1)

;--------------------------------------------------
;BASE 값 계산 상세 - 성내성
;ARG:0은 대상, ARG:1은 원가, ARG:2는 성격
@BASEUP_CAL_SEITAISEI, ARG:0, ARG:1, ARG:2
BASE:(ARG:0):기초성내성 += SEIKAKU_HOSEI_F(ARG:2, 2, ARG:1)
RETURN SEIKAKU_HOSEI_F(ARG:2, 2, ARG:1)

;--------------------------------------------------
;BASE 값 계산 상세 - 공격
;ARG:0은 대상, ARG:1은 원가, ARG:2는 성격
@BASEUP_CAL_KOUGEKI, ARG:0, ARG:1, ARG:2
BASE:(ARG:0):공격 += SEIKAKU_HOSEI_F(ARG:2, 10, ARG:1)
RETURN SEIKAKU_HOSEI_F(ARG:2, 10, ARG:1)

;--------------------------------------------------
;BASE 값 계산 상세 - 방어
;ARG:0은 대상, ARG:1은 원가, ARG:2는 성격
@BASEUP_CAL_BOUGYO, ARG:0, ARG:1, ARG:2
BASE:(ARG:0):방어 += SEIKAKU_HOSEI_F(ARG:2, 11, ARG:1)
RETURN SEIKAKU_HOSEI_F(ARG:2, 11, ARG:1)

;--------------------------------------------------
;BASE 값 계산 상세 - 민첩
;ARG:0은 대상, ARG:1은 원가, ARG:2는 성격
@BASEUP_CAL_BINSYOU, ARG:0, ARG:1, ARG:2
BASE:(ARG:0):민첩 += SEIKAKU_HOSEI_F(ARG:2, 12, ARG:1)
RETURN SEIKAKU_HOSEI_F(ARG:2, 12, ARG:1)

;--------------------------------------------------
;BASE 값 계산 상세 - 지성
;ARG:0은 대상, ARG:1은 원가, ARG:2는 성격
@BASEUP_CAL_CHISEI, ARG:0, ARG:1, ARG:2
BASE:(ARG:0):지성 += SEIKAKU_HOSEI_F(ARG:2, 13, ARG:1)
RETURN SEIKAKU_HOSEI_F(ARG:2, 13, ARG:1)

;---------------------------------------------------------
;BASE값 계산 상세 - 부위 결계
@BASEUP_CAL_SHIELD, ARG
#DIM LCOUNT
#DIM UP_VAR
FOR LCOUNT, 0, 감각수
	IF TALENT:ARG:(LCOUNT + 190) > 0
		UP_VAR = MAX(CAL_SHIELD_F(ARG) - MAXBASE:ARG:(LCOUNT + 30),0)
		MAXBASE:ARG:(LCOUNT + 30) = CAL_SHIELD_F(ARG)
		SIF BASE:ARG:(LCOUNT + 30) > 0 || DAY == 0
			BASE:ARG:(LCOUNT + 30) = LIMIT(BASE:ARG:(LCOUNT + 30) + UP_VAR, 0, MAXBASE:ARG:(LCOUNT + 30))
	ENDIF
NEXT



;결계 내구 최대값 계산식
@CAL_SHIELD_F(ARG)
#FUNCTION
LOCAL = SQRT(MAXBASE:ARG:기력 * MAXBASE:ARG:성내성 * MAXBASE:ARG:방어 * MAXBASE:ARG:방어 / 160) / 2 + 1500 + MIN(ABL:ARG:레벨 * 1000, 50000)
RETURNF LOCAL

;모든 캐릭터의 부위 결계를 체크
@CHECK_SHIELD_ALL
#DIM LCOUNT
#DIM CCOUNT
FOR CCOUNT, 0, CHARANUM
	SIF CCOUNT == MASTER
		CONTINUE
	FOR LCOUNT, 0, 감각수
		SIF TALENT:CCOUNT:(LCOUNT + 190) == 0
			MAXBASE:CCOUNT:(LCOUNT + 30) = 0
		BASE:CCOUNT:(LCOUNT + 30) = LIMIT(BASE:CCOUNT:(LCOUNT + 30), 0,MAXBASE:CCOUNT:(LCOUNT + 30))
	NEXT
NEXT

;--------------------------------------------------
;전투 기술 LV 상승
;ARG:0은 대상, ARG:1은 근거리•중거리•원거리•기초전투
@SENGIUP, ARG:0, ARG:1

IF ARG:1 == 0
	LOCAL:0 = 30
	LOCAL:1 = 5
ELSEIF ARG:1 == 1
	LOCAL:0 = 31
	LOCAL:1 = 6
ELSEIF ARG:1 == 2
	LOCAL:0 = 32
	LOCAL:1 = 7
ELSEIF ARG:1 == 3
	LOCAL:0 = 33
	LOCAL:1 = 8
ENDIF

;레벨업에 필요한 경험수 확인
IF ABL:ARG:(LOCAL:0) == 0
	LOCAL:2 = 25
ELSEIF ABL:ARG:(LOCAL:0) == 1
	LOCAL:2 = 60
ELSEIF ABL:ARG:(LOCAL:0) == 2
	LOCAL:2 = 105
ELSEIF ABL:ARG:(LOCAL:0) == 3
	LOCAL:2 = 160
ELSEIF ABL:ARG:(LOCAL:0) == 4
	LOCAL:2 = 225
ELSEIF ABL:ARG:(LOCAL:0) == 5
	LOCAL:2 = 300
ELSEIF ABL:ARG:(LOCAL:0) == 6
	LOCAL:2 = 470
ELSEIF ABL:ARG:(LOCAL:0) == 7
	LOCAL:2 = 660
ELSEIF ABL:ARG:(LOCAL:0) == 8
	LOCAL:2 = 870
ENDIF

;소질에 따른 경험수 보정
;잘하는 거리가 있으면 다른 거리는 약간 불리해지고, 서투른 거리는 그 외 다른 나머지 거리가 약간 유리해진다.
SIF TALENT:ARG:근거리특기 == 1 && ARG:1 == 0 || TALENT:ARG:중거리특기 == 1 && ARG:1 == 1 || TALENT:ARG:원거리특기 == 1 && ARG:1 == 2
	TIMES LOCAL:2, 0.9
SIF TALENT:ARG:근거리서툼 == 1 && ARG:1 == 0 || TALENT:ARG:중거리서툼 == 1 && ARG:1 == 1 || TALENT:ARG:원거리서툼 == 1 && ARG:1 == 2
	TIMES LOCAL:2, 1.1
SIF TALENT:ARG:근거리특기 == 1 && ARG:1 != 0
	TIMES LOCAL:2, 1.05
SIF TALENT:ARG:중거리특기 == 1 && ARG:1 != 1
	TIMES LOCAL:2, 1.05
SIF TALENT:ARG:원거리특기 == 1 && ARG:1 != 2
	TIMES LOCAL:2, 1.05
SIF TALENT:ARG:근거리서툼 == 1 && ARG:1 != 0
	TIMES LOCAL:2, 0.95
SIF TALENT:ARG:중거리서툼 == 1 && ARG:1 != 1
	TIMES LOCAL:2, 0.95
SIF TALENT:ARG:원거리서툼 == 1 && ARG:1 != 2
	TIMES LOCAL:2, 0.95

;레벨업 가능 여부
IF EXP:ARG:(LOCAL:1) >= LOCAL:2 && ABL:ARG:(LOCAL:0) < 9
	ABL:ARG:(LOCAL:0) += 1
	PRINTFORML %PRINT_TRANSCALLNAME(ARG)%의 %ABLNAME:(LOCAL:0)%Lv이 올랐다.

	;전투 기초 Lv5로 변신 능력이 몸에 뱀
	IF TALENT:ARG:변신능력 == -1 && ABL:ARG:(LOCAL:0) >= 5 && LOCAL:0 == 33
		PRINTL 
		PRINTFORML %조사처리(PRINT_TRANSCALLNAME(ARG),"는")% 전투의 기초를 완전히 마스터했다!
		IF TALENT:ARG:변신능력획득 > 0
			PRINTW
			RESULT = 1
		ELSEIF TALENT:ARG:변신능력획득 < 0
			PRINTW
			RESULT = 0
		ELSE
			PRINTFORML %PRINT_TRANSCALLNAME(ARG)%에게 변신 능력을 설정하시겠습니까?
			PRINTL  [0] 아니오
			PRINTL  [1] 네
			$INPUT_LOOP_0
			INPUT
		ENDIF
		IF RESULT == 0
			TALENT:ARG:변신능력 = 0
			TALENT:ARG:변신시ＴＳ = 0
			PRINTL 
			PRINTFORML %PRINT_TRANSCALLNAME(ARG)%에게 변신 능력을 설정하지 않았습니다.
		ELSEIF RESULT == 1
			TALENT:ARG:변신능력 = 1

			;프로필 설정된 캐릭터가 아니면 상세한 사이즈는 생성하지 않도록 해둔다
			IF CFLAG:34
				CALL GENERATE_CHAR_SIZE, ARG:0, 1
				MAXBASE:ARG:신장 = RESULT:2
				MAXBASE:ARG:체중 = RESULT:3
				MAXBASE:ARG:가슴둘레 = RESULT:4
				MAXBASE:ARG:허리둘레 = RESULT:5
				MAXBASE:ARG:골반둘레 = RESULT:6
			ENDIF

			PRINTL 
			PRINTFORML %PRINT_TRANSCALLNAME(ARG)%에게 변신 능력을 설정하였습니다.
			PRINTFORML 변신 후 이름을 설정하겠습니까?
			PRINTL  [0] 아니오
			PRINTL  [1] 네
			$INPUT_LOOP_1
			INPUT
			IF RESULT == 0
				PRINTL 
			ELSEIF RESULT == 1
				PRINTL 
				CALL FIRSTSETTING_CHARA_TRANSAFTERNAME, ARG
				IF CSTR:ARG:0 != ""
					CFLAG:ARG:3 = 1
					CALL FIRSTSETTING_CHARA_TRANSAFTERCALLNAME, ARG
				ENDIF
			ELSE
				GOTO INPUT_LOOP_1
			ENDIF
		ELSE
			GOTO INPUT_LOOP_0
		ENDIF
	ENDIF

	PRINTL 
	;실적
	CALL GET_STATE_TROPHY,ARG
ENDIF

;--------------------------------------------------
;FEAT에 의한 스테이터스 보너스
@FEAT_BONUS_F, ARG, ARG:1
#FUNCTION
#DIM FEAT_BONUS,7
VARSET FEAT_BONUS
;0　체력
;1　마력
;2　성(聖)내성
;3　공격
;4　방어
;5　민첩
;6　지성
IF TALENT:ARG:고속회피 > 0
	FEAT_BONUS:3 -= 5
ENDIF
IF TALENT:ARG:신기소유자 > 0
	FEAT_BONUS:2 -= 10
	FEAT_BONUS:3 += 5
	FEAT_BONUS:4 += 5
	FEAT_BONUS:5 += 5
ENDIF
IF TALENT:ARG:투쟁본능 > 0
	FEAT_BONUS:2 -= 5
	FEAT_BONUS:3 += 10
ENDIF
IF TALENT:ARG:넘치는생명력 > 0
	FEAT_BONUS:6 -= 15
ENDIF
IF TALENT:ARG:정령교신 > 0
	FEAT_BONUS:3 -= 5
	FEAT_BONUS:4 -= 5
	FEAT_BONUS:5 -= 5
	FEAT_BONUS:6 += 10
ENDIF
IF TALENT:ARG:야수의후손 > 0
	FEAT_BONUS:6 -= 10
ENDIF
IF TALENT:ARG:밤악마의귀족 > 0
	FEAT_BONUS:0 += 100
	FEAT_BONUS:1 += 100
	FEAT_BONUS:2 += 10
	FEAT_BONUS:3 += 10
	FEAT_BONUS:4 += 10
	FEAT_BONUS:5 += 10
	FEAT_BONUS:6 += 10
ENDIF
IF TALENT:ARG:숨겨진힘 > 0
	FEAT_BONUS:3 -= 5
	FEAT_BONUS:4 -= 5
	FEAT_BONUS:5 -= 5
	FEAT_BONUS:6 -= 5
ENDIF
IF TALENT:ARG:행운아 > 0
	FEAT_BONUS:3 -= 5
	FEAT_BONUS:4 -= 5
	FEAT_BONUS:5 -= 5
	FEAT_BONUS:6 -= 5
ENDIF
IF TALENT:ARG:근력강화 > 0
	FEAT_BONUS:3 += 5
ENDIF
IF TALENT:ARG:날개 > 0
	FEAT_BONUS:4 += 5
ENDIF
IF TALENT:ARG:소형체구 > 0
	FEAT_BONUS:5 += 5
ENDIF
IF TALENT:ARG:지혜의극치 > 0
	FEAT_BONUS:0 -= 50
	FEAT_BONUS:1 -= 50
	FEAT_BONUS:2 -= 5
	FEAT_BONUS:6 += 10
ENDIF
IF TALENT:ARG:사냥꾼의직감 > 0
	FEAT_BONUS:6 -= 5
ENDIF
IF TALENT:ARG:심안 > 0
	FEAT_BONUS:4 -= 5
	FEAT_BONUS:5 -= 5
ENDIF
RETURNF FEAT_BONUS:(ARG:1)

;--------------------------------------------------
;스테이터스를 레벨로 배수한다
@LEVELSTATUS, ARG = 0
SIF ARG == 0
	ARG = TARGET
MAXBASE:ARG:체력 = LEVELSTATUS_UP(BASE:ARG:기초체력+FEAT_BONUS_F(ARG,0),ABL:ARG:레벨,400+(BASE:ARG:기초체력-1000)/5,99999)
MAXBASE:ARG:기력 = LEVELSTATUS_UP(BASE:ARG:기초기력+FEAT_BONUS_F(ARG,1),ABL:ARG:레벨,400+(BASE:ARG:기초기력-1000)/5,99999)
MAXBASE:ARG:성내성 = LEVELSTATUS_UP(BASE:ARG:기초성내성+FEAT_BONUS_F(ARG,2),ABL:ARG:레벨/3+1,10+(BASE:ARG:기초성내성-100)/2,9999)
MAXBASE:ARG:공격 = LEVELSTATUS_UP(BASE:ARG:공격+FEAT_BONUS_F(ARG,3),ABL:ARG:레벨,50,9999)
MAXBASE:ARG:방어 = LEVELSTATUS_UP(BASE:ARG:방어+FEAT_BONUS_F(ARG,4),ABL:ARG:레벨,50,9999)
MAXBASE:ARG:민첩 = LEVELSTATUS_UP(BASE:ARG:민첩+FEAT_BONUS_F(ARG,5),ABL:ARG:레벨,50,9999)
MAXBASE:ARG:지성 = LEVELSTATUS_UP(BASE:ARG:지성+FEAT_BONUS_F(ARG,6),ABL:ARG:레벨,50,9999)
CALL BASEUP_CAL_SHIELD, ARG

;--------------------------------------------------
;스테이터스의 레벨 배율 계산
;BASE의 값×레벨로 스테이터스를 산출한다　　　　
;식 ((√(220 * Lv) + 95) * BASE + 5.2 * Lv + 95) / 100
@LEVELSTATUS_UP, ARG:0, ARG:1, ARG:2,ARG:3 = 0
#FUNCTION
LOCAL:0 = SQRT(220 * ARG:1)

LOCAL:1 = 52 * ARG:1
TIMES LOCAL:1, 0.1

LOCAL:2 = (LOCAL:1 + 95) * ARG:2
LOCAL:2 += (LOCAL:0 + 95) * ARG:0
LOCAL:2 /= 100

SIF LOCAL:2 > ARG:3 && ARG:3
	LOCAL:2 = ARG:3
SIF LOCAL:2 < 1
	LOCAL:2 = 1

RETURNF LOCAL:2

;--------------------------------------------------
;레벨업 체크
@CHECK_LEVELUP
LOCAL = 0
WHILE JUEL:경험치 > 100 && ABL:레벨 < 998
	JUEL:경험치 -= 100
	LOCAL += 1
WEND
IF LOCAL > 0
	ABL:레벨 += LOCAL
	PRINTFORML %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 레벨이 {LOCAL} 올랐다!
	CALL LEVELSTATUS,TARGET
	;실적
	CALL GET_STATE_TROPHY,TARGET
ENDIF
SIF ABL:레벨 >= 999
	JUEL:경험치 = 0


;--------------------------------------------------
;소비 체력에서 행동 가능 여부를 반환한다. 가능하면 1, 불가능하면 0
;ARG	행동의 종류
;대상	행동자가 누구인가
;RESULT	행동 가능이라면 0, bit로 행동 불가능한 이유가 반환된다
@CHECK_ACTION(ARG, 대상)
#DIM 대상
#DIM CONST bit체력 = 0
#DIM CONST bit밤악마 = 1
#DIM CONST bit전사 = 2
#DIM CONST bit지원무효 = 3
#DIM CONST bit임신 = 4
#DIM CONST bit결손 = 5
#DIM CONST bit폐인 = 6;번식자루
LOCAL = 0
;체력 500 이하는 행동불가
SIF GROUPMATCH(ARG, 예정_출격, 예정_단련, 예정_활동, 예정_방위, 예정_지원, 예정_정보) && BASE:대상:체력 <= 강제휴식체력
	SETBIT LOCAL, bit체력
;밤악마의귀족은 낮 일부 활동 불가
SIF GROUPMATCH(ARG, 예정_출격, 예정_활동, 예정_방위, 예정_정보, 예정_자유) && (TALENT:대상:밤악마의귀족 && TIME == 0)
	SETBIT LOCAL, bit밤악마

;사지결손 시에는 휴식과 지원 이외 불가
SIF GROUPMATCH(ARG, 예정_출격, 예정_활동, 예정_방위, 예정_정보, 예정_단련) && (TALENT:대상:사지결손 > 0 || TALENT:대상:번식자루 > 0)
	SETBIT LOCAL, bit결손
;(아마도 마음이)부서져 폐인화됐다면 휴식 이외 불가
SIF !GROUPMATCH(ARG, 예정_휴식) && (TALENT:대상:번식자루 > 0)
	SETBIT LOCAL, bit폐인

;타고난전사는 일부 활동 불가
SIF GROUPMATCH(ARG, 예정_활동, 예정_지원) && TALENT:대상:타고난전사
	SETBIT LOCAL, bit전사
;단련의 체력 감소량이 현재 체력 이상이면 불가
IF ARG == 예정_단련
	CALL TRAINING_DOWNTAIRYOKU(대상)
	LOCAL:1 = RESULT
	SIF BASE:대상:체력 < LOCAL:1
		SETBIT LOCAL, bit체력
ENDIF
;지원 무효라면 불가
SIF ARG == 예정_지원 && GETBATTLESITUATION("지원 무효")
	SETBIT LOCAL, bit지원무효
;임신(발각됨) 중이라면 일부 활동 불가
SIF GROUPMATCH(ARG, 예정_출격, 예정_방위) && GROUPMATCH(TALENT:대상:임신, 1, 3, 5)
	SETBIT LOCAL, bit임신
RETURN LOCAL

;ARG	TARGET
;ARG:1	CHECK_ACTION이 반환한 NG 이유 bit
;ARGS	취하려던 행동 문자열
@PRINT_NGACTION_F(ARG,ARG:1,ARGS)
#FUNCTIONS
IF GETBIT(ARG:1, 6)
	RESULTS = %조사처리(CALLNAME:ARG,"는")% 폐인화되어 있기 때문에, 휴식 이외에는 아무것도 할 수 없습니다.
ELSEIF GETBIT(ARG:1, 5)
	RESULTS = %조사처리(CALLNAME:ARG,"는")% 사지결손된 상태이기 때문에, %ARGS%할 수 없습니다.
ELSEIF GETBIT(ARG:1, 4)
	RESULTS = %조사처리(CALLNAME:ARG,"는")% 임신 중이기 때문에, 휴식을 취합니다.
ELSEIF GETBIT(ARG:1, 1)
	RESULTS = %조사처리(CALLNAME:ARG,"는")% %ARGS%하지 못\@GETBIT(RESULT, 0) ? 하고, 체력이 얼마 남지 않았기 # 하기 \@때문에, 휴식을 취합니다.
ELSE
	RESULTS = %조사처리(CALLNAME:ARG,"는")% 체력이 얼마 남지 않았기 때문에, 휴식을 취합니다.
ENDIF
RETURNF RESULTS



;--------------------------------------------------
;BASE 소비값에 대한 기초값 보정
;ARG = 참조하는 캐릭터
;ARG:1 = 소비량
;ARG:2 = 체력인지 기력인지
@SYOUHI_KEIGEN,ARG,ARG:1,ARG:2
#FUNCTION
LOCAL = ARG:1
ARG:2 += 50
IF ARG:2 == 50 || ARG:2 == 51
	LOCAL:1 = 1000
ELSE
	LOCAL:1 = 100
ENDIF
IF BASE:ARG:(ARG:2) > LOCAL:1
	LOCAL:2 = (BASE:ARG:(ARG:2) - LOCAL:1)
	LOCAL = MAX(LOCAL - (LOCAL * LOCAL:2 / 2500),LOCAL / 2)
ENDIF
RETURNF LOCAL



;--------------------------------------------------
;CSV참조계의 식중 함수판
@CSVBASE_F,ARG,ARG:1
#FUNCTION
CSVBASE ARG,ARG:1
RETURNF RESULT

@CSVCSTR_F,ARG,ARG:1
#FUNCTIONS
CSVCSTR ARG,ARG:1
RETURNF RESULTS

@CSVABL_F,ARG,ARG:1
#FUNCTION
CSVABL ARG,ARG:1
RETURNF RESULT

@CSVTALENT_F,ARG,ARG:1
#FUNCTION
CSVTALENT ARG,ARG:1
RETURNF RESULT

@CSVMARK_F,ARG,ARG:1
#FUNCTION
CSVMARK ARG,ARG:1
RETURNF RESULT

@CSVEXP_F,ARG,ARG:1
#FUNCTION
CSVEXP ARG,ARG:1
RETURNF RESULT

@CSVJUEL_F,ARG,ARG:1
#FUNCTION
CSVJUEL ARG,ARG:1
RETURNF RESULT

@CSVEQUIP_F,ARG,ARG:1
#FUNCTION
CSVEQUIP ARG,ARG:1
RETURNF RESULT

@CSVCFLAG_F,ARG,ARG:1
#FUNCTION
CSVCFLAG ARG,ARG:1
RETURNF RESULT



;--------------------------------------------------
;PALAM LV를 반환하는 식중 함수
@PALAMLV_F, ARG
#FUNCTION

IF PALAM:LOCAL < 100
	LOCAL = 0 
ELSEIF PALAM:LOCAL < 300
	LOCAL = 1 
ELSEIF PALAM:LOCAL < 600
	LOCAL = 2 
ELSEIF PALAM:LOCAL < 1500
	LOCAL = 3 
ELSEIF PALAM:LOCAL < 3000
	LOCAL = 4 
ELSEIF PALAM:LOCAL < 6000
	LOCAL = 5 
ELSEIF PALAM:LOCAL < 10000
	LOCAL = 6 
ELSEIF PALAM:LOCAL < 30000
	LOCAL = 7 
ELSEIF PALAM:LOCAL < 60000
	LOCAL = 8 
ELSEIF PALAM:LOCAL < 150000
	LOCAL = 9 
ELSEIF PALAM:LOCAL < 300000
	LOCAL = 10 
ELSE
	LOCAL = 11
ENDIF

RETURNF LOCAL




;--------------------------------------------------
;탐색도 상승
@RESEARCH_PROGRESS, ARG
SIF FLAG:47 < FLAG:46 && FLAG:47 + ARG >= FLAG:46
	PRINTW 보스 촉수의 거처를 특정하였다!
FLAG:47 += ARG
RETURN




;--------------------------------------------------
;고유 번호 검색
@CHARAID_F, ARG
#FUNCTION
FOR LOCAL, 0, CHARANUM
	SIF LOCAL == MASTER
		CONTINUE
	SIF CFLAG:LOCAL:240 == ARG
		RETURNF LOCAL
NEXT
RETURNF 0



;-------------------------------------------------
;소질치 취득
;ARG   대상 캐릭터
;ARG:1 변신 상태 0=통상 1=변신 시
;ARGS  소질명
@CHARATALENT_F, ARG, ARG:1, ARGS
#FUNCTION
VARSET LOCALS
;변신 시
IF CFLAG:ARG:1 > 0
	IF ARG:1 > 0
		IF ARGS == "왜소함" || ARGS == "장신"
			IF TALENT:ARG:왜소함 > 0
				LOCALS = 왜소함
			ELSEIF TALENT:ARG:장신 > 0
				LOCALS = 장신
			ENDIF
		ELSEIF ARGS == "절벽" || ARGS == "빈유" || ARGS == "거유" || ARGS == "폭유" || ARGS == "초유" || ARGS == "마유" || ARGS == "기유"
			IF TALENT:ARG:빈유 == 2
				LOCALS = 절벽
			ELSEIF TALENT:ARG:빈유 == 1
				LOCALS = 빈유
			ELSEIF TALENT:ARG:거유 == 5
				LOCALS = 기유
			ELSEIF TALENT:ARG:거유 == 4
				LOCALS = 마유
			ELSEIF TALENT:ARG:거유 == 3
				LOCALS = 초유
			ELSEIF TALENT:ARG:거유 == 2
				LOCALS = 폭유
			ELSEIF TALENT:ARG:거유 == 1
				LOCALS = 거유
			ENDIF
		ELSEIF ARGS == "남자"
			SIF ISMALE(ARG)
				LOCALS = 남자
		ELSEIF ARGS == "오토코노코"
			SIF TALENT:ARG:오토코노코 > 0
				LOCALS = 오토코노코
		ELSEIF ARGS == "후타나리"
			SIF TALENT:ARG:후타나리 > 0
				LOCALS = 후타나리
		ELSEIF ARGS == "순산형" || ARGS == "포동포동" || ARGS == "유아배" || ARGS == "날씬함" || ARGS == "빵뎅이" || ARGS == "왕뎅이"
			IF TALENT:ARG:변신시외관 == 1
				LOCALS = 순산형
			ELSEIF TALENT:ARG:변신시외관 == 2
				LOCALS = 포동포동
			ELSEIF TALENT:ARG:변신시외관 == 3
				LOCALS = 유아배
			ELSEIF TALENT:ARG:변신시외관 == 4
				LOCALS = 날씬함
			ELSEIF TALENT:ARG:변신시외관 == 5
				LOCALS = 빵뎅이
			ELSEIF TALENT:ARG:변신시외관 == 6
				LOCALS = 왕뎅이
			ENDIF
		ENDIF
	ELSE
		IF ARGS == "왜소함" || ARGS == "장신"
			LOCAL = TALENT:ARG:장신 - TALENT:ARG:왜소함 - TALENT:ARG:변신시체격변동
			IF LOCAL < 0
				LOCALS = 왜소함
			ELSEIF LOCAL > 0
				LOCALS = 장신
			ELSEIF TALENT:ARG:변신시체격변동 == 0
				IF TALENT:ARG:왜소함 > 0
					LOCALS = 왜소함
				ELSEIF TALENT:ARG:장신 > 0
					LOCALS = 장신
				ENDIF
			ENDIF
		ELSEIF ARGS == "절벽" || ARGS == "빈유" || ARGS == "거유" || ARGS == "폭유" || ARGS == "초유" || ARGS == "마유" || ARGS == "기유"
			LOCAL = TALENT:ARG:거유 - TALENT:ARG:빈유 - TALENT:ARG:변신시가슴사이즈변동
			IF LOCAL < -1
				LOCALS = 절벽
			ELSEIF LOCAL < 0
				LOCALS = 빈유
			ELSEIF LOCAL > 4
				LOCALS = 기유
			ELSEIF LOCAL > 3
				LOCALS = 마유
			ELSEIF LOCAL > 2
				LOCALS = 초유
			ELSEIF LOCAL > 1
				LOCALS = 폭유
			ELSEIF LOCAL > 0
				LOCALS = 거유
			ELSEIF TALENT:ARG:변신시가슴사이즈변동 == 0
				IF TALENT:ARG:빈유 == 2
					LOCALS = 절벽
				ELSEIF TALENT:ARG:빈유 == 1
					LOCALS = 빈유
				ELSEIF TALENT:ARG:거유 == 5
					LOCALS = 기유
				ELSEIF TALENT:ARG:거유 == 4
					LOCALS = 마유
				ELSEIF TALENT:ARG:거유 == 3
					LOCALS = 초유
				ELSEIF TALENT:ARG:거유 == 2
					LOCALS = 폭유
				ELSEIF TALENT:ARG:거유 == 1
					LOCALS = 거유
				ENDIF
			ENDIF
		ELSEIF ARGS == "남자"
			IF ISFEMALE(ARG)
				SIF TALENT:ARG:변신시ＴＳ > 0
					LOCALS = 남자
			ELSEIF TALENT:ARG:변신시ＴＳ == 0
					LOCALS = 남자
			ENDIF
		ELSEIF ARGS == "오토코노코"
			SIF TALENT:ARG:변신시오토코노코 > 0
				LOCALS = 오토코노코
		ELSEIF ARGS == "후타나리"
			SIF TALENT:ARG:변신시후타나리 > 0
				LOCALS = 후타나리
		ELSEIF ARGS == "순산형" || ARGS == "포동포동" || ARGS == "유아배" || ARGS == "날씬함" || ARGS == "빵뎅이" || ARGS == "왕뎅이"
			IF TALENT:ARG:외관 == 1
				LOCALS = 순산형
			ELSEIF TALENT:ARG:외관 == 2
				LOCALS = 포동포동
			ELSEIF TALENT:ARG:외관 == 3
				LOCALS = 유아배
			ELSEIF TALENT:ARG:외관 == 4
				LOCALS = 날씬함
			ELSEIF TALENT:ARG:외관 == 5
				LOCALS = 빵뎅이
			ELSEIF TALENT:ARG:외관 == 6
				LOCALS = 왕뎅이
			ENDIF
		ENDIF
	ENDIF
;평상시
ELSE
	IF ARG:1 > 0
		IF ARGS == "왜소함" || ARGS == "장신"
			LOCAL = TALENT:ARG:장신 - TALENT:ARG:왜소함 + TALENT:ARG:변신시체격변동
			IF LOCAL < 0
				LOCALS = 왜소함
			ELSEIF LOCAL > 0
				LOCALS = 장신
			ENDIF
		ELSEIF ARGS == "절벽" || ARGS == "빈유" || ARGS == "거유" || ARGS == "폭유" || ARGS == "초유" || ARGS == "마유" || ARGS == "기유"
			LOCAL = TALENT:ARG:거유 - TALENT:ARG:빈유 + TALENT:ARG:변신시가슴사이즈변동
			IF ISMALE(ARG)
				SIF TALENT:ARG:변신시ＴＳ == 0
					LOCAL = 0
			ELSEIF TALENT:ARG:변신시ＴＳ > 0
					LOCAL = 0
			ENDIF
			IF LOCAL < -1
				LOCALS = 절벽
			ELSEIF LOCAL < 0
				LOCALS = 빈유
			ELSEIF LOCAL > 4
				LOCALS = 기유
			ELSEIF LOCAL > 3
				LOCALS = 마유
			ELSEIF LOCAL > 2
				LOCALS = 초유
			ELSEIF LOCAL > 1
				LOCALS = 폭유
			ELSEIF LOCAL > 0
				LOCALS = 거유
			ENDIF
		ELSEIF ARGS == "남자"
			IF ISMALE(ARG)
				SIF TALENT:ARG:변신시ＴＳ == 0
					LOCALS = 남자
			ELSEIF TALENT:ARG:변신시ＴＳ > 0
					LOCALS = 남자
			ENDIF
		ELSEIF ARGS == "오토코노코"
			SIF TALENT:ARG:변신시오토코노코 > 0
				LOCALS = 오토코노코
		ELSEIF ARGS == "후타나리"
			SIF TALENT:ARG:변신시후타나리 > 0
				LOCALS = 후타나리
		ELSEIF ARGS == "순산형" || ARGS == "포동포동" || ARGS == "유아배" || ARGS == "날씬함" || ARGS == "빵뎅이" || ARGS == "왕뎅이"
			IF TALENT:ARG:변신시외관 == 1
				LOCALS = 순산형
			ELSEIF TALENT:ARG:변신시외관 == 2
				LOCALS = 포동포동
			ELSEIF TALENT:ARG:변신시외관 == 3
				LOCALS = 유아배
			ELSEIF TALENT:ARG:변신시외관 == 4
				LOCALS = 날씬함
			ELSEIF TALENT:ARG:변신시외관 == 5
				LOCALS = 빵뎅이
			ELSEIF TALENT:ARG:변신시외관 == 6
				LOCALS = 왕뎅이
			ENDIF
		ENDIF
	ELSE
		IF ARGS == "왜소함" || ARGS == "장신"
			IF TALENT:ARG:왜소함 > 0
				LOCALS = 왜소함
			ELSEIF TALENT:ARG:장신 > 0
				LOCALS = 장신
			ENDIF
		ELSEIF ARGS == "절벽" || ARGS == "빈유" || ARGS == "거유" || ARGS == "폭유" || ARGS == "초유" || ARGS == "마유" || ARGS == "기유"
			IF TALENT:ARG:빈유 == 2
				LOCALS = 절벽
			ELSEIF TALENT:ARG:빈유 == 1
				LOCALS = 빈유
			ELSEIF TALENT:ARG:거유 == 5
				LOCALS = 기유
			ELSEIF TALENT:ARG:거유 == 4
				LOCALS = 마유
			ELSEIF TALENT:ARG:거유 == 3
				LOCALS = 초유
			ELSEIF TALENT:ARG:거유 == 2
				LOCALS = 폭유
			ELSEIF TALENT:ARG:거유 == 1
				LOCALS = 거유
			ENDIF
		ELSEIF ARGS == "남자"
			SIF ISMALE(ARG)
				LOCALS = 남자
		ELSEIF ARGS == "오토코노코"
			SIF TALENT:ARG:오토코노코 > 0
				LOCALS = 오토코노코
		ELSEIF ARGS == "후타나리"
			SIF TALENT:ARG:후타나리 > 0
				LOCALS = 후타나리
		ELSEIF ARGS == "순산형" || ARGS == "포동포동" || ARGS == "유아배" || ARGS == "날씬함" || ARGS == "빵뎅이" || ARGS == "왕뎅이"
			IF TALENT:ARG:외관 == 1
				LOCALS = 순산형
			ELSEIF TALENT:ARG:외관 == 2
				LOCALS = 포동포동
			ELSEIF TALENT:ARG:외관 == 3
				LOCALS = 유아배
			ELSEIF TALENT:ARG:외관 == 4
				LOCALS = 날씬함
			ELSEIF TALENT:ARG:외관 == 5
				LOCALS = 빵뎅이
			ELSEIF TALENT:ARG:외관 == 6
				LOCALS = 왕뎅이
			ENDIF
		ENDIF
	ENDIF
ENDIF
IF (LOCALS == ARGS) > 0
	RETURNF 1
ELSE
	RETURNF 0
ENDIF


;-------------------------------------------------
;소질차 판정
;변신 전과 변신 후의 소질에 차이가 없으면 0을 돌려준다
@TALENT_CHANGE_F, ARG
#FUNCTION
SIF TALENT:ARG:변신시체격변동 != 0
	RETURNF 1
SIF TALENT:ARG:변신시가슴사이즈변동 != 0
	RETURNF 1
SIF TALENT:ARG:변신시ＴＳ != 0
	RETURNF 1
SIF TALENT:ARG:외관 != TALENT:ARG:변신시외관
	RETURNF 1
RETURNF 0





;-------------------------------------------------
;시간 경과 표현의 간략화
;ARG가 1 또는 생략이면 길고, 2라면 짧음. 컨피그에서 클릭수 삭감 설정 가능
@DOT_AFTER, ARG = 1
IF ARG == 1
	IF CONFIG_CHECK_SCREEN_F(3) == 0
		PRINTW ・・
		PRINTW ・・・・
	ELSE
		PRINTL ・・
		PRINTL ・・・・
	ENDIF
	PRINTW ・・・・・・
ELSEIF ARG == 2
	IF CONFIG_CHECK_SCREEN_F(3) == 0
		PRINTW ・
		PRINTW ・・
	ELSE
		PRINTL ・
		PRINTL ・・
	ENDIF
	PRINTW ・・・
ENDIF
;받은 RESULT를 그대로 돌려준다
RETURN RESULT


;0=미출현　1=K촉수 출현　2～=천사의 나무:제(N-2) 형태
@GET_LASTBOSS_PHASE_F()
#FUNCTION
;최종 보스(K)
SIF FLAG:101 == 1
	RETURNF 1
;히든 보스 제ｎ 형태
SIF FLAG:101 == 2
	RETURNF 2+FLAG:21
RETURNF 0

;현재 전투 중인 적의 종별이 일치하는지 체크
@ENEMY_TYPE_CHECK_F(ARGS)
#FUNCTION
;보스 촉수
IF ARGS=="BOSS"
	RETURNF (FLAG:110 == 0 && FLAG:73 == 0  && FLAG:10 == 0) ? 1 # 0
;최종 보스/히든 보스
ELSEIF ARGS=="LASTBOSS"
	;K촉수는 １ 히든 보스는 ２＋형태 변화 횟수(GET_LASTBOSS_PHASE_F()취급)
	RETURNF (FLAG:110 == 0 && FLAG:73 == 0  && FLAG:10 == 1) ? (FLAG:101 == 1 ? 1 # 2+FLAG:21) # 0
ELSEIF ARGS=="MOB"
	RETURNF (FLAG:110 == 0 && FLAG:73 == 0 && FLAG:10 == 2) ? 1 # 0
ELSEIF ARGS=="CITIZEN"
	RETURNF (FLAG:110 == 0 && FLAG:73 > 0) ? 1 # 0
ELSEIF ARGS=="AKUOTI"
	RETURNF (FLAG:110 > 0) ? 1 # 0
ENDIF
RETURNF -1

;이른바 유사 MATCH 함수 (FIND하여 참이라면 MATCH 위치 이후의 문자열을 잘라내어 반환한다)
;　RESULTS:0　MATCH 문자열
;　RESULTS:1　MATCH 문자열 이전의 문자열
;　RESULTS:2　MATCH 문자열 이후의 문자열
@STRMATCH(ARGS:0,ARGS:1)
	STRFINDU ARGS:0,ARGS:1
	IF RESULT>-1
		;MATCH 부분 이전
		SUBSTRINGU ARGS:0,0,RESULT
		RESULTS:1 '= RESULTS
		;MATCH 부분 이후
		SUBSTRINGU ARGS:0,RESULT+STRLENSU(ARGS:1)
		RESULTS:2 '= RESULTS
		;MATCH 부분
		SUBSTRINGU ARGS:0,RESULT,STRLENSU(ARGS:1)		

	ELSE
		RESULTS:0 '= ""
		RESULTS:1 '= ""
	ENDIF
RETURN
;식중함수판(MATCH 문자열만 반환한다)
@STRMATCH_F(ARGS:0,ARGS:1)
#FUNCTIONS
	STRFINDU ARGS:0,ARGS:1
	IF RESULT>-1
		;MATCH 부분
		SUBSTRINGU ARGS:0,RESULT,STRLENSU(ARGS:1)
	ELSE
		RESULTS:0 '= ""
	ENDIF
RETURNF RESULTS:0
