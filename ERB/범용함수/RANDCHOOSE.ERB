;-------------------------------------------------------------
;랜덤 추첨에 후보 ARG를 추가한다
@ADDRANDCHOOSE(ARG)
#DIM HOGE
;음수는 후보로 삼을 수 없다
IF ARG < 0
	PRINTW ERROR 후보로 음수를 지정할 수 없습니다.
	RETURN
ENDIF
RANDCHOOSE_NUM:0 += 1
HOGE = RANDCHOOSE_NUM:0
$LOOP
IF RANDCHOOSE_NUM:HOGE == 0
	RANDCHOOSE_NUM:HOGE = ARG + 1
ELSEIF HOGE < RANDCHOOSE_MAX
	HOGE ++
	GOTO LOOP
ELSE
	PRINTW ERROR ADDRANDCHOOSE 의 후보가 너무 많습니다.
ENDIF
RANDCHOOSE_NUM:0 = HOGE


;-------------------------------------------------------------
;랜덤 추첨 결과를 RESULT에 돌려준다
@RANDCHOOSE
LOCAL = RAND:(RANDCHOOSE_NUM:0)
LOCAL += 1
RESULT = RANDCHOOSE_NUM:LOCAL - 1
RETURN RESULT


;-------------------------------------------------------------
;랜덤 추첨 결과를 반환하는 식중 함수
@RANDCHOOSE_F()
#FUNCTION
LOCAL = RAND:(RANDCHOOSE_NUM:0)
LOCAL += 1
LOCAL:1 = RANDCHOOSE_NUM:LOCAL - 1
RETURNF LOCAL:1


;-------------------------------------------------------------
;지정한 후보를 클리어  (ARG = 0으로 모두 클리어)
@CLEARRANDCHOOSE(ARG = 0)
IF ARG <= -1
	ARRAYSHIFT RANDCHOOSE_NUM,-1,0,RANDCHOOSE_NUM:0
	RANDCHOOSE_NUM:0 -= 1
ELSEIF ARG == 0
	VARSET RANDCHOOSE_NUM
ELSEIF ARG != 0 && ARG <= RANDCHOOSE_MAX - 1
	ARRAYSHIFT RANDCHOOSE_NUM,-1,0,ARG
	RANDCHOOSE_NUM:0 -= 1
ENDIF

;-------------------------------------------------------------
;특정 값을 가진 후보를 모두 클리어
@CLEARSPECIFICCHOOSE(ARG)
LOCAL:1 = 0
FOR LOCAL,1,RANDCHOOSE_NUM:0 + 1
	IF RANDCHOOSE_NUM:LOCAL == (ARG + 1)
		ARRAYREMOVE RANDCHOOSE_NUM, LOCAL, 1
		LOCAL -= 1
		LOCAL:1 += 1
	ENDIF
NEXT
RANDCHOOSE_NUM:0 -= LOCAL:1

;-------------------------------------------------------------
;후보의 수를 반환하는 식중 함수
@CHOICECOUNT_F()
#FUNCTION
LOCAL = RANDCHOOSE_NUM:0
RETURNF LOCAL


















