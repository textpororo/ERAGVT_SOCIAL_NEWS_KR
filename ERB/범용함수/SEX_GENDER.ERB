;●性別周りの判定を整理
;	・生物学的オスorメス			ISMALE(id)  ISFEMALE(id)
;	・외관上の男性or女性(오토코노코用)		ISMANLY(id) ISGIRLY(id)
;	・ちんまん有無チェック(후타나리用)	ISPENIS(id) ISVAGINA(id)

;-------------------------------------------------
;「性器の有無を判定(후타나리等も考慮)」
;ペニス判定　ARG番目のキャラがペニスを持っていれば1を返す
@ISPENIS, ARG = -999
#FUNCTION
SIF ARG == -999
	ARG = TARGET
SIF ISMALE(ARG) || TALENT:ARG:후타나리 > 0
	RETURNF 1
RETURNF 0
;ヴァギナ判定　ARG番目のキャラがヴァギナを持っていれば1を返す
@ISVAGINA, ARG = -999
#FUNCTION
SIF ARG == -999
	ARG = TARGET
SIF ISFEMALE() || TALENT:ARG:후타나리 > 0
	RETURNF 1
RETURNF 0

;-------------------------------------------------
;「女に見える외관」
;周囲が女の子扱いする判定(지문용)　女性または오토코노코なら１が返る
@ISGIRLY(ARG=-1)
#FUNCTION
	LOCAL=ARG==-1 ? TARGET # ARG
	IF ISFEMALE(LOCAL) || TALENT:LOCAL:오토코노코
		LOCAL=1
	ELSE
		LOCAL=0
	ENDIF
RETURNF LOCAL
;「男らしい외관の남자」
;周囲が남자扱いする判定(지문용、↑の逆)　남자かつ오토코노코でなければ１が返る
;※なお、現行GVTはガチで男に見える男装＝男の娘の逆パターンに非対応です
;　(支払う分岐処理の複雑さに対して需要が薄いと考えられるため)
@ISMANLY(ARG=-1)
#FUNCTION
	LOCAL=ARG==-1 ? TARGET # ARG
	IF ISFEMALE(LOCAL) || TALENT:LOCAL:오토코노코
		LOCAL=0
	ELSE
		LOCAL=1
	ENDIF
RETURNF LOCAL
;「凌辱対象(穴)となり得る」
;通常のISGIRLY() ＋ 양성평등オプションOnなら無条件に１が返る
@ISHOLE(ARG=-1)
#FUNCTION
	LOCAL=ARG==-1 ? TARGET # ARG
	SIF ARG == 0
		RETURNF 1
	;♀、오토코노코、양성평등オプションなら1
	;※※※※※※※※※※※※※※※※※※※※※※※※
	;ここうっかりISHOLE()でラップしないように注意！！
	;※※※※※※※※※※※※※※※※※※※※※※※※
	IF ISGIRLY(LOCAL) || CONFIG_CHECK_MANIAC_F(5) == 1
		LOCAL = 1
	ELSE
		LOCAL = 0
	ENDIF
RETURNF LOCAL


;-------------------------------------------------
;現在の変身状態と無関係に、特定の変身状態における外見を確認
;「변신능력を持ち、変身後の姿がGIRLYであるキャラ」なら１
;①通常変身する♀
;②TS変身する♂
;③男の娘に変身するキャラ
@ISGIRLY_AfterTrans(ARG=-1)
#FUNCTION
	LOCAL=ARG==-1 ? TARGET # ARG
	;未変身時に呼ばれた場合
	IF CFLAG:1==0
		IF TALENT:LOCAL:변신능력 == 1 && ((ISFEMALE(LOCAL) && TALENT:LOCAL:변신시ＴＳ == 0) || (ISMALE(LOCAL) && TALENT:LOCAL:변신시ＴＳ == 1) || (TALENT:LOCAL:변신시오토코노코 == 1))
			LOCAL=1
		ELSE
			LOCAL=0
		ENDIF
	;変身時に呼ばれた場合は現在の外見判定でよい
	ELSE
		IF ISGIRLY()
			LOCAL=1
		ELSE
			LOCAL=0
		ENDIF
	ENDIF
RETURNF LOCAL
;「변신능력を持ち、変身後の姿がMANLYであるキャラ」なら１
;①通常変身する♂
;②TS変身する♀
@ISMANLY_AfterTrans(ARG=-1)
#FUNCTION
	LOCAL=ARG==-1 ? TARGET # ARG
	;未変身時に呼ばれた場合
	IF CFLAG:1==0
		IF TALENT:LOCAL:변신능력 == 1 && ((ISMALE(LOCAL) && TALENT:LOCAL:변신시ＴＳ == 0) || (ISFEMALE(LOCAL) && TALENT:LOCAL:변신시ＴＳ == 1))
			LOCAL=1
		ELSE
			LOCAL=0
		ENDIF
	;変身時に呼ばれた場合は現在の外見判定でよい
	ELSE
		IF ISMANLY()
			LOCAL=1
		ELSE
			LOCAL=0
		ENDIF
	ENDIF
RETURNF LOCAL


;-------------------------------------------------
;判定時点での物理的な性(SEX)
;♀メス
@ISFEMALE(ARG=-1)
#FUNCTION
	LOCAL=ARG==-1 ? TARGET # ARG
	IF TALENT:(LOCAL):남자 <= 0
		LOCAL=1
	ELSE
		LOCAL=0
	ENDIF
RETURNF LOCAL
;♂オス
@ISMALE(ARG=-1)
#FUNCTION
	LOCAL=ARG==-1 ? TARGET # ARG
	IF TALENT:(LOCAL):남자 > 0
		LOCAL=1
	ELSE
		LOCAL=0
	ENDIF
RETURNF LOCAL
;-------------------------------------------------
;TS変身系の判定
;現在TS変身によって♀状態なら１
@ISTSFEMALE(ARG=-1)
#FUNCTION
	LOCAL=ARG==-1 ? TARGET # ARG
	IF CFLAG:1>=1 &&  ISFEMALE(LOCAL) && TALENT:LOCAL:변신시ＴＳ > 0
		LOCAL=1
	ELSE
		LOCAL=0
	ENDIF
RETURNF LOCAL
;現在TS変身によって♂状態なら１
@ISTSMALE(ARG=-1)
#FUNCTION
	LOCAL=ARG==-1 ? TARGET # ARG
	IF CFLAG:1>=1 &&  ISMALE(LOCAL) && TALENT:LOCAL:변신시ＴＳ > 0
		LOCAL=1
	ELSE
		LOCAL=0
	ENDIF
RETURNF LOCAL

;기생されて촉수出せる状態なら1
;	(主に흑화♀が촉수責めできるかどうかの分岐で使う)
@ISTENTACLER(ARG=-1)
#FUNCTION
	LOCAL=ARG==-1 ? TARGET # ARG
	SIF ARG == 0
		RETURNF 1
	IF TALENT:LOCAL:기생 && CONFIG_CHECK_MANIAC_F(13) == 1
		LOCAL = 1
	ELSE
		LOCAL = 0
	ENDIF
RETURNF LOCAL

