;------------------------------------------------------------------------
;전투 스타일과 관련된 처리군



;전투 스타일의 이름을 반환하는 식중 함수
;ARG=대상 캐릭터의 번호, ARG:1=현재 거리
@FSTYLE_NAME_F,ARG,ARG:1
#FUNCTIONS
VARSET LOCALS
SELECTCASE CDFLAG:(ARG):(ARG:1):전투스타일
	CASE 1
		LOCALS = 연속
	CASE 2
		LOCALS = 교란
	CASE 3
		LOCALS = 장갑
	CASE 4
		LOCALS = 중격
	CASE 5
		LOCALS = 반격
	CASE 6
		LOCALS = 광역
	CASE 7
		LOCALS = 전력
	CASE 8
		LOCALS = 지략
	CASE 9
		LOCALS = 설치
	CASE 10
		LOCALS = 사역
	;CASE 11
		;LOCALS = 변형
	CASEELSE
		LOCALS = 통상
ENDSELECT
RETURNF LOCALS


;전투 스타일별 공격력 실수치(実数値)를 반환하는 식중 함수
;ARG   = 대상 캐릭터 번호
;ARG:1 = 현재 거리
;ARG:2 = 캐릭터 메이킹 중이라면 1
@FSTYLE_ATTACK, ARG, ARG:1, ARG:2 = 0
#DIM ATTACK
VARSET LOCAL
;대상 캐릭터의 각 능력을 취득
LOCAL:10 = MAXBASE:ARG:공격
LOCAL:11 = MAXBASE:ARG:방어
LOCAL:12 = MAXBASE:ARG:민첩
LOCAL:13 = MAXBASE:ARG:지성
;캐릭터 메이킹 중이라면 미가산 보너스를 반영
IF ARG:2 > 0
	LOCAL:10 += CFLAG:ARG:53
	LOCAL:11 += CFLAG:ARG:54
	LOCAL:12 += CFLAG:ARG:55
	LOCAL:13 += CFLAG:ARG:56
ENDIF
;의상에 따른 보정
CALL CLOTH_BATTLE_HOSEI, "KOUGEKI", ARG
LOCAL:10 = LOCAL:10 * RESULT / 100
CALL CLOTH_BATTLE_HOSEI, "BOUGYO", ARG
LOCAL:11 = LOCAL:11 * RESULT / 100
CALL CLOTH_BATTLE_HOSEI, "BINSYOU", ARG
LOCAL:12 = LOCAL:12 * RESULT / 100
CALL CLOTH_BATTLE_HOSEI, "CHISEI", ARG
LOCAL:13 = LOCAL:13 * RESULT / 100
;왜소함・장신이라면 보정
IF TALENT:왜소함 == 1
	TIMES LOCAL:10, 0.95
	TIMES LOCAL:12, 1.05
ENDIF
IF TALENT:장신 == 1
	TIMES LOCAL:10, 1.05
	TIMES LOCAL:12, 0.95
ENDIF
;계산 개시
SELECTCASE FSTYLE_NAME_F(ARG,ARG:1)
	CASE "연속"
		ATTACK = LOCAL:10 * 30 /100 + LOCAL:12 * 25 /100
	CASE "장갑"
		ATTACK = LOCAL:10 * 45 /100 + LOCAL:11 * 40 /100
	CASE "교란"
		ATTACK = LOCAL:10 * 75 /100 + LOCAL:12 * 35 /100
	CASE "중격"
		ATTACK = LOCAL:10 * 90 /100 + LOCAL:11 * 30 /100
	CASE "반격"
		ATTACK = LOCAL:10 * 70 /100 + LOCAL:11 * 50 /100	
	CASE "광역"
		ATTACK = LOCAL:10 * 120 /100
	CASE "전력"
		ATTACK = LOCAL:10 * 150 /100
	CASE "지략"
		ATTACK = LOCAL:10 * 50 /100 + LOCAL:13 * 20 /100
	CASE "설치"
		ATTACK = LOCAL:10 * 50 /100 + LOCAL:13 * 30 /100
	CASE "사역"
		ATTACK = LOCAL:10 * 30 /100 + LOCAL:13 * 25 /100
	;CASE "변형"
	;	ATTACK = LOCAL:10 * 100 /100 + LOCAL:?? * ?? /100
	CASE "통상"
		ATTACK = LOCAL:10 * 100 /100
ENDSELECT

;[반격] 스타일의 경우 버스트, 1차 공격의 순공격력(일명 깡공)이 약화된다
IF FSTYLE_NAME_F(TARGET,TCVARn:0) == "반격"
	;버스트 공격일 경우 반감
	IF GETBIT(TCVARn:217, 0)
		ATTACK = LOCAL:10 * 35 /100 + LOCAL:11 * 25 /100
	;1차 공격일 경우 1/3 로 감소
	ELSEIF TCVARn:2 != 자세：반격성공
		ATTACK = LOCAL:10 * 25 /100 + LOCAL:11 * 15 /100
	ENDIF
ENDIF
RETURN ATTACK





;전투 스타일 정보를 취득
@SET_FSTYLE_INFO, ARG,ARG:1
SELECTCASE FSTYLE_NAME_F(ARG,ARG:1)
	CASE "연속"
		RESULTS:0 = 위력 영향도：공격*0.30 + 민첩*0.25
		RESULTS:1 = 추가 효과　：매 공격마다 2회의 연속 피해를 입힌다
		RESULTS:2 = BURST 성능 ：총 ６회의 연속 공격, 모두 직격하면 고위력의 피해를 입힌다
	CASE "교란"
		RESULTS:0 = 위력 영향도：공격*0.75 + 민첩*0.35
		RESULTS:1 = 추가 효과　：치명타율 ＵＰ　그러나 방어력에 패널티
		RESULTS:2 = BURST 성능 ：위력이 상승하며, 일정 확률로 상대를 기죽인다
	CASE "장갑"
		RESULTS:0 = 위력 영향도：공격*0.45 + 방어*0.40
		RESULTS:1 = 추가 효과　：받는 피해량 경감　치명타 피해를 입지 않는다
		RESULTS:2 = BURST 성능 ：직격률이 상승하며, 공격 후 오토 가드
	CASE "중격"
		RESULTS:0 = 위력 영향도：공격*0.90 + 방어*0.30
		RESULTS:1 = 추가 효과　：위력이 높다　그러나 회피율에 패널티
		RESULTS:2 = BURST 성능 ：위력이 더욱 상승하며, 명중률이 감소한다
	CASE "반격"
		RESULTS:0 = 위력 영향도：공격*0.70 + 방어*0.50
		RESULTS:1 = 추가 효과　：공격 후 반격 준비, 이후 방어 혹은 회피 성공 시 반격　「방어」→「반격」커맨드 변경 및 성능 강화
		RESULTS:2 = BURST 성능 ：전투 중에 반격을 성공시켜 축적한 피해량을 더한 일격을 가한다
	CASE "광역"
		RESULTS:0 = 위력 영향도：공격*1.20
		;역) カス当たり를 "가드 대미지(공격이 가드될 시의 위력)"로 의역하였으며, 이보다 더 잘 어울리는 번역명을 추천해 주신다면 감사하겠습니다
		;추가 효과 원문은 カス当たりの威力が高い　ただし直撃しにくい
		RESULTS:1 = 추가 효과　：공격이 빗나갈 확률이 적다　그러나 직격률 감소 및 공격이 가드될 확률 증가
		RESULTS:2 = BURST 성능 ：확정적으로 직격하며, 그 반동으로 회피할 수 없게 된다
	CASE "전력"
		RESULTS:0 = 위력 영향도：공격*1.50
		RESULTS:1 = 추가 효과　：공격이 잘 가드되지 않는다　피로도에 따라 직격률 DOWN　반동으로 추가 피로 축적
		RESULTS:2 = BURST 성능 ：필사의 각오로 쏘아붙이는 강렬한 일격
	CASE "지략"
		RESULTS:0 = 위력 영향도：공격*0.50 + 지성*0.20
		RESULTS:1 = 추가 효과　：상대방과의 지성차에 따라 치명타 배율 증가（최대 100％）
		RESULTS:2 = BURST 성능 ：치명타 확률 증가, 공격 후에【관찰한다】효과
	CASE "설치"
		RESULTS:0 = 위력 영향도：공격*0.50 + 지성*0.30
		RESULTS:1 = 추가 효과　：직격하거나 공격이 가드될 시 상대방의 방심도가 증가
		RESULTS:2 = BURST 성능 ：방심도 상승 효과 ２배
	CASE "사역"
		RESULTS:0 = 위력 영향도：공격*0.30 + 지성*0.25
		RESULTS:1 = 추가 효과　：매 공격마다 2회의 연속 피해를 입힌다
		RESULTS:2 = BURST 성능 ：위력이 더욱 상승하며, 명중률이 감소한다
	CASE "통상"
		RESULTS:0 = 위력 영향도：공격*1.00
		RESULTS:1 = 추가 효과　：표준적인 성능　직격률이 상대적으로 조금 높다
		RESULTS:2 = BURST 성능 ：대미지 효율이 좋은 강공격
	;CASE "변형"
	;	RESULTS:0 = 위력 영향도  ：공격*0.00 + ??*0.00
	;	RESULTS:1 = 추가 효과  　：방어, 민첩, 지성 중 제일 높은 스탯에 따라 서로 다른 추가 효과를 얻는다
	;	RESULTS:2 = BURST 성능 ：아직 미정
ENDSELECT
RETURN

