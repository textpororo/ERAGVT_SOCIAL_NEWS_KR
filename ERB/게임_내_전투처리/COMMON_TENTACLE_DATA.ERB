;--------------------------------------------------
;보스 촉수・최종 보스 촉수 섬멸 상황 확인
;지역 변수 LOCAL:0은 체크용
;LOCAL:1은 살아 남아 있는 촉수의 총 마릿수가 들어간다
@TENTACLE_SURVIVE, ARGS
LOCAL:0 = 1
LOCAL:1 = 0
LOCAL:2 = 0
LOCAL:3 = 0
;보스 잔존or잡몹전
IF ENEMY_TYPE_CHECK_F("BOSS") == 1 || ENEMY_TYPE_CHECK_F("MOB") == 1
	FOR LOCAL:2, 0, FLAG:3
		CALL TENTACLE_SURVIVE_CHECK, LOCAL:0
		IF RESULT > 0
			SELECTCASE ARGS
				CASE "NAME"
					CALL TENTACLE_ACCESS, "NAME"
				CASE "NUM"
					LOCAL:1 += 1
			ENDSELECT
		ENDIF
		LOCAL:0 *= 2
	NEXT

	;반환할 값 선정
	SELECTCASE ARGS
		CASE "NUM"
			RETURN LOCAL:1
	ENDSELECT
;최종 보스만
ELSE
	FOR LOCAL:2, 0, FLAG:4
		CALL TENTACLE_SURVIVE_CHECK, LOCAL:0
		IF RESULT > 0
			SELECTCASE ARGS
				CASE "NAME"
					CALL TENTACLE_ACCESS, "NAME"
				CASE "NUM"
					LOCAL:1 += 1
			ENDSELECT
		ENDIF
		LOCAL:0 *= 2
	NEXT

	;반환할 값 선정
	SELECTCASE ARGS
		CASE "NUM"
			RETURN LOCAL:1
	ENDSELECT
ENDIF

;--------------------------------------------------
;비트값을 던져 살아 있는 보스 촉수・최종 보스 촉수의 번호를 반환한다
;BOSSx.ERB의 번호를 반환한다
@TENTACLE_SURVIVE_CHECK, ARG
;보스 잔존or잡몹전
IF FLAG:100 > 0 || ENEMY_TYPE_CHECK_F("MOB") == 1
	IF FLAG:100 & ARG
		;Ｃ촉수
		IF ARG == 1
			RETURN 1
		;V촉수
		ELSEIF ARG == 2
			RETURN 2
		;A촉수
		ELSEIF ARG == 4
			RETURN 3
		;B촉수
		ELSEIF ARG == 8
			RETURN 4
		;S촉수
		ELSEIF ARG == 16
			RETURN 5
		;P촉수
		ELSEIF ARG == 32
			RETURN 6
		;H촉수
		ELSEIF ARG == 64
			RETURN 7
		;０이거나 음수
		ELSEIF ARG <= 0
			PRINTL 
			PRINTL #####     ERROR !!     #####
			PRINTL @TENTACLE_SURVIVE_CHECK 에서
			PRINTFORML 참조하려고 하는 보스 ID가 ０이거나 음수(ARG = {ARG})로 되어 있습니다.
			PRINTL 원인 규명을 위해, 커뮤니티에 가능한 한 상세한 발생 상황을 보고해 주세요.
			PRINTW 
		ELSE
			PRINTL 
			PRINTL #####     ERROR !!     #####
			PRINTL @TENTACLE_SURVIVE_CHECK 에서
			PRINTFORML 참조하려고 하는 보스 ID가 범위 외의 값(ARG = {ARG})으로 되어 있습니다.
			PRINTL 원인 규명을 위해, 커뮤니티에 가능한 한 상세한 발생 상황을 보고해 주세요.
			PRINTW 
		ENDIF
	ENDIF
;생존 보스＝최종 보스만
ELSE
	IF FLAG:101 & ARG
		;K촉수
		IF ARG == 1
			RETURN 1
		;천사의 나무
		ELSEIF ARG == 2
			RETURN 2
		;０이거나 음수
		ELSEIF ARG <= 0
			PRINTL 
			PRINTL #####     ERROR !!     #####
			PRINTL @TENTACLE_SURVIVE_CHECK 에서
			PRINTFORML 참조하려고 하는 최종 보스 ID가 ０이거나 음수(ARG = {ARG})로 되어 있습니다.
			PRINTL 원인 규명을 위해, 커뮤니티에 가능한 한 상세한 발생 상황을 보고해 주세요.
			PRINTW 
		ELSE
			PRINTL 
			PRINTL #####     ERROR !!     #####
			PRINTL @TENTACLE_SURVIVE_CHECK 에서
			PRINTFORML 참조하려고 하는 최종 보스 ID가 범위 외의 값(ARG = {ARG})으로 되어 있습니다.
			PRINTL 원인 규명을 위해, 커뮤니티에 가능한 한 상세한 발생 상황을 보고해 주세요.
			PRINTW 
		ENDIF
	ENDIF
ENDIF

;--------------------------------------------------
;BOSSx.ERB의 번호를 바탕으로 보스 촉수・최종 보스 촉수의 비트값을 반환한다
;위 함수의 역판(逆版)
@TENTACLE_BITVALUE, ARG
;보스or잡몹
IF ENEMY_TYPE_CHECK_F("BOSS") == 1 || ENEMY_TYPE_CHECK_F("MOB") == 1
	;Ｃ촉수
	IF ARG == 1
		RETURN 1
	;V촉수
	ELSEIF ARG == 2
		RETURN 2
	;A촉수
	ELSEIF ARG == 3
		RETURN 4
	;B촉수
	ELSEIF ARG == 4
		RETURN 8
	;S촉수
	ELSEIF ARG == 5
		RETURN 16
	;P촉수
	ELSEIF ARG == 6
		RETURN 32
	;H촉수
	ELSEIF ARG == 7
		RETURN 64
	;０이거나 음수
	ELSEIF ARG <= 0
		PRINTL 
		PRINTL #####     ERROR !!     #####
		PRINTL @TENTACLE_BITVALUE 에서
		PRINTFORML 참조하려고 하는 보스 ID가 ０이거나 음수(ARG = {ARG})로 되어 있습니다.
		PRINTL 원인 규명을 위해, 커뮤니티에 가능한 한 상세한 발생 상황을 보고해 주세요.
		PRINTW 
	ELSE
		PRINTL 
		PRINTL #####     ERROR !!     #####
		PRINTL @TENTACLE_BITVALUE 에서
		PRINTFORML 참조하려고 하는 보스 ID가 범위 외의 값(ARG = {ARG})으로 되어 있습니다.
		PRINTL 원인 규명을 위해, 커뮤니티에 가능한 한 상세한 발생 상황을 보고해 주세요.
		PRINTW 
	ENDIF
;최종 보스
ELSE
	;K촉수
	IF ARG == 1
		RETURN 1
	;천사의 나무
	ELSEIF ARG == 2
		RETURN 2
	;０이거나 음수
	ELSEIF ARG <= 0
		PRINTL 
		PRINTL #####     ERROR !!     #####
		PRINTL @TENTACLE_BITVALUE 에서
		PRINTFORML 참조하려고 하는 최종 보스 ID가 ０이거나 음수(ARG = {ARG})로 되어 있습니다.
		PRINTL 원인 규명을 위해, 커뮤니티에 가능한 한 상세한 발생 상황을 보고해 주세요.
		PRINTW 
	ELSE
		PRINTL 
		PRINTL #####     ERROR !!     #####
		PRINTL @TENTACLE_BITVALUE 에서
		PRINTFORML 참조하려고 하는 최종 보스 ID가 범위 외의 값(ARG = {ARG})으로 되어 있습니다.
		PRINTL 원인 규명을 위해, 커뮤니티에 가능한 한 상세한 발생 상황을 보고해 주세요.
		PRINTW 
	ENDIF
ENDIF

;--------------------------------------------------
;촉수 파일에 접근 (전투용)
;FLAG:10의 값으로 접근처가 보스 촉수인지 최종 보스 촉수인지 판정한다
;패치1 유체 또는 잡몹 촉수 생성용　치환 BOSS→%SAVESTR:13%
;
;이거 적의 이름을 반환하는 식중함수판을 만들 수는 없을까... 맨날 CALL TENTACLE_ACCESS 하고 나서 다음 행에 받아 적어야 하니까 불편한데
@TENTACLE_ACCESS, ARGS

;보스or잡몹
RESULTS'="【ERROR："+SAVESTR:13+"_"+TOSTR(FLAG:11)+"에 대한 TENTACLE_ACCESS('"+ARGS+"') 함수 실패】"
IF GET_LASTBOSS_PHASE_F() == 0 || ENEMY_TYPE_CHECK_F("MOB") == 1 || ENEMY_TYPE_CHECK_F("CITIZEN") == 1
	SELECTCASE ARGS
		CASE "NAME"
			CALL TENTACLE_ACCESS, "GETNAME"
			PRINTFORM %RESULTS%
		CASE "GETNAME"
			TRYCALLFORM TENTACLE_%SAVESTR:13%_{FLAG:11}_GETNAME
		CASE "HP"
			TRYCALLFORM TENTACLE_%SAVESTR:13%_{FLAG:11}_HP
			RETURN RESULT
		CASE "SYASEI"
			TRYCALLFORM TENTACLE_%SAVESTR:13%_{FLAG:11}_SYASEI
			RETURN RESULT
		CASE "SAKUSEI"
			TRYCALLFORM TENTACLE_%SAVESTR:13%_{FLAG:11}_SAKUSEI
			RETURN RESULT
		CASE "YUDAN"
			TRYCALLFORM TENTACLE_%SAVESTR:13%_{FLAG:11}_YUDAN
			RETURN RESULT
		CASE "KOUGEKI"
			TRYCALLFORM TENTACLE_%SAVESTR:13%_{FLAG:11}_KOUGEKI
			RETURN RESULT
		CASE "BOUGYO"
			TRYCALLFORM TENTACLE_%SAVESTR:13%_{FLAG:11}_BOUGYO
			RETURN RESULT
		CASE "BINSYOU"
			TRYCALLFORM TENTACLE_%SAVESTR:13%_{FLAG:11}_BINSYOU
			RETURN RESULT
		CASE "CHISEI"
			TRYCALLFORM TENTACLE_%SAVESTR:13%_{FLAG:11}_CHISEI
			RETURN RESULT
		CASE "SHORT"
			TRYCALLFORM TENTACLE_%SAVESTR:13%_{FLAG:11}_SHORT
			RETURN RESULT
		CASE "MIDDLE"
			TRYCALLFORM TENTACLE_%SAVESTR:13%_{FLAG:11}_MIDDLE
			RETURN RESULT
		CASE "LONG"
			TRYCALLFORM TENTACLE_%SAVESTR:13%_{FLAG:11}_LONG
			RETURN RESULT
		CASE "HOLD"
			TRYCALLFORM TENTACLE_%SAVESTR:13%_{FLAG:11}_HOLD
			RETURN RESULT
		CASE "ATTACK_ROUTINE"
			TRYCALLFORM TENTACLE_%SAVESTR:13%_{FLAG:11}_ATTACK_ROUTINE
			RETURN RESULT
		CASE "SEX_ROUTINE"
			TRYCALLFORM TENTACLE_%SAVESTR:13%_{FLAG:11}_SEX_ROUTINE
			RETURN RESULT
		CASE "PALAM_HOSEI"
			TRYCALLFORM TENTACLE_%SAVESTR:13%_{FLAG:11}_PALAM_HOSEI
			RETURN RESULT:0, RESULT:1, RESULT:2, RESULT:3, RESULT:4, RESULT:5, RESULT:6, RESULT:7, RESULT:8, RESULT:9, RESULT:10, RESULT:11
	ENDSELECT
;최종 보스
ELSE
	SELECTCASE ARGS
		CASE "NAME"
			CALL TENTACLE_ACCESS, "GETNAME"
			PRINTFORM %RESULTS%
		CASE "GETNAME"
			TRYCALLFORM TENTACLE_LASTBOSS_{FLAG:11}_GETNAME
		CASE "HP"
			TRYCALLFORM TENTACLE_LASTBOSS_{FLAG:11}_HP
			RETURN RESULT
		CASE "SYASEI"
			TRYCALLFORM TENTACLE_LASTBOSS_{FLAG:11}_SYASEI
			RETURN RESULT
		CASE "SAKUSEI"
			TRYCALLFORM TENTACLE_LASTBOSS_{FLAG:11}_SAKUSEI
			RETURN RESULT
		CASE "YUDAN"
			TRYCALLFORM TENTACLE_LASTBOSS_{FLAG:11}_YUDAN
			RETURN RESULT
		CASE "KOUGEKI"
			TRYCALLFORM TENTACLE_LASTBOSS_{FLAG:11}_KOUGEKI
			RETURN RESULT
		CASE "BOUGYO"
			TRYCALLFORM TENTACLE_LASTBOSS_{FLAG:11}_BOUGYO
			RETURN RESULT
		CASE "BINSYOU"
			TRYCALLFORM TENTACLE_LASTBOSS_{FLAG:11}_BINSYOU
			RETURN RESULT
		CASE "CHISEI"
			TRYCALLFORM TENTACLE_LASTBOSS_{FLAG:11}_CHISEI
			RETURN RESULT
		CASE "SHORT"
			TRYCALLFORM TENTACLE_LASTBOSS_{FLAG:11}_SHORT
			RETURN RESULT
		CASE "MIDDLE"
			TRYCALLFORM TENTACLE_LASTBOSS_{FLAG:11}_MIDDLE
			RETURN RESULT
		CASE "LONG"
			TRYCALLFORM TENTACLE_LASTBOSS_{FLAG:11}_LONG
			RETURN RESULT
		CASE "HOLD"
			TRYCALLFORM TENTACLE_LASTBOSS_{FLAG:11}_HOLD
			RETURN RESULT
		CASE "ATTACK_ROUTINE"
			TRYCALLFORM TENTACLE_LASTBOSS_{FLAG:11}_ATTACK_ROUTINE
			RETURN RESULT
		CASE "SEX_ROUTINE"
			TRYCALLFORM TENTACLE_LASTBOSS_{FLAG:11}_SEX_ROUTINE
			RETURN RESULT
		CASE "PALAM_HOSEI"
			TRYCALLFORM TENTACLE_LASTBOSS_{FLAG:11}_PALAM_HOSEI
			RETURN RESULT:0, RESULT:1, RESULT:2, RESULT:3, RESULT:4, RESULT:5, RESULT:6, RESULT:7, RESULT:8, RESULT:9, RESULT:10, RESULT:11
	ENDSELECT
ENDIF

;--------------------------------------------------
;촉수 파일에 접근 (유폐용)
;CFLAG:20의 값으로 접근처가 보스 촉수인지 최종 보스 촉수인지 판정한다
@TENTACLE_ACCESS_PRISON, ARG, ARGS
IF CFLAG:ARG:20 == 0
	SELECTCASE ARGS
		CASE "NAME"
			CALL TENTACLE_ACCESS_PRISON, ARG, "GETNAME"
			PRINTFORM %RESULTS%
		CASE "GETNAME"
			TRYCALLFORM TENTACLE_BOSS_{CFLAG:ARG:21}_GETNAME
		CASE "PRISON_ROUTINE"
			TRYCALLFORM TENTACLE_BOSS_{CFLAG:ARG:21}_PRISON_ROUTINE
			RETURN RESULT
		CASE "PALAM_HOSEI"
			TRYCALLFORM TENTACLE_BOSS_{CFLAG:ARG:21}_PALAM_HOSEI
			RETURN RESULT:0, RESULT:1, RESULT:2, RESULT:3, RESULT:4, RESULT:5, RESULT:6, RESULT:7, RESULT:8, RESULT:9, RESULT:10, RESULT:11
	ENDSELECT
ELSEIF CFLAG:ARG:20 == 1
	SELECTCASE ARGS
		CASE "NAME"
			CALL TENTACLE_ACCESS_PRISON, ARG, "GETNAME"
			PRINTFORM %RESULTS%
		CASE "GETNAME"
			TRYCALLFORM TENTACLE_LASTBOSS_{CFLAG:ARG:21}_GETNAME
		CASE "PRISON_ROUTINE"
			TRYCALLFORM TENTACLE_LASTBOSS_{CFLAG:ARG:21}_PRISON_ROUTINE
			RETURN RESULT
		CASE "PALAM_HOSEI"
			TRYCALLFORM TENTACLE_BOSS_{CFLAG:ARG:21}_PALAM_HOSEI
			RETURN RESULT:0, RESULT:1, RESULT:2, RESULT:3, RESULT:4, RESULT:5, RESULT:6, RESULT:7, RESULT:8, RESULT:9, RESULT:10, RESULT:11
	ENDSELECT
ENDIF

;--------------------------------------------------
;보스 촉수・최종 보스 촉수의 보정
;계산식 ((Lv * 10 + 95) * BASE) / 100 + BONUS
@TENTACLE_STATUS_HOSEI, ARG,ARG:1 = 100
CALL TENTACLE_LEVEL
LOCAL:0 = RESULT * 10
LOCAL:2 = ((LOCAL:0 + 95) * ARG) / 100 + ARG:1
RETURN LOCAL:2

;--------------------------------------------------
;보스 촉수・최종 보스   촉수의 레벨
@TENTACLE_LEVEL
#DIM 격파수
#DIM 기초레벨
#DIM 일수레벨

;격파수
CALL TENTACLE_SURVIVE, "NUM"
격파수 = FLAG:3 - RESULT
;격파한 촉수의 수에 따라 기초 레벨 상승
LOCAL = 3
SIF GAME_OPTION_CHECK_F(OPTION_hardcore)
	LOCAL += 2
SIF GAME_OPTION_CHECK_F(OPTION_solo)
	LOCAL += 1
SIF GAME_OPTION_CHECK_F(OPTION_endless)
	LOCAL -= 1
SIF GAME_OPTION_CHECK_F(OPTION_가입은퇴있음)
	LOCAL -= 1
SIF GAME_OPTION_CHECK_F(OPTION_스탯저하있음)
	LOCAL -= 1
SIF LOCAL < -4
	LOCAL = -4

기초레벨 = 격파수 * LOCAL + 4
;잡몹적은 3/4
SIF ENEMY_TYPE_CHECK_F("MOB") == 1
	기초레벨 = 기초레벨 * 3 / 4

;일수 경과에 따른 레벨 업
;상승 기준은 (경과 일수/한 마리당 제한 시간)*1.5
LOCAL = 150
;※ENDLESS는 보통보다 완만한 상승
SIF GAME_OPTION_CHECK_F(OPTION_endless)
	LOCAL = LOCAL/3
;※HARDCORE는 보통보다 급격한 상승
SIF GAME_OPTION_CHECK_F(OPTION_hardcore)
	LOCAL = LOCAL*4/3
;INSTANT도 완만하다
SIF GAME_OPTION_CHECK_F(OPTION_스탯저하있음)
	LOCAL = LOCAL*2/3
SIF GAME_OPTION_CHECK_F(OPTION_가입은퇴있음)
	LOCAL = LOCAL/2

SIF LOCAL < 0
	LOCAL = 0

일수레벨 = LOCAL * DAY / FLAG:2 / 100

;계산 결과
LOCAL = 기초레벨 + 일수레벨
RETURN LOCAL



;--------------------------------------------------
;보스 촉수의 ERB 수를 반환한다
;지금은 아직 최대 １００가지 밖에 만들 수 없으니 주의
@GET_BOSS_ERB_NUM
VARSET LOCAL
LOCAL = 1
WHILE LOCAL:1 == 0
	TRYCCALLFORM TENTACLE_BOSS_{LOCAL}
		LOCAL += 1
	CATCH
		LOCAL:1 = 1
	ENDCATCH
WEND
RETURN LOCAL - 1

;--------------------------------------------------
;최종 보스 촉수의 ERB 수를 반환한다
;지금은 아직 최대 １００가지 밖에 만들 수 없으니 주의
@GET_LASTBOSS_ERB_NUM
VARSET LOCAL
LOCAL = 1
WHILE LOCAL:1 == 0
	TRYCCALLFORM TENTACLE_LASTBOSS_{LOCAL}
		LOCAL += 1
	CATCH
		LOCAL:1 = 1
	ENDCATCH
WEND
RETURN LOCAL - 1





;--------------------------------------------------
;보스 격파 시의 플래그 처리 관계
@AFTER_KILLED_BOSS
;턴 종료까지 보스 출현을 억제
FLAG:49 = 1
CALL RESEARCH_QUOTA


;--------------------------------------------------
;탐색 기준량(norma)
;보스의 레벨과 동일한 산출 방법
@RESEARCH_QUOTA
#DIM LCOUNT
#DIM 격파수
#DIM 토벌보정
#DIM 일수보정
CALL TENTACLE_SURVIVE, "NUM"
격파수 = MIN(FLAG:3 - RESULT + 1, 9)
;격파한 촉수의 수에 따라 기준량 증가
LOCAL = 12
SIF GAME_OPTION_CHECK_F(OPTION_hardcore)
	LOCAL += 6
SIF GAME_OPTION_CHECK_F(OPTION_solo)
	LOCAL -= 6
SIF GAME_OPTION_CHECK_F(OPTION_가입은퇴있음)
	LOCAL += 1
SIF GAME_OPTION_CHECK_F(OPTION_스탯저하있음)
	LOCAL -= 1

;계산 개시
토벌보정 = 격파수 * LOCAL / 3 + 24
SIF GAME_OPTION_CHECK_F(OPTION_제한시간없음)
	토벌보정 /= 2
SIF GAME_OPTION_CHECK_F(OPTION_solo)
	토벌보정 -= 12

;일수 경과에 따른 보정을 건다
LOCAL = 2
SIF GAME_OPTION_CHECK_F(OPTION_제한시간없음)
	LOCAL -= 4
SIF GAME_OPTION_CHECK_F(OPTION_hardcore)
	LOCAL += 1

일수보정 = SQRT(DAY) * LOCAL


LOCAL = 토벌보정 + 일수보정
SIF LOCAL < 10
	LOCAL = 10
IF GAME_OPTION_CHECK_F(OPTION_solo) && LOCAL >= 60 + RAND:5
	LOCAL = 60
ELSEIF LOCAL >= 90 + RAND:5
	LOCAL = 90
ENDIF
SIF GET_LASTBOSS_PHASE_F() >= 1
	LOCAL = LOCAL * 6 / 5

FLAG:46 = LOCAL
FLAG:47 = 0


;--------------------------------------------------
;잡몹 격파 기준량(norma)
;보스의 레벨과 동일한 산출 방법
@MOB_KILL_QUOTA
#DIM 격파수
#DIM 토벌보정
#DIM 일수보정

CALL TENTACLE_SURVIVE, "NUM"
격파수 = MIN(FLAG:3 - RESULT + 1, 9)
;격파한 촉수의 수에 따라 기준량 증가
LOCAL = 2
SIF GAME_OPTION_CHECK_F(OPTION_hardcore)
	LOCAL += 1
SIF GAME_OPTION_CHECK_F(OPTION_solo)
	LOCAL -= 1
SIF GAME_OPTION_CHECK_F(OPTION_스탯저하있음)
	LOCAL -= 1

토벌보정 = 격파수 * LOCAL / 3 + 2

;일수 경과에 따른 보정을 건다
LOCAL = 150
SIF GAME_OPTION_CHECK_F(OPTION_hardcore)
	LOCAL = LOCAL*4/3
SIF GAME_OPTION_CHECK_F(OPTION_가입은퇴있음)
	LOCAL = LOCAL*2/3

SIF LOCAL < 0
	LOCAL = 0

일수보정 = LOCAL * DAY / FLAG:2 / 100


LOCAL = 토벌보정 + 일수보정
SIF GET_LASTBOSS_PHASE_F() >= 1
	LOCAL /= 2
SIF GAME_OPTION_CHECK_F(OPTION_solo) && LOCAL >= 8 + RAND:5
	LOCAL = 8
SIF LOCAL >= 15 + RAND:5
	LOCAL = 15

RETURN LOCAL



