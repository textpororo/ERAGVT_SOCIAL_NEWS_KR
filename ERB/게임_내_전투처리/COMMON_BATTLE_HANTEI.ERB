;--------------------------------------------------
;촉수나 적 캐릭터와의 전투에 관한 공통 함수

;--------------------------------------------------
;행동의 성공 여부 판정：캐릭터 측
@ACT_HANTEI_CHARA_TO_TENTACLE, ARGS, ARG = 0
#DIM CHISEI_COR
#DIM CHISEI_CAL
#DIM CCOUNT

;캐릭터의 체력＋기력의 잔량을 구한다
LOCAL:0 = PERCENT_CAL_F(BASE:체력 * 2 + BASE:기력, MAXBASE:체력 * 2 + MAXBASE:기력)
;소모도의 최저치는 방어력 의존
SIF LOCAL:0 < MIN(SQRT(MAX(BASE:방어 - 100, 0)) * 8, 75)
	LOCAL:0 = MIN(SQRT(MAX(BASE:방어 - 100, 0)) * 8, 75)
;FEAT 효과에 의한 소모도 경감
SIF TALENT:불굴 > 0
	LOCAL:0 = LOCAL:0 + (100 - LOCAL:0) / 2
;마비로 잔량 반감
SIF (TCVARn:12 & 마비)
	LOCAL:0 /= 2
;SP변신 중에는 체력•기력의 감소에 따른 영향을 받지 않는다
SIF CFLAG:1 == 2
	LOCAL:0 = 100

;적의 체력 잔량에 따른 보정
IF PERCENT_CAL_F(FLAG:13, FLAG:12) > 50
	LOCAL:1 = 100
ELSEIF PERCENT_CAL_F(FLAG:13, FLAG:12) > 25
	LOCAL:1 = 105
ELSE
	LOCAL:1 = 110
ENDIF
;적의 방심도에 따른 보정
SIF PERCENT_CAL_F(FLAG:17, FLAG:16)
	LOCAL:1 = LOCAL:1 * (100 + PERCENT_CAL_F(FLAG:17, FLAG:16) / 2) / 100

;풀어낸다의 경우
IF ARGS == "HURIHODOKU"
	;캐릭터의 방어를 본다
	LOCAL:2 = MAXBASE:방어
	;「EX:방」중에는 방어치 상승
	SIF GETBIT(TCVARn:3, 1)
		LOCAL:2 *= 2
	;풀어낸다일 경우에는 지성 보정이 들어간다
	CHISEI_COR = MAXBASE:지성 / 4

	;왜소함・장신이라면 민첩에 보정
	SIF TALENT:왜소함 == 1
		TIMES LOCAL:2, 0.90
	SIF TALENT:장신 == 1
		TIMES LOCAL:2, 1.05

	;변신 능력이 있는 캐릭터의 보정
	IF FLAG:73 > 0
		;단, 악질 시민전에서는 변신해도 미변신 기준의 배율을 참조한다
		;(변신의 유무는 플레이버이기 때문에)
		LOCAL:2 = MIN(LOCAL:2,CORRECTION_TRANS(LOCAL:2))
	ELSE
		LOCAL:2 = CORRECTION_TRANS(LOCAL:2)
	ENDIF
	;의상에 따른 보정
	CALL CLOTH_BATTLE_HOSEI, "BOUGYO"
	LOCAL:2 = LOCAL:2 * RESULT / 100
	CALL CLOTH_BATTLE_HOSEI, "CHISEI"
	CHISEI_COR = CHISEI_COR * RESULT / 100

	;심경의 보정
	CALL SHINKYOU_CHECK, "BOUGYO", LOCAL:2
	LOCAL:2 = RESULT
	CALL SHINKYOU_CHECK, "CHISEI", CHISEI_COR
	CHISEI_COR = RESULT

	;전투 지원에 의한 보정
	CALL CALC_CHISEI_SHIEN(1)
	CHISEI_COR += RESULT
	;소모도에 따른 지성으로의 보정
	CHISEI_COR = CHISEI_COR * 2 * (100 - LOCAL:0) / 100

	;(악질 시민전에서는 영향이 적다)
	IF FLAG:73 > 0
		CHISEI_COR = MAXBASE:지성 / 8
	ENDIF

	IF FLAG:73 > 0 && CFLAG:1 > 0
		;악질 시민전에서 변신 상태라면 적당히 약화시켜 둔다
		LOCAL:2 = MIN(LOCAL/2,50)
	ENDIF

;통상 행동
ELSE
	;캐릭터의 민첩을 본다
	LOCAL:2 = MAXBASE:민첩
	CHISEI_COR = 0
	;구출 중에는 민첩 4 반감
	;(원문) 救出中は민첩四半減
	TRYCALL KYUSHUTU_NOW
	;왜소함・장신이라면 민첩에 보정
	SIF TALENT:왜소함 == 1
		TIMES LOCAL:2, 1.05
	SIF TALENT:장신 == 1
		TIMES LOCAL:2, 0.90

	;변신 능력이 있는 캐릭터의 보정
	LOCAL:2 = CORRECTION_TRANS(LOCAL:2)

	;의상에 따른 보정
	CALL CLOTH_BATTLE_HOSEI, "BINSYOU"
	LOCAL:2 = LOCAL:2 * RESULT / 100

	;심경의 보정
	CALL SHINKYOU_CHECK, "BINSYOU", LOCAL:2
	LOCAL:2 = RESULT
ENDIF

IF FLAG:111 == 0
	;촉수의 민첩
	CALL TENTACLE_ACCESS, "BINSYOU"
	LOCAL:3 = RESULT
ELSE
	;적 캐릭터의 민첩
	LOCAL:3 = MAXBASE:(FLAG:111):민첩

	;적 캐릭터가 왜소함・장신이라면 민첩에 보정
	SIF TALENT:(FLAG:111):왜소함 == 1
		TIMES LOCAL:3, 1.05
	SIF TALENT:(FLAG:111):장신 == 1
		TIMES LOCAL:3, 0.90

	;적 캐릭터의 전투 기술에 따른 보정
	LOCAL:3 = CORRECTION_SENGI_AVOID(LOCAL:3, 0, FLAG:111)
ENDIF

;적의 방심에 따른 보정(악질 시민전에서는 무효)
SIF TFLAG:2 >= 1 && FLAG:73 == 0
	TIMES LOCAL:3, 0.25

;민첩의 차이에 따른 보정
SIF FLAG:999 == 1 && ARG == 0
	PRINTFORML 민첩차 {PERCENT_CAL_F(LOCAL:2, LOCAL:3)}％
LOCAL:4 = CORRECTION_BINSYOU_F(PERCENT_CAL_F(LOCAL:2, LOCAL:3))

;성공치 계산
LOCAL:5 = LOCAL:0 * LOCAL:1 * LOCAL:4 / 10000

IF FLAG:999 == 1 && ARG == 0
	PRINTL CHARA TO TENTACLE 기초 성공치 계산
	PRINTFORML LOCAL:0 ＝ {LOCAL:0}
	PRINTFORML LOCAL:1 ＝ {LOCAL:1}
	PRINTFORML LOCAL:2 ＝ {LOCAL:2}
	PRINTFORML LOCAL:3 ＝ {LOCAL:3}
	PRINTFORML LOCAL:4 ＝ {LOCAL:4}
	PRINTFORML LOCAL:5 ＝ {LOCAL:5}
ENDIF

;행동마다의 보정
SELECTCASE ARGS
	CASE "ATTACK_RANGE_SHORT"
		;근거리 공격 보정：1.05배
		TIMES LOCAL:5, 1.05

		;전투 기술에 따른 보정
		LOCAL:5 = CORRECTION_SENGI(LOCAL:5, 0, TARGET)

		;공격 성공 최저치
		IF ENEMY_TYPE_CHECK_F("MOB") == 1 && LOCAL:5 < 15
			LOCAL:5 = 15
		ELSEIF LOCAL:5 < 30
			LOCAL:5 = 30
		ENDIF
		;공격 성공 최고치
		SIF LOCAL:5 > 80
			LOCAL:5 = 80

		;레벨이 낮은 상대의 레벨차 보정 보너스(～95%)
		CALL TENTACLE_LEVEL
		LOCAL:5 += MIN(15,3*MAX(0,ABL:레벨-RESULT))

		;EX공에 의한 명중률 상승 효과
		SIF GETBIT(TCVARn:3, 0)
			LOCAL:5 += 25
			
	CASE "ATTACK_RANGE_MIDDLE"

		;전투 기술에 따른 보정
		LOCAL:5 = CORRECTION_SENGI(LOCAL:5, 1, TARGET)

		;공격 성공 최저치
		IF ENEMY_TYPE_CHECK_F("MOB") == 1 && LOCAL:5 < 12
			LOCAL:5 = 12
		ELSEIF LOCAL:5 < 25
			LOCAL:5 = 25
		ENDIF
		;공격 성공 최고치
		SIF LOCAL:5 > 85
			LOCAL:5 = 85

		;레벨이 낮은 상대의 레벨차 보정 보너스(～95%)
		CALL TENTACLE_LEVEL
		LOCAL:5 += MIN(10,2*MAX(0,ABL:레벨-RESULT))

		;EX공에 의한 명중률 상승 효과
		SIF GETBIT(TCVARn:3, 0)
			LOCAL:5 += 30
			
	CASE "ATTACK_RANGE_LONG"
		;원거리 공격 보정：0.95배
		TIMES LOCAL:5, 0.95
		
		;전투 기술에 따른 보정
		LOCAL:5 = CORRECTION_SENGI(LOCAL:5, 2, TARGET)

		;공격 성공 최저치
		IF ENEMY_TYPE_CHECK_F("MOB") == 1 && LOCAL:5 < 9
			LOCAL:5 = 9
		ELSEIF LOCAL:5 < 20
			LOCAL:5 = 20
		ENDIF
		;공격 성공 최고치
		SIF LOCAL:5 > 90
			LOCAL:5 = 90

		;레벨이 낮은 상대의 레벨차 보정 보너스(～95%)
		CALL TENTACLE_LEVEL
		LOCAL:5 += MIN(5,MAX(0,ABL:레벨-RESULT))

		;EX공에 의한 명중률 상승 효과
		SIF GETBIT(TCVARn:3, 0)
			LOCAL:5 += 35

	CASE "HURIHODOKU"
		;풀어낸다 성공 최저치
		SIF LOCAL:5 < 20
			LOCAL:5 = 20

		;풀어낸다일 경우에는 지성 보정이 들어간다
		SIF FLAG:73 == 0
			LOCAL:5 += MIN(CHISEI_COR / 15,30)

		;체력＋기력이 50% 이상 남아 있는 동시에 성공치가 45% 이하일 경우, 45%로 보정
		SIF LOCAL:5 <= 45 && LOCAL:O > 49
			LOCAL:5 = 45

		;체력＋기력이 75% 이상 남아 있는 동시에 성공치가 60% 이하일 경우, 60%로 보정
		SIF LOCAL:5 <= 60 && LOCAL:O > 74
			LOCAL:5 = 60

		;구속력에 따른 수정
		IF FLAG:111 == 0 && ARGS == "HURIHODOKU"
			CALL TENTACLE_ACCESS, "HOLD"
			LOCAL:5 = LOCAL:5 * 100 / RESULT
		ENDIF

		;날뛴다：성공, 또는 견딘 다음의 턴은 풀어내기 쉬워진다
		SIF TCVARn:2 == 자세：견딘다 || TCVARn:2 == 자세：날뛴다방어
			LOCAL:5 += 20
		;노려본 턴은 조금만 풀어내기 쉬워진다
		SIF TCVARn:2 == 자세：노려본다
			LOCAL:5 += 10
		;떼어낸 다음의 턴은 꽤나 풀어내기 쉬워진다
		SIF PREVCOM == 40
			LOCAL:5 += 40
		;날뛴다：실패인 다음의 턴은 풀어내기가 어려워진다
		SIF TCVARn:2 == 자세：날뛴다실패
			LOCAL:5 -= 10

		;풀어낸다 성공 최고치
		SIF LOCAL:5 > 80
			LOCAL:5 = 80

		;「EX:방」중에는 성공률 상승
		SIF GETBIT(TCVARn:3, 1)
			LOCAL:5 += 35

		;풀어낸다 강화 효과
		IF FLAG:903
			LOCAL:5 += 10
		;풀어낸다 약화 효과
		ELSEIF FLAG:908
			LOCAL:5 -= 10
		ENDIF

		;FEAT 효과에 의한 풀어낸다 보정
		SIF TALENT:근력강화 > 0
			LOCAL:5 += 10
		SIF TALENT:소형체구 > 0
			LOCAL:5 -= 10

		;황홀 상태라면 보정
		SIF (TCVARn:12 & 황홀)
			LOCAL:5 -= 10
		;적이 방심했고, 황홀 상태가 아닌 경우 100% 성공
		IF TFLAG:2 >= 1 && (TCVARn:12 & 황홀) == 0
			IF FLAG:73==0
				LOCAL:5 = 100
			;악질 시민전에서는 보너스만
			ELSE
				LOCAL:5 += 10
			ENDIF
		ENDIF
		;;악질 시민으로부터의 구속은 5턴째 이후에서 확정으로 풀린다
		;SIF FLAG:73 > 0 && TFLAG:0 > 4
		;	LOCAL:5 = 100

		;풀어낸다 성공 최저치를 재설정
		SIF LOCAL:5 < 10
			LOCAL:5 = 10

ENDSELECT

;원래 汎用 스타일이라고 써져 있었는데, FIGHT_STYLE.ERB의 이 함수에는 없는 거라 비슷한 효과인 통상으로 바꿨다
;[통상] 스타일의 직격 보너스
IF FSTYLE_NAME_F(TARGET,TCVARn:0) == "통상" && ARGS != "HURIHODOKU"
	LOCAL:5 += 5
[SKIPSTART]
;[지략] 스타일의 직격 보너스
ELSEIF FSTYLE_NAME_F(TARGET,TCVARn:0) == "지략" && ARGS != "HURIHODOKU"
	;캐릭터의 지성을 본다
	LOCAL:6 = MAXBASE:지성
	CALL CLOTH_BATTLE_HOSEI, "CHISEI"
	LOCAL:6 = LOCAL:6 * RESULT / 100
	CALL SHINKYOU_CHECK, "CHISEI", LOCAL:6
	LOCAL:6 = RESULT
	LOCAL:5 += LIMIT(LOCAL:6 * LOCAL:6 / 4000, 0, 50)
[SKIPEND]
;[광역] 스타일의 직격 패널티
ELSEIF FSTYLE_NAME_F(TARGET,TCVARn:0) == "광역" && ARGS != "HURIHODOKU"
	TIMES LOCAL:5, 0.25
;[전력] 스타일의 직격 패널티
ELSEIF FSTYLE_NAME_F(TARGET,TCVARn:0) == "전력" && ARGS != "HURIHODOKU"
	LOCAL:5 = MAX(LOCAL:5 - CFLAG:99 + 15,LOCAL:5 / 2 + 15)
ENDIF
;의상에 의한 직격 보정
CALL CLOTH_BATTLE_HOSEI, "HIT"
LOCAL:5 += RESULT
;FEAT에 의한 직격률 수정
SIF TALENT:공세구축 > 0
	LOCAL:5 += 6
SIF TALENT:숨겨진힘 > 0 && PERCENT_CAL_F(BASE:체력 + BASE:기력, MAXBASE:체력 + MAXBASE:기력) <= 25
	LOCAL:5 += 12
SIF TALENT:심안 > 0
	LOCAL:5 += 3
SIF TALENT:공생 > 0
	LOCAL:5 += 2
;슈퍼 스코프에 의한 상승#####
SIF CFLAG:TARGET:43 == 501 && ARGS != "HURIHODOKU"
	LOCAL:5 += 10
;BRAVE HIT(동일 거리에서 연속 공격)에 의한 직격 보너스
SIF TFLAG:30 > 1 && ARGS != "HURIHODOKU"
	LOCAL:5 += GET_BRAVE_HIT(TFLAG:30 - 1,"명중")
;COUNTER ATTACK에 의한 직격 보너스
SIF TFLAG:32 > 0 && ARGS != "HURIHODOKU"
	LOCAL:5 += GET_COUNTER_ATTACK(1)
;공중전특기에 의한 직격 보너스
SIF GETBIT(TCVARn:216, 1) && TALENT:공중전특기 && ARGS != "HURIHODOKU"
	LOCAL:5 += GET_AIR_STRIKE(2)
;공중 성능 강화에 따른 직격 보너스
CALL CLOTH_BATTLE_HOSEI, "AIRPLUS",TARGET
SIF GETBIT(TCVARn:216, 1) && RESULT > 0 && ARGS != "HURIHODOKU"
	LOCAL:5 += 25
;공중전서툼에 의한 직격 패널티
SIF GETBIT(TCVARn:216, 1) && TALENT:공중전서툼 && ARGS != "HURIHODOKU"
	LOCAL:5 -= GET_AIR_STRIKE(3)
;반격, 강화 반격의 직격 보너스
SIF TCVARn:2 == 자세：반격
	LOCAL:5 += 10
SIF TCVARn:2 == 자세：강화반격
	LOCAL:5 += 20
;버스트 공격에 의한 직격률 수정
IF GETBIT(TCVARn:217,0)
	SELECTCASE FSTYLE_NAME_F(TARGET, TCVARn:0)
		CASE "장갑"
			LOCAL:5 = LOCAL:5 * 125 / 100 + 5
		CASE "중격"
			LOCAL:5 = LOCAL:5 * 90 / 100 - 5
		CASE "광역"
			LOCAL:5 = 100
		CASE "전력"
			LOCAL:5 = LOCAL:5 + 10 - CFLAG:99
		CASE "사역"
			LOCAL:5 = LOCAL:5 * 90 / 100 - 5
		CASE "통상"
			LOCAL:5 = LOCAL:5 * 125 / 100 + 5
	ENDSELECT
ENDIF

;DEBUG
IF FLAG:999 == 1 && ARG == 0
	IF ARGS == "HURIHODOKU"
		PRINTFORML 캐릭터의 방어 {MAXBASE:방어} {LOCAL:2}
	ELSE
		PRINTFORML 캐릭터의 민첩 {MAXBASE:민첩} {LOCAL:2}
	ENDIF
	SIF ARGS == "HURIHODOKU"
		PRINTFORML 캐릭터의 지성 {MAXBASE:지성} {CHISEI_COR}
	PRINTFORML 적의 민첩　 {LOCAL:3}
	SIF TCVARn:2 == 자세：반격
		PRINTL 반격 직격률 보너스 ＋10％
	SIF TCVARn:2 == 자세：강화반격
		PRINTL 강화 반격 직격률 보너스 ＋20％
	IF ARGS == "HURIHODOKU"
		PRINTFORML 행동 성공치：{LOCAL:5 - MIN(CHISEI_COR / 15,30)} {LOCAL:5}
	ELSE
		PRINTFORML 행동 성공치：{LOCAL:5}
	ENDIF
	PRINTL 
ENDIF
SIF LOCAL:5 < 0
	LOCAL:5 = 0


IF RAND:100 < LOCAL:5
	RETURN 1, LOCAL:5
ELSE
	RETURN 0, LOCAL:5
ENDIF

RETURN RESULT, LOCAL:5

;--------------------------------------------------
;가드 대미지의 판정: 캐릭터 쪽
;역) カス当たり를 "가드 대미지"로 의역하였으며, 이보다 더 잘 어울리는 번역명을 추천해 주신다면 감사하겠습니다
@ACT_HANTEI_CHARA_TO_TENTACLE_GUARD, ARGS
;캐릭터의 체력＋기력의 잔량을 구한다
LOCAL:0 = PERCENT_CAL_F(BASE:체력 * 2 + BASE:기력, MAXBASE:체력 * 2 + MAXBASE:기력)
;소모도 최저치는 방어력 의존
SIF LOCAL:0 < MIN(SQRT(MAX(BASE:방어 - 100, 0)) * 8, 75)
	LOCAL:0 = MIN(SQRT(MAX(BASE:방어 - 100, 0)) * 8, 75)
;FEAT 효과에 의한 소모도 경감
SIF TALENT:불굴 > 0
	LOCAL:0 = LOCAL:0 + (100 - LOCAL:0) / 2
;SP변신 중에는 기력 및 체력의 감소로 인한 영향을 받지 않는다
SIF CFLAG:1 == 2
	LOCAL:0 = 100

;적의 체력 잔량에 의한 보정
IF PERCENT_CAL_F(FLAG:13, FLAG:12) > 50
	LOCAL:1 = 100
ELSEIF PERCENT_CAL_F(FLAG:13, FLAG:12) > 25
	LOCAL:1 = 105
ELSE
	LOCAL:1 = 110
ENDIF
;적의 방심도에 의한 보정
SIF PERCENT_CAL_F(FLAG:17, FLAG:16)
	LOCAL:1 = LOCAL:1 * (100 + PERCENT_CAL_F(FLAG:17, FLAG:16) / 2) / 100

;캐릭터의 민첩성에 따른 고정값
LOCAL:2 = MAXBASE:민첩

;왜소함・장신이라면 보정
SIF TALENT:왜소함 == 1
	TIMES LOCAL:2, 1.05
SIF TALENT:장신 == 1
	TIMES LOCAL:2, 0.90

IF FLAG:111 == 0
	;촉수의 민첩
	CALL TENTACLE_ACCESS, "BINSYOU"
	LOCAL:3 = RESULT
ELSE
	;적 캐릭터의 민첩
	LOCAL:3 = MAXBASE:(FLAG:111):민첩

	;적 캐릭터가 왜소함・장신이라면 민첩에 보정
	SIF TALENT:(FLAG:111):왜소함 == 1
		TIMES LOCAL:3, 1.05
	SIF TALENT:(FLAG:111):장신 == 1
		TIMES LOCAL:3, 0.90

	;적 캐릭터의 전투 기술에 따른 보정
	LOCAL:3 = CORRECTION_SENGI_AVOID(LOCAL:3, 0, FLAG:111)
ENDIF

;민첩의 차이에 따른 보정
SIF FLAG:999 == 1
	PRINTFORML 민첩차 {PERCENT_CAL_F(LOCAL:2, LOCAL:3)}％
LOCAL:4 = CORRECTION_BINSYOU_F(PERCENT_CAL_F(LOCAL:2, LOCAL:3)) / 2 + 50

;성공치 계산
LOCAL:5 = LOCAL:0 * LOCAL:1 * LOCAL:4 / 10000

;DEBUG 모드일 시 세부 사항 표시
IF FLAG:999 == 1
	PRINTL 가드 대미지 기초 성공치 계산
	PRINTFORML LOCAL:0 ＝ {LOCAL:0}
	PRINTFORML LOCAL:1 ＝ {LOCAL:1}
	PRINTFORML LOCAL:2 ＝ {LOCAL:2}
	PRINTFORML LOCAL:3 ＝ {LOCAL:3}
	PRINTFORML LOCAL:4 ＝ {LOCAL:4}
	PRINTFORML LOCAL:5 ＝ {LOCAL:5}
ENDIF

;행동마다의 확률 보정
SELECTCASE ARGS
	CASE "ATTACK_RANGE_SHORT"
		;근거리 공격 보정：0.95배
		TIMES LOCAL:5, 0.95
		
		;전투 기술에 따른 보정
		LOCAL:5 = CORRECTION_SENGI(LOCAL:5, 0, TARGET)
		
		;공격 성공 최저치
		SIF LOCAL:5 < 35
			LOCAL:5 = 35
			
	CASE "ATTACK_RANGE_MIDDLE"

		;전투 기술에 따른 보정
		LOCAL:5 = CORRECTION_SENGI(LOCAL:5, 1, TARGET)

		;공격 성공 최저치
		SIF LOCAL:5 < 45
			LOCAL:5 = 45
			
	CASE "ATTACK_RANGE_LONG"
		;원거리 공격 보정：1.05배
		TIMES LOCAL:5, 1.05
		
		;전투 기술에 따른 보정
		LOCAL:5 = CORRECTION_SENGI(LOCAL:5, 2, TARGET)
		
		;공격 성공 최저치
		SIF LOCAL:5 < 50
			LOCAL:5 = 50
			
ENDSELECT

;[광역] 스타일의 가드 대미지 확률 보너스
SIF FSTYLE_NAME_F(TARGET,TCVARn:0) == "광역" && ARGS != "HURIHODOKU"
	LOCAL:5 += 35
;[전력] 스타일의 가드 대미지 확률 패널티
SIF FSTYLE_NAME_F(TARGET,TCVARn:0) == "전력" && ARGS != "HURIHODOKU"
	TIMES LOCAL:5, 0.85
;슈퍼 스코프에 의한 감소#####
SIF CFLAG:TARGET:43 == 501 && ARGS != "HURIHODOKU"
	LOCAL:5 -= 10

;EX공에 의한 명중률 상승 효과
SIF GETBIT(TCVARn:3, 0)
	LOCAL:5 += 100

;반격, 강화 반격의 가드 대미지 확률 보너스
SIF TCVARn:2 == 자세：반격
	LOCAL:5 += 15
SIF TCVARn:2 == 자세：강화반격
	LOCAL:5 += 25

;버스트 공격에 따른 가드 대미지 확률 수정
IF GETBIT(TCVARn:217,0)
	SELECTCASE FSTYLE_NAME_F(TARGET, TCVARn:0)
		CASE "전력"
			LOCAL:5 = 0
	ENDSELECT
ENDIF

;DEBUG
IF FLAG:999 == 1
	PRINTFORML 캐릭터의 민첩 {MAXBASE:민첩} {LOCAL:2}
	PRINTFORML 적의 민첩　 {LOCAL:3}
	SIF TCVARn:2 == 자세：반격
		PRINTL 반격 가드 대미지 확률 보너스 ＋15％
	SIF TCVARn:2 == 자세：강화반격
		PRINTL 강화 반격 가드 대미지 확률 보너스 ＋25％
	PRINTFORML 행동 성공률：{LOCAL:5}％
	PRINTL 
ENDIF

IF RAND:100 < LOCAL:5
	RETURN 1
ELSE
	RETURN 0
ENDIF

RETURN RESULT

;--------------------------------------------------
;행동의 성공 여부 판정：촉수 측（판정 성공=캐릭터가 공격을 회피=촉수의 공격이 미스된다）
@ACT_HANTEI_TENTACLE_TO_CHARA, ARGS

;캐릭터의 체력＋기력의 잔량을 구한다
LOCAL:0 = PERCENT_CAL_F(BASE:체력 * 2 + BASE:기력, MAXBASE:체력 * 2 + MAXBASE:기력)
;소모도의 최저치는 방어력 의존
SIF LOCAL:0 < MIN(SQRT(MAX(BASE:방어 - 100, 0)) * 8, 75)
	LOCAL:0 = MIN(SQRT(MAX(BASE:방어 - 100, 0)) * 8, 75)
;FEAT 효과에 의한 소모도 경감
SIF TALENT:불굴 > 0
	LOCAL:0 = LOCAL:0 + (100 - LOCAL:0) / 2
;마비로 잔량 반감
SIF (TCVARn:12 & 마비)
	LOCAL:0 /= 2
;SP변신 중에는 체력•기력의 감소에 의한 영향을 받지 않는다
SIF CFLAG:1 == 2
	LOCAL:0 = 100

;적의 체력 잔량에 따른 보정
IF PERCENT_CAL_F(FLAG:13, FLAG:12) > 50
	LOCAL:1 = 100
ELSEIF PERCENT_CAL_F(FLAG:13, FLAG:12) > 25
	LOCAL:1 = 105
ELSE
	LOCAL:1 = 110
ENDIF
;적의 방심도에 따른 보정
SIF PERCENT_CAL_F(FLAG:17, FLAG:16)
	LOCAL:1 = LOCAL:1 * (100 + PERCENT_CAL_F(FLAG:17, FLAG:16) / 2) / 100

;캐릭터의 민첩
LOCAL:2 = MAXBASE:민첩

;＠중국판 역수출
;흉부 중량 패널티
IF ISFEMALE() 
	IF TALENT:변신능력 == 1 && CFLAG:1 == 2
		LOCAL:2 -= LOCAL:2 * (1+MAXBASE:가슴무게) / (1+BASE:체중)
	ELSE
		LOCAL:2 -= LOCAL:2 * (1+BASE:가슴무게) / (1+BASE:체중)
	ENDIF
ENDIF

;왜소함・장신이라면 민첩에 보정
IF TALENT:왜소함 == 1
	TIMES LOCAL:2, 1.05
ELSEIF TALENT:장신 == 1
	TIMES LOCAL:2, 0.90
ENDIF

;캐릭터의 방어
LOCAL:6 = MAXBASE:방어
;「EX:방」중에는 방어치 상승
SIF GETBIT(TCVARn:3, 1)
	LOCAL:6 *= 2

;변신 능력이 있는 캐릭터의 보정
LOCAL:2 = CORRECTION_TRANS(LOCAL:2)
LOCAL:6 = CORRECTION_TRANS(LOCAL:6)

;의상에 따른 보정
CALL CLOTH_BATTLE_HOSEI, "BINSYOU"
LOCAL:2 = LOCAL:2 * RESULT / 100
CALL CLOTH_BATTLE_HOSEI, "BOUGYO"
LOCAL:6 = LOCAL:6 * RESULT / 100

;심경의 보정
CALL SHINKYOU_CHECK, "BINSYOU", LOCAL:2
LOCAL:2 = RESULT
CALL SHINKYOU_CHECK, "BOUGYO", LOCAL:6
LOCAL:6 = RESULT

;변신 행동에 따른 보정
SIF SELECTCOM == 0 ||  SELECTCOM == 73
	TIMES LOCAL:2, 1.50

;전투 기술에 따른 보정
LOCAL:2 = CORRECTION_SENGI_AVOID(LOCAL:2, 0, TARGET)
LOCAL:6 = CORRECTION_SENGI_AVOID(LOCAL:6, 0, TARGET)

IF FLAG:111 == 0
	;촉수의 민첩
	CALL TENTACLE_ACCESS, "BINSYOU"
	LOCAL:3 = RESULT
ELSE
	;적 캐릭터의 민첩
	LOCAL:3 = MAXBASE:(FLAG:111):민첩

	;적 캐릭터가 왜소함・장신이라면 민첩에 보정
	SIF TALENT:(FLAG:111):왜소함 == 1
		TIMES LOCAL:3, 1.05
	SIF TALENT:(FLAG:111):장신 == 1
		TIMES LOCAL:3, 0.90

	;적 캐릭터의 전투 기술에 따른 보정
	IF TCVARn:0 == 1
		LOCAL:3 = CORRECTION_SENGI(LOCAL:3, 0, FLAG:111)
	ELSEIF TCVARn:0 == 2
		LOCAL:3 = CORRECTION_SENGI(LOCAL:3, 1, FLAG:111)
	ELSEIF TCVARn:0 == 3
		LOCAL:3 = CORRECTION_SENGI(LOCAL:3, 2, FLAG:111)
	ENDIF
ENDIF

;적의 방심에 따른 보정
SIF TFLAG:2 >= 1
	TIMES LOCAL:3, 0.50

;공격 부위수에 따른 보정
SIF TFLAG:12 == 1
	TIMES LOCAL:3, 1.2
SIF TFLAG:12 == 3
	TIMES LOCAL:3, 0.8
;사악한 파동은 명중하기 쉽다
SIF ARGS == "AVOID_HADOU"
	TIMES LOCAL:3, 1.5

;민첩의 차이에 따른 보정
SIF FLAG:999 == 1
	PRINTFORML 민첩차 {PERCENT_CAL_F(LOCAL:2, LOCAL:3)}％
LOCAL:4 = CORRECTION_BINSYOU_F(PERCENT_CAL_F(LOCAL:2, LOCAL:3))


IF FLAG:111 == 0
	;촉수의 전투 기술에 따라 보정
	IF TCVARn:0 == 1
		CALL TENTACLE_ACCESS, "SHORT"
		LOCAL:4 = LOCAL:4 * 100 / RESULT
	ELSEIF TCVARn:0 == 2
		CALL TENTACLE_ACCESS, "MIDDLE"
		LOCAL:4 = LOCAL:4 * 100 / RESULT
	ELSEIF TCVARn:0 == 3
		CALL TENTACLE_ACCESS, "LONG"
		LOCAL:4 = LOCAL:4 * 100 / RESULT
	ENDIF
ENDIF

;성공치 계산
LOCAL:5 = LOCAL:0 * LOCAL:1 * LOCAL:4 / 10000

;의상에 의한 회피 보정
CALL CLOTH_BATTLE_HOSEI, "AVOID"
LOCAL:5 += RESULT
;가속 장치에 의한 회피율 상승#####
SIF CFLAG:TARGET:43 == 502
	LOCAL:5 += 5
;[중격] 스타일의 회피 패널티
SIF FSTYLE_NAME_F(TARGET,TCVARn:0) == "중격"
	TIMES LOCAL:5, 0.70
;BRAVE HIT에 의한 회피 패널티
SIF TFLAG:30 > 1
	LOCAL:5 -= GET_BRAVE_HIT(TFLAG:30 - 1,"회피")

;반격 시의 회피 보정
IF TCVARn:2 == 자세：반격
	TIMES LOCAL:5, 0.75
ELSEIF TCVARn:2 == 자세：강화반격
	TIMES LOCAL:5, 0.90
ENDIF

;거리에 따른 보정
;근거리라면 회피가 0.85배
;원거리라면 회피가 1.15배
IF TCVARn:0 == 1
	TIMES LOCAL:5, 0.85
ELSEIF TCVARn:0 == 3
	TIMES LOCAL:5, 1.15
ENDIF

IF FLAG:999 == 1
	PRINTL TENTACLE TO CHARA 기초 성공치 계산
	PRINTFORML LOCAL:0 ＝ {LOCAL:0}
	PRINTFORML LOCAL:1 ＝ {LOCAL:1}
	PRINTFORML LOCAL:2 ＝ {LOCAL:2}
	PRINTFORML LOCAL:3 ＝ {LOCAL:3}
	PRINTFORML LOCAL:4 ＝ {LOCAL:4}
	PRINTFORML LOCAL:5 ＝ {LOCAL:5}
	PRINTFORML LOCAL:6 ＝ {LOCAL:6}
ENDIF

;행동마다의 보정
SELECTCASE ARGS
	CASE "AVOID_KOUGEKI"
		;근거리
		;상한 80%
		IF TCVARn:0 == 1
			;최저치 30%
			SIF LOCAL:5 < MIN(LOCAL:6 / 15,30)
				LOCAL:5 = MIN(LOCAL:6 / 15,30)

			;최고치
			SIF LOCAL:5 > 80
				LOCAL:5 = 80

			;EX방에 의한 회피율 상승 효과
			SIF GETBIT(TCVARn:3, 1)
				LOCAL:5 += 35

		;중거리
		;상한 85%
		ELSEIF TCVARn:0 == 2
			;최저치 35%
			SIF LOCAL:5 < MIN(LOCAL:6 / 12,35)
				LOCAL:5 = MIN(LOCAL:6 / 12,35)

			;최고치
			SIF LOCAL:5 > 85
				LOCAL:5 = 85

			;EX방에 의한 회피율 상승 효과
			SIF GETBIT(TCVARn:3, 1)
				LOCAL:5 += 40

		;원거리
		;상한 90%
		ELSEIF TCVARn:0 == 3
			;최저치 40%
			SIF LOCAL:5 < MIN(LOCAL:6 / 10,40)
				LOCAL:5 = MIN(LOCAL:6 / 10,40)

			;최고치
			SIF LOCAL:5 > 90
				LOCAL:5 = 90

			;EX방에 의한 회피율 상승 효과
			SIF GETBIT(TCVARn:3, 1)
				LOCAL:5 += 45
		ENDIF

	CASE "AVOID_KARAMITUKU"
		;근거리
		;상한 70%
		IF TCVARn:0 == 1
			;최저치 30%
			SIF LOCAL:5 < MIN(LOCAL:6 / 15,30)
				LOCAL:5 = MIN(LOCAL:6 / 15,30)

			;최고치
			SIF LOCAL:5 > 70
				LOCAL:5 = 70

			;EX방에 의한 회피율 상승 효과
			SIF GETBIT(TCVARn:3, 1)
				LOCAL:5 += 35
		;중거리
		;상한 75%
		ELSEIF TCVARn:0 == 2
			;최저치 35%
			SIF LOCAL:5 < MIN(LOCAL:6 / 12,35)
				LOCAL:5 = MIN(LOCAL:6 / 12,35)
			;최고치
			SIF LOCAL:5 > 75
				LOCAL:5 = 75

			;EX방에 의한 회피율 상승 효과
			SIF GETBIT(TCVARn:3, 1)
				LOCAL:5 += 40

		;원거리
		;상한 80%
		ELSEIF TCVARn:0 == 3
			;최저치 40%
			SIF LOCAL:5 < MIN(LOCAL:6 / 10,40)
				LOCAL:5 = MIN(LOCAL:6 / 10,40)

			;최고치
			SIF LOCAL:5 > 80
				LOCAL:5 = 80

			;EX방에 의한 회피율 상승 효과
			SIF GETBIT(TCVARn:3, 1)
				LOCAL:5 += 45
		ENDIF

	CASE "AVOID_TAIEKI"
		;근거리
		;상한 85%
		IF TCVARn:0 == 1
			;최저치 40%
			SIF LOCAL:5 < MIN(LOCAL:6 / 10,40)
				LOCAL:5 = MIN(LOCAL:6 / 10,40)

			;최고치
			SIF LOCAL:5 > 85
				LOCAL:5 = 85

			;EX방에 의한 회피율 상승 효과
			SIF GETBIT(TCVARn:3, 1)
				LOCAL:5 += 40

		;중거리
		;상한 80%
		ELSEIF TCVARn:0 == 2
			;최저치 35%
			SIF LOCAL:5 < MIN(LOCAL:6 / 12,35)
				LOCAL:5 = MIN(LOCAL:6 / 12,35)

			;최고치
			SIF LOCAL:5 > 80
				LOCAL:5 = 80

			;EX방에 의한 회피율 상승 효과
			SIF GETBIT(TCVARn:3, 1)
				LOCAL:5 += 45

		;원거리
		;상한 75%
		ELSEIF TCVARn:0 == 3
			;최저치 30%
			SIF LOCAL:5 < MIN(LOCAL:6 / 15,30)
				LOCAL:5 = MIN(LOCAL:6 / 15,30)

			;최고치
			SIF LOCAL:5 > 75
				LOCAL:5 = 75

			;EX방에 의한 회피율 상승 효과
			SIF GETBIT(TCVARn:3, 1)
				LOCAL:5 += 50

		ENDIF

	CASE "AVOID_OSHITAOSU"
		;근거리
		;상한 70%
		IF TCVARn:0 == 1
			;최저치 30%
			SIF LOCAL:5 < MIN(LOCAL:6 / 15,30)
				LOCAL:5 = MIN(LOCAL:6 / 15,30)

			;최고치
			SIF LOCAL:5 > 70
				LOCAL:5 = 70

			;EX방에 의한 회피율 상승 효과
			SIF GETBIT(TCVARn:3, 1)
				LOCAL:5 += 35

		;중거리
		;상한 75%
		ELSEIF TCVARn:0 == 2
			;최저치 35%
			SIF LOCAL:5 < MIN(LOCAL:6 / 12,35)
				LOCAL:5 = MIN(LOCAL:6 / 12,35)
			;최고치
			SIF LOCAL:5 > 75
				LOCAL:5 = 75

			;EX방에 의한 회피율 상승 효과
			SIF GETBIT(TCVARn:3, 1)
				LOCAL:5 += 40

		;원거리
		;상한 80%
		ELSEIF TCVARn:0 == 3
			;최저치 40%
			SIF LOCAL:5 < MIN(LOCAL:6 / 10,40)
				LOCAL:5 = MIN(LOCAL:6 / 10,40)

			;최고치
			SIF LOCAL:5 > 80
				LOCAL:5 = 80

			;EX방에 의한 회피율 상승 효과
			SIF GETBIT(TCVARn:3, 1)
				LOCAL:5 += 45

		ENDIF

	CASE "AVOID_HADOU"
		;근거리
		;상한 75%
		IF TCVARn:0 == 1
			;최저치 40%
			SIF LOCAL:5 < MIN(LOCAL:6 / 10,40)
				LOCAL:5 = MIN(LOCAL:6 / 10,40)

			;최고치
			SIF LOCAL:5 > 75
				LOCAL:5 = 75

			;EX방에 의한 회피율 상승 효과
			SIF GETBIT(TCVARn:3, 1)
				LOCAL:5 += 55

		;중거리
		;상한 70%
		ELSEIF TCVARn:0 == 2
			;최저치 35%
			SIF LOCAL:5 < MIN(LOCAL:6 / 12,35)
				LOCAL:5 = MIN(LOCAL:6 / 12,35)

			;최고치
			SIF LOCAL:5 > 70
				LOCAL:5 = 70

			;EX방에 의한 회피율 상승 효과
			SIF GETBIT(TCVARn:3, 1)
				LOCAL:5 += 60

		;원거리
		;상한 65%
		ELSEIF TCVARn:0 == 3
			;최저치 30%
			SIF LOCAL:5 < MIN(LOCAL:6 / 15,30)
				LOCAL:5 = MIN(LOCAL:6 / 15,30)

			;최고치
			SIF LOCAL:5 > 65
				LOCAL:5 = 65

			;EX방에 의한 회피율 상승 효과
			SIF GETBIT(TCVARn:3, 1)
				LOCAL:5 += 65

		ENDIF
				
		;파동 공격은 반격 자세일 경우 회피율이 더 감소
		SIF TCVARn:2 == 자세：반격 || TCVARn:2 == 자세：강화반격
			LOCAL:5 -= 10

ENDSELECT

;대공 공격에 대하여 공중에 있으면 회피율 감소
SIF GETBIT(TCVARn:216, 1) && (TFLAG:11 & 8) && RAND:100 < 25
	LOCAL:5 = 0

;착지 중에는 회피율 0%（거리에 따른 빗나감(空振り)은 발생한다）
;FEAT에 의한 착지 패널티 회피
SIF GETBIT(TCVARn:216, 2) && TALENT:소형체구 == 0
	LOCAL:5 = 0

;반격 자세일 경우 최종 회피율 감소
;SIF TCVARn:2 == 자세：반격 || TCVARn:2 == 자세：강화반격
;	LOCAL:5 -= 20

;버스트 공격에 따른 회피율 수정
IF GETBIT(TCVARn:217,0)
	SELECTCASE FSTYLE_NAME_F(TARGET, TCVARn:0)
		CASE "광역"
			LOCAL:5 = 0
		CASE "전력"
			LOCAL:5 = 0
		;버스트 공격 리워크 필요
		;CASE "반격"
		;	LOCAL:5 = 0
	ENDSELECT
ENDIF

;공격한 장소에 캐릭터가 없다
SIF (TCVARn:0 == 1 && GETBIT(TFLAG:11, 0) == 0) || (TCVARn:0 == 2 && GETBIT(TFLAG:11, 1) == 0) || (TCVARn:0 == 3 && GETBIT(TFLAG:11, 2) == 0)
	LOCAL:5 = -1

;통상 범위에 대하여 공중에 있다
;[에어마스터] FEAT에 대한 추가 효과 발동 금지 플래그를 세운다
IF GETBIT(TCVARn:216, 1) && GETBIT(TFLAG:11, 3) == 0 && RAND:100 < GET_AIR_STRIKE(1)
	LOCAL:5 = -1
	TCVARn:210 |= 추가발동금지
ENDIF

;【아무것도안한다】를 골랐다면 확정적으로 피격된다
SIF TCVARn:2 == 자세：아무것도안한다
	LOCAL:5 = 0

IF FLAG:999 == 1
	PRINTFORML 캐릭터의 민첩 {MAXBASE:민첩} {LOCAL:2}
	PRINTFORML 캐릭터의 방어 {MAXBASE:방어} {LOCAL:6}
	PRINTFORML 적의 민첩　 {LOCAL:3}
	PRINTFORML 행동 성공치：{LOCAL:5}
	PRINTL 
ENDIF

IF LOCAL:5 == -1
	RETURN 2
ELSEIF RAND:100 < LOCAL:5
	RETURN 1
ELSE
	;FEAT 효과에 의한 절대 회피
	IF TALENT:고속회피 > 0 && TFLAG:80 == 0 && TCVARn:2 != 자세：아무것도안한다
		TFLAG:80 += 1
		SETCOLOR 255,128,0
		PRINTFORMW [고속회피] 가 발동, 1회의 공격을 무조건 회피한다！
		RESETCOLOR
		PRINTL 
		RETURN 1
	ENDIF
	RETURN 0
ENDIF

RETURN RESULT

;--------------------------------------------------
;철수의 성공 여부 판정
@ACT_HANTEI_TETTAI_TENTACLE

;캐릭터의 체력＋기력의 잔량을 구한다
LOCAL:0 = PERCENT_CAL_F(BASE:체력 * 2 + BASE:기력, MAXBASE:체력 * 2 + MAXBASE:기력) + 20
;소모도의 최저치는 방어력 의존
SIF LOCAL:0 < MIN(SQRT(MAX(BASE:방어 - 100, 0)) * 8, 75) + 20
	LOCAL:0 = MIN(SQRT(MAX(BASE:방어 - 100, 0)) * 8, 75) + 20
;FEAT 효과에 의한 소모도 경감
SIF TALENT:불굴 > 0
	LOCAL:0 = LOCAL:0 + (100 - LOCAL:0) / 2
;끈적끈적, 쓰러짐으로 잔량 반감
SIF (TCVARn:12 & 끈적끈적) || (TCVARn:12 & 쓰러짐)
	LOCAL:0 /= 2
;SP변신 중에는 체력•기력의 감소에 의한 영향을 받지 않는다
SIF CFLAG:1 == 2
	LOCAL:0 = 120

;적의 체력 잔량에 따른 보정
IF PERCENT_CAL_F(FLAG:13, FLAG:12) > 50
	LOCAL:1 = 100
ELSEIF PERCENT_CAL_F(FLAG:13, FLAG:12) > 25
	LOCAL:1 = 120
ELSE
	LOCAL:1 = 140
ENDIF
;적의 방심도에 따른 보정
SIF PERCENT_CAL_F(FLAG:17, FLAG:16)
	LOCAL:1 = LOCAL:1 * (100 + PERCENT_CAL_F(FLAG:17, FLAG:16)) / 100

;캐릭터의 민첩
LOCAL:2 = MAXBASE:민첩

;왜소함・장신이라면 민첩에 보정
SIF TALENT:왜소함 == 1
	TIMES LOCAL:2, 1.05
SIF TALENT:장신 == 1
	TIMES LOCAL:2, 0.90

;변신 능력이 있는 캐릭터의 보정
LOCAL:2 = CORRECTION_TRANS(LOCAL:2)

;＠중국판
;흉부 중량 패널티
IF ISFEMALE() 
	IF TALENT:변신능력 == 1 && CFLAG:1 == 2
		LOCAL:2 -= LOCAL:2 * (1+MAXBASE:가슴무게) / (1+BASE:체중)
	ELSE
		LOCAL:2 -= LOCAL:2 * (1+BASE:가슴무게) / (1+BASE:체중)
	ENDIF
ENDIF

;의상에 따른 보정
CALL CLOTH_BATTLE_HOSEI, "BINSYOU"
LOCAL:2 = LOCAL:2 * RESULT / 100

;심경의 보정
CALL SHINKYOU_CHECK, "BINSYOU", LOCAL:2
LOCAL:2 = RESULT

IF FLAG:111 == 0
	;촉수의 민첩
	CALL TENTACLE_ACCESS, "BINSYOU"
	LOCAL:3 = RESULT
ELSE
	;적 캐릭터의 민첩
	LOCAL:3 = MAXBASE:(FLAG:111):민첩

	;적 캐릭터가 왜소함이라면 민첩에 보정
	SIF TALENT:(FLAG:111):왜소함 == 1
		TIMES LOCAL:3, 1.05
ENDIF

;적의 방심에 따른 보정
SIF TFLAG:2 >= 1
	TIMES LOCAL:3, 0.50

;민첩의 차이에 따른 보정
SIF FLAG:999 == 1
	PRINTFORML 민첩차 {PERCENT_CAL_F(LOCAL:2, LOCAL:3)}％
LOCAL:4 = CORRECTION_BINSYOU_F(PERCENT_CAL_F(LOCAL:2, LOCAL:3))


;성공치 계산
LOCAL:5 = LOCAL:0 * LOCAL:1 * LOCAL:4 / 10000

;행동마다의 보정
;철수 성공 최저치
SIF LOCAL:5 < 50
	LOCAL:5 = 50
;전투 지원 효과
LOCAL:5 += 20 * FLAG:43

;관객이 있으면 확정 실패
SIF FLAG:70 + FLAG:71 > 0
	LOCAL:5 = 0

IF FLAG:999 == 1
	PRINTFORML 캐릭터의 민첩 {MAXBASE:민첩} {LOCAL:2}
	PRINTFORML 적의 민첩　 {LOCAL:3}
	PRINTFORML 행동 성공치：{LOCAL:5}
	PRINTL 
	PRINTFORML 도주 성공률을 입력해 주십시오.（음수 입력으로 변경 없음）
	INPUT
	SIF RESULT >= 0
		LOCAL:5 = RESULT
ENDIF

IF RAND:100 < LOCAL:5
	RETURN 1
ELSEIF ENEMY_TYPE_CHECK_F("CITIZEN") == 1
	RETURN 1
ELSE
	RETURN 0
ENDIF

RETURN RESULT

;--------------------------------------------------
;피해량 계산
@DAMAGE, ARGS

;실공격력
CALL FSTYLE_ATTACK,TARGET,TCVARn:0
LOCAL:5 = RESULT
;「EX공」중에는 공격치 상승
IF GETBIT(TCVARn:3, 0)
	IF CFLAG:1 == 2
		LOCAL:5 = LOCAL:5 * 150 / 100
	ELSE
		LOCAL:5 = LOCAL:5 * 200 / 100
	ENDIF
ENDIF
;변신 능력이 있는 캐릭터의 보정
LOCAL:5 = CORRECTION_TRANS(LOCAL:5)

;＠중국판
;유두피어싱 공격 보너스
;　★장난스런 보정이 되어 있는데 이거 괜찮은거야？
;　　(대응 이벤트 구현 시 조정)
IF TALENT:유두피어싱 == 1 ;1급 유두피어싱 보정 1.4배
	TIMES LOCAL:5, 1.40
ELSEIF TALENT:유두피어싱 == 2 ;2급 유두피어싱 보정 1.6배
	TIMES LOCAL:5, 1.60
ELSEIF TALENT:유두피어싱 == 3 ;3급 유두피어싱 보정 1.8배
	TIMES LOCAL:5, 1.80
ELSEIF TALENT:유두피어싱 == 4 ;4급 유두피어싱 보정 2.0배
	TIMES LOCAL:5, 2.00
ELSEIF TALENT:유두피어싱 == 5 ;5급 유두피어싱 보정 5배
	TIMES LOCAL:5, 5.00
ENDIF
;거유 공격 보너스
IF ISFEMALE() 
	;SP변신 중에는 변신 시 거유화의 보너스가 타는적인 조정이라고 생각된다(아마도)
	IF TALENT:변신능력 == 1 && CFLAG:1 == 2
		LOCAL:5 += LOCAL:5 * (1 + MAXBASE:가슴무게) / (1 + BASE:체중)
	ELSE
		LOCAL:5 += LOCAL:5 * (1 + BASE:가슴무게) / (1 + BASE:체중)
	ENDIF
ENDIF


;방어력
LOCAL:6 = MAXBASE:방어
;「EX방」중에는 방어치 상승
SIF GETBIT(TCVARn:3, 1)
	LOCAL:6 = LOCAL:6 * 200 / 100
;변신 능력이 있는 캐릭터의 보정
LOCAL:6 = CORRECTION_TRANS(LOCAL:6)

;의상에 따른 보정
CALL CLOTH_BATTLE_HOSEI, "BOUGYO"
LOCAL:6 = LOCAL:6 * RESULT / 100

;[교란] 스타일의 방어 패널티
IF FSTYLE_NAME_F(TARGET,TCVARn:0) == "교란"
	TIMES LOCAL:6, 0.85
;[장갑] 스타일의 방어 보너스
ELSEIF FSTYLE_NAME_F(TARGET,TCVARn:0) == "장갑"
	LOCAL:6 += 50
	TIMES LOCAL:6, 1.15
;[반격] 스타일의 버스트 반격 효과(방어 50% 감소)
;버스트 공격 리워크 필요
;ELSEIF GETBIT(TCVARn:217, 0) && FSTYLE_NAME_F(TARGET,TCVARn:0) == "반격"
;	TIMES LOCAL:6, 0.50
ENDIF

SELECTCASE ARGS
	CASE "ATTACK_RANGE_SHORT"
		;거리에 따른 공격력 보정（근） 2.50배의 보정이 들어간다
		CALL SHINKYOU_CHECK, "KOUGEKI", LOCAL:5
		LOCAL:0 = RESULT
		TIMES LOCAL:0, 2.50

		;전투 기술에 따른 공격력 보정
		LOCAL:0 = CORRECTION_SENGI_DAMAGE(LOCAL:0, 0, TARGET)

		IF FLAG:111 == 0
			;촉수의 방어력
			CALL TENTACLE_ACCESS, "BOUGYO"
			LOCAL:1 = RESULT
		ELSE
			;적 캐릭터의 방어력
			LOCAL:1 = MAXBASE:(FLAG:111):방어
		ENDIF

		;적의 방심에 따른 보정
		SIF TFLAG:2 >= 1
			TIMES LOCAL:1, 0.50

		;특기・서툼에 따른 보정
		SIF TALENT:근거리특기
			TIMES LOCAL:0, 1.20
		SIF TALENT:근거리서툼
			TIMES LOCAL:0, 0.60
		SIF TALENT:중거리특기
			TIMES LOCAL:0, 0.85
		SIF TALENT:중거리서툼
			TIMES LOCAL:0, 1.15
		SIF TALENT:원거리특기
			TIMES LOCAL:0, 0.85
		SIF TALENT:원거리서툼
			TIMES LOCAL:0, 1.10
		SIF TALENT:근거리특기 && TALENT:중거리특기 && TALENT:원거리특기
			TIMES LOCAL:0, 1.16

	CASE "ATTACK_RANGE_MIDDLE"
		;거리에 따른 공격력 보정（중） 2.25배의 보정이 들어간다
		CALL SHINKYOU_CHECK, "KOUGEKI", LOCAL:5
		LOCAL:0 = RESULT
		TIMES LOCAL:0, 2.25

		;전투 기술에 따른 공격력 보정
		LOCAL:0 = CORRECTION_SENGI_DAMAGE(LOCAL:0, 1, TARGET)

		IF FLAG:111 == 0
			;촉수의 방어력
			CALL TENTACLE_ACCESS, "BOUGYO"
			LOCAL:1 = RESULT
		ELSE
			;적 캐릭터의 방어력
			LOCAL:1 = MAXBASE:(FLAG:111):방어
		ENDIF

		;적의 방심에 따른 보정
		SIF TFLAG:2 >= 1
			TIMES LOCAL:1, 0.50

		;특기・서툼에 따른 보정
		SIF TALENT:근거리특기
			TIMES LOCAL:0, 0.90
		SIF TALENT:근거리서툼
			TIMES LOCAL:0, 1.20
		SIF TALENT:중거리특기
			TIMES LOCAL:0, 1.25
		SIF TALENT:중거리서툼
			TIMES LOCAL:0, 0.70
		SIF TALENT:원거리특기
			TIMES LOCAL:0, 0.90
		SIF TALENT:원거리서툼
			TIMES LOCAL:0, 1.10
		SIF TALENT:근거리특기 && TALENT:중거리특기 && TALENT:원거리특기
			TIMES LOCAL:0, 1.085

	CASE "ATTACK_RANGE_LONG"
		;거리에 따른 공격력 보정（원） 2.00배의 보정이 들어간다
		CALL SHINKYOU_CHECK, "KOUGEKI", LOCAL:5
		LOCAL:0 = RESULT
		TIMES LOCAL:0, 2.00

		;전투 기술에 따른 공격력 보정
		LOCAL:0 = CORRECTION_SENGI_DAMAGE(LOCAL:0, 2, TARGET)

		IF FLAG:111 == 0
			;촉수의 방어력
			CALL TENTACLE_ACCESS, "BOUGYO"
			LOCAL:1 = RESULT
		ELSE
			;적 캐릭터의 방어력
			LOCAL:1 = MAXBASE:(FLAG:111):방어
		ENDIF

		;적의 방심에 따른 보정
		SIF TFLAG:2 >= 1
			TIMES LOCAL:1, 0.50

		;특기・서툼에 따른 보정
		SIF TALENT:근거리특기
			TIMES LOCAL:0, 0.95
		SIF TALENT:근거리서툼
			TIMES LOCAL:0, 1.20
		SIF TALENT:중거리특기
			TIMES LOCAL:0, 0.95
		SIF TALENT:중거리서툼
			TIMES LOCAL:0, 1.10
		SIF TALENT:원거리특기
			TIMES LOCAL:0, 1.30
		SIF TALENT:원거리서툼
			TIMES LOCAL:0, 0.80
		SIF TALENT:근거리특기 && TALENT:중거리특기 && TALENT:원거리특기
			TIMES LOCAL:0, 1.0275

	CASE "ABARERU"
		;공격력 보정（無） 잡몹 촉수는 0.125배, 보스 촉수는 1.00배, 적 캐릭터라면 2.00배의 보정이 들어간다
		CALL SHINKYOU_CHECK, "KOUGEKI", LOCAL:5
		LOCAL:0 = RESULT
		IF ENEMY_TYPE_CHECK_F("MOB") == 1
			TIMES LOCAL:0, 0.125
		ELSEIF FLAG:111 == 0
			TIMES LOCAL:0, 1.00
		ELSE
			TIMES LOCAL:0, 2.00
		ENDIF

		IF FLAG:111 == 0
			;촉수의 방어력 반감
			CALL TENTACLE_ACCESS, "BOUGYO"
			LOCAL:1 = RESULT / 2

			;촉수의 뱡심에 따른 보정
			SIF TFLAG:2 >= 1
				TIMES LOCAL:1, 0.50
		ENDIF

		;【날뛴다】일 경우「EX:공」의 효과가 더욱이 2배
		SIF GETBIT(TCVARn:3, 0)
			LOCAL:1 *= 2

	CASE "CHARA"
		IF FLAG:111 == 0
			;촉수의 공격력
			CALL TENTACLE_ACCESS, "KOUGEKI"
			LOCAL:0 = RESULT
		ELSE
			;적 캐릭터의 공격력
			LOCAL:0 = MAXBASE:(FLAG:111):공격

			;적 캐릭터의 전투 기술에 따른 보정
			IF TCVARn:0 == 1
				LOCAL:0 = CORRECTION_SENGI_DAMAGE(LOCAL:0, 0, FLAG:111)
			ELSEIF TCVARn:0 == 2
				LOCAL:0 = CORRECTION_SENGI_DAMAGE(LOCAL:0, 1, FLAG:111)
			ELSEIF TCVARn:0 == 3
				LOCAL:0 = CORRECTION_SENGI_DAMAGE(LOCAL:0, 2, FLAG:111)
			ENDIF
		ENDIF
		
		;상대방의 방심에 따른 보정
		SIF TFLAG:2 >= 1
			TIMES LOCAL:0, 0.75
		
		;거리에 따른 공격력 보정
		CALL TENTACLE_LEVEL
		IF TCVARn:0 == 1
			LOCAL:3 = 75 + RESULT * 2
		ELSEIF TCVARn:0 == 2
			LOCAL:3 = 75 + RESULT * 2
		ELSEIF TCVARn:0 == 3
			LOCAL:3 = 75 + RESULT * 2
		ELSEIF TCVARn:0 == 0
			LOCAL:3 = (75 + RESULT) / 2
		ENDIF
		LOCAL:0 = LOCAL:0 * LOCAL:3 / 100
		
		;공격 부위수에 따른 공격력 보정
		SIF TFLAG:12 == 2
			TIMES LOCAL:0, 0.90
		SIF TFLAG:12 == 3
			TIMES LOCAL:0, 0.80
		
		;캐릭터의 방어력
		CALL SHINKYOU_CHECK, "BOUGYO", LOCAL:6
		LOCAL:1 = RESULT

		;방어 플래그가 세워져 있는 경우에는 피해량 반감
		SIF TCVARn:2 == 자세：방어
			LOCAL:0 /= 2
		;반격 플래그가 세워져 있는 경우에는 피해량 감소
		;전체 공격일 경우 피해량 감소 효과 저하
		;구속 공격은 불가능
		IF TCVARn:2 == 자세：반격 && (TFLAG:10 != 2 || 5)
			IF TFLAG:12 < 3
				TIMES LOCAL:0, 0.65
			ELSE
				TIMES LOCAL:0, 0.85
			ENDIF
		ENDIF
		;피격될 때마다, 방어 기초값에 따라 ＥＸ게이지가 상승한다
		SIF CFLAG:1 != 2
			TCVARn:6 += LIMIT(SQRT(MAX(BASE:방어 - 100,0)), 0, 20) + 5
ENDSELECT

;피해량 계산
;0.26 버전까지 쓰이던 식　최소 피해량은 10
;LOCAL:3 = LIMIT(LOCAL:0 - LOCAL:1, 10, LOCAL:0 - LOCAL:1)

;0.27 버전부터 쓰이는 식　최소 피해량은 80
;기본 피해량 500 * 공격력 수정(공격/방어)
;LOCAL:6 = 배율 보정치
LOCAL:6 = 100

LOCAL:3 = MAX(500000 * (LOCAL:0 + LOCAL:6) / (LOCAL:1 + LOCAL:6) / 1000, 80)

;방어 기초값에 따른 직접 감산
SIF ARGS == "CHARA"
	LOCAL:3 = MAX(LOCAL:3 + 100 - (LOCAL:1 - LEVELSTATUS_UP(100,ABL:레벨,50,9999)) * 175 / 100, 80)

;버스트 공격에 따른 입히는 피해량 수정
;(원문) バースト공격による与ダメージ修正
IF GETBIT(TCVARn:217,0) && ARGS != "CHARA"
	SELECTCASE FSTYLE_NAME_F(TARGET, TCVARn:0)
		CASE "연속"
			LOCAL:3 = LOCAL:3 * 40 / 100
		CASE "교란"
			LOCAL:3 = LOCAL:3 * 110 / 100
		CASE "중격"
			LOCAL:3 = LOCAL:3 * 150 / 100
		CASE "전력"
			LOCAL:3 = LOCAL:3 * 150 / 100
		CASE "사역"
			LOCAL:3 = LOCAL:3 * 300 / 100
		CASE "통상"
			LOCAL:3 = LOCAL:3 * 110 / 100
	ENDSELECT
	;FEAT 효과에 의한 버스트 위력 상승
	SIF TALENT:풀버스트 > 0
		LOCAL:3 = LOCAL:3 * 125 / 100
ENDIF

;버스트 공격에 따른 받는 피해량 수정
;(원문) バースト공격による被ダメージ修正
IF GETBIT(TCVARn:217,0) && ARGS == "CHARA"
	SELECTCASE FSTYLE_NAME_F(TARGET, TCVARn:0)
		CASE "전력"
			LOCAL:3 = LOCAL:3 * 125 / 100
	ENDSELECT
ENDIF

;CHAIN HIT에 의한 피해량 보너스
SIF TFLAG:31 > 1 && ARGS != "CHARA"
	LOCAL:3 = LOCAL:3 * GET_CHAIN_HIT(TFLAG:31 - 1) / 100
;COUNTER ATTACK에 의한 피해량 경감
SIF TFLAG:32 > 0 && ARGS == "CHARA"
	LOCAL:3 = LOCAL:3 * (100 - GET_COUNTER_ATTACK(2)) / 100
;EVADER에 의한 피해량 보너스
SIF TFLAG:33 > 1 && ARGS != "CHARA"
	LOCAL:3 = LOCAL:3 * ((GET_EVADER_ATK(TFLAG:33 - 1) - 100) / 2 + 100) / 100

;피로에 따른 피해량 패널티
IF CFLAG:99 > 0 && ARGS != "CHARA"
	IF CFLAG:99 >= 50
		LOCAL:7 = 750
	ELSEIF CFLAG:99 >= 30
		LOCAL:7 = 500
	ELSEIF CFLAG:99 >= 20
		LOCAL:7 = 250
	ENDIF
ELSE
	LOCAL:7 = 0
ENDIF
LOCAL:3 = LOCAL:3 * (1000 - LOCAL:7) / 1000

SIF FLAG:73 > 0
	LOCAL:3 = 1

LOCAL:4 = RAND:100
IF LOCAL:4 < 20
	TIMES LOCAL:3, 1.08
ELSEIF LOCAL:4 < 40
	TIMES LOCAL:3, 1.05
ELSEIF LOCAL:4 < 60
ELSEIF LOCAL:4 < 80
	TIMES LOCAL:3, 0.95
ELSE
	TIMES LOCAL:3, 0.92
ENDIF

;DEBUG
IF FLAG:999 == 1
	PRINTL 최종 피해량 계산
	PRINTFORML 캐릭터의 공격（보정 전{MAXBASE:공격}）（보정 후{LOCAL:5}）
	PRINTFORML 캐릭터의 방어（보정 전{MAXBASE:방어}）（보정 후{LOCAL:6}）
	PRINTFORML 공격 쪽의 공격력 {LOCAL:0}
	PRINTFORML 방어 쪽의 방어력 {LOCAL:1}
	PRINT 강화 반격에 의한 절대 방어
	IF TCVARn:2 == 자세：강화반격
		PRINTL O
	ELSE
		PRINTL X
	ENDIF
	PRINTFORML LOCAL:3 {LOCAL:3}
	SIF ARGS != "CHARA"
		PRINTFORML 피해량 패널티 {(LOCAL:7)/10}％
	PRINTL 
ENDIF

;강화 반격일 경우 절대 방어
SIF TCVARn:2 == 자세：강화반격
	LOCAL:3 = 0

RETURN LOCAL:3
