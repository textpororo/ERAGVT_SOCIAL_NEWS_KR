;--------------------------------------------------
;전투 시에 호출되는 UI 표시 처리
@SHOW_STATUS
#DIM CHISEI_CAL
#DIM CCOUNT
RESETCOLOR

;--------------------------------------------------
;여기서부터 표시
;--------------------------------------------------
;캐릭터의 기초 스테이터스
CALL SHOW_STATUS_BASE_DISPBATTLE, TARGET



PRINTL 

PRINT 상태　　　
IF ISMALE()
	SETCOLOR 0, 180, 205
	PRINT [남자]
	SETCOLOR 255, 180, 180
	SIF TALENT:오토코노코 > 0
		PRINT [오토코노코]
ELSE
	IF TALENT:처녀 > 0
		SETCOLOR 255, 0, 255
	ELSEIF TALENT:처녀 == 0
		SETCOLOR 120, 0, 120
	ELSE
		SETCOLOR 120, 120, 120
	ENDIF
	IF TALENT:처녀 > 1
		PRINT [성처녀]
	ELSEIF TALENT:처녀 > 0
		PRINT [처녀]
	ELSEIF TALENT:처녀 == 0 || TALENT:처녀 == -1
		PRINT [비처녀]
	ENDIF
	SETCOLOR 255,255,255
	SIF STAIN:0 & 4 || STAIN:1 & 4 || STAIN:2 & 4
		PRINTFORM [정액범벅]
	SETCOLOR 255, 180, 120
	SIF TALENT:후타나리 > 0
		PRINT [후타나리]
ENDIF
SETCOLOR 80, 80, 255
SIF CFLAG:1 == 1
	PRINT [변신]
SIF CFLAG:1 == 2
	PRINT [ＳＰ변신]

SETCOLOR 255, 128, 255
SIF TCVARn:40 > 0
	PRINT [절정금지]

IF CFLAG:99 < 20
	SETCOLOR 50, 255, 90
ELSEIF CFLAG:99 < 50
	SETCOLOR 40, 146, 73
ELSE
	SETCOLOR 30, 102, 36
ENDIF
SIF CFLAG:99
	PRINTFORM [피로 {CFLAG:99}]

SETCOLOR 75,150,250
SIF FLAG:70
	PRINTFORM [관중 {FLAG:70}명]
SETCOLOR 150,75,250
IF GETBATTLESITUATION("상시 촬영")
	IF GETBATTLESITUATION("전송")
		PRINTFORM [전송중]
	ELSE
		PRINTFORM [촬영중]
	ENDIF
ENDIF
SIF FLAG:71
	PRINTFORM [영상 촬영자 {FLAG:71}명]


SETCOLOR 250,75,15
;습격, 도망치지 못한 시민
IF GETBATTLESITUATION("철수 불가") == 1 || ((FLAG:70 + FLAG:71) > 0 && CONFIG_CHECK_EVENT_F(3) > 0)
	PRINTFORM [철수불능]
;악질 시민off인 동시에 통상 전투일 시
ELSEIF TFLAG:0 < 8 && CONFIG_CHECK_EVENT_F(3) == 0 && PERCENT_CAL_F(BASE:체력, MAXBASE:체력) > 50 && PERCENT_CAL_F(BASE:기력, MAXBASE:기력) > 50 && FLAG:45 == 0
	PRINTFORM [철수 가능까지 {8 - TFLAG:0}턴]
ENDIF

IF TCVARn:12 == 0
	SETCOLOR 150,150,250
	PRINTFORM [정상]
	;생리 주기 표시
	CALL PRINT_ESTRUS_CYCLE(TARGET)
ELSE
	SETCOLOR 250,180,50
	SIF (TCVARn:12 & 기절)
		PRINTFORM [기절]
	SETCOLOR 250,0,150
	SIF (TCVARn:12 & 발정)
		PRINTFORM [발정]
	SETCOLOR 250,250,0
	SIF (TCVARn:12 & 마비)
		PRINTFORM [마비]
	SETCOLOR 0,250,150
	SIF (TCVARn:12 & 끈적끈적)
		PRINTFORM [끈적끈적]

	;생리 주기 표시
	CALL PRINT_ESTRUS_CYCLE(TARGET)

	SETCOLOR 150,0,250
	SIF (TCVARn:12 & 쓰러짐)
		PRINTFORM [쓰러짐]
	SETCOLOR 255,182,193
	SIF (TCVARn:12 & 황홀)
		PRINTFORM [황홀]
	SETCOLOR 150,0,250
	SIF (TCVARn:12 & 배란)
		PRINTFORM [강제배란]
	SETCOLOR 200,50,50
	SIF (TCVARn:12 & 강구속)
		PRINTFORM [강구속]
ENDIF

IF TALENT:피임결계 > 0 && BASE:성내성 > 0
	SETCOLOR 200,200,80
	PRINTFORM [피임결계]
ENDIF
RESETCOLOR
PRINTL 

PRINT 심경　　　
;CALL SHINKYOU_CHECK, "PRINTSTATE", 0
CALL SHINKYOU_CHECK, "PRINT", 0
PRINTL 

PRINTL


;--------------------------------------------------
;전투・적 정보

;촉수
IF ENEMY_TYPE_CHECK_F("AKUOTI") == 0
	CALL TENTACLE_ACCESS, "NAME"
	IF ENEMY_TYPE_CHECK_F("BOSS") == 1
		PRINT (ＢＯＳＳ)
	ELSEIF ENEMY_TYPE_CHECK_F("LASTBOSS") >= 1
		PRINT (ＬＡＳＴ)
	ELSEIF ENEMY_TYPE_CHECK_F("MOB") == 1
		PRINT (ＭＯＢ)
	ENDIF
	CALL TENTACLE_LEVEL
	PRINTFORM  Lv.{RESULT} 
;적 캐릭터
ELSEIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
	IF CFLAG:(FLAG:111):0 == 상태_세뇌
		PRINTFORM 세뇌된
		PRINTFORM  %PRINT_TRANSCALLNAME(FLAG:111)%
		PRINT [
		CALL TENTACLE_ACCESS_PRISON,FLAG:111, "NAME"
		PRINT ]
	ELSEIF CFLAG:(FLAG:111):0 == 상태_흑화
		;흑화 변신명이 설정되어 있다면 사용한다
		IF CSTR:(FLAG:111):55 != ""
			PRINTFORM %PRINT_TRANSNAME(FLAG:111)%
		ELSE
			PRINT 흑화한
			PRINTFORM  %PRINT_TRANSCALLNAME(FLAG:111)%
		ENDIF
	ENDIF
	PRINTFORM  Lv.{ABL:(FLAG:111):레벨} 
ENDIF

;유폐・세뇌 상황
LOCAL = 0
LOCAL:1 = 0
LOCALS'=""
LOCALS:1'=""
;보스 촉수
IF ENEMY_TYPE_CHECK_F("AKUOTI") == 0
	FOR CCOUNT, 1, CHARANUM
		;누군가를 세뇌하고 있는 적이라면 그 첨병을 표시
		IF CFLAG:CCOUNT:0 == 상태_세뇌 && CFLAG:CCOUNT:21 == FLAG:11
			LOCAL:1 += 1
			LOCALS:1+="["+CALLNAME:CCOUNT+"]"
		;누군가를 유폐하고 있는 적이라면 그 희생자를 표시
		ELSEIF CFLAG:CCOUNT:0 == 상태_유폐 && CFLAG:CCOUNT:21 == FLAG:11
			LOCAL += 1
			LOCALS+="["+CALLNAME:CCOUNT+"]"
		ENDIF
	NEXT
;흑화
ELSE
	FOR CCOUNT, 1, CHARANUM
		IF CCOUNT == FLAG:111
			;
		;누군가를 세뇌하고 있는 적이라면 그 첨병을 표시
		ELSEIF CFLAG:CCOUNT:0 == 상태_세뇌 && CFLAG:CCOUNT:21 == FLAG:111
			LOCAL:1 += 1
			LOCALS:1+="["+CALLNAME:CCOUNT+"]"
		;누군가를 유폐하고 있는 적이라면 그 희생자를 표시
		ELSEIF CFLAG:CCOUNT:0 == 상태_유폐 && CFLAG:CCOUNT:21 == FLAG:111
			LOCAL += 1
			LOCALS+="["+CALLNAME:CCOUNT+"]"
		ENDIF
	NEXT
ENDIF
SIF LOCAL
	PRINTFORM 　유폐 중：%LOCALS%
SIF LOCAL:1
	PRINTFORM 　세뇌 중：%LOCALS:1%
;SIF LOCAL || LOCAL:1
;	PRINTL 
PRINTL 


;전투 거리
CALL SHOW_DISTANCE_WINDOW()

CALL SHOW_TRAIN_PALAM_STATUS("하단부")

;공백 행
;PRINTL 


;통상 잡몹전(제한 15턴)
;통상 보스전(제한 50턴)
;내습 이벤트(15턴을 버티면 승리)
IF 턴상한 > 0
	PRINT 남은 시간
	PRINTFORM %UNICODE(0x208D)%
	IF GETBATTLESITUATION("추격전") || GETBATTLESITUATION("탈출전")
		CALLFORM COLOR_BAR(턴상한-TFLAG:0, 턴상한, 20,210,40,70,-160,18,6,10,2,UNICODE(0x25AE),UNICODE(0x25AE))
	ELSE
		CALLFORM COLOR_BAR(턴상한-TFLAG:0, 턴상한, 20,70,210,40,-160,10,18,6,2,UNICODE(0x25AE),UNICODE(0x25AE))
	ENDIF
	PRINTFORM %UNICODE(0x208E)%
	PRINTFORM （{턴상한-TFLAG:0, 5}/{턴상한,5}）
	PRINTL 
;시간 무제한일 경우
ELSE
	PRINT 남은 시간
	PRINTFORM %UNICODE(0x208D)%
	CALLFORM COLOR_BAR(0, 1, 20,40,40,40,-160,0,0,0,2,UNICODE(0x25AE),UNICODE(0x25AE))
	PRINTFORM %UNICODE(0x208E)%
	PRINTFORM （-----/-----）
	PRINTL
ENDIF



;체력
CALL COLORSENTENCE_ENEMYBAR,"적 체력", FLAG:13, FLAG:12, 20
;방심
IF TFLAG:2 >= 1
	PRINT 　<<방심 중>> 
ELSEIF FLAG:20 >= 100 || ENEMY_TYPE_CHECK_F("AKUOTI") == 1
	PRINTFORM 　(방심도：{FLAG:17 * 100 / FLAG:16}％) 
ELSEIF TFLAG:24 > 0
	PRINTFORM 선제 공격 가능！(잔여 {TFLAG:24}턴)
ENDIF
PRINTL 
	
;사정 게이지
IF ENEMY_TYPE_CHECK_F("AKUOTI") == 0 && FLAG:11 == 901
	PRINTFORM 적 절정
	CALL COLORSENTENCE_ENEMYBAR,"적 절정", FLAG:15, FLAG:14, 20
ELSEIF FLAG:73 > 0
	PRINTFORM 적 사정
	CALL COLORSENTENCE_ENEMYBAR,"적 사정", FLAG:15, FLAG:14, 20
ELSEIF ENEMY_TYPE_CHECK_F("AKUOTI") == 0
	PRINTFORM %STR:2500% 사정
	CALL COLORSENTENCE_ENEMYBAR,"적 사정", FLAG:15, FLAG:14, 20
ELSEIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1 && (ISPENIS(FLAG:111) || TALENT:(FLAG:111):기생 > 0)
	PRINTFORM 적 사정
	CALL COLORSENTENCE_ENEMYBAR,"적 사정", FLAG:15, FLAG:14, 20
ELSE
	PRINTFORM 적 절정
	CALL COLORSENTENCE_ENEMYBAR,"적 절정", FLAG:15, FLAG:14, 20
ENDIF
PRINTL 
;;DEBUG모드이거나 해석도 ７５％이상, 또는 상대가 세뇌/흑화 캐릭터라면 스테이터스 표시
IF FLAG:999 == 1 || FLAG:20 >= 75 || ENEMY_TYPE_CHECK_F("AKUOTI") > 0
	IF ENEMY_TYPE_CHECK_F("AKUOTI") == 0
		CALL TENTACLE_ACCESS, "KOUGEKI"
		SIF RESULT < 0
			RESULT = 0
		PRINTFORM 공격：{RESULT,3,RIGHT} 
		CALL TENTACLE_ACCESS, "BOUGYO"
		SIF RESULT < 0
			RESULT = 0
		PRINTFORM 방어：{RESULT,3,RIGHT} 
		CALL TENTACLE_ACCESS, "BINSYOU"
		SIF RESULT < 0
			RESULT = 0
		PRINTFORM 민첩：{RESULT,3,RIGHT} 
		CALL TENTACLE_ACCESS, "CHISEI"
		SIF RESULT < 0
			RESULT = 0
		PRINTFORM 지성：{RESULT,3,RIGHT} 
		PRINTL 
		IF FLAG:20 >= 100
			CALL TENTACLE_ACCESS, "SHORT"
			PRINTFORM 근：{RESULT,3,RIGHT} 
			CALL TENTACLE_ACCESS, "MIDDLE"
			PRINTFORM 중：{RESULT,3,RIGHT} 
			CALL TENTACLE_ACCESS, "LONG"
			PRINTFORM 원：{RESULT,3,RIGHT} 
			CALL TENTACLE_ACCESS, "HOLD"
			PRINTFORM 구속력：{RESULT} 
			PRINTL 
		ELSE
			PRINTFORML 해석도：{FLAG:20}％
		ENDIF
	ELSEIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
		PRINTFORM 공격：{MAXBASE:(FLAG:111):공격,3,RIGHT} 
		PRINTFORM 방어：{MAXBASE:(FLAG:111):방어,3,RIGHT} 
		PRINTFORM 민첩：{MAXBASE:(FLAG:111):민첩,3,RIGHT} 
		PRINTFORM 지성：{MAXBASE:(FLAG:111):지성,3,RIGHT} 
		PRINTL 
		PRINTFORM 근：{ABL:(FLAG:111):근거리,3,RIGHT} 
		PRINTFORM 중：{ABL:(FLAG:111):중거리,3,RIGHT} 
		PRINTFORM 원：{ABL:(FLAG:111):원거리,3,RIGHT} 
		PRINTL 
	ENDIF
ELSE
	PRINTFORML 해석도：{FLAG:20}％
ENDIF



SIF CONFIG_CHECK_SCREEN_F(2) > 0
	RETURN 0

DRAWLINE
IF TCVARn:0 == 0
	IF (TCVARn:12 & 강구속)
		SETCOLOR 200,50,50
		PRINT   <<강구속>>
	ELSE
		SETCOLOR 255, 146, 238
		PRINT   <<구속중>>
	ENDIF
	RESETCOLOR
	PRINTL 
ENDIF

;--------------------------------------------------
;파라미터 표시
@SHOW_STATUS_PALAM
#DIM CAL_VAR, 2

;확장도 표시
IF CFLAG:34 > 0 && CONFIG_CHECK_MANIAC_F(16) == 1
	CALL PRINTFORM_GAPING_NOW,TARGET, CFLAG:1
ELSE
	PRINTL 
ENDIF

REPEAT 12
	LOCAL = COUNT
	SIF COUNT > 3
		LOCAL += 6

	IF COUNT < 감각수
		IF BASE:(COUNT + 30) > 0
			CALL PERCENT_CAL, BASE:(COUNT + 30), MAXBASE:(COUNT + 30)
			CAL_VAR:0 = LIMIT(255 - RESULT * RESULT * 4 / 200, 0, 255)
			CAL_VAR:1 = LIMIT(-215 - RESULT * RESULT / 100 + RESULT * 5, 0, 255)
			PRINTFORM %TALENTNAME:(COUNT + 190)%　%UNICODE(0x208D)%
			CALL COLOR_BAR(BASE:(COUNT + 30),MAXBASE:(COUNT + 30),13,CAL_VAR:0,CAL_VAR:1,20,-160,6,32,20,1,UNICODE(0x25AE),UNICODE(0x25AE))
			PRINTFORM %UNICODE(0x208E)% 
		ELSE
			PRINTFORM %PALAMNAME:LOCAL%
			PRINTFORM {PALAM:LOCAL,8}
		ENDIF
	ELSE
		PRINTFORM %PALAMNAME:LOCAL%
		PRINTFORM {PALAM:LOCAL,8}
	ENDIF

	IF COUNT >= 감각수 || BASE:(COUNT + 30) <= 0
		;파라미터 취득 레벨 표시
		IF PALAM:LOCAL < 100
			PRINT   -  [Lv.0] 
		ELSEIF PALAM:LOCAL < 300
			PRINT   -  [Lv.1] 
		ELSEIF PALAM:LOCAL < 600
			PRINT   -  [Lv.2] 
		ELSEIF PALAM:LOCAL < 1500
			PRINT   -  [Lv.3] 
		ELSEIF PALAM:LOCAL < 3000
			PRINT   -  [Lv.4] 
		ELSEIF PALAM:LOCAL < 6000
			PRINT   -  [Lv.5] 
		ELSEIF PALAM:LOCAL < 10000
			PRINT   -  [Lv.6] 
		ELSEIF PALAM:LOCAL < 30000
			PRINT   -  [Lv.7] 
		ELSEIF PALAM:LOCAL < 60000
			PRINT   -  [Lv.8] 
		ELSEIF PALAM:LOCAL < 150000
			PRINT   -  [Lv.9] 
		ELSEIF PALAM:LOCAL < 300000
			PRINT   - [Lv.10] 
		ELSE
			PRINT   - [LvMAX] 
		ENDIF
	ENDIF
	
	SIF COUNT == 2 || COUNT == 5 || COUNT == 8
		PRINTL 
REND
PRINTL 




;------------------------------------------------------------------------------------
;거리 표시
@PRINT_DISTANCE
VARSET LOCALS
PRINT [
IF TCVARn:0 == 0
	IF TCVARn:12 & 강구속
		SETCOLOR 200,50,50
		LOCALS = 강구속
	ELSE
		SETCOLOR 255, 146, 238
		LOCALS = 구속중
	ENDIF
ELSE
	IF TCVARn:0 == 1
		SETCOLOR 255,0,0
		LOCALS = 근
	ELSEIF TCVARn:0 == 2
		SETCOLOR 255,255,0
		LOCALS = 중
	ELSEIF TCVARn:0 == 3
		SETCOLOR 64,255,64
		LOCALS = 원
	ENDIF
	IF GETBIT(TCVARn:216, 1)
		LOCALS:1 = 공중
	ELSEIF GETBIT(TCVARn:216, 2)
		LOCALS:1 = 착지
	ELSE
		LOCALS:1 = 거리
	ENDIF
ENDIF
PRINTFORM %LOCALS%
IF GETBIT(TCVARn:216, 1)
	SETCOLOR 0,60,255
ELSEIF GETBIT(TCVARn:216, 2)
	SETCOLOR 255,128,0
ENDIF
PRINTFORM %LOCALS:1%
RESETCOLOR
PRINT ]

;전투 지원에 의한 지성 보정률 계산
;ARG　지원 보정의 기초값 패턴
@CALC_CHISEI_SHIEN(ARG)
#DIM CHISEI_CAL
#DIM CCOUNT

LOCAL = 0
FOR CCOUNT, 0, CHARANUM
	SIF CCOUNT == MASTER || CCOUNT > 6
		CONTINUE
	CALL CHECK_ACTION(예정_지원,CCOUNT)
	SIF RESULT > 0
		CONTINUE
	IF CFLAG:CCOUNT:100 == 예정_지원
		IF ARG == 0
			CHISEI_CAL = MAXBASE:CCOUNT:지성 / 4
		ELSEIF ARG == 1
			CHISEI_CAL = MAXBASE:CCOUNT:지성 / 8
		ELSEIF ARG == 2
			CHISEI_CAL = MAX((BASE:CCOUNT:지성 - 100),2) / 2
		ENDIF
		;의상에 따른 보정
		CALL CLOTH_BATTLE_HOSEI, "CHISEI", CCOUNT
		CHISEI_CAL = CHISEI_CAL * RESULT / 100
		;지원자가 업무용 이어폰 장비
		SIF CFLAG:CCOUNT:43 == 511
			CHISEI_CAL = CHISEI_CAL * 110 / 100 + 5
		;지원자가 헌신적
		SIF TALENT:CCOUNT:헌신적 > 0
			CHISEI_CAL = CHISEI_CAL * 110 / 100 + 5
		;지원자가 사지결손
		SIF TALENT:CCOUNT:사지결손 > 0
			CHISEI_CAL = CHISEI_CAL * 150 / 100 + 5;대량 보너스(극단적이지만 게임 밸런스 측면의 조치라고 생각된다)
			
		;대상자가 제멋대로굼
		SIF TALENT:TARGET:제멋대로굼 > 0
			CHISEI_CAL = CHISEI_CAL * 25 / 100
		LOCAL += CHISEI_CAL
	ENDIF
NEXT
RETURN LOCAL




@SHOW_DISTANCE_WINDOW()
IF TCVARn:0 == 0
	LOCAL = 0
ELSEIF TCVARn:0 == 1
	LOCAL = 3
ELSEIF TCVARn:0 == 2
	LOCAL = 9
ELSEIF TCVARn:0 == 3
	LOCAL = 21
ENDIF
;랜덤으로 미묘하게 위치가 변동한다…라는 처리지만 Ex 공방이나 에어 스트라이크 전환 시에 변동하는 일이 있으므로 본 목적에 미묘했다
;턴 내에서 고정이 되도록 (날짜＋턴 수)의 여분 등으로 해 둔다
;IF TCVARn:0 != 0 && RAND:2 == 0
;	LOCAL -= RAND:2
;	LOCAL += RAND:2
;ENDIF
LOCAL:1=0
IF TCVARn:0 != 0 && (DAY+TFLAG:0) % 4 == 0
	LOCAL -= 1
	LOCAL:1 -= 1
ELSEIF TCVARn:0 != 0 && (DAY+TFLAG:0) % 4 == 1
	LOCAL += 1
	LOCAL:1 += 1
ENDIF

IF TCVARn:0 == 0
	SETCOLORBYNAME HOTPINK
ENDIF
PRINTL ┏━━━━━━━━━━━━━━━━━━━━┓



;공중전특기 이외라면 에어라도 ２행상에는 가지 않는다
IF !TALENT:TARGET:공중전특기
	PRINT ┃　　　　　　　　　　　　　　　　　　　　┃
	PRINTL 【위험 예측】
	PRINT ┃
ELSE
	PRINT ┃
ENDIF

REPEAT LOCAL+7
	PRINT  
REND
IF GETBIT(TCVARn:216, 1)
	IF ISFEMALE()
		SETCOLORBYNAME HOTPINK
		PRINT ♀
		RESETCOLOR
	ELSE
		SETCOLORBYNAME BLUE
		PRINT ♂
		RESETCOLOR
	ENDIF
ELSE
	PRINT 　
ENDIF
REPEAT 31 -LOCAL
	PRINT  
REND
IF TCVARn:0 == 0
	SETCOLORBYNAME HOTPINK
ENDIF
PRINT ┃

;공중전특기라면 에어로 표시되는 것은 ２행상
IF TALENT:TARGET:공중전특기
	PRINT 【위험 예측】
	PRINTL
	PRINT ┃　　　　　　　　　　　　　　　　　　　　┃
ELSE
	PRINT
ENDIF

;공중이 노려질 확률
RESETCOLOR
PRINT  공중:
CALL FORECAST_OUTPUT_NUM, (TCVARn:0 == 0 ? 0 # TCVARn:33)
PRINT  %
PRINTL

IF TCVARn:0 == 0
	SETCOLORBYNAME HOTPINK
ENDIF

IF TCVARn:0 > 0
	PRINT ┃ 
	SETCOLORBYNAME HOTPINK
	PRINT ≪
	IF ENEMY_TYPE_CHECK_F("AKUOTI") == 0
		CALL TENTACLE_ACCESS, "GETNAME"
		LOCALS = %RESULTS%
	;적 캐릭터
	ELSEIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
		LOCALS = %PRINT_TRANSCALLNAME(FLAG:111)%
	ENDIF
	PRINTFORM %SUBSTRING(LOCALS,0,1)%
	PRINT ≫


	RESETCOLOR
ELSE
	PRINT ┃ 
	SETCOLORBYNAME ORANGE
	PRINT ≪
	IF ENEMY_TYPE_CHECK_F("AKUOTI") == 0
		CALL TENTACLE_ACCESS, "GETNAME"
		LOCALS = %RESULTS%
	;적 캐릭터
	ELSEIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
		LOCALS = %PRINT_TRANSCALLNAME(FLAG:111)%
	ENDIF
	PRINTFORM %SUBSTRING(LOCALS,0,1)%

	RESETCOLOR
	IF ISFEMALE()
		SETCOLORBYNAME HOTPINK
		PRINT ♀
		RESETCOLOR
	ELSE
		SETCOLORBYNAME BLUE
		PRINT ♂
		RESETCOLOR
	ENDIF
	SETCOLORBYNAME ORANGE
	PRINT ≫
	RESETCOLOR
ENDIF

REPEAT LOCAL
	IF COUNT % 2 == 0
		PRINT _
	ELSE
		PRINT -
	ENDIF
REND
SETCOLORBYNAME HOTPINK
IF TCVARn:0 == 0
	RESETCOLOR
ELSEIF !GETBIT(TCVARn:216, 1) && TCVARn:0 > 0
	IF ISFEMALE()
		SETCOLORBYNAME HOTPINK
		PRINT ♀
		RESETCOLOR
	ELSE
		SETCOLORBYNAME BLUE
		PRINT ♂
		RESETCOLOR
	ENDIF
ELSE
	IF LOCAL % 2 == 1
		PRINT -_
	ELSE
		PRINT _-
	ENDIF
ENDIF
RESETCOLOR
REPEAT 22-LOCAL
	IF COUNT % 2 == 1
		PRINT _
	ELSE
		PRINT -
	ENDIF
REND

PRINT  
CALL PRINT_DISTANCE

IF TCVARn:0 == 0
	SETCOLORBYNAME HOTPINK
ENDIF

PRINT ┃
;지상을 노려질 확률
PRINTPLAIN  
CALL PRINT_FORECAST
PRINTL

IF TCVARn:0 == 0
	SETCOLORBYNAME HOTPINK
ENDIF

PRINTL ┗━━━━━━━━━━━━━━━━━━━━┛
RESETCOLOR