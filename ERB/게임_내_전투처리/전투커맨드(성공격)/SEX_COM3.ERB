;Ｖ공격（강）

;성 커맨드 분류
@SEX_TYPE_COM3
RETURN 삽입

@SEX_COM3,ARG = 0,ARG:1 = 0
;EX_COM	추가 고문 실행 부위（비트）
;	1：Ｃ　2：Ｖ　4：Ａ　8：Ｂ
;추가 고문
CALL SEX_COMEX_RANDOM, Ｖ, ARG:1
EX_COM = RESULT:0
SH_COM = RESULT:1

;청순파라면 한 번만 자동으로 Ｖ방어
SIF TALENT:청순파 > 0 && TCVARn:2 != 100
	CALL AUTO_V_DEFENCE, TARGET

;사정 부위 플래그:질을 세운다, Ｖ방어를 하고 있다면 입을 세운다
IF TCVARn:2 == 자세：Ｖ방어 || BASE:Ｖ결계내구력 > 0
	TFLAG:4 = TFLAG:4 | 입
	;결계가 없으면 균열 사정 플래그를 세운다
	SIF BASE:Ｖ결계내구력 <= 0
		TFLAG:4 = TFLAG:4 | 균열
ELSE
	TFLAG:4 = TFLAG:4 | 질
	INSERT = INSERT | 질
ENDIF
;Ａ추가 공격이 있다면 애널 사정 플래그를 세운다
IF (EX_COM & Ａ)
	TFLAG:4 = TFLAG:4 | 애널
	INSERT = INSERT | 애널
ENDIF

;흑화 캐릭터의 페니스 위치를 세트
SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1 && (ISPENIS(FLAG:111))
	TFLAG:18 = 질

;취득 파라미터 계산
;BASE값의 명시적 초기화
REPEAT 12
	LOCAL:COUNT = 0
REND

;방심도 상승
TFLAG:3 += 50

;사정치 250
CALL TENTACLE_SYASEI_UP, 250

;촉수 크기 설정
CALL SET_TENTACLE_SIZE, FLAG:10, FLAG:11, ENEMY_TYPE_CHECK_F("AKUOTI"), FLAG:111, 3

;쾌Ｖ
IF ABL:Ｖ감각 == 0
	LOCAL:1 = 400
ELSEIF ABL:Ｖ감각 == 1
	LOCAL:1 = 1000
ELSEIF ABL:Ｖ감각 == 2
	LOCAL:1 = 2000
ELSEIF ABL:Ｖ감각 == 3
	LOCAL:1 = 4000
ELSEIF ABL:Ｖ감각 == 4
	LOCAL:1 = 10000
ELSEIF ABL:Ｖ감각 >= 5
	LOCAL:1 = 20000
ENDIF
;Ｖ방어 효과
SIF TCVARn:2 == 자세：Ｖ방어
	LOCAL:1 /= 4
;메인 부위 절정 예정의 예측
CALL PALAM_VABCestimate(LOCAL,1,-1)


IF ENEMY_TYPE_CHECK_F("MOB") == 1
	TRYCALLFORM MESSAGE_MOB_{ARG}_COM3, EX_COM, SH_COM
	;처녀라면 고통이 대량으로 들어간다
	IF TALENT:처녀 > 0 && TCVARn:2 != 자세：Ｖ방어 && BASE:Ｖ결계내구력 <= 0
		LOCAL:10 = 10000
		TALENT:처녀 = -1
		CFLAG:206 = 2
		TCVARn:1 = 1
	ELSE
		LOCAL:10 = 0
	ENDIF
ELSE
	;처녀라면 고통이 대량으로 들어간다
	IF TALENT:처녀 > 0 && TCVARn:2 != 자세：Ｖ방어 && BASE:Ｖ결계내구력 <= 0
		LOCAL:10 = 10000
		CALL LOSTVIRGIN
	ELSE
		LOCAL:10 = 0
	ENDIF
	;지문: 세뇌/흑화 캐릭터가 조작 캐릭터에게 Ｖ공격（강）을 했다
	SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
		CALL MESSAGE_OTHER_SEX_COM3
	;지문: COM3
	CALL MESSAGE_SEX_COM3, EX_COM, SH_COM
ENDIF

;결계에 따른 촉수 개수의 변동
CALL SET_TENTACLE_POOL
;의상에의 피해
CALL CLOTH_BATTLE_DAMAGE, 8

;BASE
IF TCVARn:2 == 자세：Ｖ방어
	EXP:Ｖ경험 += 1
	SIF FLAG:15 >= FLAG:14
		EXP:펠라경험 += 1
ELSEIF EX_COM & Ｖ
	EXP:Ｖ경험 += 2
	SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 0 && INCEST_F(TARGET, FLAG:10 * 100 + FLAG:11, 1) > 0
		EXP:근친교배경험 += 1
	SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1 && INCEST_F(TARGET, FLAG:111) > 0
		EXP:근친교배경험 += 1
ENDIF



;공순
IF ABL:순종 <= 2
	LOCAL:5 = 200
ELSEIF ABL:순종 == 3
	LOCAL:5 = 500
ELSEIF ABL:순종 == 4
	LOCAL:5 = 1000
ELSEIF ABL:순종 >= 5
	LOCAL:5 = 2000
ENDIF

;습득
IF ABL:봉사정신 == 0
	LOCAL:6 = 20
ELSEIF ABL:봉사정신 == 1
	LOCAL:6 = 50
ELSEIF ABL:봉사정신 == 2
	LOCAL:6 = 100
ELSEIF ABL:봉사정신 == 3
	LOCAL:6 = 200
ELSEIF ABL:봉사정신 == 4
	LOCAL:6 = 500
ELSEIF ABL:봉사정신 >= 5
	LOCAL:6 = 1000
ENDIF

;윤활이 낮으면 고통이 들어간다
IF PALAM:윤활 < 1000
	LOCAL:10 += 5000
ELSEIF PALAM:윤활 < 2000
	LOCAL:10 += 1000
ENDIF
SIF BASE:Ｖ결계내구력 > 0
	LOCAL:10 = 0

;Ｖ감각이 낮으면 공포가 들어간다
IF ABL:Ｖ감각 == 0
	LOCAL:11 = 4000
ELSEIF ABL:Ｖ감각 == 1
	LOCAL:11 = 1000
ELSEIF ABL:Ｖ감각 == 2
	LOCAL:11 = 400
ELSEIF ABL:Ｖ감각 == 3
	LOCAL:11 = 200
ELSE
ENDIF
SIF BASE:Ｖ결계내구력 > 0
	LOCAL:11 /= 2

;추가 고문 실행
IF EX_COM
	CALL SEX_COMEX, Ｖ, 1, EX_COM | SH_COM
	REPEAT 12
		LOCAL:COUNT += RESULT:COUNT
	REND
ENDIF

;성내성이 나머지 50% 이상이면 파생되기 어렵다
IF PERCENT_CAL_F(BASE:성내성, MAXBASE:성내성) >= 50 && RAND:2 == 0
;ＶＡ삽입 성공＆사정 게이지 부족이라면 양 구멍 피스톤＋강구속 상태
ELSEIF (EX_COM & Ｖ) && (EX_COM & Ａ) && FLAG:15 < FLAG:14 && BASE:Ｖ결계내구력 == 0 && BASE:Ａ결계내구력 == 0 && TCVARn:2 != 자세：Ｖ방어 && (ENEMY_TYPE_CHECK_F("MOB") == 0 && ENEMY_TYPE_CHECK_F("CITIZEN") == 0) && ISFEMALE()
	TFLAG:17 = 19
	;상태 이상：강구속
	CALL STATE_CHANGE_PKOUSOKU, 100
;Ｖ삽입 성공＆사정 게이지 부족이라면 교배 피스톤＋강구속 상태
ELSEIF FLAG:15 < FLAG:14 && BASE:Ｖ결계내구력 == 0 && TCVARn:2 != 자세：Ｖ방어 && (ENEMY_TYPE_CHECK_F("MOB") == 0 && ENEMY_TYPE_CHECK_F("CITIZEN") == 0) && ISFEMALE()
	TFLAG:17 = 15
	;상태 이상：강구속
	CALL STATE_CHANGE_PKOUSOKU, 100
ELSE
	;행동 예약 없음
	TFLAG:17 = -1
ENDIF
;촉수의 행동을 기록
TFLAG:20 = 3

;습득 기본값
LOCAL:6 += 1500

;욕정 기본값
LOCAL:7 += 0

;굴복 기본값
LOCAL:8 += 2500

;치정 기본값
LOCAL:9 += 200

;고통 기본값
LOCAL:10 += 100

;공포 기본값
LOCAL:11 += 1000

;체력 소비 기본값
LOCAL:12 += 200

;계산으로
CALL PALAM_CAL, LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10, LOCAL:11, LOCAL:12
