
;공격 커맨드의 공통 처리
@COM_ATTACK_COMMON
#DIM ATTACK_NUM
#DIM HIT_FLAG

VARSET LOCAL

ATTACK_NUM = 0
HIT_FLAG = 0


;전투 스타일 변경 체계(scheme)
TRYCALLFORM KOJO_ROOT(CFLAG:6, "BATTLE_CHARA_BATTLE_STYLE_CHANGE")

;[연속] 스타일에 따른 공격 횟수 증가
SIF FSTYLE_NAME_F(TARGET, TCVARn:0) == "연속"
	ATTACK_NUM += 1
;[연속] 버스트에 따른 공격 횟수 증가
SIF GETBIT(TCVARn:217, 0) && FSTYLE_NAME_F(TARGET, TCVARn:0) == "연속"
	ATTACK_NUM += 4
;[사역] 스타일에 따른 공격 횟수 증가
SIF GETBIT(TCVARn:217, 0) == 0 && FSTYLE_NAME_F(TARGET, TCVARn:0) == "사역"
	ATTACK_NUM += 1

;거리 표시
CALL PRINT_DISTANCE
PRINT 　
;무기 상세 표시
FONTBOLD
SIF CSTR:(TCVARn:0 + 4) != ""
	PRINTFORM 『%CSTR:(TCVARn:0 + 4)%』
PRINTFORML <<%FSTYLE_NAME_F(TARGET,TCVARn:0)%>>
FONTREGULAR
;콤보 표시
CALL PRINT_COMBO
PRINTL 

[SKIPSTART]
;[반격] 스타일은 거리와 상관 없이 공통된 지문을 사용한다
IF FSTYLE_NAME_F(TARGET, TCVARn:0) == "반격"
	;공격 이후 반격 준비
	IF TCVARn:2 != 자세：반격
		;지문: 반격
		CALL MESSAGE_BATTLE_CHARA_HANGEKI
		;지문: 세뇌/흑화 캐릭터가 조작 캐릭터의 반격 태세를 봤다
		SIF FLAG:110 == 1
			CALL MESSAGE_OTHER_BATTLE_CHARA_HANGEKI
		TCVARn:2 = 자세：반격
		RETURN 1
	;반격 성공
	ELSE
		CALL MESSAGE_BATTLE_CHARA_ATTACK_HANGEKI
	ENDIF
ENDIF
[SKIPEND]

;지문: 반격 성공
SIF TCVARn:2 == 자세：반격성공
	CALL MESSAGE_BATTLE_CHARA_ATTACK_HANGEKI

;지문
IF TCVARn:0 == 1
	CALL MESSAGE_BATTLE_CHARA_ATTACK_RANGE_SHORT
ELSEIF TCVARn:0 == 2
	CALL MESSAGE_BATTLE_CHARA_ATTACK_RANGE_MIDDLE
ELSEIF TCVARn:0 == 3
	CALL MESSAGE_BATTLE_CHARA_ATTACK_RANGE_LONG
ENDIF

$ATTACK_AGAIN
;명중 판정
IF TCVARn:0 == 1
	CALL ACT_HANTEI_CHARA_TO_TENTACLE, "ATTACK_RANGE_SHORT"
ELSEIF TCVARn:0 == 2
	CALL ACT_HANTEI_CHARA_TO_TENTACLE, "ATTACK_RANGE_MIDDLE"
ELSEIF TCVARn:0 == 3
	CALL ACT_HANTEI_CHARA_TO_TENTACLE, "ATTACK_RANGE_LONG"
ENDIF
IF ENEMY_TYPE_CHECK_F("LASTBOSS") >= 1 && FLAG:11 == 2 && FLAG:21 == 1 && (TFLAG:13 & TCVARn:0) == 0
	PRINTFORML 기다리고 있던 촉수의 본체를 향한 공격은 막혀 버렸다……
	PRINTL 
	RESULT = 0
ENDIF

;직격 판정에 실패할 시
IF RESULT == 0
	;가드 대미지(カス当たり) 판정
	IF TCVARn:0 == 1
		CALL ACT_HANTEI_CHARA_TO_TENTACLE_GUARD, "ATTACK_RANGE_SHORT"
	ELSEIF TCVARn:0 == 2
		CALL ACT_HANTEI_CHARA_TO_TENTACLE_GUARD, "ATTACK_RANGE_MIDDLE"
	ELSEIF TCVARn:0 == 3
		CALL ACT_HANTEI_CHARA_TO_TENTACLE_GUARD, "ATTACK_RANGE_LONG"
	ENDIF

	;판정에 성공했을 시
	IF RESULT
		;가드 대미지(カス当たり) 플래그
		HIT_FLAG = -1
		;체인 가산
		TFLAG:31 += 1
		;피해량 계산
		IF TCVARn:0 == 1
			CALL DAMAGE, "ATTACK_RANGE_SHORT"
			;[광역] 스타일은 가드 대미지(カス当たり)의 위력이 높다
			IF FSTYLE_NAME_F(TARGET,TCVARn:0) == "광역"
				LOCAL:1 = MIN(RESULT * 100 * MIN(BASE:체력 * 100 / MAXBASE:체력 + 20, 40) / 10000 + 100, RESULT) * (RAND:26 + 75) / 100
			ELSE
				LOCAL:1 = MIN(RESULT * 25 * MIN(BASE:체력 * 100 / MAXBASE:체력 + 15, 30) / 10000 + 50, RESULT)
			ENDIF
		ELSEIF TCVARn:0 == 2
			CALL DAMAGE, "ATTACK_RANGE_MIDDLE"
			;[광역] 스타일은 가드 대미지(カス当たり)의 위력이 높다
			IF FSTYLE_NAME_F(TARGET,TCVARn:0) == "광역"
				LOCAL:1 = MIN(RESULT * 200 * MIN(BASE:체력 * 100 / MAXBASE:체력 + 15, 30) / 10000 + 100, RESULT) * (RAND:51 + 50) / 100
			ELSE
				LOCAL:1 = MIN(RESULT * 100 * MIN(BASE:체력 * 100 / MAXBASE:체력 + 10, 20) / 10000 + 50, RESULT)
			ENDIF
		ELSEIF TCVARn:0 == 3
			CALL DAMAGE, "ATTACK_RANGE_LONG"
			;[광역] 스타일은 가드 대미지(カス当たり)의 위력이 높다
			IF FSTYLE_NAME_F(TARGET,TCVARn:0) == "광역"
				LOCAL:1 = MIN(RESULT * 300 * MIN(BASE:체력 * 100 / MAXBASE:체력 + 10, 20) / 10000 + 100, RESULT) * (RAND:76 + 25) / 100
			ELSE
				LOCAL:1 = MIN(RESULT * 300 * MIN(BASE:체력 * 100 / MAXBASE:체력 + 5, 10) / 10000 + 50, RESULT)
			ENDIF
		ENDIF
		
		;[반격] 버스트 공격의 경우 축적 피해량의 1/5의 수치로 가산된다
		;축적 피해량의 1/5 만큼의 수치가 보존된다
		;단, 버스트 공격이 활성화된 상태로 강화 반격에 성공할 시, 버스트 공격 효과가 적용되지 않는다
		SIF GETBIT(TCVARn:217, 0) && TCVARn:2 != 자세：반격성공 && FSTYLE_NAME_F(TARGET, TCVAR:0) == "반격"
			LOCAL:1 += TCVARn:205 / 5

		FLAG:13 -= LOCAL:1
		;지문: 가드 대미지(カス当たり)
		CALL MESSAGE_BATTLE_CHARA_ATTACK_GUARD, ATTACK_NUM
		;지문: 세뇌/흑화 캐릭터가 조작 캐릭터의 공격을 가드했다(カス当たり)
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_BATTLE_CHARA_ATTACK_GUARD, ATTACK_NUM
		FONTBOLD
		PRINTFORML {LOCAL:1}의 피해량을 입혔다！
		FONTREGULAR
		PRINTW 
		IF LOCAL:1 >= 5000
			CALL UNLOCK_ACHIEVEMENT(263,"파괴신의 일격")
		ENDIF
		LOCAL:2 = 1
	;직격에 실패한 후에 가드 대미지 판정에도 실패하면 빗맞춘다
	ELSE
		;공격 미스 플래그
		HIT_FLAG = 0
		;체인 리셋
		TFLAG:31 = 0
		;지문: 공격 실패
		CALL MESSAGE_BATTLE_CHARA_ATTACK_FALSE, ATTACK_NUM
		;지문: 세뇌/흑화 캐릭터가 조작 캐릭터의 공격을 회피했다
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_BATTLE_CHARA_ATTACK_FALSE, ATTACK_NUM
	ENDIF

	;심경 변화：「고양」또는「소침」
	SIF TCVARn:1 == 0 && ATTACK_NUM == 0
		CALL SHINKYOU_CHANGE, "KOUYOU_SYOUTIN"

	;방심도 상승의 추가 효과
	LOCAL:3 = 0
	;[설치] 스타일
	IF FSTYLE_NAME_F(TARGET,TCVARn:0) == "설치" && LOCAL:2
		LOCAL:3 += 125
		;[설치] 버스트의 효과
		SIF GETBIT(TCVARn:217, 0)
			LOCAL:3 += 125
	ENDIF
	IF LOCAL:3 > 0
		IF (FLAG:110 == 0)
			CALL TENTACLE_ACCESS, "NAME"
		ELSEIF (FLAG:110 == 1)
			PRINTFORM %PRINT_TRANSCALLNAME(FLAG:111)%
		ENDIF
		LOCALS = {LOCAL:3}
		TOFULL LOCALS
		PRINTFORML 의 방심도가 조금 상승했다……（＋%RESULTS%）
		PRINTW 
		FLAG:17 += LOCAL:3
	ENDIF
	;[교란] 버스트의 기죽임 효과 10％
	IF GETBIT(TCVARn:217, 0) && FSTYLE_NAME_F(TARGET,TCVARn:0) == "교란" && RAND:10 == 0 && HIT_FLAG == -1
		TFLAG:1 = 1
		PRINTFORML 상대의 자세를 무너뜨렸다！
	ENDIF
;직격 시의 처리
ELSE
	;체인 가산
	TFLAG:31 += 1
	;의상에 따른 보정
	RESULT = 0
	CALL CLOTH_BATTLE_HOSEI, "CRITICAL"
	;FEAT에 의한 치명타 보정
	SIF TALENT:야수의후손 > 0
		RESULT += 6
	SIF TALENT:숨겨진힘 > 0 && PERCENT_CAL_F(BASE:체력 + BASE:기력, MAXBASE:체력 + MAXBASE:기력) <= 40
		RESULT += 12
	SIF TALENT:심안 > 0
		RESULT += 3
	SIF TALENT:공생 > 0
		RESULT += 2
	SIF TALENT:에어마스터 > 0 && GETBIT(TCVARn:216, 1)
		RESULT += 10
	;[교란] 스타일의 치명타 보정
	SIF FSTYLE_NAME_F(TARGET,TCVARn:0) == "교란"
		RESULT += 5
	;[지략] 버스트의 치명타 확률 향상
	SIF GETBIT(TCVARn:217, 0) && FSTYLE_NAME_F(TARGET,TCVARn:0) == "지략"
		RESULT += 5
	;EVADER의 치명타 보정
	SIF TFLAG:33 > 1
		RESULT += GET_EVADER_CRT(TFLAG:33 - 1)
	;방어력에 따른 치명타 보정 (숨겨진 효과)
	LOCAL:0 = MAX(BASE:방어 - 100, 0)
	IF LOCAL:0 < 0
		LOCAL:0 = ABS(LOCAL:0)
		RESULT -= MIN(SQRT(LOCAL:0) * 2 / 3 , 12)
	ELSE
		RESULT += MIN(SQRT(LOCAL:0) * 2 / 3 , 12)
	ENDIF
	;근거리는 치명타를 내기 쉽다. 게다가 BRAVE HIT 중에는 추가 보정이 걸린다
	SIF TCVARn:0 == 1
		RESULT += 3 + (TFLAG:30) * 2
	;중거리는 치명타를 내기가 조금 쉽다
	SIF TCVARn:0 == 2
		RESULT += 1
	;치명타 판정
	;치명타 배율(LOCAL:0)에 들어가는 값은 1 = 10% 와 같이 보면 된다
	IF RAND:100 < 5 + RESULT
		LOCAL:0 = 15
		;버스트 공격 시에는 치명타 배율이 상승
		SIF GETBIT(TCVARn:217, 0)
			LOCAL:0 += 5
		;「숨겨진힘」의 숨김 보정
		SIF TALENT:숨겨진힘 > 0 && PERCENT_CAL_F(BASE:체력 + BASE:기력, MAXBASE:체력 + MAXBASE:기력) <= 40
			LOCAL:0 += 5
		SIF TALENT:숨겨진힘 > 0 && PERCENT_CAL_F(BASE:체력 + BASE:기력, MAXBASE:체력 + MAXBASE:기력) <= 15
			LOCAL:0 += 5
		
		;[지략] 스타일의 치명타 배율 보정
		;적과의 지성차를 계산하는 FORECAST.ERB의 예측 확률 계산식을 가져오고, 여기서 LOCAL:4, 5, 6 을 쓴다
		;근데 전투 시작 시부터 전투 지원 보정을 딱 계산해서 넣어주는 함수 없나? 이러면 너무 지저분한데
		IF FSTYLE_NAME_F(TARGET,TCVARn:0) == "지략"
			;캐릭터의 지성을 가져온다
			LOCAL:4 = MAXBASE:지성
			
			;기력에 따른 보정
			CALL PERCENT_CAL, BASE:기력 * 2, MAXBASE:기력
			SIF RESULT < 100
				LOCAL:4 = LOCAL:4 * (RESULT + 300) /400
			
			;의상에 의한 보정
			CALL CLOTH_BATTLE_HOSEI, "CHISEI"
			LOCAL:4 = LOCAL:4 * RESULT / 100
			
			;심경에 따른 보정
			CALL SHINKYOU_CHECK, "CHISEI", LOCAL:4
			LOCAL:4 = RESULT
			
			;전투 지원에 의한 보정
			CALL CALC_CHISEI_SHIEN(0)
			LOCAL:4 += RESULT

			;보스 촉수/적 캐릭터의 지성
			IF ENEMY_TYPE_CHECK_F("AKUOTI")
				LOCAL:5 = MAXBASE:(FLAG:111):지성
			ELSE
				CALL TENTACLE_ACCESS, "CHISEI"
				LOCAL:5 = RESULT
				;우선 보스 촉수의 지성 보정 1.5배는 그대로 두고, 추후 밸런스 조정을 하자
				TIMES LOCAL:5, 1.50
			ENDIF
			
			;지성차를 백분율로 구한다
			LOCAL:6 = PERCENT_CAL_F(LOCAL:5,LOCAL:4)
			
			;DEBUG 표시
			IF FLAG:999 == 1
				PRINTFORML PC 지성：{LOCAL:4}(보정 전 {MAXBASE:지성})
				CALL TENTACLE_ACCESS, "CHISEI"
				SIF FLAG:110
					RESULT = LOCAL:5
				PRINTFORML 적 지성：{LOCAL:5}(보정 전 {RESULT})
				PRINT 지성차 
				SIF LOCAL:6 > 0
					PRINT +
				PRINTFORML {LOCAL:6}％
			ENDIF
			
			;+0% ~ 100% 범위로 제한한다, 조만간 민첩차 보정식처럼 얘도 보정식을 하나 만들어 줘야겠다
			SIF LOCAL:6 < 0
				LOCAL:6 = 0
			SIF LOCAL:6 > 100
				LOCAL:6 = 100
				
			LOCAL:0 += LOCAL:6 / 10
		ENDIF
	ELSE
		LOCAL:0 = 10
	ENDIF

	;피해량 계산
	IF TCVARn:0 == 1
		CALL DAMAGE, "ATTACK_RANGE_SHORT"
	ELSEIF TCVARn:0 == 2
		CALL DAMAGE, "ATTACK_RANGE_MIDDLE"
	ELSEIF TCVARn:0 == 3
		CALL DAMAGE, "ATTACK_RANGE_LONG"
	ENDIF

	LOCAL:1 = RESULT * LOCAL:0 / 10

	;[반격] 버스트 공격의 축적 피해량 가산
	;치명타 계산까지 이행한 이후의 최종 피해량에 가산한다
	;단, 버스트 공격이 활성화된 상태로 강화 반격에 성공할 시, 버스트 공격 효과가 적용되지 않는다
	SIF GETBIT(TCVARn:217, 0) && TCVARn:2 != 자세：반격성공 && FSTYLE_NAME_F(TARGET, TCVAR:0) == "반격"
		LOCAL:1 += TCVARn:205 / 5

	FLAG:13 -= LOCAL:1
	LOCAL:2 = LOCAL:1
	RESULT = 0
	IF LOCAL:0 > 10
		;치명타 플래그
		HIT_FLAG = 2
		;지문: 공격이 치명타로 들어갔다
		CALL MESSAGE_BATTLE_CHARA_ATTACK_CRITICAL_HIT, ATTACK_NUM
		;지문: 세뇌/흑화 캐릭터가 조작 캐릭터의 공격을 치명타로 받았다
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_BATTLE_CHARA_ATTACK_CRITICAL_HIT, ATTACK_NUM
	ELSE
		;직격 플래그
		HIT_FLAG = 1
		;지문: 공격이 통상 명중했다
		CALL MESSAGE_BATTLE_CHARA_ATTACK_HIT, ATTACK_NUM
		;지문: 세뇌/흑화 캐릭터가 조작 캐릭터의 공격에 피격됐다
		SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			CALL MESSAGE_OTHER_BATTLE_CHARA_ATTACK_HIT, ATTACK_NUM
	ENDIF
	;마무리 연출용
	SIF FLAG:13 <= 0 && RESULT == 999
		LOCAL:1 = LOCAL:1 * 16
	FONTBOLD
	PRINTFORML {LOCAL:1}의 피해량을 입혔다！
	FONTREGULAR
	SIF FLAG:999 && HIT_FLAG == 2
		PRINTFORML 치명타 배율：{LOCAL:0 * 10}％
	PRINTW 
	IF LOCAL:2 >= 5000
		CALL UNLOCK_ACHIEVEMENT(263,"파괴신의 일격")
	ENDIF

	;방심도 상승의 추가 효과
	LOCAL:3 = 0
	;[설치] 스타일
	IF FSTYLE_NAME_F(TARGET,TCVARn:0) == "설치"
		LOCAL:3 += 300
		;[설치] 버스트의 효과
		SIF GETBIT(TCVARn:217, 0)
			LOCAL:3 += 300
	ENDIF
	IF LOCAL:3 > 0
		IF (FLAG:110 == 0)
			CALL TENTACLE_ACCESS, "NAME"
		ELSEIF (FLAG:110 == 1)
			PRINTFORM %PRINT_TRANSCALLNAME(FLAG:111)%
		ENDIF
		LOCALS = {LOCAL:3}
		TOFULL LOCALS
		PRINTFORML 의 방심도가 조금 상승했다……（＋%RESULTS%）
		PRINTW 
		FLAG:17 += LOCAL:3
	ENDIF
	;[교란] 버스트의 기죽임 효과 50％, 치명타일 시 100％
	IF GETBIT(TCVARn:217, 0) && FSTYLE_NAME_F(TARGET,TCVARn:0) == "교란"
		IF HIT_FLAG == 2 || RAND:2 == 0
			TFLAG:1 = 1
			PRINTFORML 상대의 자세를 무너뜨렸다！
		ENDIF
	ENDIF
ENDIF

;추격 발동
IF ATTACK_NUM
	ATTACK_NUM --
	GOTO ATTACK_AGAIN
ENDIF

;[전력] 스타일의 반동
SIF FSTYLE_NAME_F(TARGET,TCVARn:0) == "전력"
	TFLAG:99 += 2

;버스트 공격의 반동
IF GETBIT(TCVARn:217, 0)
	SELECTCASE FSTYLE_NAME_F(TARGET, TCVARn:0)
		CASE "연속"
			TFLAG:99 += 2
		CASE "장갑"
			TFLAG:99 += 2
		CASE "교란"
			TFLAG:99 += 2
		CASE "중격"
			TFLAG:99 += 3
		;버스트 공격과 강화 반격은 함께 사용할 수 없으므로 무효
		CASE "반격"
			IF TCVARn:2 == 자세：반격성공
			ELSE
				TFLAG:99 += 3
				IF TCVARn:205 > TCVARn:206
					;지문: 에너지 과잉 축적
					CALL MESSAGE_BATTLE_TENTACLE_ATTACK_OVERCHARGE
					;지문: 세뇌/흑화 캐릭터가 조작 캐릭터의 에너지 과잉 축적을 봤다
					SIF ENEMY_TYPE_CHECK_F("AKUOTI")
						CALL MESSAGE_OTHER_BATTLE_TENTACLE_ATTACK_OVERCHARGE
					
					;축적 피해량이 한도치의 1%를 초과할 때마다 최대 체력의 1%만큼 캐릭터가 피해를 입는다
					;상한은 50%
					CALL PERCENT_CAL(TCVARn:205,TCVARn:206)
					RESULT -= 100
					SIF RESULT > 50
						RESULT = 50

					LOCAL:2 = MAXBASE:체력 * RESULT / 100

					FONTBOLD
					PRINTFORML {LOCAL:2}의 피해를 입었다！
					SIF BASE:체력 - (LOCAL:2) <= 0
						LOCAL:2 = BASE:체력
					BASE:체력 -= LOCAL:2

					;기력까지 소모할 지는 일단 지켜보자
					;PRINTFORML %BASENAME:1%이 {LOCAL:2} 줄었다！
					;FONTREGULAR
					;PRINTL 
					;SIF BASE:기력 - (LOCAL:2) <= 0
					;	LOCAL:2 = BASE:기력
					;BASE:기력 -= LOCAL:2

					;과잉 축적 5%마다 피로도가 3씩 상승한다
					FOR RESULT, RESULT, 0, -5
						TFLAG:99 += 3
					NEXT
				ENDIF
			ENDIF
		CASE "광역"
			TFLAG:99 += 3
		CASE "전력"
			TFLAG:99 += 4
		CASE "지략"
			TFLAG:99 += 2
		CASE "설치"
			TFLAG:99 += 1
		CASE "사역"
			TFLAG:99 += 3
		CASE "통상"
			TFLAG:99 += 1
	ENDSELECT
	;FEAT 효과에 의한 반동 증가
	SIF TALENT:핫스타트 > 0
		TFLAG:99 += 1
	SIF TALENT:풀버스트 > 0
		TFLAG:99 += 1

	;[장갑] 버스트의 오토 가드 효과
	IF FSTYLE_NAME_F(TARGET,TCVARn:0) == "장갑"
		TCVARn:2 = 자세：방어
		PRINTFORML %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 방어 태세를 갖췄다！
	ENDIF
	;[지략] 버스트의 관찰(관찰한다) 효과
	IF FSTYLE_NAME_F(TARGET,TCVARn:0) == "지략"
		TFLAG:25 = 1
		PRINTFORML %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 관찰 상태가 됐다！
	ENDIF
ENDIF

;새디스트의 효과
IF TALENT:새드기질 > 0 && HIT_FLAG > 0
	;기력이 미량으로 회복
	LOCAL:1 = RAND:MAX((MAXBASE:기력 * HIT_FLAG * 8 / 100), 1) + 10
	SIF BASE:기력 + LOCAL:1 >= MAXBASE:기력
		LOCAL:1 = MAXBASE:기력 - BASE:기력
	BASE:기력 += LOCAL:1
	;욕정이 미량으로 증가
	COMMON_PALAM:7 += LOCAL:1 * 10
	SIF LOCAL:1 > 0
		PRINTFORML 새디스틱한 쾌락으로 %BASENAME:1%이 {LOCAL:1} 회복됐다.
ENDIF

;[반격] 스타일의 통상 반격 준비
;버스트 공격의 경우 반격을 취하지 않는다
IF FSTYLE_NAME_F(TARGET,TCVARn:0) == "반격" && (TCVARn:2 != 자세：반격 || TCVARn:2 != 자세：반격성공) && GETBIT(TCVARn:217, 0) == 0
	;지문: 반격 준비
	CALL MESSAGE_BATTLE_CHARA_HANGEKI
	;지문: 세뇌/흑화 캐릭터가 조작 캐릭터의 반격 태세를 봤다
	SIF ENEMY_TYPE_CHECK_F("AKUOTI")
		CALL MESSAGE_OTHER_BATTLE_CHARA_HANGEKI
	TCVARn:2 = 자세：반격
ENDIF

RETURN 1



;공중 공격 플래그 관리
@ATTACK_AIR_FLAG
IF GETBIT(TCVARn:216, 1) && GETBIT(TCVARn:216, 0) == 0
	TCVARn:216 = 4
	;FEAT 효과에 의한 착지 시의 반동 증가
	SIF TALENT:날개 > 0
		TFLAG:99 += 1
ELSEIF GETBIT(TCVARn:216, 0) && BASE:공중대시
	SETBIT TCVARn:216, 1
	BASE:공중대시 -= 1
	;공중 공격의 반동
	TFLAG:99 += 1
	;FEAT 효과에 의한 반동 경감
	SIF TALENT:공중부유 > 0
		TFLAG:99 -= 1
	;FEAT 효과에 의한 반동 증가
	SIF TALENT:에어마스터 > 0
		TFLAG:99 += 1
ENDIF


