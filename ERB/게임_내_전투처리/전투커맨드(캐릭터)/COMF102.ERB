;풋잡 공격
@COM102
#DIM SELECT
;거리 표시
CALL PRINT_DISTANCE
PRINTL 

SELECT = -1
VARSET LOCAL

;성 커맨드 선택 루틴으로 판정
;삽입계 성 커맨드가 나오면 카운터 발동 성공
CALL ENEMY_ACTION_SEX_ROUTINE
SELECT = RESULT

;선택한 커맨드의 분류 호출
IF ENEMY_TYPE_CHECK_F("MOB") == 1
	TRYCALLFORM SEX_TYPE_MOB_{FLAG:11}_COM{SELECT}
ELSE
	TRYCALLFORM SEX_TYPE_COM{SELECT}
ENDIF
LOCAL = RESULT

;삽입계 커맨드가 선택된 경우 카운터 발동
;기교 의존으로 발동 확률 향상
IF (LOCAL & 삽입) && RAND:100 < 95 || (RAND:100 < SQRT(1 + 50 * ABL:기교))
	;사정 부위 플래그: 균열과 오발을 세운다
	TFLAG:4 = TFLAG:4 | 균열
	TFLAG:4 = TFLAG:4 | 오발

	;방심도 상승
	TFLAG:3 += 105 + 5 * ABL:기교

	;지문: 풋잡 공격
	CALL MESSAGE_BATTLE_CHARA_SEX_ATTACK102
	;지문: 세뇌/흑화 캐릭터가 조작 캐릭터의「풋잡공격」을 봤다
	SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
		CALL MESSAGE_OTHER_BATTLE_CHARA_SEX_ATTACK102

	;사정 게이지를 기록
	LOCAL:1 = FLAG:15

	;사정
	LOCAL = MIN(MAXBASE:성내성 / 2, 150) + 150 + 75 * ABL:기교
	CALL TENTACLE_SYASEI_UP, LOCAL

	PRINTL
	IF (FLAG:110 == 0)
		CALL TENTACLE_ACCESS, "NAME"
	ELSEIF (FLAG:110 == 1)
		PRINTFORM %PRINT_TRANSCALLNAME(FLAG:111)%
	ENDIF
	PRINT 에게 
	FONTBOLD
	PRINTFORM {FLAG:15 - LOCAL:1} 의 쾌락
	FONTREGULAR
	PRINTL 을 줬다！
	PRINTW 

	;촉수의 사정 체크
	CALL TENTACLE_SYASEI_CHECK
	PRINTL 

	;습득 획득
	COMMON_PALAM:6 += 1050 + 25 * (ABL:순종 + ABL:봉사정신 * 2)

	;행동 예약을 비운다
	TFLAG:17 = -1

	;촉수를 행동 불능으로 만든다
	TFLAG:1 = 1

	;행동 포인트를 +1
	EX:행동포인트 += 1

	RETURN 1
;카운터에 실패
ELSE
	PRINTFORML %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 풋잡 자세를 취했다！
	PRINTW 
	PRINTFORML 그러나 %PRINT_TRANSCALLNAME(TARGET)%의 행동은 실패했다！
	PRINTL 

	;습득 획득
	COMMON_PALAM:6 += 250 + 25 * (ABL:순종 + ABL:봉사정신 * 2)

	;행동 포인트를 +1
	EX:행동포인트 += 1

	FLAG:17 = SELECT

	RETURN 1
ENDIF
