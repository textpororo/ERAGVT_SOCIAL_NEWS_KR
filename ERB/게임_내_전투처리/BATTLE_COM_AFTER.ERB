;전투 커맨드 실행 후 처리
@SOURCE_CHECK

;강구속(피스톤) 중이거나 통째로 삼킬 준비 중에는 패배／시간 만료 패배가 지연된다
#DIM 턴내패배취소,1
턴내패배취소=0

;**********************************************************
;플레이어 행동 직후의 공통 처리
;**********************************************************
;ＳＰ변신 종료까지는 피로가 축적되지 않는다
IF CFLAG:1 == 2
	TFLAG:99 += 1
;그 이외라면 행동의 반동으로 피로가 축적된다
ELSEIF TFLAG:99 > 0
	CFLAG:99 += TFLAG:99
	RESULTS = {TFLAG:99}
	TOFULL RESULTS
	PRINTFORML %PRINT_CALLNAME(TARGET)%의 신체 밑바닥에 피로가 쌓였다……（＋%RESULTS%）
	PRINTW 
	TFLAG:99 = 0
ENDIF

;**********************************************************
;장비 효과의 발동
;**********************************************************
;TK-드론
SIF CFLAG:TARGET:43 == 506
	CALL TK_DRONE
;우주 씰
SIF CFLAG:TARGET:43 == 507
	CALL HP_AUTOREGAIN
;심부름꾼
SIF CFLAG:TARGET:43 == 509
	CALL SERVANT
	
;**********************************************************
;히든 보스 형태 변화 처리
;**********************************************************
IF ENEMY_TYPE_CHECK_F("LASTBOSS") >= 3
	CALL PERCENT_CAL, FLAG:13, FLAG:12
	IF ENEMY_TYPE_CHECK_F("LASTBOSS") == 3 && RESULT <= 75
		PRINTL …
		PRINTL ……
		PRINTW ………
		PRINTL 
		PRINTL 두꺼운 갑각을 뚫고, 겨우겨우 내부에 일격이 도달한다……
		PRINTL 그랬더니「천사의 나무」의 꽃봉오리가 활짝 피어, 주위에 요상한 향기가 자욱이 낀다……！！
		PRINTW 「천사의 나무」는「낙원의 꽃」으로 변화했다……！！
		PRINTL 
		FLAG:21 = 2
	ELSEIF ENEMY_TYPE_CHECK_F("LASTBOSS") == 4 && RESULT <= 37
		PRINTL …
		PRINTL ……
		PRINTW ………
		PRINTL 
		PRINTL 적이 고통에 몸부림치며, 살덩어리 속 꽃잎의 색깔이 바래 간다……
		PRINTL 육종과도 같은 꽃이 차례차례로 시들어 가고, 적의「핵」이 모습을 드러냈다……！！
		PRINTW 「낙원의 꽃」은「타락의 핵」으로 변화했다……！！
		PRINTL 
		FLAG:21 = 3
	ELSEIF ENEMY_TYPE_CHECK_F("LASTBOSS") == 5 && RESULT <= 10
		PRINTL …
		PRINTL ……
		PRINTW ………
		PRINTL 
		PRINTL 둘러 쳐진 살덩이의 뿌리로부터 영양이 빨아 올려가자,
		PRINTW 찢긴 채 썩어 문드러진 적의 촉수가 순식간에 재생되어 간다……！！
		LOCAL = FLAG:12 * 70 / 100 - FLAG:13
		PRINTFORMW 「타락의 핵」의 체력이 {LOCAL} 회복됐다……！！
		PRINTL 
		FLAG:13 += LOCAL
		FLAG:21 = 4
	ELSEIF ENEMY_TYPE_CHECK_F("LASTBOSS") == 6 && FLAG:13 <= 0
		PRINTL …
		PRINTL ……
		PRINTW ………
		PRINTL 
		PRINTL  쩌저적, 하고 금가는 듯한 소리가 울려 퍼져, 모든 것이 제지됐다……
		PRINTL 마지막 일격이 물을 끼얹은 듯이 이형 기둥들의 움직임을 멈췄고,
		PRINTW 조용히「타락의 핵」이 너덜너덜하게 풍화되어, 땅에 떨어져 부서져 갔다.
		PRINTL 
		PRINTL 중추를 잃은 주위의 육괴들이 생기를 잃어 돌처럼 굳었고,
		PRINTW 처음에는 먼 땅울림과 함께, 이윽고 성대한 굉음이 되어 와르르 붕괴를 시작했다……
		PRINTL 
		PRINTW 모든 것이 흙먼지에 휩싸여 아무것도 보이지 않게 됐다……
		PRINTL 
		PRINTL …
		PRINTL ……
		PRINTW ………
		PRINTL 땅울림이 가라앉았을 때에는, 이미 아무것도 남아 있지 않았다.
		PRINTW 주변에 펼쳐진 것은 싸움으로 황폐해진 폐허와, 무지개빛 모래가 되어 흩어지는 촉수의 잔해뿐.
		PRINTL 찾아다녀도 생존자의 기척은 없어,
		PRINTFORML %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 사람 하나 없는 황야에 홀로 서 있을 수밖에 없었다.
		PRINTL 「천사의 나무」에게 거두어진 이들은, 함께 모래가 되어 사라져 버린 것이었다.
		PRINTW 이래서야, 대체 무엇을 위해 싸운 것인가――
		PRINTL 
		PRINTL …
		PRINTL ……
		PRINTL ………
		PRINTW ――아니, 틀렸다！　무너져 내린 잔해에 숨어 있듯이 소녀가 쓰러져 있었다！
		PRINTL 필사적으로 모래를 파헤쳐 벌거벗은 채 기절해 있던 소녀를 구해내어 껴안자,
		PRINTW 약하디약하게도, 하지만 확실하게 느껴지는 고동의 감촉이 가슴을 울렸다.
		PRINTL 또 저편에서 희미하게 들리는 소리에 귀를 기울이니,
		PRINTL 주위에서는 서로 말을 걸며 구조 활동에 힘쓰는 사람들의 활기가 전해져 왔다.
		PRINTW 그렇다, 인류를 덮친 위협은 이미 떠난 것이었다.
		PRINTL 
		PRINTFORML 안도감 때문인지 안심하여 그 자리에 털썩 주저앉은 %PRINT_TRANSCALLNAME(TARGET)%의 뺨을,
		PRINTW 무지갯빛 바람이 부드럽게 쓰다듬고 갔다.……
		PRINTL 
		FLAG:21 = 0
	ENDIF
ENDIF

;**********************************************************
;전투 종료 처리１　승리
;**********************************************************
IF FLAG:13 <= 0
	;모든 보스 촉수의 피해 축적치가 회복
	IF ENEMY_TYPE_CHECK_F("MOB") == 0 && ENEMY_TYPE_CHECK_F("CITIZEN") == 0
		CALL GET_BOSS_ERB_NUM
		FOR LOCAL,1,RESULT + 1
			FLAG:(300 + LOCAL) -= RAND:(11) + 50
			SIF FLAG:(300 + LOCAL) < 0
				FLAG:(300 + LOCAL) = 0
		NEXT
	ENDIF

	;전투 결과 플래그：승리
	TFLAG:98 = 1

	;★현시점에서 악질 시민전은 승리 분기로 들어가지 않는다
	;　(구속에서 탈출＝타임 업 시 분기로 파생)
	;　악질 시민전 확장에 맞추어 요개변

	;보스 촉수에게 승리
	IF ENEMY_TYPE_CHECK_F("BOSS") == 1 || ENEMY_TYPE_CHECK_F("LASTBOSS") >= 1
	
		;지문: 전투 승리
		CALL MESSAGE_BATTLE_END_WIN
	
		;보스 촉수
		IF ENEMY_TYPE_CHECK_F("BOSS") == 1
			;색적 타겟을 쓰러뜨렸을 경우 색적을 초기화
			SIF FLAG:18 == FLAG:11
				FLAG:18 = 0
			;쓰러뜨린 보스의 축적 피해를 초기화
			FLAG:(FLAG:11 + 300) = 0
			;ENDLESS모드에서는 비트를 눕히지 않고 보스의 총수만을 늘린다
			IF GAME_OPTION_CHECK_F(OPTION_endless) && FLAG:11 > 0
				FLAG:3 += 1
				;같은 보스와 연속으로 조우하지 않도록 한다
				CALL TENTACLE_SURVIVE,"NUM"
				$ENDLESS_TARGET_LOOP
				FLAG:18 = RAND:RESULT
				SIF FLAG:18 == FLAG:11
					GOTO ENDLESS_TARGET_LOOP
			;ENDLESS모드 이외에서는 쓰러뜨린 보스 촉수의 비트를 눕힌다
			ELSEIF FLAG:11 > 0
				CLEARBIT FLAG:100,FLAG:11 - 1
			ENDIF



			;촉수혈
			TRYCALL SUPART_BLOOD
		;최종 보스（Ｋ）or 히든 보스 촉수 격파 시
		ELSEIF ENEMY_TYPE_CHECK_F("LASTBOSS") > 0 && GET_LASTBOSS_PHASE_F() > 0
			;색적 타겟을 쓰러뜨렸을 경우 색적을 초기화
			SIF FLAG:18 == FLAG:11
				FLAG:18 = 0

			;쓰러뜨린 최종 보스의 축적 피해를 초기화
			FLAG:(FLAG:11 + 400) = 0

			;주회차＆HARDCORE에서 Ｋ촉수를 격파한 경우
			IF FLAG:854 > 0 && GAME_OPTION_CHECK_F(OPTION_hardcore) && GET_LASTBOSS_PHASE_F() == 1
				;히든 보스를 등장시킬 준비
				FLAG:4 += 1
				FLAG:1 += 5
				FLAG:21 = 1
			;히든 보스 격파
			ELSEIF GET_LASTBOSS_PHASE_F() == 2
				;엔딩
				FLAG:64 = -1
				CALL UNLOCK_ACHIEVEMENT(270,"월등한 최강 대 최강")
			ELSE
				;엔딩
				FLAG:64 = -1
			ENDIF

			;쓰러뜨린 최종 보스 촉수의 비트를 눕힌다
			SIF FLAG:11 > 0
				CLEARBIT FLAG:101,FLAG:11 - 1

		ENDIF
		
		;보스 격파 시의 공통 처리
		IF BASE:체력 == MAXBASE:체력 && BASE:기력 == MAXBASE:기력 && TFLAG:0 >= 10
			CALL UNLOCK_ACHIEVEMENT(266,"퍼펙트 킬")
		ENDIF

		CALL AFTER_KILLED_BOSS

		;보스에게 세뇌당한 캐릭터의 구출
		LOCAL:1 = 0
		FOR LOCAL, 0, CHARANUM
			SIF LOCAL == MASTER
				CONTINUE
			IF CFLAG:LOCAL:0 == 2 && CFLAG:LOCAL:20 == FLAG:10 && CFLAG:LOCAL:21 == FLAG:11
				CALL UNLOCK_ACHIEVEMENT(273,"난 지금까지 무엇을…？")
				IF LOCAL:1 == 0
					;지문: 세뇌 캐릭터의 구출
					CALL MESSAGE_BATTLE_END_RESCUE_SENNOU
				ENDIF
				CFLAG:LOCAL:0 = -1
				CFLAG:LOCAL:20 = 0
				CFLAG:LOCAL:21 = 0
				CFLAG:LOCAL:30 = 0
				CFLAG:LOCAL:31 = 0
				BASE:LOCAL:체력 = 1
				BASE:LOCAL:기력 = 1
				BASE:LOCAL:성내성 = 1
				BASE:LOCAL:성내성 = 1
				CFLAG:LOCAL:220 = 0
				LOCAL:2 = TARGET
				TARGET = LOCAL
				;지문: 세뇌 캐릭터의 구출된 쪽
				CALL MESSAGE_OTHER_BATTLE_END_RESCUED_SENNOU
				TARGET = LOCAL:2
				LOCAL:1 = 1
			ENDIF
		NEXT
	
		;사로잡힌 캐릭터의 구출
		LOCAL:1 = 0
		FOR LOCAL, 0, CHARANUM
			SIF LOCAL == MASTER
				CONTINUE
		
			IF CFLAG:LOCAL:0 == 1 && CFLAG:LOCAL:20 == FLAG:10 && CFLAG:LOCAL:21 == FLAG:11
				CALL UNLOCK_ACHIEVEMENT(271,"얼른 도와줄게！")
				IF LOCAL:1 == 0
					;지문: 사로잡힌 캐릭터의 구출
					CALL MESSAGE_BATTLE_END_RESCUE
				ENDIF
				CFLAG:LOCAL:0 = -1
				CFLAG:LOCAL:20 = 0
				CFLAG:LOCAL:21 = 0
				CFLAG:LOCAL:30 = 0
				CFLAG:LOCAL:31 = 0
				BASE:LOCAL:체력 = 1
				BASE:LOCAL:기력 = 1
				BASE:LOCAL:성내성 = 1
				CFLAG:LOCAL:220 = 0
				LOCAL:2 = TARGET
				TARGET = LOCAL
				;지문: 구출된 쪽
				CALL MESSAGE_OTHER_BATTLE_END_RESCUED
				TARGET = LOCAL:2
				LOCAL:1 = 1
			ENDIF
		NEXT


		;최종 보스가 존재하지 않는 경우
		IF FLAG:100 == 0 && FLAG:101 == 0
			;미출현이라면
			IF FLAG:64 != -1
				;최종 보스 격파에 따른 히든 보스 출현
				IF FLAG:854 > 0 && GAME_OPTION_CHECK_F(OPTION_hardcore) && ENEMY_TYPE_CHECK_F("LASTBOSS") > 1 && FLAG:21 > 0
					FLAG:101 = 2
					CALL MESSAGE_BATTLE_END_LASTBOSSAPPEAR_2
				;최종 보스 출현
				ELSEIF ENEMY_TYPE_CHECK_F("LASTBOSS") == 0
					FLAG:101 = 1
					CALL MESSAGE_BATTLE_END_LASTBOSSAPPEAR
				ENDIF
			;출현한 이후 최종 보스를 쓰러뜨렸다면
			ELSE
				;지문: 완전 섬멸
				CALL MESSAGE_BATTLE_END_PERFECT
				;실적
				IF GAME_OPTION_CHECK_F(OPTION_solo) && TALENT:Ｃ민감 && TALENT:Ｖ민감 && TALENT:Ａ민감 && TALENT:Ｂ민감
					CALL UNLOCK_ACHIEVEMENT(260,"촉수 따위에겐 지지 않아！")
				ENDIF
				IF GAME_OPTION_CHECK_F(OPTION_solo) && EXP:유폐경험 >= 1
					CALL UNLOCK_ACHIEVEMENT(261,"불굴의 투지")
				ENDIF
				LOCAL:97 = 0
				LOCAL:98 = 0
				FOR LOCAL:99,0,CHARANUM
					SIF LOCAL:99 == MASTER
						CONTINUE
					SIF CFLAG:(LOCAL:99):231 > 0
						LOCAL:98 ++
					SIF JUEL:(LOCAL:99):수련P > LOCAL:97
						LOCAL:97 = JUEL:(LOCAL:99):수련P
				NEXT
				IF LOCAL:97 >= 1000
					CALL UNLOCK_ACHIEVEMENT(259,"기가 막히는 자린고비")
				ENDIF
				IF LOCAL:98 >= 3
					CALL UNLOCK_ACHIEVEMENT(265,"NEXT GENERATION")
				ENDIF
				;엔딩플래그를 세운다
				BEGIN TURNEND
			ENDIF
		ENDIF



	;흑화 캐릭터에게 승리했다
	ELSEIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
		FLAG:110 = 0
	
		;지문: 적 캐릭터와의 전투 승리
		CALL MESSAGE_BATTLE_END_WIN_ENEMY
		;지문: 세뇌/흑화 캐릭터가 조작 캐릭터에게 패배했다
		CALL MESSAGE_OTHER_BATTLE_END_WIN_ENEMY

		IF BASE:체력 == MAXBASE:체력 && BASE:기력 == MAXBASE:기력 && TFLAG:0 >= 10
			CALL UNLOCK_ACHIEVEMENT(266,"퍼펙트 킬")
		ENDIF

		;적 캐릭터의 구출
		CALL UNLOCK_ACHIEVEMENT(272,"정신 차리세요！")
		;지문: 적 캐릭터의 구출
		CALL MESSAGE_BATTLE_END_RESCUE_ENEMY
		
		;적이 세뇌였는지 흑화였는지 일시 보존
		LOCAL:3 = CFLAG:(FLAG:111):0
		
		CFLAG:(FLAG:111):0 = -1
		CFLAG:(FLAG:111):20 = 0
		CFLAG:(FLAG:111):21 = 0
		CFLAG:(FLAG:111):30 = 0
		CFLAG:(FLAG:111):31 = 0
		BASE:(FLAG:111):체력 = 1
		BASE:(FLAG:111):기력 = 1
		BASE:(FLAG:111):성내성 = 1
		;지문: 세뇌/흑화 상태로 쓰러진 쪽
		CALL MESSAGE_OTHER_BATTLE_END_RESCUED_ENEMY
		
		;세뇌 캐릭터의 구출
		IF LOCAL:3 == 3
			LOCAL:1 = 0
			FOR LOCAL, 0, CHARANUM
				SIF LOCAL == MASTER
					CONTINUE
				
				IF FLAG:111 == CFLAG:LOCAL:21 && CFLAG:LOCAL:20 == 2 && CFLAG:LOCAL:0 == 2
					CALL UNLOCK_ACHIEVEMENT(273,"난 지금까지 무엇을…？")
					IF LOCAL:1 == 0
						;지문: 세뇌 캐릭터의 구출
						CALL MESSAGE_BATTLE_END_RESCUE_SENNOU
					ENDIF
					CFLAG:LOCAL:0 = -1
					CFLAG:LOCAL:20 = 0
					CFLAG:LOCAL:21 = 0
					CFLAG:LOCAL:30 = 0
					CFLAG:LOCAL:31 = 0
					CFLAG:LOCAL:100 = 103
					BASE:LOCAL:체력 = 1
					BASE:LOCAL:기력 = 1
					BASE:LOCAL:성내성 = 1
					CFLAG:LOCAL:220 = 0
					LOCAL:2 = TARGET
					TARGET = LOCAL
					;지문: 세뇌 캐릭터의 구출된 쪽
					CALL MESSAGE_OTHER_BATTLE_END_RESCUED_SENNOU
					TARGET = LOCAL:2
					LOCAL:1 = 1
				ENDIF
			NEXT
		ENDIF
		
		;사로잡힌 캐릭터의 구출
		IF LOCAL:3 == 3
			LOCAL:1 = 0
			FOR LOCAL, 0, CHARANUM
				SIF LOCAL == MASTER
					CONTINUE
				
				IF CFLAG:(FLAG:111):240 == CFLAG:LOCAL:21 && CFLAG:LOCAL:20 == 2 && CFLAG:LOCAL:0 == 1
					CALL UNLOCK_ACHIEVEMENT(271,"얼른 도와줄게！")
					IF LOCAL:1 == 0
						;지문: 사로잡힌 캐릭터의 구출
						CALL MESSAGE_BATTLE_END_RESCUE_AKUOTI
					ENDIF
					CFLAG:LOCAL:0 = -1
					CFLAG:LOCAL:20 = 0
					CFLAG:LOCAL:21 = 0
					CFLAG:LOCAL:30 = 0
					CFLAG:LOCAL:31 = 0
					CFLAG:LOCAL:100 = 103
					BASE:LOCAL:체력 = 1
					BASE:LOCAL:기력 = 1
					BASE:LOCAL:성내성 = 1
					CFLAG:LOCAL:220 = 0
					LOCAL:2 = TARGET
					TARGET = LOCAL
					;지문: 구출된 쪽
					CALL MESSAGE_OTHER_BATTLE_END_RESCUED
					TARGET = LOCAL:2
					LOCAL:1 = 1
				ENDIF
			NEXT
		ENDIF
	
	;잡몹적에게 승리
	ELSEIF ENEMY_TYPE_CHECK_F("MOB") == 1
		;지문: 전투 승리
		CALL MESSAGE_BATTLE_END_WIN

		;탐색도 상승
		LOCAL = 8 + RAND:5
		SIF CFLAG:100 != 101
			LOCAL /= 2
		;FEAT 효과에 따른 탐색도 상승
		SIF TALENT:사냥꾼의직감 > 0
			LOCAL += 7
		PRINTFORML 탐색도가 {LOCAL} 상승했다.
		CALL RESEARCH_PROGRESS, LOCAL
		PRINTL 


	ENDIF
	
	BEGIN AFTERTRAIN
ENDIF

;전투 중 플래그가 접혀 있으면 이 앞으로 나아가지 않는다
SIF FLAG:700 == 0
	RETURN

;이전 턴의 공격 장소를 유지
TFLAG:13 = TFLAG:11

;자신의 행동에 따른 PALAM 보정값 처리
;--------------------------------------------------------------------------------------
;부서지지 않는 의상용 판정
LOCAL:1 = 0
SIF (CFLAG:1 == 0 && TCVARn:20 == -1) || (CFLAG:1 == 1 && TCVARn:22 == -1)
	LOCAL:1 |= 2
SIF TCVARn:24 == -1 || (CLOTH_NO_INNER > 0 && LOCAL:1 > 0)
	LOCAL:1 |= 1
SIF CFLAG:(CFLAG:1==0 ? 40 # 41) == 199 && FIGURE_SPLIT(EQUIP:(CFLAG:1==0 ? 99 # 199), 1) == 0
	LOCAL:1 = 0
;운동할 때 클리토리스/페니스에 쾌감이 쑤신다
;쾌Ｃ
IF ABL:Ｃ감각 <= 2
	LOCAL = 0
ELSEIF ABL:Ｃ감각 <= 4
	LOCAL = 4
ELSEIF ABL:Ｃ감각 == 5
	LOCAL = 8
ELSEIF ABL:Ｃ감각 == 6
	LOCAL = 12
ELSEIF ABL:Ｃ감각 == 7
	LOCAL = 20
ELSEIF ABL:Ｃ감각 >= 8 && ABL:Ｃ감각 < 12
	LOCAL = (ABL:Ｃ감각 * 2 / 3) * 15
ELSEIF ABL:Ｃ감각 >= 10
	LOCAL = (ABL:Ｃ감각 * 2 / 3) * 30
ENDIF
SIF ISPENIS()
	LOCAL += 20
CALL CLOTH_BATTLE_HOSEI, "ANTICUP"
	LOCAL -= RESULT
SIF RESULT
	LOCAL /= 2
;직전의 행동
IF SELECTCOM == 8 || SELECTCOM == 9
	LOCAL += 25
ELSEIF SELECTCOM == 1 || SELECTCOM == 5 || SELECTCOM == 6 || SELECTCOM == 7 || SELECTCOM == 16 || SELECTCOM == 17
	LOCAL += 15
ELSEIF SELECTCOM == 2 || SELECTCOM == 3
	LOCAL += 5
ELSE
	LOCAL /= 8
ENDIF
IF ISPENIS()
	;속옷이 없고, 발기하지 않았다
	IF (CLOTH_CHECK(TARGET) & 1) == 0 && (LOCAL:1 & 1) == 0 && PALAM:욕정 < 2000
		LOCAL /= 2
	;속옷이 있고, 발기하지 않았다
	ELSEIF ((CLOTH_CHECK(TARGET) & 1) || (LOCAL:1 & 1)) && PALAM:욕정 < 2000
		LOCAL *= 2
	;풀 발기이며 모조리 드러난 상태라면 진동으로 흔들려서 괴롭다
	ELSEIF (CLOTH_CHECK(TARGET) & 1) == 0 && (LOCAL:1 & 1) == 0
		LOCAL *= 8
	;속옷이 있는 상태에서 풀 발기라면 쓸려서 괴롭다
	ELSE
		LOCAL *= 16
	ENDIF
;속옷으로 클리쨩이 쓸려서 괴롭다
ELSEIF (CLOTH_CHECK(TARGET) & 1) || (LOCAL:1 & 1)
	LOCAL *= 6
;속옷이 없으면 쓸리지 않는다
ELSE
	LOCAL *= 0
ENDIF
SIF TALENT:음핵
	LOCAL = LOCAL * 175 / 100
SIF LOCAL < 100
	LOCAL = 0
COMMON_PALAM:0 += LOCAL

;운동할 때 가슴에 쾌감이 쑤신다
;쾌Ｂ
IF ABL:Ｂ감각 <= 2
	LOCAL = 0
ELSEIF ABL:Ｂ감각 <= 4
	LOCAL = 5
ELSEIF ABL:Ｂ감각 == 5
	LOCAL = 10
ELSEIF ABL:Ｂ감각 == 6
	LOCAL = 20
ELSEIF ABL:Ｂ감각 == 7
	LOCAL = 40
ELSEIF ABL:Ｂ감각 >= 8 && ABL:Ｂ감각 < 12
	LOCAL = (ABL:Ｂ감각 * 2 / 3) * 20
ELSEIF ABL:Ｂ감각 >= 12
	LOCAL = (ABL:Ｂ감각 * 2 / 3) * 40
ENDIF
SIF TALENT:모유체질
	LOCAL += 5
CALL CLOTH_BATTLE_HOSEI, "ANTIBUP"
	LOCAL -= RESULT
SIF RESULT
	LOCAL /= 2
;직전의 행동
IF SELECTCOM == 8 || SELECTCOM == 9
	LOCAL += 25
ELSEIF SELECTCOM == 1 || SELECTCOM == 5 || SELECTCOM == 6 || SELECTCOM == 7 || SELECTCOM == 16 || SELECTCOM == 17
	LOCAL += 15
ELSEIF SELECTCOM == 2 || SELECTCOM == 3
	LOCAL += 5
ELSE
	LOCAL /= 8
ENDIF
;빈유는 옷을 입고 있으면 쓸려서 괴롭다
;남자를 추가
IF TALENT:빈유 > 0 || ISMALE()
	IF (CLOTH_CHECK(TARGET) & 2) || (CLOTH_CHECK(TARGET) & 4) || (LOCAL:1 & 2)
		LOCAL *= 8
	ELSE
		LOCAL = 0
	ENDIF
;거유는 옷이 없어지면 흔들려서 괴롭다. 여자아이적으로 출렁임은 ＮＧ라고요＞＜
ELSEIF TALENT:거유 > 0
	IF (CLOTH_CHECK(TARGET) & 2) || (CLOTH_CHECK(TARGET) & 4) || (LOCAL:1 & 2)
		LOCAL *= 4
	ELSE
		LOCAL *= 16
	ENDIF
	SIF TALENT:거유 > 3
		LOCAL *= 2
	SIF TALENT:거유 > 4
		LOCAL *= 2
;거유가 아니더라도 옷이 없으면 흔들려서 괴롭다
ELSE
	IF (CLOTH_CHECK(TARGET) & 2) || (CLOTH_CHECK(TARGET) & 4) || (LOCAL:1 & 2)
		LOCAL *= 3
	ELSE
		LOCAL *= 12
	ENDIF
ENDIF
SIF TALENT:음유
	LOCAL = LOCAL * 175 / 100
IF PALAM:욕정 < 2000
	LOCAL /= 2
ELSEIF PALAM:욕정 > 10000
	LOCAL = LOCAL * 125 / 100
ENDIF
SIF LOCAL < 100
	LOCAL = 0
COMMON_PALAM:3 += LOCAL

;운동에 의한 영향 표시
IF COMMON_PALAM:0 || (COMMON_PALAM:3 && ISGIRLY())
	PRINTL 
ENDIF
IF COMMON_PALAM:0 && BASE:Ｃ결계내구력 == 0
	IF SELECTCOM == 8 || SELECTCOM == 9
		PRINT 저항하고자 몸부림치는
	ELSEIF SELECTCOM == 1 || SELECTCOM == 2 || SELECTCOM == 3 || SELECTCOM == 5 || SELECTCOM == 6 || SELECTCOM == 7 || SELECTCOM == 16 || SELECTCOM == 17
		PRINT 격렬한 운동으로
	ELSE
		PRINT 사소한 움직임이라도
	ENDIF
	PRINTFORM  %PRINT_TRANSCALLNAME(TARGET)%의 
	SIF ABL:Ｃ감각 >= 6 || TALENT:음핵
		PRINT 완전히 개발된 
	SIF ABL:Ｃ감각 >= 3 || TALENT:Ｃ민감
		PRINT 민감한 
	IF ISPENIS()
		SIF PALAM:욕정 >= 2000
			PRINT 발기 
		PRINT 페니스가 
	ELSE
		PRINT 클리토리스가 
	ENDIF
	IF (CLOTH_CHECK(TARGET) & 1)
		PRINTL 속옷에 쓸려 버려서,
	ELSE
		PRINTL 흔들려 버려서,
	ENDIF

	IF COMMON_PALAM:0 < 250
		PRINT 조금씩 쑤시고 있다・・・
	ELSEIF COMMON_PALAM:0 < 500
		PRINT 감각을 자극받아 쑤셔 버리고 만다・・・
	ELSEIF COMMON_PALAM:0 < 2000
		PRINT 쾌락을 느껴 버리고 있다・・・
	ELSE
		PRINT 강한 쾌락을 느껴 버리고 있다・・・
	ENDIF
	PRINTL 
ENDIF
IF COMMON_PALAM:3 && ISGIRLY() && BASE:Ｂ결계내구력 == 0
	IF SELECTCOM == 8 || SELECTCOM == 9
		PRINT 저항하고자 몸부림치는
	ELSEIF SELECTCOM == 1 || SELECTCOM == 2 || SELECTCOM == 3 || SELECTCOM == 5 || SELECTCOM == 6 || SELECTCOM == 7 || SELECTCOM == 16 || SELECTCOM == 17
		PRINT 격렬한 운동으로
	ELSE
		PRINT 사소한 움직임이더라도
	ENDIF
	PRINTFORM  %PRINT_TRANSCALLNAME(TARGET)%의 
	SIF ABL:Ｂ감각 >= 6 || TALENT:음유
		PRINT 완전히 개발된 
	IF TALENT:거유 > 0
		PRINT 풍만
		IF ABL:Ｂ감각 >= 3 || TALENT:Ｂ민감
			PRINT 하고 
		ELSE
			PRINT 한 
		ENDIF
	ENDIF
	SIF ABL:Ｂ감각 >= 3 || TALENT:Ｂ민감
		PRINT 민감한 
	IF ISMALE()
		;#region
		PRINTFORM 젖꼭지가 
		;#endregion
	ELSE 
		PRINTFORM 가슴이 
	ENDIF
	IF (CLOTH_CHECK(TARGET) & 2) || (CLOTH_CHECK(TARGET) & 4)
		PRINTL 옷에 쓸려 버려서,
	ELSE
		SIF TALENT:거유 > 0 && (SELECTCOM == 1 || SELECTCOM == 2 || SELECTCOM == 3 || SELECTCOM == 5 || SELECTCOM == 6 || SELECTCOM == 7 || SELECTCOM == 8 || SELECTCOM == 9 || SELECTCOM == 16 || SELECTCOM == 17)
			PRINT 크게 
		PRINTL 출렁이고 말아서,
	ENDIF

	IF COMMON_PALAM:3 < 250
		PRINT 조금씩 쑤시고 있다……
	ELSEIF COMMON_PALAM:3 < 500
		PRINT 감각을 자극받아 쑤셔 버리고 만다……
	ELSEIF COMMON_PALAM:3 < 2000
		PRINT 쾌락을 느껴 버리고 있다……
	ELSE
		PRINT 강한 쾌락을 느껴 버리고 있다……
	ENDIF
	PRINTL 
ENDIF
IF COMMON_PALAM:0 || (COMMON_PALAM:3 && ISGIRLY())
	PRINTW 
ENDIF

;--------------------------------------------------------------------------------------
;심경의 자동 변화
SIF RAND:100 < (TCVARn:11 * TCVARn:11) && (TCVARn:12 & 기절) == 0
	CALL SHINKYOU_CHANGE, "NORMAL"

;--------------------------------------------------------------------------------------
;끈적끈적 지속 판정, 반격은 순서를 맨 마지막에 둬야 하므로 HANGEKI_TO_TENTACLE 함수에서 따로 처리한다
IF (TCVARn:12 & 끈적끈적) && (TCVARn:2 == 자세：방어 || TCVARn:2 == 자세：날뛴다방어 || TCVARn:2 == 자세：날뛴다실패 || TCVARn:2 == 자세：날뛴다치명타) && RAND:100 < MAX(75 - CFLAG:99 * 3, 25)
	PRINTFORML ○ %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 달라붙은 점액을 털어냈다！
	PRINTFORML 　 %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% [끈적끈적] 상태로부터 회복했다！
	PRINTW 
	TCVARn:12 -= 끈적끈적
ELSEIF (TCVARn:12 & 끈적끈적) && TCVARn:2 == 자세：방어
	PRINTFORML × %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 달라붙은 점액을 털어내는 데에 실패했다！
	PRINTW 
ENDIF

;서브 이벤트：촉수 구속구에 의한 피간
CALL SUBEVENT_BATTLE_ACTTENTACLECLOTH

;서브 이벤트：촉수옷에 의한 피간
CALL SUBEVENT_BATTLE_ACTTENTACLESUIT

;구속 중이 아니라면 촉수의 행동 기록을 초기화
SIF TCVARn:0 != 0
	TFLAG:20 = -999

;의상의 정보를 갱신
CALL REFRESH_CLOTH_DATA

;--------------------------------------------------------------------------------------
;선제 공격으로 촉수의 자세가 흐트러진다
SIF TFLAG:24 > 0
	TFLAG:1 = 1

;**********************************************************
;촉수/적 측의 행동
;**********************************************************
;촉수/적의 자세가 흐트러져 있으면 행동할 수 없다
PRINTL 
FONTBOLD
SETCOLOR 150, 0, 255
PRINTL ********** 적의 행동 **********
RESETCOLOR
FONTREGULAR
PRINTL 
;체공 상태가 아니라면 공격 범위에서 하늘을 제외한다
SIF GETBIT(TCVARn:216, 1) == 0
	CLEARBIT TFLAG:11, 3

;날뛴다의 관계의 추가 처리
;날뛴다 판정에 성공한 경우의 처리
IF TCVARn:2 == 자세：날뛴다방어
	FONTBOLD
	PRINTFORML 날뛴다 저항 판정：성공
	FONTREGULAR
	PRINT 구속을 풀지는 못했지만, 
	IF ENEMY_TYPE_CHECK_F("AKUOTI") == 0
		CALL TENTACLE_ACCESS, "GETNAME"
		PRINTFORM %조사처리(RESULTS,"는")%
	ELSEIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
		PRINTFORM %조사처리(PRINT_TRANSCALLNAME(FLAG:111),"는")%
	ENDIF
	PRINTFORML  %PRINT_TRANSCALLNAME(TARGET)%의 저항에 허를 찔려 자세가 흐트러져 있다！
	PRINTW 

	;촉수의 자세가 흐트러진다
	TFLAG:1 = 1
ELSEIF TCVARn:2 == 자세：날뛴다실패
	FONTBOLD
	PRINTFORML 날뛴다 저항 판정：실패
	FONTREGULAR

	IF ENEMY_TYPE_CHECK_F("BOSS") == 1 || ENEMY_TYPE_CHECK_F("LASTBOSS") == 1
		IF RAND:7 == 0
			IF TALENT:주관시점 > 0
				PRINTFORML 꽉 감긴 촉수는 꿈쩍도 않은 채, %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 있으나 마나한 힘을 들이받았다・・・
			ELSE
				PRINTFORML 꽉 감긴 촉수는 꿈쩍도 하지 않아, %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 자신의 무력함에 입술을 씹었다・・・
			ENDIF
		ELSEIF RAND:6 == 0
			PRINTFORML %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 촉수를 떼어내려 했지만, 아슬아슬하게 힘이 부족해 실패하고 말았다・・・
		ELSEIF RAND:5 == 0
			PRINTFORML %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 휘감긴 촉수를 떼어내려 했지만, 수가 너무 많아서 끝이 없다・・・
		ELSEIF RAND:4 == 0
			PRINTFORML %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 촉수에 저항하려 했지만, 손발이 억눌려 꼼짝도 못 하게 돼 버렸다・・・
		ELSEIF RAND:3 == 0
			PRINTFORML %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 필사적으로 저항했지만, 억지로 깔려 눕혀지고 말았다・・・
		ELSEIF RAND:2 == 0
			PRINTFORML %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 세게 조여오는 심한 고통에, 무심코 틈을 드러내고 말았다・・・
		ELSE
			PRINTFORML %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 갑작스런 애무에 허를 찔려, 저항할 힘을 미처 못 내고 말았다・・・
		ENDIF
	ELSE
		IF RAND:4 == 0
			PRINTFORML %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 촉수에 저항하려 했지만, 손발이 억눌려 꼼짝도 못 하게 돼 버렸다・・・
		ELSEIF RAND:3 == 0
			PRINTFORML %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 필사적으로 저항했지만, 억지로 깔려 눕혀지고 말았다・・・
		ELSEIF RAND:2 == 0
			PRINTFORML %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 세게 조여오는 심한 고통에, 무심코 틈을 드러내고 말았다・・・
		ELSE
			PRINTFORML %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 갑작스런 애무에 허를 찔려, 저항할 힘을 미처 못 내고 말았다・・・
		ENDIF
	ENDIF
	PRINTW 
ELSEIF TCVARn:2 == 자세：날뛴다치명타
	FONTBOLD
	PRINTFORML 날뛴다 저항 판정：치명타
	FONTREGULAR
	IF ENEMY_TYPE_CHECK_F("AKUOTI") == 0
		CALL TENTACLE_ACCESS, "GETNAME"
		PRINTFORM %조사처리(RESULTS,"가")%
	ELSEIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
		PRINTFORM %조사처리(PRINT_TRANSCALLNAME(FLAG:111),"가")%
	ENDIF
	PRINTFORML  예상치 못한 반격에 기죽은 사이에, %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 구속을 벗어났다!
	PRINTW 

	;풀어낸다 처리
	TCVARn:0 = 2
	TFLAG:4 = 0

	;촉수의 자세가 흐트러진다
	TFLAG:1 = 1
	;자동으로 전투 메뉴를 선택한다
	TCVARn:8 = 1
ENDIF


;적의 자세가 흐트러져 있다
IF TFLAG:1 == 1
	;강구속을 푼다
	SIF (TCVARn:12 & 강구속) > 0
		TCVARn:12 -= 강구속
	;플래그류 초기화
	TFLAG:1 = 0
	TFLAG:16 = -1
	TFLAG:17 = -1
	TFLAG:20 = -1
	IF ENEMY_TYPE_CHECK_F("AKUOTI") == 0
		CALL TENTACLE_ACCESS, "GETNAME"
		PRINTFORM %조사처리(RESULTS,"는")%
	ELSEIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
		PRINTFORM %조사처리(PRINT_TRANSCALLNAME(FLAG:111),"는")%
	ENDIF
	PRINTL  자세를 가다듬고 있다・・・
	PRINTL 
	IF ENEMY_TYPE_CHECK_F("AKUOTI")
		CALL SELECT_ENEMY_ACTION
	ELSE
		CALL SELECT_TENTACLE_ACTION
	ENDIF
	
	;의복의 손상에 의한 치정 취득을 위해 SOURCE의 계산으로
	;실제 계산은 COMMON_PALAM_CAL 내부에서 행하고 있다
	CALL PALAM_CAL, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0
ELSE
	CALL ENEMY_ACTION
	;반격 플래그를 지운다
	SIF TCVAR:2 == 자세：반격 || TCVAR:2 == 자세：강화반격 || TCVAR:2 == 자세：반격성공
		TCVAR:2 = 자세：통상
ENDIF

;강구속or통째로 삼키기 준비 중에는 패배 확정 후에도 계속한다
SIF (TCVARn:12 & 강구속) > 0
	턴내패배취소=1
;강구속or통째로 삼키기 준비 중에는 패배 확정 후에도 계속한다
SIF TFLAG:23 ==  100
	턴내패배취소=1


;**********************************************************
;전투 종료 처리２　패배
;**********************************************************
;악질 시민전에서 감금되는 경우
IF FLAG:73 > 0 && BASE:체력 == 0 && BASE:기력 == 0 && BASE:성내성 == 0 && TCVARn:0 == 0
	;패배 취급
	TFLAG:98 = 2
	PRINTL
	PRINTFORML ・・
	PRINTFORML ・・・・
	PRINTFORML ・・・・・・
	PRINTFORM 남자들은 저항이 약해진 %조사처리(PRINT_TRANSCALLNAME(TARGET),"를")% 밀어 넘어뜨리곤, 질릴 때까지 좋을대로 몸을 능욕했다……
	;★성경험 적당히 늘려야 할 테지
	;서브 이벤트：악질 시민에게 패배하여 범해졌다
	CALL SUBEVENT_BATTLE_RAPED_CITIZEN

	IF CONFIG_CHECK_PRISON_F(10)
		CALL CALC_GANGBANG("전투 후",5,2)

		PRINTFORM そして男たちはぴくりとも動かなくなった%PRINT_TRANSCALLNAME(TARGET)%
		PRINTDATAL
		DATAFORM を何度か蹴って「大人しく」させ、
		DATAFORM の首を慣れた手つきで締め上げ、
		DATAFORM の股間を蹴り飛ばし、
		ENDDATA

		PRINTDATA
		DATAFORM 手錠と目隠しを付けて
		DATAFORM 媚毒スプレーをたっぷり噴き付けて
		DATAFORM 촉수由来の違法媚薬をトドメとばかりに打ち込んで
		DATAFORM 違法改造スタンガンで悲痛な絶叫と共に意識を奪うと
		ENDDATA

		;＠중국판(略式処刑)
		;＠翻訳までデッドコード化
		IF 1==0 && RAND(24) < 3 && CONFIG_CHECK_MANIAC_F(14) == 1
			PRINTFORML そのまま%조사처리(PRINT_TRANSCALLNAME(TARGET),"를")%持ち帰り、新しい性玩具として使おうと計画した。
			PRINTL 
			PRINTL  しかし
			PRINTL 
			PRINTFORML リーダー格の男は%조사처리(PRINT_TRANSCALLNAME(TARGET),"가")%魔法少女である事に気付き、
			PRINTFORML もしも%조사처리(PRINT_TRANSCALLNAME(TARGET),"가")%支援を受けているなら、拠点に持ち帰るのは危険だと判断した。
			PRINTFORMW 男たちは、代わりに%조사처리(PRINT_TRANSCALLNAME(TARGET),"를")%「処分」できる場所を探すことにした……
			PRINTL 　
			PRINTL 　
			PRINTL 　
			FONTBOLD
			SETCOLOR 102,0,0
			PRINTW 　　　―――――　　　ＷＡＲＮＩＮＧ　！！　　―――――
			PRINTL 
			PRINTW 　　　―――――　　　ＷＡＲＮＩＮＧ　！！　　―――――
			PRINTL 
			PRINTW 　　　―――――　　　ＷＡＲＮＩＮＧ　！！　　―――――
			RESETCOLOR
			PRINTL 　
			PRINTL 　
			PRINTL 패배・처형
			FONTREGULAR
			FORCEWAIT 
			PRINTL 　
			PRINTL 
			CALL INTIMIDATION_SNUFF
		ELSE
			PRINTFORMW 近くの車に押し込み連れ去った。
			PRINTL
			CALL MESSAGE_CITIZEN_HIACED(TARGET,"남자들","협박")
		ENDIF

		RESETCOLOR
		CFLAG:0 = 4
		;救出までの必要ポイント
		CFLAG:71 = 30
	;監禁OFF時の代替テキスト
	ELSE

		PRINTFORML そして男たちは意識の無い%조사처리(PRINT_TRANSCALLNAME(TARGET),"를")%車のトランクに押し込み、「目的地」に向かって発進させる。
		PRINTFORM やがて男たちは人気の無い場所で車を停め、
		PRINTDATA
		DATAFORM 薄暗い下水道
		DATAFORM 粗大ごみ置き場
		DATAFORM 生ゴミ置き場
		DATAFORM 燃えるゴミ置き場
		DATAFORM ゴミ処理場
		DATAFORM 廃道沿いの茂み
		DATAFORM 放棄区域の入り口
		DATAFORM 汚染区域の入り口
		DATAFORM 暗い路地裏
		DATAFORM 山奥の茂み
		DATAFORM 山道の脇
		DATAFORM 開いたマンホールの中
		ENDDATA
		PRINTFORML に%조사처리(PRINT_TRANSCALLNAME(TARGET),"를")%抱えて투척捨てた。
		PRINTFORML 男たちは精液に汚れた全裸の「廃品」など一顧だにせず意気揚々と「次の獲物」を探しに去ってゆき、
		PRINTFORML 壊れたダッチワイフのように動かない%PRINT_TRANSCALLNAME(TARGET)%의 身体だけが闇の中に残されていた……
		CALL CALC_GANGBANG("전투 후",5,1)
	ENDIF
	PRINTL

	;유폐or死亡時は編成から外れる処理によりキャラ枠が手前に詰まるため、「現在行動中のキャラ」の플래그를１つ戻す
	SIF CFLAG:0 == 1 || CFLAG:0 == 9 || CFLAG:0 == 4
		FLAG:799 -= 1

	BEGIN AFTERTRAIN



;通常の敗北処理
ELSEIF BASE:체력 == 0 && BASE:기력 == 0 && BASE:성내성 == 0 && FLAG:13 > 0 && (ENEMY_TYPE_CHECK_F("MOB") == 0 && ENEMY_TYPE_CHECK_F("CITIZEN") == 0)
	;P촉수なら丸飲み準備플래그를立てておく
	IF ENEMY_TYPE_CHECK_F("BOSS") == 1 && FLAG:11==6 && TFLAG:23>=0 && TFLAG:23<100
		TFLAG:23=100
		턴내패배취소=1
	ENDIF

	;※강구속(挿入)中または丸飲み準備中に敗北状態になっていた場合は即座に戦闘終了せずフィニッシュまで見せる
	IF 턴내패배취소>0
		;기절하지 않았다면 기절한다
		CALL STATE_CHANGE_KIZETU, 100

	ELSE
		;발정으로 힘이 다한 경우도 여기로 날아온다
		$BATTLE_LOSE

		PRINTW 
		;전투 결과 플래그：전투 패배
		TFLAG:98 = 2
		
		PRINTL 
		PRINT 전투 결과：패배 - 
		IF ENEMY_TYPE_CHECK_F("AKUOTI") == 0
			CALL TENTACLE_ACCESS, "NAME"
			PRINTL 의 섬멸 실패
		ELSEIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
			PRINTFORML %PRINT_TRANSCALLNAME(FLAG:111)%의 해방 실패
		ENDIF
		PRINTL 
		IF TALENT:주관시점 > 0
			PRINTFORML %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 힘이 다하여 쓰러졌다・・・
		ELSE
			PRINTFORML %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 힘이 다하여 쓰러지고 말았다・・・
		ENDIF
		PRINTL 
		
		;캐릭터의 유폐
		;세뇌 캐릭터에게 패배한 경우에는, 그 캐릭터를 세뇌시킨 촉수가 대신 유폐한다
		;흑화 캐릭터에게 패배한 경우에는, 기생을 가지고 있다면 흑화 캐릭터가 유폐한다
		;기생이 없는 흑화 캐릭터에게 패배한 경우에는, 다 범해지고 나서 해방된다
		
		;ENDLESS모드 이외에서 캐릭터가 잡히면 기한이 연장된다
		SIF GAME_OPTION_CHECK_F(OPTION_endless) == 0
			DAY:1 += ABL:레벨 / 2 + 7

		;음문이 남아 있다면 오염도(얼마나 함락에 가까워지고 있는지 나타내는 내부치)가 상승한다 (최대 75%)
		IF CONFIG_CHECK_MANIAC_F(10) == 1
			CALL TATTOO_ACCESS, "PROGRESS_VAR"
			IF RESULT
				LOCAL = MIN(RESULT,75)
				CALL CHECK_CONTAMINATION
				CFLAG:30 = RESULT * LOCAL / 100
			ENDIF
		ELSE
				CFLAG:30 = 0
		ENDIF

		;ＴＳ 할 수 없는 남자임과 동시에 양성평등 OFF라면 즉시 사망
		IF ISHOLE(TARGET) == 0 && TALENT:변신시ＴＳ < 1 && CONFIG_CHECK_PRISON_F(0) == 0
			CFLAG:0 = 9
			CFLAG:20 = FLAG:10
			CFLAG:21 = FLAG:11

			;지문: 즉사
			PRINTFORML 남자인 %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 그대로 숨통이 끊겨,
			PRINTFORML %STR:2500%에게 거두어지고 말았다・・・
			PRINTW 
			PRINTFORML %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 로스트됐습니다.
			PRINTW 

			;첫 유폐라면 이상경험을 붙인다
			SIF EXP:유폐경험 == 0
				EXP:이상경험 += 1

			EXP:유폐경험 += 1
		;촉수에게 패배하여 유폐된다
		ELSEIF (FLAG:110 == 0)
			CFLAG:0 = 1
			CFLAG:20 = FLAG:10
			CFLAG:21 = FLAG:11

			;지문: 패배
			CALL MESSAGE_BATTLE_END_LOSS

			;첫 유폐라면 이상경험을 붙인다
			SIF EXP:유폐경험 == 0
				EXP:이상경험 += 1

			EXP:유폐경험 += 1
		ELSEIF (FLAG:110 == 1)
			;세뇌 캐릭터에게 패배하여 유폐된다
			IF (CFLAG:(FLAG:111):0 == 2)
				CFLAG:0 = 1
				CFLAG:20 = CFLAG:(FLAG:111):20
				CFLAG:21 = CFLAG:(FLAG:111):21
				
				;지문: 패배
				CALL MESSAGE_BATTLE_END_LOSS
				;지문: 세뇌/흑화 캐릭터가 조작 캐릭터에게 승리했다
				CALL MESSAGE_OTHER_BATTLE_END_LOSS
				
				;첫 유폐라면 이상경험을 붙인다
				SIF EXP:유폐경험 == 0
					EXP:이상경험 += 1
				
				EXP:유폐경험 += 1
			
			;흑화 캐릭터에게 패배
			ELSEIF (CFLAG:(FLAG:111):0 == 3)
				
				;흑화 캐릭터가【기생】을 보유하고 있다면,흑화 촉수 있음일 경우 유폐된다
				IF (TALENT:(FLAG:111):기생 == 1) && CONFIG_CHECK_MANIAC_F(13) == 1
					CFLAG:0 = 1
					CFLAG:20 = 2
					CFLAG:21 = CFLAG:(FLAG:111):240
					
					;지문: 패배
					CALL MESSAGE_BATTLE_END_LOSS
					;지문: 세뇌/흑화 캐릭터가 조작 캐릭터에게 승리했다
					CALL MESSAGE_OTHER_BATTLE_END_LOSS
					
					;첫 유폐라면 이상경험을 붙인다
					SIF EXP:유폐경험 == 0
						EXP:이상경험 += 1
					
					EXP:유폐경험 += 1
				;아니라면 범해진다
				ELSE
					;서브 이벤트：흑화 캐릭터에게 패배하여 범해졌다
					CALL SUBEVENT_BATTLE_RAPED_ENEMY
				ENDIF
			ENDIF
		ENDIF

		;유폐or사망 시에는 편성에서 벗어나는 처리로 캐릭터 범위가 앞에 막히기 때문에,현재 행동중인 캐릭터」의 플래그를１개 되돌린다
		SIF CFLAG:0 == 1 || CFLAG:0 == 9 || CFLAG:0 == 4
			FLAG:799 -= 1


		BEGIN AFTERTRAIN

	ENDIF
ENDIF

;**********************************************************
;전투 종료 처리３　제한 시간 종료
;**********************************************************
;※강구속(삽입) 중에 패배 상태가 된 경우에는 종료하지 않고 피니쉬까지 보여준다
IF BASE:체력 == 0 && BASE:기력 == 0 && BASE:성내성 == 0 && 턴내패배취소>0
	;기절하지 않았다면 기절한다
	CALL STATE_CHANGE_KIZETU, 100
	;시간이 흐르지 않는다
	TFLAG:0 -= 1

;통상적인 제한 시간 경과일 경우
;　통상 전투…기정 턴 경과로 종료
;　악질 시민…구속에서 탈출하면 즉시 전투 종료
ELSEIF (턴상한!=-1 && TFLAG:0 >= 턴상한) || (FLAG:73 > 0 && TCVARn:0 != 0)
	;구출 기능
	TRYCALL KYUSHUTU_TIMEUP_HANTEI
	;지문: 전투 시간 종료
	IF FLAG:73 > 0
		CALL MESSAGE_BATTLE_END_CITIZEN_TIMEUP
	ELSE
		CALL MESSAGE_BATTLE_END_TIMEUP
	ENDIF
	;지문: 세뇌/흑화 캐릭터가 조작 캐릭터와 비겼다
	SIF ENEMY_TYPE_CHECK_F("AKUOTI") == 1
		CALL MESSAGE_OTHER_BATTLE_END_TIMEUP
	
	;서브 이벤트：촉수 구속구 강제 장착
	SIF (ENEMY_TYPE_CHECK_F("MOB") == 0 && ENEMY_TYPE_CHECK_F("CITIZEN") == 0)
		CALL SUBEVENT_BATTLE_SETTENTACLECLOTH

	BEGIN AFTERTRAIN
ENDIF

;--------------------------------------------------------------------------------------
;턴 종료 시의 플래그 초기화
TFLAG:18 = 0

;--------------------------------------------------------------------------------------
;자동 풀어내기 실행
IF CONFIG_CHECK_BALANCE_F(2) > 0
	CALL AUTO_UNTANGLE
ELSE
	TFLAG:22 = 1
ENDIF

;--------------------------------------------------------------------------------------
;발정에 의한 배란 발생 판정
CALL HATUJOU_TO_HAIRAN

;--------------------------------------------------------------------------------------
;【스마타애태우기】에 의한 다음 턴 행동 지정 처리
IF SELECTCOM == 103 && TFLAG:17 < 0 && RAND:100 < 95
	IF ENEMY_TYPE_CHECK_F("MOB") == 1
		TRYCALLFORM TENTACLE_MOB_{FLAG:11}_REACTION_REF, 1
	ELSEIF GET_LASTBOSS_PHASE_F() >= 1
		TRYCALLFORM TENTACLE_LASTBOSS_{FLAG:11}_REACTION_REF, 1
	ELSE
		TRYCALLFORM TENTACLE_BOSS_{FLAG:11}_REACTION_REF, 1
	ENDIF
	SIF RESULT >= 0
		TFLAG:17 = RESULT
ENDIF

;--------------------------------------------------------------------------------------
;턴 종료 시의 상태 이상 처리
IF TCVARn:12 > 0 && TCVARn:12 != 배란
	PRINTL 
	FONTBOLD
	PRINTL 상태 이상
	FONTREGULAR
ENDIF

;강구속 지속 판정
SIF (TCVARn:12 & 강구속)
	PRINTFORML × %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 행동이 제한된 상태이다……
;발정 지속 판정
IF (TCVARn:12 & 발정) && (TCVARn:12 & 기절) == 0
	IF TALENT:주관시점 > 0
		PRINTFORML × %PRINT_TRANSCALLNAME(TARGET)%의 몸은 미약의 효과로 욕정해 버린 상태이다……
	ELSE
		PRINTFORML × %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 미약의 효과로 욕정해 버리고 있다……
	ENDIF
	FONTBOLD
	IF BASE:성내성 > 0
		IF MAXBASE:성내성 >= 300
			LOCAL = RAND:8 + 3
		ELSEIF MAXBASE:성내성 >= 200
			LOCAL = RAND:6 + 2
		ELSE
			LOCAL = RAND:4 + 1
		ENDIF
		PRINTFORML 　 성내성이 {LOCAL} 감소했다！
		PRINTL 
		BASE:성내성 = MAX(BASE:성내성 - LOCAL, 0)
	ELSE
		IF BASE:체력 > 0
			LOCAL = MAX(BASE:체력 / 40, 40) + RAND:30
			PRINTFORML 　 체력이 {LOCAL} 감소했다！
			BASE:체력 = MAX(BASE:체력 - LOCAL, 0)
		ENDIF
		IF BASE:기력 > 0
			LOCAL = MAX(BASE:기력 / 20, 80) + RAND:60
			PRINTFORML 　 기력이 {LOCAL} 감소했다！
			BASE:기력 = MAX(BASE:기력 - LOCAL, 0)
		ENDIF
	ENDIF
	FONTREGULAR
	PALAM:욕정 += LOCAL * 125
ENDIF
;마비 지속 판정
IF (TCVARn:12 & 마비)
	IF (TCVARn:104 > 0 && RAND:(100 - 20 * (CFLAG:TARGET:43 == 508)) < MAX(25 - CFLAG:99 * 5 + TCVARn:104 * 5, 15)) || TCVARn:104 == -999
		PRINTFORML ○ %PRINT_TRANSCALLNAME(TARGET)%의 마비가 풀려났다！
		PRINTFORML 　 %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% [마비] 상태로부터 회복했다！
		TCVARn:12 -= 마비
		TCVARn:104 = 0
	ELSE
		PRINTFORML × %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 신체가 마비되어 민첩성이 떨어진 상태이다……
		TCVARn:104 += 1
	ENDIF
ENDIF
;끈적끈적 처리
IF (TCVARn:12 & 끈적끈적)
	PRINTFORML × %PRINT_TRANSCALLNAME(TARGET)%의 몸에 점액이 달라붙어 떨어지지 않는다……
	IF CFLAG:1 > 0 && (TCVARn:23 > 0 || TCVARn:25 > 0)
		PRINTFORML 　 %PRINT_TRANSCALLNAME(TARGET)%의 의상이 조금씩 녹아 간다！
		LOCAL = RAND:5 + 1
		;의상에의 피해를 반영
		FONTBOLD
		PRINTFORML 　 의상에 {LOCAL}의 손상을 받았다！
		FONTREGULAR
		PRINTL 
		CALL CLOTH_BATTLE_DAMAGE, LOCAL, 1
	ELSEIF TCVARn:21 > 0 || TCVARn:25 > 0
		PRINTFORML 　 %PRINT_TRANSCALLNAME(TARGET)%의 의상이 조금씩 녹아 간다！
		LOCAL = RAND:5 + 1
		;의상에의 피해를 반영
		FONTBOLD
		PRINTFORML 　 의상에 {LOCAL}의 손상을 받았다！
		FONTREGULAR
		PRINTL 
		CALL CLOTH_BATTLE_DAMAGE, LOCAL, 1
	ENDIF
ENDIF
;쓰러짐 지속 판정
IF (TCVARn:12 & 쓰러짐) && (TCVARn:12 & 기절) == 0
	IF (TCVARn:106 > 1 && RAND:(100 - 20 * (CFLAG:TARGET:43 == 508)) < 60 + TCVARn:106 * 5) || TCVARn:106 == -999
		PRINTFORML ○ %PRINT_TRANSCALLNAME(TARGET)%의 다리 떨림이 멈췄다！
		PRINTFORML 　 %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% [쓰러짐] 상태로부터 회복했다！
		TCVARn:12 -= 쓰러짐
		TCVARn:106 = 0
	ELSE
		PRINTFORML × %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 다리가 떨려 마음대로 움직일 수가 없다……
		TCVARn:106 += 1
	ENDIF
ENDIF
;황홀 지속 판정
CALL PERCENT_CAL, BASE:기력, MAXBASE:기력
IF (TCVARn:12 & 황홀) && (TCVARn:12 & 기절) == 0
	IF (TCVARn:107 > 1 && RAND:(100 - 20 * (CFLAG:TARGET:43 == 508)) < RESULT / 4 + 35 + TCVARn:107 * 10) || TCVARn:107 == -999
		PRINTFORML ○ %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 정신을 바로잡았다！
		PRINTFORML 　 %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% [황홀] 상태로부터 회복했다！
		TCVARn:12 -= 황홀
		TCVARn:107 = 0
	ELSE
		PRINTFORML × %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 절정의 여운으로 힘이 들어가지 않는다……
		TCVARn:107 += 1
	ENDIF
ELSE
	TCVARn:107 = 0
ENDIF
;기절 지속 판정
IF (TCVARn:12 & 기절)
	IF BASE:체력 == 0 && BASE:기력 == 0 && BASE:성내성 == 0
		IF TALENT:주관시점 > 0
			PRINTFORML × %PRINT_TRANSCALLNAME(TARGET)%의 의식은 몽롱한 상태이다……
		ELSE
			PRINTFORML × %PRINT_TRANSCALLNAME(TARGET)%의 의식이 돌아올 기미는 없어 보인다……
		ENDIF
		TCVARn:101 += 1
	ELSEIF TCVARn:101 > 0 && RAND:(100 - 20 * (CFLAG:TARGET:43 == 508)) < MAX(35 - CFLAG:99 * 3 + TCVARn:101 * 10, 10) || TCVARn:101 == -999
		IF TALENT:주관시점 > 0
			PRINTFORML ○ %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 의식을 회복했다！
			PRINTFORML 　 %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% [몽롱] 상태로부터 회복했다！	
		ELSE
			PRINTFORML ○ %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 의식을 되찾았다！
			PRINTFORML 　 %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% [기절] 상태로부터 회복했다！
		ENDIF
		TCVARn:12 -= 기절
		TCVARn:101 = 0
	ELSE
		IF TALENT:주관시점 > 0
			PRINTFORML × %PRINT_TRANSCALLNAME(TARGET)%의 의식은 몽롱한 상태이다……
		ELSE
			PRINTFORML × %PRINT_TRANSCALLNAME(TARGET)%의 의식이 돌아올 기미는 없어 보인다……
		ENDIF
		TCVARn:101 += 1
	ENDIF
ELSE
	TCVARn:101 = 0
ENDIF

;발정의 효과로 체력, 기력, 성내성이 다하면 패배 판정을 행한다
IF BASE:체력 == 0 && BASE:기력 == 0 && BASE:성내성 == 0 && FLAG:13 > 0 && (ENEMY_TYPE_CHECK_F("MOB") == 0 && ENEMY_TYPE_CHECK_F("CITIZEN") == 0)
	IF 턴내패배취소 == 0
		GOTO BATTLE_LOSE
	ELSE
		;기절하고 있지 않다면 기절한다
		CALL STATE_CHANGE_KIZETU, 100
	ENDIF
ENDIF

;선제 공격 중이라면 선제 공격 카운트가 진행된다
IF TFLAG:24 > 0
	TFLAG:24 -= 1
;그 외라면 전투 시간 카운트가 진행된다
ELSE
	TFLAG:0 += 1
ENDIF

;추가 고문 장소 초기화
EX_COM = 0
SH_COM = 0
;INSERT = 0



