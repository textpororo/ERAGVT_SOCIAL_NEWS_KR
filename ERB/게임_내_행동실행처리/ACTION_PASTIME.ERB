;--------------------------------------------------
;여가 활동 (息抜き)
@PASTIME
#DIM REST = 0
FLAG:11 = 0

;만약 초중고 대학에 다니지 않을 경우, 30의 배수일에 편입할 기회를 준다
IF TALENT:학생 == 0 && DAY % 30 == 0 && DAY > 29
	CALL PASTIME_SelectSchool
ENDIF

;변신 가능 캐릭터는 변신 상태가 될지 선택할 수 있다
CALL ACTION_TRANSFORMATION_SELECT, TARGET, "여가 활동"

PRINTFORM [0] 거리로 나간다　　　
PRINTFORM [1] 여행간다　　　　
PRINTFORM [2] 운동하러 간다　　　
;학교는 낮에만 표시한다
PRINTL
SIF TIME > 0
	SETCOLOR 105,105,105
SIF TALENT:학생 > 0
	PRINTFORM [3] 학교에 간다　　　　
RESETCOLOR
PRINTL 
PRINTL
DRAWLINE

;스케줄 기능
IF CFLAG:113 > 0
	CALL RES_SCHEDULE(113)

	;통학은 낮에만 가능(스케줄 스킵)
	IF RESULT == 0 && TIME:0 == 1
		;다 안 되면 행선지 랜덤으로 한다
		RESULT = 1
		FOR LOCAL,1,NUM_SCHEDULE_F(113)+1
			CALL RES_SCHEDULE(113)
			;통학 이외의 스케줄을 찾는다
			SIF RESULT != 0
				BREAK
		NEXT
	ENDIF

	IF RESULT == 0
		CALL PASTIME_학교에간다, RESULT
	ELSEIF RESULT == 1
		IF TIME:0 == 0
			SELECTCASE RAND:4
			CASE 0
				CALL PASTIME_학교에간다, -1
			CASE 1
				CALL PASTIME_거리로나간다, RAND:4
			CASE 2
				CALL PASTIME_여행간다, RAND:8
			CASE 3
				CALL PASTIME_운동한다, RAND:4
			ENDSELECT
		ELSE
			SELECTCASE 1+RAND:3
			CASE 1
				CALL PASTIME_거리로나간다, RAND:4
			CASE 2
				CALL PASTIME_여행간다, RAND:8
			CASE 3
				CALL PASTIME_운동한다, RAND:4
			ENDSELECT
		ENDIF
	ELSEIF RESULT == 2
		CALL PASTIME_거리로나간다, RAND:4
	ELSEIF RESULT == 3
		CALL PASTIME_여행간다, RAND:8
	ELSEIF RESULT == 4
		CALL PASTIME_운동한다, RAND:4
	ELSEIF INRANGE(RESULT,5,8)
		CALL PASTIME_거리로나간다, RESULT-5
	ELSEIF INRANGE(RESULT,9,16)
		CALL PASTIME_여행간다, RESULT-9
	ELSEIF INRANGE(RESULT,17,20)
		CALL PASTIME_운동한다, RESULT-17
	ENDIF


	;	IF RESULT == 3
	;		PRINTL 
	;		PRINT 스케쥴：학교에 간다
	;		IF TALENT:학생 > 0 && TIME == 0
	;			PRINT （실행 불능）
	;			PRINTL 
	;			PRINTL 수동으로 행동을 선택하십시오.
	;			GOTO INPUT_LOOP
	;		ENDIF
	;	ENDIF
	;ELSE
	;	$INPUT_LOOP
	;	INPUT
ELSE
	$INPUT_LOOP
	INPUT
	PRINTL 
	IF INRANGE(RESULT, 0, 3)
		DRAWLINE
		SELECTCASE RESULT
			;거리로 나간다
			CASE 0
				CALL PASTIME_거리로나간다(-1)
				REST = RESULT
			;여행간다 (遠出をする)
			CASE 1
				CALL PASTIME_여행간다(-1)
				REST = RESULT
			;운동하러 나간다
			CASE 2
				CALL PASTIME_운동한다(-1)
				REST = RESULT
			;학교
			CASE 3
				IF TALENT:학생 < 1
					PRINTL 올바른 값을 입력해 주십시오.
					GOTO INPUT_LOOP
				ELSEIF TIME > 0
					PRINTL 야간에는 학교에 갈 수 없습니다.
					GOTO INPUT_LOOP
				ELSE
					CALL PASTIME_학교에간다(-1)
					REST = RESULT
				ENDIF
		ENDSELECT
	ELSE
		PRINTL 올바른 값을 입력해 주십시오.
		GOTO INPUT_LOOP
	ENDIF
ENDIF

IF FLAG:73 > 0
	SIF CFLAG:1 > 0
		CALL TRANSFORM, 0
	RETURN
ENDIF

CALL PASTIME_REST

;매료경험의 상승과 FEAT 효과
LOCAL = CALC_CHARM_FEAT_OTHER(TARGET)
IF LOCAL > 0
	EXP:매료경험 += LOCAL
	PRINTFORML 매료경험이 {LOCAL} 올랐다!
ENDIF

CALL _ABLUP, 1
SIF CFLAG:1 > 0
	CALL TRANSFORM, 0
BEGIN TURNEND

;--------------------------------------------------
;여가 활동의 회복 처리
@PASTIME_REST
;체력・기력・성내성・피로도 회복
;체력은 기본 10% 회복한다
;기력은 기본 30% 회복한다
;성내성은 50% 회복한다
;피로도는 시설 확장을 해나가면 최종적으로 역전, 원문은 "疲労度は施設拡張していくと最終的に逆転"

LOCAL:1 = 10
LOCAL:2 = 30

IF TALENT:회복빠름 > 0
	LOCAL:1 += 5
	LOCAL:2 += 5
ELSEIF TALENT:회복느림 > 0
	LOCAL:1 -= 5
	LOCAL:2 += 5
ENDIF
;FEAT 효과에 따른 회복량 보정
IF TALENT:마력저장 > 0
	LOCAL:1 -= 5
	LOCAL:2 -= 5
ENDIF
IF TALENT:불로장수 > 0
	LOCAL:1 -= 5
	LOCAL:2 -= 5
ENDIF
SIF TALENT:넘치는생명력 > 0
	LOCAL:1 += 10
	LOCAL:2 += 10
LOCAL = MAXBASE:체력 * LOCAL:1 / 100 + 200
BASE:체력 = LIMIT(LOCAL + BASE:체력, 0, MAXBASE:체력)

LOCAL = MAXBASE:기력 * LOCAL:2 / 100 + 200
BASE:기력 = LIMIT(LOCAL + BASE:기력, 0, MAXBASE:기력)

BASE:성내성 = LIMIT(BASE:성내성 + (MAXBASE:성내성 / 2), 0, MAXBASE:성내성)

IF CFLAG:0 != -1 && CFLAG:999 != 0
	;구상
	;TRYCALLFORM KOJO_ROOT(CFLAG:6, "REST")

	;피로 회복
	IF CFLAG:99 > 0
		CFLAG:99 -= 10
		SIF CFLAG:99 < 0
			CFLAG:99 = 0
		IF CFLAG:99 == 0
			PRINTFORML %CALLNAME:TARGET%의 몸에 쌓인 피로가 완전히 풀렸다!
		ELSE
			PRINTFORML %CALLNAME:TARGET%의 몸에 쌓인 피로가 조금 풀렸다……
		ENDIF
	ENDIF
	PRINTL 

	SIF BASE:체력 == MAXBASE:체력 && BASE:기력 == MAXBASE:기력 && CFLAG:99 == 0
		PRINTFORML %CALLNAME:TARGET%의 체력, 기력과 피로도가 완전히 회복됐다!

	IF CONFIG_CHECK_SCREEN_F(3) == 0
		PRINTW 
	ELSE
		PRINTL 
	ENDIF
ENDIF

;--------------------------------------------------
;완전 여체화 시의 횟수 덮어쓰기
@PASTIME_TSFLAG_OVERWRITE

;헌팅남에게 강간당한 횟수
CFLAG:320 = CFLAG:321

;치한당한 횟수
CFLAG:325 = CFLAG:326
CFLAG:327 = CFLAG:328

;운동 공원의 횟수
CFLAG:330 = CFLAG:331
CFLAG:332 = CFLAG:333

;피트니스 클럽의 횟수
CFLAG:334 = CFLAG:335
CFLAG:336 = CFLAG:337

;마사지 살롱의 횟수
CFLAG:338 = CFLAG:339
CFLAG:340 = CFLAG:341

;수영장(pool)의 횟수
CFLAG:342 = CFLAG:343
CFLAG:344 = CFLAG:345

;학교 이벤트
CFLAG:350 = CFLAG:351
CFLAG:352 = CFLAG:353
CFLAG:354 = CFLAG:355


