;--------------------------------------------------
;정보 수집
@GATHER_INFORMATION
#DIM CCOUNT
#DIM ACTION
#DIM LOST
ACTION = 0
;행방불명된 동료가 있는지 없는지 서치(히로인 시점에서는 거두어진 캐릭터도 찾는 편이 자연스럽지만 일단 제외)
LOST = CHARANUM_PRISON()+CHARANUM_ENSLAVED()

;변신 가능 캐릭터는 변신 상태가 될지 선택할 수 있다
CALL ACTION_TRANSFORMATION_SELECT, TARGET, "정보 수집"


PRINTFORM [0] 소문난 이야기 탐문 
PRINTFORM [1] 사건 수사　　　　　
SIF CFLAG:122 > 0
	PRINTFORM [2] 정보 구입　　　　　
SIF LOST > 0
	PRINTFORM [3] 동료 수색　　　　　
PRINTL 
PRINTL
DRAWLINE

IF CFLAG:112 > 0
	CALL RES_SCHEDULE, 112
	IF RESULT == 0
		PRINTL 
		PRINT 스케쥴：소문난 이야기 탐문
		PRINTL 
	ENDIF
	IF RESULT == 1
		PRINTL 
		PRINT 스케쥴：사건 수사
		PRINTL 
	ENDIF
	IF RESULT == 2
		PRINTL 
		PRINT 스케쥴：정보 구입
		PRINTL 
		;IF CFLAG:122 == 0
		;	PRINT （실행 불능）
		;	PRINTL 
		;	PRINTL 수동으로 행동을 선택하십시오.
		;	GOTO INPUT_LOOP
		;	RESULT = 0
		;ENDIF
	ENDIF
	IF RESULT == 3
		PRINTL 
		PRINT 스케쥴：동료 수색
		IF LOST == 0
			PRINT （실행 불능）
			PRINTL 동료 수색 대신 소문난 이야기 탐문을 진행하겠습니다.
			;PRINTL 
			;PRINTL 수동으로 행동을 선택하십시오.
			;GOTO INPUT_LOOP
			RESULT = 0
		ENDIF
	ENDIF
ELSE
	$INPUT_LOOP
	INPUT
ENDIF

PRINTL 
IF RESULT == 0
	ACTION = 0
ELSEIF RESULT == 1
	ACTION = 1
ELSEIF RESULT == 2 && CFLAG:122 > 0
	ACTION = 2
ELSEIF RESULT == 3 && LOST > 0
	ACTION = 3
ELSE
	PRINTL 올바른 값을 입력해 주십시오.
	GOTO INPUT_LOOP
ENDIF

CFLAG:101 = RESULT

CALL MESSAGE_GATHER_INFORMATION, ACTION
CALL _ABLUP, 1
;악질 시민전에 들어서지 않았다면 변신을 해제
SIF CFLAG:1 > 0 && FLAG:73 == 0
	CALL TRANSFORM, 0

BEGIN TURNEND

;정보 수집 메시지
@MESSAGE_GATHER_INFORMATION, ARG
#DIM GATHER
#DIM HANTEI
#DIM NAKADASHI
GATHER = 0
DRAWLINE
SELECTCASE ARG
	;소문난 이야기 탐문
	CASE 0
		PRINTL 소문난 이야기 탐문
		PRINTL 
		;정보 수집의 판정
		HANTEI = RAND:85 + SQRT(EXP:매료경험) * 2
		;내성적
		SIF TALENT:내성적 > 0
			HANTEI -= 15
		;사교적
		SIF TALENT:사교적 > 0
			HANTEI += 15
		;FEAT 효과에 따른 행운 보정
		SIF TALENT:행운아 > 0
			HANTEI += 20

		;커넥션『소문을 좋아하는 친구』
		IF CFLAG:120 > 0 && RAND:2 == 0 && CFLAG:1 == 0
			PRINTFORML %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 소문을 좋아하는 친구에게 눈에 띄는 이야기가 없는지 물어봤다.
			HANTEI += RAND:25 + RAND:25 + 25
		;학생
		ELSEIF RAND:4 != 0 && TALENT:학생 > 0 && TIME == 0 && CFLAG:1 == 0
			;초등학생일 경우
			IF TALENT:학생 == 1
				LOCALS = 초등학교
			;중학생일 경우
			ELSEIF TALENT:학생 == 2
				LOCALS = 중학교
			;고등학생일 경우
			ELSEIF TALENT:학생 == 3
				LOCALS = 고등학교
			;대학생일 경우
			ELSEIF TALENT:학생 == 4
				LOCALS = 대학
			ENDIF
			PRINTFORML %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 자신이 다니는 %LOCALS%에서 이상한 소문은 없었는지 묻고 다녔다.
		;일상 생활
		ELSEIF RAND:2 == 0 && CFLAG:1 == 0
			PRINTFORML %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 일상생활 속에서 가능한 한 정보를 모았다.
		;주위 사람들
		ELSE
			PRINTFORML %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 주위 사람들로부터 은근슬쩍 정보를 모았다.
		ENDIF
		;평화롭다면 소문이 줄어든다
		HANTEI = HANTEI - SQRT(FLAG:852) / 10

		;정보 수집 결과
		IF HANTEI < 10
			PRINTFORML 하지만 그다지 정보가 모이지 않았다・・・
			GATHER = 2 + RAND:3
		ELSEIF HANTEI < 50
			PRINTDATAL
			DATAFORM 생각보다 정보가 많이 모인건 좋지만, 애매모호한 이야기가 너무 많다・・・
			DATAFORM 실없는 소문뿐이었지만, 그 배후에 촉수가 관련되어 있을 가능성은 높다・・・
			DATAFORM 언뜻 보면 무관한 화제 속에, 한 가지 신경 쓰이는 정보가 섞여 있었다・・・
			ENDDATA
			GATHER = 4 + RAND:3
		ELSEIF HANTEI < 75
			PRINTFORML 그 결과, 적의 거처에 힌트가 될 만한 이야기를 들을 수 있었다!
			GATHER = 8 + RAND:3
		ELSE
			PRINTFORML 그 결과, 꽤 신빙성이 높은 목격 정보를 얻을 수 있었다!
			GATHER = 12 + RAND:3
		ENDIF
		IF CONFIG_CHECK_SCREEN_F(3) == 0
			PRINTW 
		ELSE
			PRINTL 
		ENDIF
		PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%에 의한 조사 결과, 탐색도가 {GATHER} 상승했습니다.
		CALL RESEARCH_PROGRESS, GATHER
		;매료경험 상승과 FEAT 효과
		LOCAL = CALC_CHARM_FEAT_OTHER(TARGET)
		IF LOCAL > 0
			EXP:매료경험 += LOCAL
			PRINTFORML 매료경험이 {LOCAL} 올랐다!
		ENDIF
		;소문을 좋아하는 친구와의 연줄 획득
		IF CFLAG:120 == 0 && HANTEI >= 50 && RAND:4 == 0 && CFLAG:1 == 0
			PRINTL 
			CALL DOT_AFTER, 2
			PRINTFORML 빰 빠바밤! %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")%『인맥：소문을 좋아하는 친구』와 친해졌다!
			CFLAG:120 = 1
		;정보원과의 연줄 획득
		ELSEIF CFLAG:122 == 0 && RAND:12 == 0
			HANTEI = 0
			PRINTL 
			CALL DOT_AFTER, 2
			PRINTFORML 사건을 조사하고 있던 %PRINT_TRANSCALLNAME(TARGET)%의 앞에, 보수에 따라 정보를 제공해 주겠다는 이가 나타났다.
			IF CFLAG:1 > 0
				SETBIT HANTEI, 1
			ELSE
				SETBIT HANTEI, 0
			ENDIF
			;통통한 남자(小太りの男)
			IF RAND:7 == 0
				PRINTFORML 살이 찌고 자못 천박한 분위기가 감도는 남자인데・・・
				SETBIT HANTEI, 2
			;점잖은 남자
			ELSEIF RAND:6 == 0
				PRINTFORML 공손하고 점잖은 모습을 보이는 남자지만, 눈빛에서 날카로운 무언가가 느껴진다・・・
				SETBIT HANTEI, 3
			;탐정풍의 남자
			ELSEIF RAND:5 == 0
				PRINTFORML 본인은 탐정을 자처하고 있지만, 미묘하게 수상쩍다・・・
				SETBIT HANTEI, 4
			;상스러운 놈
			ELSEIF RAND:4 == 0
				PRINTFORML 위험한 기색이 느껴진다. 아마도 건실한 인간은 아닐 터・・・
				SETBIT HANTEI, 2
				SETBIT HANTEI, 3
			;시티 헌터적인 그 녀석
			ELSEIF RAND:3 == 0
				PRINTFORML 말도 많고 입도 경박한 게, 아무래도 '불량배 2호' 스러운 느낌이 확 도는 남자인데・・・
				SETBIT HANTEI, 2
				SETBIT HANTEI, 4
			;노인
			ELSEIF RAND:2 == 0
				PRINTFORML 차분한 분위기의 노신사와 같은 모습인데・・・
				SETBIT HANTEI, 3
				SETBIT HANTEI, 4
			;특징 없음
			ELSE
				PRINTFORML 특별한 특징이 없는 평범한 남자처럼 보이는데……
			ENDIF
			PRINTL [0] 연락처를 묻는다
			PRINTL [1] 무시한다
			$INPUT_LOOP_0_122
			INPUT
			IF RESULT == 0
				PRINTFORML 빠밤 빠밤! %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")%『인맥：정보원』과 거래할 수 있게 됐다!
				CFLAG:122 = HANTEI
			ELSEIF RESULT == 1
				PRINTFORML %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 협력 제의를 거절했다・・・
			ELSE
				GOTO INPUT_LOOP_0_122
			ENDIF
		ENDIF
	;사건 수사
	CASE 1
		PRINTL 사건 수사
		PRINTL 
		;지성 의존으로 성취도 판정
		HANTEI = SQRT(MAX(BASE:지성 - 50, 0)) * 4 + RAND:(SQRT(MAX(BASE:지성, 0)) + 5)
		;내성적
		SIF TALENT:내성적 > 0
			HANTEI -= 5
		;사교적
		SIF TALENT:사교적 > 0
			HANTEI += 5
		;FEAT 효과에 따른 행운 보정
		SIF TALENT:행운아 > 0
			HANTEI += 10

		PRINTFORM %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 
		IF CFLAG:121 > 0
			PRINT 본인의 연줄을 구사하여, 
			HANTEI = HANTEI + CFLAG:121 * 20
		ENDIF
		IF RAND:3 == 0
			LOCAL = RAND:3 + 1
			SIF RAND:4 == 0
				LOCAL += RAND:3 + 1
			SIF RAND:4 == 0
				LOCAL += RAND:3 + 1
			IF RAND:2 == 0
				LOCALS = 소녀
			ELSE
				LOCALS = 여성
			ENDIF
			SIF LOCAL > 1
				LOCALS += "들"
			SIF LOCAL >= 3
				PRINT 하룻밤에 
			PRINTFORM {LOCAL}명
			;SIF LOCAL >= 5
				;PRINT も
			;한국어에 맞도록 수정
			IF LOCAL >= 5
				PRINT 이나 되는 
			ELSE
				PRINT 의 
			ENDIF
			PRINTFORM %조사처리(LOCALS,"가")% 성적 피해를 당했다고 하는 
			LOCALS = 
		ELSEIF RAND:2 == 0
			LOCAL = RAND:3 + 1
			IF LOCAL >= 2 && RAND:3 == 0
				LOCALS = 젊은이들
			ELSEIF LOCAL >= 2 && RAND:2 == 0
				LOCALS = 남녀
			ELSEIF RAND:2 == 0
				LOCALS = 여성
			ELSE
				LOCALS = 남성
			ENDIF
			IF LOCAL >= 2
				PRINTFORM {LOCAL}명의 
			ELSE
				PRINTFORM 어떤 
			ENDIF
			PRINTFORM %조사처리(LOCALS,"가")% 행방 불명이 됐다는 
			LOCALS = 수수께끼의 실종
		ELSE
			PRINTFORM {RAND:4 + 1}인의 사망자를 냈다고 하는 
			LOCALS = 엽기 살인
		ENDIF
		PRINTL 
		IF RAND:4 == 0
			PRINTFORML 가장 최근에 일어난 %LOCALS% 사건에 대해 알아보기로 했다.
		ELSEIF RAND:3 == 0
			PRINTFORML 미궁에 빠진 그 사건에 대해 다시 한 번 조사해 보기로 했다.
		ELSEIF RAND:2 == 0
			PRINTFORML 지금 각종 언론을 떠들썩하게 만들고 있는%LOCALS% 사건에 대해 알아보기로 했다.
		ELSE
			PRINTFORML 세상에 공표되어 있지 않은 %LOCALS% 사건에 대해 알아보기로 했다.
		ENDIF
		PRINTL 
		CALL DOT_AFTER
		IF HANTEI < 40
			PRINTFORML 안타깝게도, 단서를 전혀 잡아낼 수가 없었다・・・
			GATHER = 0
		ELSEIF HANTEI < 50
			PRINTFORML 수사 결과, 촉수 생물의 단서를 조금이나마 발견할 수 있었다・・・
			GATHER = 5 + RAND:5
		ELSEIF HANTEI < 60
			PRINTFORML 수사 결과, 촉수 생물의 흔적을 찾을 수 있었다!
			GATHER = 10 + RAND:5
		ELSEIF HANTEI < 70
			PRINTFORML 수사 결과, 촉수 생물의 이동 경로를 잡는 데 성공했다!
			GATHER = 15 + RAND:5
		ELSE
			PRINTFORML 수사 결과, 촉수 생물에 대한 중요한 단서를 발견하는 데 성공했다!
			GATHER = 20 + RAND:5
		ENDIF



		;지성 상승량
		LOCAL:1 = 0
		IF TALENT:변신능력 == -1 && RAND:4 == 0
			LOCAL:1 = 3
		ELSEIF RAND:3 == 0
			LOCAL:1 = 2
		ELSEIF RAND:3 < 2
			LOCAL:1 = 1
		ENDIF
		IF GATHER > 0 || LOCAL:1 > 0
			IF CONFIG_CHECK_SCREEN_F(3) == 0
				PRINTW 
			ELSE
				PRINTL 
			ENDIF
			IF GATHER > 0
				PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%에 의한 조사 결과. 탐색도가 {GATHER} 상승했습니다.
				CALL RESEARCH_PROGRESS, GATHER
			ENDIF
			IF LOCAL:1 > 0
				BASE:지성 += LOCAL:1
				PRINTFORML 지성의 기초값이 {LOCAL:1} 올랐다!
			ENDIF
		ENDIF

		;악질 시민 인카운트 처리
		IF RAND:10000>FLAG:852*2+500 &&( ISHOLE())
			PRINTL 
			PRINTL ・
			PRINTL ・・
			PRINTL ・・・
			PRINTFORM %조사처리(PRINT_TRANSCALLNAME(TARGET),"가")% 
			PRINTDATA
			DATAFORM 인기척이 없는 폐건물로
			DATAFORM 어두컴컴한 뒷골목으로
			DATAFORM 봉쇄된 지하도로
			DATAFORM 폐기된 하수도로
			DATAFORM 전망이 안 좋은 공원으로
			ENDDATA
			PRINTFORML  조사하러 가자,
			PRINTFORML 어디선가 꾀죄죄한 차림의 남자들이 나타나 %조사처리(PRINT_TRANSCALLNAME(TARGET),"를")% 둘러싸 왔다.
			PRINTFORML 남자들은「사냥감」을 찾았다고 판단한 듯, 히죽거리는 표정으로 거리낌 없이 %PRINT_TRANSCALLNAME(TARGET)%의 신체를 둘러본다.
			PRINTFORML %조사처리(PRINT_TRANSCALLNAME(TARGET),"가")% 항의하고자 입을 떼는 순간,
			;＠중국판 추가 분기
			LOCALS'=""
			LOCAL=RAND:3
			IF FLAG:999 == 1
				PRINTL
				PRINTL DEBUG：전투 상황을 입력해 주십시오. (0-2)
				INPUT
				LOCAL=RESULT
			ENDIF
			SELECTCASE LOCAL
				CASE 0 
					PRINTFORML 남자들은 재빨리 스프레이를 꺼내 뿌려댔다.
					PRINTFORML 순간적으로 반응하지 못하고 들이마시고 만 %PRINT_TRANSCALLNAME(TARGET)%……
					PRINTFORML 분명 항의의 말을 냈어야 할 그 목구멍에서는, 코에 걸린 요염한 숨소리가 새어 나오고 만다.
					PRINTFORM 뿜어져 나온 것은 촉수액이 함유된 미약이었는지, 
					LOCALS'="강제 발정"
				CASE 1
					PRINTFORML 남자 중 한 명이 갑자기 전기 충격기를 치켜들고 가슴팍을 겨누며 찔러왔다.
					PRINTFORML 예상치 못한 행위에 반응이 일순간 늦어지고 만 %PRINT_TRANSCALLNAME(TARGET)%……
					PRINTFORML 분명 항의의 말을 냈어야 할 그 목구멍에서는, 오염하고도 비통한 외침이 흘러 나올 뿐이었다.
					PRINTFORM 몸 곳곳에 고압 전류가 흘러 다녀, 
					LOCALS'="강제 마비"
				CASE 2
					PRINTFORML 등 뒤의 남자가 갑자기 %PRINT_TRANSCALLNAME(TARGET)%의 팔을 잡아 왔다!
					PRINTFORML 인식을 빼앗긴 그 다음 순간, 이번에는 전방의 남자가 %PRINT_TRANSCALLNAME(TARGET)%의 명치에 니킥을 꽂아 넣는다……
					PRINTFORML 분명 항의의 말을 냈어야 할 그 목구멍에서는, 구토나 다름없는 날숨과 괴로운 비명이 터져 나온다.
					PRINTL 
					FONTBOLD
					LOCAL:1 = MIN(BASE:TARGET:체력/4+200,BASE:TARGET:체력)
					PRINTFORML {LOCAL:1}의 피해를 입었다!
					FONTREGULAR
					PRINTL 
					BASE:TARGET:체력 -= LOCAL:1
					PRINTFORM 단 일격에 실신 직전까지 이르는 노련한「사냥」에, 
			ENDSELECT

			PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%의 전신에서 금세 힘이 빠져 나간다……

			PRINTFORMW ……아무래도 %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 위험한 장소에 너무 많이 발을 들여놓은 것 같다.
			;악질 시민 인카운트 이벤트 처리
			CALL ENCOUNT_CITIZEN(6002,LOCALS)
			SIF RAND:100 < 30
				CFLAG:825 += 1
			RETURN
		ENDIF


		;경찰 관계자와의 인맥 획득
		IF CFLAG:121 == 0 && HANTEI >= 40 && RAND:(MAX(10 - HANTEI / 10, 2)) == 0
			PRINTL 
			CALL DOT_AFTER, 2
			PRINTFORML 빰 빠바밤! %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")%『인맥：경찰 관계자』와 연이 닿았다!
			CFLAG:121 = 1
		ELSEIF CFLAG:121 == 1 && HANTEI >= 70 && RAND:(MAX(10 - HANTEI / 12, 2)) == 0
			PRINTL 
			CALL DOT_AFTER, 2
			PRINTFORML 빰 빠바밤! %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")%『인맥：경찰 상층부』와 협력하게 됐다!
			CFLAG:121 = 2
		;정보원とのコネ獲得
		ELSEIF CFLAG:122 == 0 && RAND:6 == 0
			HANTEI = 0
			PRINTL 
			CALL DOT_AFTER, 2
			PRINTFORML 사건을 조사하고 있던 %PRINT_TRANSCALLNAME(TARGET)%의 앞에, 보수에 따라 정보를 제공해 주겠다는 이가 나타났다.
			IF CFLAG:1 > 0
				SETBIT HANTEI, 1
			ELSE
				SETBIT HANTEI, 0
			ENDIF
			;통통한 남자
			IF RAND:7 == 0
				PRINTFORML 살이 찌고 자못 천박한 분위기가 감도는 남자인데・・・
				SETBIT HANTEI, 2
			;고상함な男
			ELSEIF RAND:6 == 0
				PRINTFORML 공손하고 점잖은 모습을 보이는 남자지만, 눈빛에서 날카로운 무언가가 느껴진다・・・
				SETBIT HANTEI, 3
			;탐정풍의 남자
			ELSEIF RAND:5 == 0
				PRINTFORML 본인은 탐정을 자처하고 있지만, 미묘하게 수상쩍다・・・
				SETBIT HANTEI, 4
			;상스러운 놈
			ELSEIF RAND:4 == 0
				PRINTFORML 위험한 기색이 느껴진다. 아마도 건실한 인간은 아닐 터・・・
				SETBIT HANTEI, 2
				SETBIT HANTEI, 3
			;시티 펑크적인 그 녀석
			ELSEIF RAND:3 == 0
				PRINTFORML 말도 많고 입도 경박한 게, 아무래도 "불량배 2호" 스러운 느낌이 확 도는 남자인데・・・
				SETBIT HANTEI, 2
				SETBIT HANTEI, 4
			;노인
			ELSEIF RAND:2 == 0
				PRINTFORML 차분한 분위기의 노신사와 같은 모습인데・・・
				SETBIT HANTEI, 3
				SETBIT HANTEI, 4
			;특징 없음
			ELSE
				PRINTFORML 특별한 특징이 없는 평범한 남자처럼 보이는데・・・
			ENDIF
			PRINTL [0] 연락처를 묻는다
			PRINTL [1] 무시한다
			$INPUT_LOOP_1_122
			INPUT
			IF RESULT == 0
				PRINTFORML 빠밤 빠밤! %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")%『인맥：정보원』과 거래할 수 있게 됐다!
				CFLAG:122 = HANTEI
			ELSEIF RESULT == 1
				PRINTFORML %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 협력 제의를 거절했다・・・
			ELSE
				GOTO INPUT_LOOP_1_122
			ENDIF
		ENDIF
	;정보 구입
	CASE 2
		PRINTL 정보 구입
		PRINTL 
		HANTEI = 0
		;정보원과의 연줄을 본다
		IF GETBIT(CFLAG:122, 2) && GETBIT(CFLAG:122, 3)
			LOCALS = 무법자
		ELSEIF GETBIT(CFLAG:122, 2) && GETBIT(CFLAG:122, 4)
			LOCALS = 경박한 남자
		ELSEIF GETBIT(CFLAG:122, 3) && GETBIT(CFLAG:122, 4)
			LOCALS = 노신사
		ELSEIF GETBIT(CFLAG:122, 2)
			LOCALS = 천박한 남자
		ELSEIF GETBIT(CFLAG:122, 3)
			LOCALS = 점잖은 남자
		ELSEIF GETBIT(CFLAG:122, 4)
			LOCALS = 탐정풍의 남자
		ELSE
			LOCALS = 평범한 남자
		ENDIF
		IF CFLAG:1 > 0 && GETBIT(CFLAG:122, 1)
			LOCAL:99 = 1
		ELSEIF GETBIT(CFLAG:122, 0)
			LOCAL:99 = 1
		ELSE
			LOCAL:99 = 0
		ENDIF
		PRINTFORM %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 정보원(%LOCALS%)과 연락을 취했다
		;안면 없음
		IF LOCAL:99 == 0
			PRINTFORML .【소지금：{MONEY}】
			PRINTFORM ……%조사처리(PRINT_TRANSCALLNAME(TARGET),"가")% 변신
			IF CFLAG:1 > 0
				PRINT 한
			ELSE
				PRINT 하지 않은
			ENDIF
			PRINTFORML 상태로 만난 적은 없었을 텐데, 상대가 의심하지 않을까?
			PRINTFORML 라는 건 괜한 걱정이라는 듯, 상대는 깊이 따지지 않고 상담을 시작했다・・・
		ELSE
			PRINTFORML ・・・【소지금：{MONEY}】
		ENDIF
		IF CONFIG_CHECK_SCREEN_F(3) == 0
			PRINTW 
		ELSE
			PRINTL 
		ENDIF
		CLEARLINE 1
		;가격 결정
		LOCAL = RAND:25 * 25 + 500
		LOCAL:1 = RAND:50 * 50 + 5000
		;상대방이 천박하고 하드보일드하지 않은 경우(경박한 남자), 조작 캐릭터가 여자라면 가격을 올린다
		IF GETBIT(CFLAG:122, 2) && GETBIT(CFLAG:122, 4) == 0 && ISGIRLY()
			LOCAL = LOCAL * 150 / 100
			LOCAL:1 = LOCAL:1 * 150 / 100
		ENDIF
		;상대가 엄격한 성격이라면(점잖은 남자) 가격과 성과가 높아진다
		IF GETBIT(CFLAG:122, 3)
			LOCAL = LOCAL * 150 / 100
			LOCAL:1 = LOCAL:1 * 150 / 100
		ENDIF
		;상대가 하드보일드하지 않다면 상객(귀한 손님) 이외에는 진심을 내지 않는다
		IF GETBIT(CFLAG:122, 4) > 0 || GETBIT(CFLAG:122, 5) == 0
			LOCAL = LOCAL * 110 / 100
			LOCAL:1 = LOCAL:1 * 110 / 100
		ENDIF
		;캐릭터가 내성적임과 동시에 상대가 하드보일드하지 않다면 신변을 보일 수 있다
		IF TALENT:내성적 > 0 && GETBIT(CFLAG:122, 3) == 0
			LOCAL:2 = 110
		;캐릭터가 사교적임과 동시에 상대가 엄격한 성격이 아니라면 값을 깎는다
		ELSEIF TALENT:사교적 > 0 && GETBIT(CFLAG:122, 3) == 0
			LOCAL:2 = 90
		ELSE
			LOCAL:2 = 100
		ENDIF
		SIF MONEY < LOCAL * LOCAL:2 / 100
			SETCOLOR 105,105,105
		PRINTFORML  [0] 정보를 구입한다 ({LOCAL * LOCAL:2 / 100})
		RESETCOLOR
		SIF (GETBIT(CFLAG:122, 4) == 0 && GETBIT(CFLAG:122, 5) == 0) || MONEY < LOCAL:1 * LOCAL:2 / 100
			SETCOLOR 105,105,105
		PRINTFORML  [1] 추가 정보까지 구입한다 ({LOCAL:1 * LOCAL:2 / 100})
		RESETCOLOR
		SIF (TALENT:음란 > 0 || GETBIT(CFLAG:122, 6)) && (GETBIT(CFLAG:122, 2) || GETBIT(CFLAG:122, 4) == 0) && ISGIRLY()
			PRINTFORML  [2] 몸으로 정보를 산다
		PRINTL [99] 정보를 사지 않고 떠난다
		PRINTL [-1] 연을 끊는다
		$INPUT_2
		INPUT
		IF RESULT == 0 && MONEY >= LOCAL * LOCAL:2 / 100
			MONEY -= LOCAL * LOCAL:2 / 100
			SIF LOCAL >= 1000
				HANTEI = 1
			SIF GETBIT(CFLAG:122, 2) && GETBIT(CFLAG:122, 4) == 0 && ISGIRLY()
				LOCAL = LOCAL / 2
			SIF GETBIT(CFLAG:122, 4) == 0 || GETBIT(CFLAG:122, 5) > 0
				LOCAL = LOCAL * 100 / 120
			GATHER = SQRT(LOCAL) / 3 + 5
		ELSEIF RESULT == 1 && MONEY >= LOCAL:1 * LOCAL:2 / 100 && (GETBIT(CFLAG:122, 4) || GETBIT(CFLAG:122, 5))
			MONEY -= LOCAL:1 * LOCAL:2 / 100
			SIF GETBIT(CFLAG:122, 2) && GETBIT(CFLAG:122, 4) == 0 && ISGIRLY()
				LOCAL:1 = LOCAL:1 / 2
			SIF GETBIT(CFLAG:122, 4) == 0 || GETBIT(CFLAG:122, 5) > 0
				LOCAL:1 = LOCAL:1 * 100 / 120
			GATHER = SQRT(LOCAL:1) / 4
		ELSEIF RESULT == 2 && (TALENT:음란 > 0 || GETBIT(CFLAG:122, 6)) && (GETBIT(CFLAG:122, 2) || GETBIT(CFLAG:122, 4) == 0) && ISGIRLY()
			PRINTFORML 어떤「협상」을 권유하시겠습니까?
			PRINTL  [0] 입으로 봉사한다
			SIF TALENT:거유 > 0
				PRINTL  [1] 가슴으로 봉사한다
			PRINTL  [2] 스마타
			SIF TALENT:보추 > 0
				PRINT  [3] 애널 본방 
			SIF ISFEMALE()
				PRINT  [3] 본방(실전) 
			SIF TALENT:처녀 > 0
				PRINT (처녀)
			PRINTL 
			$INPUT_2_2
			INPUT
			GATHER = 0
			IF RESULT == 0
				PRINTFORML %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 눈을 치뜬 채 무릎을 꿇고, 남자에게 입으로 봉사하기 시작했다・・・
				IF CONFIG_CHECK_SCREEN_F(3) == 0
					PRINTW 
				ELSE
					PRINTL 
				ENDIF
				IF LOCALS == "천박한 남자"
					PRINT 제대로 씻지도 않아 냄새가 나는 
				ELSE
					PRINT 딱딱하게 발기한
				ENDIF
				PRINTFORML 자지를 입에 넣어, 혀로 자극을 주고 있다.
				IF TALENT:음란 > 0 && TALENT:초심 < 1
					PRINTFORML 적극적인 봉사에 남자가 참지 못해 사정하자,
					PRINTFORM %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 매혹적인 표정으로
					IF RAND:2 == 0
						PRINTFORML 입을 크게 벌려 혀에 고여있는 정자를 보여주고,
						PRINTFORM 일부러 소리를 내며 우물거린 후 
					ELSE
						PRINTFORM 입 속의 정자를 양손바닥에 얹어 보이고, 그것을 다시 핥고, 홀짝거리며 
					ENDIF
					PRINTFORML 정액을 삼켜 보였다・・・
				ELSE
					PRINTFORML 남자는 %조사처리(PRINT_TRANSCALLNAME(TARGET),"를")% 내려다보며 정보를 조금씩 내놓으면서,
					PRINTFORML 중요한 것은 덮어 놓은 채 입 안에 사정하고 말았다・・・
				ENDIF
				IF RAND:2 == 0 && ABL:정액중독 > 2 && TALENT:초심 < 1
					IF ISMALE()
						;#region
						PRINTFORML 정액 냄새가 %PRINT_TRANSCALLNAME(TARGET)%의 엉덩이 안쪽을 쑤셔,
						;#endregion
					ELSE 
						PRINTFORML 정액 냄새가 %PRINT_TRANSCALLNAME(TARGET)%의 %ESTRUS_TEXT_F(TARGET)% 자궁을 쑤셔,
					ENDIF
					PRINTFORML 이 기세로 본방까지 허용해 버리기 직전에 가까스로 참아냈다.
				ENDIF
				PRINTW 
				GATHER += 6 + RAND:5 + EXP:매료경험 / 100 + ABL:기교 / 2
				SIF GETBIT(CFLAG:122, 5)
					GATHER += 2
				;내성적
				SIF TALENT:내성적 > 0
					GATHER -= 2
				;사교적
				SIF TALENT:사교적 > 0
					GATHER += 2
				EXP:펠라경험 += 1
				EXP:정액경험 += 1
			ELSEIF RESULT == 1
				PRINTFORML %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 눈을 치뜬 채 무릎을 꿇고, 남자에게 가슴으로 봉사하기 시작했다・・・
				IF CONFIG_CHECK_SCREEN_F(3) == 0
					PRINTW 
				ELSE
					PRINTL 
				ENDIF
				IF LOCALS == "천박한 남자"
					PRINT 제대로 씻지도 않아 냄새가 나는 
				ELSE
					PRINT 딱딱하게 발기한
				ENDIF
				PRINTFORML 자지에 침을 흠뻑 늘어뜨려,
				IF TALENT:거유 > 2
					PRINT 다이나믹한 크기의 
				ELSEIF TALENT:거유 > 1
					PRINT 농익은 
				ELSEIF TALENT:거유 > 0
					PRINT 풍만한 
				ENDIF
				PRINTFORML 가슴으로 자극을 주어 간다.
				PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%의 가슴에 파묻히는 페니스를 보며, 남자는 우월감에 젖은 듯한 만족스러운 미소를 짓고 있다.
				PRINTFORML 암컷의 본능일까 이성적인 책략일까, %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 눈앞에 튀어나온 귀두에 혀로 자극을 주었다.			
				IF TALENT:음란 > 0 && TALENT:초심 < 1
					PRINTFORML 적극적인 봉사에 남자가 참지 못해 사정하자,
					PRINTFORM %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 매혹적인 표정으로
					IF RAND:2 == 0
						PRINTFORML 입을 크게 벌려 혀에 고여있는 정자를 보여주고,
						PRINTFORM 일부러 소리를 내며 우물거린 후 
					ELSE
						PRINTFORM 입 속의 정자를 양손바닥에 얹어 보이고, 그것을 다시 핥고, 홀짝거리며 
					ENDIF
					PRINTFORML 정액을 삼켜 보였다・・・
				ELSE
					PRINTFORML 남자는 %조사처리(PRINT_TRANSCALLNAME(TARGET),"를")% 내려다보며 정보를 조금씩 내놓으면서,
					PRINTFORML 중요한 것은 덮어 놓은 채 입 안에 사정하고 말았다・・・
				ENDIF
				IF RAND:2 == 0 && ABL:정액중독 > 2 && TALENT:초심 < 1
					PRINTFORML 정액 냄새가 %PRINT_TRANSCALLNAME(TARGET)%의 %ESTRUS_TEXT_F(TARGET)% 자궁을 쑤셔,
					PRINTFORML 이 기세로 본방까지 허용해 버리기 직전에 가까스로 참아냈다.
				ENDIF
				PRINTW 
				GATHER += 6 + RAND:5 + EXP:매료경험 / 100 + ABL:기교 / 2
				SIF GETBIT(CFLAG:122, 5)
					GATHER += 2
				;내성적
				SIF TALENT:내성적 > 0
					GATHER -= 2
				;사교적
				SIF TALENT:사교적 > 0
					GATHER += 2
				EXP:펠라경험 += 1
				EXP:정액경험 += 1
			;스마타는 처녀가 유지될 수 있다
			ELSEIF RESULT == 2
				IF TALENT:보추 >0
					PRINTFORML %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 가랑이를 닫아, 
					PRINTFORML 자신의 것을 밀어낼 수 있도록, 고간의 역삼각형의 틈새로 페니스를 끼워 넣었다・・・
					PRINTFORML 남자는 %조사처리(PRINT_TRANSCALLNAME(TARGET),"가")% 사실은 여자가 아님을 알고 있음에도, 전혀 신경 쓰지 않는다・・・
					IF CONFIG_CHECK_SCREEN_F(3) == 0
						PRINTW 
					ELSE
						PRINTL 
					ENDIF
					IF LOCALS == "천박한 남자"
						PRINTFORML 남자는 %조사처리(PRINT_TRANSCALLNAME(TARGET),"를")% 벽에 밀어붙이는 듯한 자세로
						PRINTFORML 뚱뚱한 배를 흔들며 피스톤질하고 있다.
					ELSE
						PRINTFORML %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 기승위로 스스로 허리를 앞뒤로 밀며 봉사하고 있다.
					ENDIF
					IF RAND:3 == 0 && TALENT:음란 > 0 && TALENT:초심 < 1
						PRINTFORML 이윽고 남자가 한계에 다다르자 %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 허리를 구부려뜨려, 애널에 페니스를 이끌었다.
						PRINTFORML 입을 맞추며 상대의 허리에 다리를 감아, 장내사정을 받아들였다.
						NAKADASHI = 1
					ELSEIF RAND:2 == 0
						PRINTFORML 이윽고 남자가 한계에 다다르자 "장내에 싸게 해준다면 비장의 정보를 알려주마" 라고 말해왔다.
						PRINTFORM %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 한순간 주저했으나,
						IF ABL:Ａ감각 < 2
							PRINTFORML 그것만은 용서해 달라며, 눈물을 흘리며 부탁했고,
							PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%의 고간에서 머리를 내밀고 있는 즙 범벅인 귀두를 손바닥으로 어루만지며
							PRINTFORML 뜨거운 정액의 격류를 받아 들였다.
						ELSE
							PRINTFORML 장내에 내달라며 달콤하고 아양 떠는 목소리로 반복했다.
							PRINTFORML 남자는 허리 각도를 바꿔 %PRINT_TRANSCALLNAME(TARGET)%에 생삽입하자,
							PRINTFORML 격렬한 피스톤질로 단숨에 장내사정했다.
							NAKADASHI = 1
						ENDIF
					ELSE
						PRINTFORML 이윽고 남자가 한계에 다다르자 피스톤질이 빨라지더니, 사고인지 고의인지 %PRINT_TRANSCALLNAME(TARGET)%의 애널에 생삽입할 뻔했다.
						IF ABL:Ａ감각 < 2
							PRINTFORML 그것만은 용서해 달라며, 눈물을 흘리며 부탁했고,
							PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%의 고간에서 머리를 내밀고 있는 즙 범벅인 귀두를 손바닥으로 어루만지며
							PRINTFORML 뜨거운 정액의 격류를 받아 들였다.
						ELSE
							PRINTFORML 순간의 해프닝에 가슴을 쓸어내린 것도 잠시, 똑같은 상황이 몇번이나 반복되어
							PRINTFORML %조사처리(PRINT_TRANSCALLNAME(TARGET),"가")% 눈치챘을 때에는 확실하게 안쪽까지 삽입되어 있었다.
							PRINTFORML 깨달았어도 멈출 수 없는 건지, 남자는 라스트 스퍼트에 들어갔고
							PRINTFORML 쾌락에 휩쓸린 %PRINT_TRANSCALLNAME(TARGET)%도 그대로 눈치채지 못한척 장내사정을 받아들였다.
							NAKADASHI = 1
						ENDIF
					ENDIF
				ELSE
					PRINTFORML %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 가랑이를 닫아, 고간의 역삼각형의 틈새로 페니스를 끼워 넣었다・・・
					IF CONFIG_CHECK_SCREEN_F(3) == 0
						PRINTW 
					ELSE
						PRINTL 
					ENDIF
					IF LOCALS == "천박한 남자"
						PRINTFORML 남자는 %조사처리(PRINT_TRANSCALLNAME(TARGET),"를")% 벽에 밀어붙이는 듯한 자세로
						PRINTFORML 뚱뚱한 배를 흔들며 피스톤질하고 있다.
					ELSE
						PRINTFORML %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 기승위로 스스로 허리를 앞뒤로 밀며 봉사하고 있다.
					ENDIF
					IF RAND:3 == 0 && TALENT:음란 > 0 && TALENT:초심 < 1
						PRINTFORML 이윽고 남자가 한계에 다다르자 %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 허리를 구부려뜨려, 질내로 페니스를 이끌었다.
						PRINTFORML 입을 맞추며 상대의 허리에 다리를 감아, 질내사정을 받아들였다.
						NAKADASHI = 1
					ELSEIF RAND:2 == 0
						PRINTFORML 이윽고 남자가 한계에 다다르자 "질내에 싸게 해준다면 비장의 정보를 알려주마" 라고 말해왔다.
						PRINTFORML %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 한순간 주저했으나,
						IF TALENT:처녀 > 0
							PRINTFORML 그것만은 용서해 달라며, 눈물을 흘리며 부탁했고,
							PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%의 고간에서 머리를 내밀고 있는 즙 범벅인 귀두를 손바닥으로 어루만지며
							PRINTFORML 뜨거운 정액의 격류를 받아 들였다.
						ELSE
							PRINTFORML 질내에 내달라며 달콤하고 아양 떠는 목소리로 반복했다.
							PRINTFORML 남자는 허리 각도를 바꿔 %PRINT_TRANSCALLNAME(TARGET)%에 생삽입하자,
							PRINTFORML 격렬한 피스톤질로 단숨에 질내사정했다.
							NAKADASHI = 1
						ENDIF
					ELSE
						PRINTFORML 이윽고 남자가 한계에 다다르자 피스톤질이 빨라지더니, 사고인지 고의인지 %PRINT_TRANSCALLNAME(TARGET)%의 질내에 생삽입할 뻔했다.
						IF TALENT:처녀 > 0
							PRINTFORML 그것만은 용서해 달라며, 눈물을 흘리며 부탁했고,
							PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%의 고간에서 머리를 내밀고 있는 즙 범벅인 귀두를 손바닥으로 어루만지며
							PRINTFORML 뜨거운 정액의 격류를 받아 들였다.
						ELSE
							PRINTFORML 순간의 해프닝에 가슴을 쓸어내린 것도 잠시, 똑같은 상황이 몇번이나 반복되어
							PRINTFORML %조사처리(PRINT_TRANSCALLNAME(TARGET),"가")% 눈치챘을 때에는 확실하게 안쪽까지 삽입되어 있었다.
							PRINTFORML 깨달았어도 멈출 수 없는 건지, 남자는 라스트 스퍼트에 들어갔고
							PRINTFORML 쾌락에 휩쓸린 %PRINT_TRANSCALLNAME(TARGET)%도 그대로 눈치채지 못한척 질내사정을 받아들였다.
							NAKADASHI = 1
						ENDIF
					ENDIF
				ENDIF
				PRINTW 
				PRINTL 행위를 마치자, 남자는 약속했던 정보를 건네 왔다・・・
				GATHER += 10 + RAND:5 + EXP:매료경험 / 50 + ABL:기교 / 2
				SIF GETBIT(CFLAG:122, 5)
					GATHER += 5
				;내성적
				SIF TALENT:내성적 > 0
					GATHER -= 4
				;사교적
				SIF TALENT:사교적 > 0
					GATHER += 4
				JUEL:습득 += 200

				IF ISMALE()
					EXP:Ａ경험 += 2
					JUEL:쾌Ａ += 10 * ABL:Ａ감각
				ELSE
					EXP:Ｖ경험 += 2
					JUEL:쾌Ｖ += 10 * ABL:Ｖ감각
				ENDIF

				EXP:정액경험 += 2
				IF NAKADASHI > 0
					;사후피임약
					CALL AFTER_PILL, TARGET, 35, 쌩판남
					;임신 판정
					SIF ISFEMALE()
						CALL NINSIN_HANTEI, 2, 800, 쌩판남
				ENDIF
				HANTEI = 1
				PRINTW 
			ELSEIF RESULT == 3
				PRINTFORML %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 가랑이를 벌려, 남자의 삽입을 받아들였다・・・
				IF CONFIG_CHECK_SCREEN_F(3) == 0
					PRINTW 
				ELSE
					PRINTL 
				ENDIF
				IF TALENT:처녀 > 0
					TALENT:처녀 *= -1
					CFLAG:206 = 12
					PRINTL 처녀 상실
					PRINTW 
					GATHER += 5
				ENDIF
				IF LOCALS == "천박한 남자"
					PRINTFORML 남자는 %조사처리(PRINT_TRANSCALLNAME(TARGET),"를")% 교배하는 듯한 체위로 둔 채
					PRINTFORML 뚱뚱한 배를 흔들며 피스톤질 하고 있다.
				ELSE
					PRINTFORML %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 스스로 허리를 흔들며 남자에게 봉사하고 있다.
				ENDIF
				IF TALENT:음란 > 0 && TALENT:초심 < 1
					PRINTFORML 이윽고 남자가 한계에 가까워지자 %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 무의식적으로 남자의 허리에 다리를 감아,
					IF ISMALE()
						;#region
						PRINTFORML 입을 맞추며 장내사정을 받아들였다.
						;#endregion
					ELSE 
						PRINTFORML 입을 맞추며 질내사정을 받아들였다.
					ENDIF
				ELSE
					PRINTFORML 이윽고 남자가 한계에 다다르자 %조사처리(PRINT_TRANSCALLNAME(TARGET),"를")% 짓눌러,
					IF ISMALE()
						;#region
						PRINTFORML 마치 당연하다는 듯이 장내에 정액을 쏟아 부었다.
						;#endregion
					ELSE 
						PRINTFORML 마치 당연하다는 듯이 질내에 정액을 쏟아 부었다.
					ENDIF
				ENDIF
				PRINTW 
				PRINTL 행위를 끝마치자, 남자는 약속한 정보를 건네왔다・・・
				GATHER += 10 + RAND:5 + EXP:매료경험 / 50 + ABL:기교 / 2
				SIF GETBIT(CFLAG:122, 5)
					GATHER += 5
				;내성적
				SIF TALENT:내성적 > 0
					GATHER -= 4
				;사교적
				SIF TALENT:사교적 > 0
					GATHER += 4
				JUEL:습득 += 200

				IF ISMALE()
					EXP:Ａ경험 += 2
					JUEL:쾌Ａ += 10 * ABL:Ａ감각
				ELSE
					EXP:Ｖ경험 += 2
					JUEL:쾌Ｖ += 10 * ABL:Ｖ감각
				ENDIF

				EXP:정액경험 += 2
				;사후피임약
				CALL AFTER_PILL, TARGET, 35, 쌩판남
				;임신 판정
				SIF ISFEMALE()
					CALL NINSIN_HANTEI, 2, 800, 쌩판남
				HANTEI = 1
				PRINTW 
			ELSE
				GOTO INPUT_2_2
			ENDIF
		ELSEIF RESULT == 99
			PRINTFORML %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 정보를 사지 않고 떠났다・・・
			GATHER = 0
		ELSEIF RESULT == -1
			PRINTFORML %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% 정보원(%LOCALS%)과 연을 끊고 떠났다・・・
			GATHER = 0
			CFLAG:122 = 0
		ELSE
			GOTO INPUT_2
		ENDIF
		IF GATHER > 0
			IF GATHER > 0
				PRINTFORML 정보 제공의 결과, 탐색도가 {GATHER} 상승했습니다.
				CALL RESEARCH_PROGRESS, GATHER
			ENDIF
		ENDIF
		;상객(귀한 손님)
		IF HANTEI > 0 && GETBIT(CFLAG:122, 4) == 0 && GETBIT(CFLAG:122, 5) == 0 && RAND:4 != 0
			PRINTL 
			CALL DOT_AFTER, 2
			PRINTFORML 남자는 %조사처리(PRINT_TRANSCALLNAME(TARGET),"를")% 상객이라고 판단한 눈치다.
			PRINTFORML 앞으로는 아낌없이 협력해 줄 것이다・・・
			SETBIT CFLAG:122, 5
		ENDIF
		;육체 관계
		IF GATHER == 0 && GETBIT(CFLAG:122, 2) && GETBIT(CFLAG:122, 6) == 0 && ISGIRLY() && RAND:4 != 0
			PRINTL 
			CALL DOT_AFTER, 2
			IF LOCALS == "경박한 남자"
				PRINTFORML 그대로 돌아가려고 하는 %PRINT_TRANSCALLNAME(TARGET)%에게 남자는 농담조로,
				PRINTFORML "만약 돈이 없다면 다른 방법으로 보수를 지불해도 상관 없다" 라고 말했다.
				PRINTFORML 진심인지는 잘 모르겠지만・・・
			ELSE
				PRINTFORML 남자는 %PRINT_TRANSCALLNAME(TARGET)%의 몸을 보곤 히죽히죽 웃고 있다.
				PRINTFORML "만약 돈이 없다면 다른 방법으로 보수를 지불해도 상관 없다", 라고 했는데・・・
			ENDIF
			SETBIT CFLAG:122, 6
		ENDIF
	;동료 수색
	CASE 3
		PRINTL 동료 수색
		PRINTL 
		PRINTFORML 행방을 찾고 싶은 상대를 골라주십시오.
		CALL CHARA_LIST, 3, 1, 1
		HANTEI = RESULT
		PRINTFORML %조사처리(PRINT_TRANSCALLNAME(TARGET),"는")% %CALLNAME:HANTEI%의 행방에 대한 정보를 모았다.
		;달성도를 판정
		GATHER = RAND:30 + 1
		;내성적
		SIF TALENT:내성적 > 0
			GATHER -= 5
		;사교적
		SIF TALENT:사교적 > 0
			GATHER += 5
		;FEAT 효과에 따른 행운 보정
		SIF TALENT:행운아 > 0
			GATHER += 10
		SIF GATHER < 0
			GATHER = 1

		;촉수에게 유폐되어 있다
		IF CFLAG:HANTEI:0 == 1
			GATHER /= 2
			IF GATHER < 5
				PRINTFORML 하지만 그다지 정보가 모이지 않았다・・・
			ELSE
				PRINTFORML 그 결과, %조사처리(CALLNAME:HANTEI,"를")% 유폐하고 있는 촉수의 거처에 대한 힌트를 얻을 수 있었다!
				GATHER += 5
			ENDIF
			IF CONFIG_CHECK_SCREEN_F(3) == 0
				PRINTW 
			ELSE
				PRINTL 
			ENDIF
			PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%에 의한 조사 결과, 탐색도가 {GATHER} 상승했습니다.
			CALL RESEARCH_PROGRESS, GATHER
		;악질 시민에게 감금되어 있다
		ELSEIF CFLAG:HANTEI:0 == 4
			GATHER /= 2
			IF GATHER < 5
				PRINTFORML 하지만 그다지 정보가 모이지 않았다・・・
			ELSE

				PRINTFORM 그 결과, 
				PRINTDATAL
				DATAFORM %조사처리(CALLNAME:HANTEI,"가")% 행방불명된 날 밤의 목격 정보를 얻을 수 있었다!
				DATAFORM %CALLNAME:HANTEI%같은 소녀가 끌려가는 순간을 목격했다는 증언을 얻을 수 있었다!
				ENDDATA
				GATHER += 5
			ENDIF



			IF CONFIG_CHECK_SCREEN_F(3) == 0
				PRINTW 
			ELSE
				PRINTL 
			ENDIF
			;나머지 필요 포인트
			LOCAL=CFLAG:HANTEI:71-GATHER



			IF CFLAG:HANTEI:71>20 && LOCAL>0 && LOCAL<=20
				CFLAG:HANTEI:71-=GATHER
				PRINTL
				PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%에 의한 조사 결과, %조사처리(CALLNAME:HANTEI,"가")% 마지막으로 목격된 장소가 판명되었다!
			ELSEIF CFLAG:HANTEI:71>10 && LOCAL>0 && LOCAL<=10
				CFLAG:HANTEI:71-=GATHER
				PRINTL
				PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%에 의한 조사 결과, %조사처리(CALLNAME:HANTEI,"가")% 누군가에 의해 감금되어 있는 사실이 판명되었다!
			ELSEIF CFLAG:HANTEI:71>0 && LOCAL<=0
				CFLAG:HANTEI:71-=GATHER
				PRINTL
				PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%에 의한 조사 결과, %조사처리(CALLNAME:HANTEI,"가")% 감금되어 있는 장소가 특정되었다!

				;악질 시민 인카운트 처리
				IF ISHOLE() 
					PRINTL 
					PRINTL ・・・
					PRINTFORM %조사처리(PRINT_TRANSCALLNAME(TARGET),"가")% 
					PRINTDATA
					DATAFORM 특정한 폐건물 부근
					DATAFORM 폐건물과 인접한 뒷골목
					DATAFORM 폐건물로 이어지는 지하도
					DATAFORM 폐건물 지하를 통과하는 하수도
					ENDDATA
					PRINTFORML 에 조사를 하러 향하자,
					PRINTFORML 어디선가 꾀죄죄한 차림의 남자들이 나타나 %조사처리(PRINT_TRANSCALLNAME(TARGET),"를")% 둘러싸 왔다.
					PRINTFORML 남자들은「새 사냥감」이 길을 헤맸다고 생각했는지, 히죽거리는 표정으로 거리낌 없이 %PRINT_TRANSCALLNAME(TARGET)%의 신체를 둘러본다.
					PRINTFORML %CALLNAME:HANTEI%의 행방을 캐묻고자 %조사처리(PRINT_TRANSCALLNAME(TARGET),"가")% 입을 떼는 순간, 남자들은 재빨리 스프레이를 꺼내 뿌려댔다.
					PRINTFORML 순간적으로 반응하지 못하고 들이마시고 만 %PRINT_TRANSCALLNAME(TARGET)%……
					PRINTFORML 분명 항의의 말을 냈어야 할 그 목구멍에서는, 코에 걸린 요염한 숨소리가 새어 나오고 만다.
					PRINTFORML 뿜어져 나온 것은 촉수액이 함유된 미약이었는지, %PRINT_TRANSCALLNAME(TARGET)%의 전신에서 금세 힘이 빠져 나간다……
					PRINTFORMW ……아무래도 %조사처리(CALLNAME:HANTEI,"를")% 돕기 이전에, %PRINT_TRANSCALLNAME(TARGET)% 자기 자신부터 지켜야 할 것 같다.
					CALL ADDBATTLESITUATION("강제 발정,")
					CFLAG:HANTEI:71=-1
					;악질 시민 인카운트 이벤트 처리
					CALL ENCOUNT_CITIZEN(6002)
					SIF RAND:100 < 30
						CFLAG:825 += 1
					RETURN
				ENDIF
			ELSE
				PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%에 의한 조사 결과, 탐색도가 {GATHER} 상승했습니다.
			ENDIF

		;세뇌, 흑화한 상태
		ELSE
			IF GATHER < 5
				PRINTFORML 하지만 그다지 정보가 모이지 않았다・・・
			ELSEIF GATHER < 15
				PRINTFORML 그 결과, %CALLNAME:HANTEI%의 행방을 단편적으로나마 알 수 있었다・・・
			ELSE
				PRINTFORML 그 결과, %CALLNAME:HANTEI%같은 인물의 목격 정보를 포착하는 데 성공했다!
				GATHER += 5
			ENDIF
			IF CONFIG_CHECK_SCREEN_F(3) == 0
				PRINTW 
			ELSE
				PRINTL 
			ENDIF
			PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%에 의한 조사 결과, %CALLNAME:HANTEI%와의 조우율이 {GATHER} 상승했습니다.
			CFLAG:HANTEI:23 += GATHER
			SIF CFLAG:HANTEI:23 >= 90
				CFLAG:HANTEI:23 = 90
		ENDIF
ENDSELECT
;행동 결과 표시를 위한 WAIT
PRINTW 



