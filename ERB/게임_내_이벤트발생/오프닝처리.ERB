; 게임 기동 ⇒ 신규 게임 개시 or 데이터 로드 직후에 불리는 처리

;세이브 데이터 로드 직후 처리
@EVENTLOAD
REDRAW 0
;●세이브 데이터 & 글로벌 데이터 업데이트
CALL UPDATE
IF FLAG:999 == 1
	SETBGCOLOR 0,0,40
ELSE
	RESETBGCOLOR
ENDIF
SIF FLAG:64 > 0
	JUMP ENDING
;--------------------------------------------------


;신규 게임 시작 시의 처리
@EVENTFIRST
#DIM LCOUNT_MASTER_LOOP
#DIM LCOUNT_MODE_SELECT
#DIM LCOUNT_CHARAMAKE
#DIM CCOUNT
#DIMS SELECTS

REDRAW 0
RESETBGCOLOR
;실적 관련 데이터 불러오기
LOADGLOBAL
;●보통은 글로벌 데이터 업데이트 판정 및 불러오기
IF RESULT == 1
	CALL UPDATE
;●실제 최초 기동 시에는 글로벌 데이터의 초기값을 설정한다
ELSE
	;잡몹적 출현율 필터를 100%(=필터없음)로 초기화
	FOR LOCAL,0,MOB_CATEGORY
		FOR LOCAL:1,0,100
			TRYCCALLFORM TENTACLE_MOB_{LOCAL * 100 + LOCAL:1}
				MOB_FLAG:LOCAL:(LOCAL:1) = 100
			CATCH
			ENDCATCH
		NEXT
	NEXT
ENDIF

;**********************************************************
;게임 초기 설정
TIME = 1
MONEY = 5000
;컨피그・필터 초기화
CALL CONFIG_INIT(1)
;소지 의상 초기화
ITEM:100 = 1
ITEM:200 = 1
ITEM:201 = 1
ITEM:202 = 1
ITEM:299 = 1
ITEM:300 = 1
ITEM:401 = 1

;MASTER와 TARGET의 교체
;MASTER에 더미용 캐릭터를 넣고, TARGET을 지워둔다
SWAPCHARA 0, 1
DELCHARA 1

;게임 모드 설정
LCOUNT_MASTER_LOOP = LINECOUNT
$MASTER_LOOP
	LCOUNT_MODE_SELECT = LINECOUNT
	$MASTER_LOOP_MODE_SELECT

	;게임 모드 재선택에 대비하여 보스 플래그 리셋
	FLAG:100 = 0
	FLAG:101 = 0
	;초기 방위력 설정
	FLAG:852 = 5000

	CALL MODE_SELECT
	IF RESULT == 99
		CLEARLINE LINECOUNT - LCOUNT_MASTER_LOOP
		GOTO MASTER_LOOP
	ELSEIF RESULT == 999
		CLEARLINE LINECOUNT
		RESETDATA
		BEGIN TITLE
	ENDIF



;단련 설비 레벨 초기화
FLAG:50 = 1
FLAG:51 = 1

;보스 촉수, 최종 보스 촉수 총 마릿수
CALL GET_BOSS_ERB_NUM
FLAG:3 = RESULT
;Ｋ촉수만 카운트
FLAG:4 = 1

;보스 촉수, 최종 보스 촉수의 플래그 설정
REPEAT FLAG:3
	SETBIT FLAG:100, COUNT
REND
;Ｋ촉수만 설정
FLAG:101 = 0


;**********************************************************
;캐릭터 메이킹
LCOUNT_CHARAMAKE = LINECOUNT
$MASTER_LOOP_CHARAMAKE
;모드에 따른 기본 인원수
IF GAME_OPTION_CHECK_F(OPTION_solo)
	LOCAL = 1
ELSE
	LOCAL = 3
ENDIF



REPEAT LOCAL
	ADDCHARA 0
	FLAG:8 ++
REND
;PRINTL 캐릭터 메이킹을 진행합니다.
PRINTL 
CALL CHARA_MAKE_MAIN, 0
IF RESULT == -1
	REPEAT CHARANUM-1
		DELCHARA 1
	REND
	GOTO MASTER_LOOP
ENDIF
;캐릭터 메이킹 완료 처리(전원분 모아서)
CALL CHARA_MAKE_FINALIZE
PRINTL 


;**********************************************************
;모든 캐릭터 공통 처리

;전용 구상의 번호를 설정
FOR CCOUNT, 0, CHARANUM
	SIF CCOUNT == MASTER
		CONTINUE
	CFLAG:CCOUNT:6 = NO:CCOUNT
	SIF NO:CCOUNT == 0 && ISMALE(CCOUNT)
		CFLAG:CCOUNT:6 = 1
	SIF NO:CCOUNT == 0 && TALENT:CCOUNT:인조인간 > 0
		CFLAG:CCOUNT:6 = 2
	IF TALENT:CCOUNT:구상설정 == -2
		CFLAG:CCOUNT:6 = -2
	ELSEIF TALENT:CCOUNT:구상설정 == 0 && ISMALE(CCOUNT)
		CFLAG:CCOUNT:6 = 1
	ELSEIF TALENT:CCOUNT:구상설정 == 0
		CFLAG:CCOUNT:6 = 0
	ELSEIF TALENT:CCOUNT:구상설정 == 1
		CFLAG:CCOUNT:6 = 2
	ELSEIF TALENT:CCOUNT:구상설정 == 2
		CFLAG:CCOUNT:6 = 3
	ELSEIF TALENT:CCOUNT:구상설정 == 3
		CFLAG:CCOUNT:6 = 4
	ENDIF

	CFLAG:CCOUNT:100 = 예정_휴식
NEXT

;제한 일수 설정
CALL SET_LIMIT_DAY

;**********************************************************
;컨피그 프리셋 설정
CALL HEROINE_PRESET

;**********************************************************
;프롤로그
PRINTL 프롤로그를 보시겠습니까?
PRINTL [0] 아니오
PRINTL [1] 네
$INPUT_LOOP_PRO
INPUT
IF RESULT < 0 || RESULT > 1
	GOTO INPUT_LOOP_PRO
ELSEIF RESULT == 0
	PRINTL 
ELSEIF RESULT == 1
	DRAWLINE
	PRINTL 　…
	PRINTL 　……
	PRINTW 　…………
	PRINTL 　"그것" 이 언제부터 나타나게 되었는지는 확실치 않았다.
	PRINTL 　괴기소설이나 공포영화, 혹은 신사의 음란한 망상 속에서만 존재해왔을 터.
	PRINTL 　결코 현실에 있어서는 안 되는, 역겹고 기괴한 물체――
	PRINTW 　아니, “그것” 은 이미, 우리의 눈앞에서 살아 움직이는 또 하나의 생명체였다.
	PRINTL 
	PRINTL 　인간을 습격하고, 포식하며, 그리고 번식을 위한 묘상으로 삼는『촉수 괴물』
	PRINTL 　처음에는 단지 도시 전설로 회자되던 "그것" 에 불과했다……
	PRINTW 　……만, 이윽고 전환점이 되는 한 사건이 일어났다.
	PRINTL 
	PRINTFORML 　어느 날 밤, 한 지방 도시에서 십여 명의 여성이 %STR:2500%의 습격을 받은 것이다.
	PRINTL 　학원에 가던 소녀가, 일을 마치고 퇴근하던 회사원이, 집에서 남편을 기다리던 주부가…………
	PRINTL 　하룻밤 만에 여러 명의 사람들이 실종된 이 사건에는 특이한 사항이 있었다――
	PRINTW 　――오로지 젊은 여성들만이 실종되었다는 점이었다.
	PRINTL 　이후 목격자들의 증언과 더불어 여러 대의 카메라가 포착한 영상이 결정적인 증거가 됐으며,
	PRINTW 　머지 않아 "그 존재" 는 누구의 눈에도 의심할 여지가 없는 사실이 됐다.
	PRINTL 
	PRINTFORML 　인간, 특히 젊은 여자를 덮치는 괴물 그 자체라고 해야 할 %STR:2500%.
	PRINTFORML 　공적기관은 대응에 급급했지만, 대책은 지지부진하였고……
	PRINTFORMW 　오히려 %STR:2500%를 마주한 이들 중에는 "거두어져 버린" 자들도 나오고 있었다.
	PRINTL 
	PRINTFORML 　%STR:2500% 속으로 거두어진 이는 "종자" 가 심겨지고, 남자는 그대로 %STR:2500%의 말단으로 삼았으며
	PRINTFORML 　여자는 새로운 %STR:2500%의 모태로서, 사람이 아닌 무언가로 변해 버리는 것이었다.
	PRINTL 
	PRINTW 　이에 이르러 새롭게 출현한 인류의 천적에, 사람들은 어쩔 도리가 없다 여길 수밖에 없었다……
	PRINTW 
	DRAWLINE
	PRINTL 
	PRINTL 　　그러나, 그런 위협과 남몰래 싸우는 자들이 존재했다.
	FORCEWAIT
ELSE
ENDIF

;구상의 초기 설정을 호출하는 등
FOR LOCAL,0,CHARANUM
	SIF LOCAL == MASTER
		CONTINUE

	TARGET = LOCAL

	SIF CFLAG:0 != 상태_건강 && CFLAG:20 == 0 && CFLAG:21 == 0
		CFLAG:21 = RAND:7 + 1

	CALL MESSAGE_FIRST

	SIF LOCAL <= 파티인원최대치
		CFLAG:999 = 1
NEXT
;초기 세트 캐릭터, CSV 캐릭터용 의상 획득 처리
FOR LOCAL, 0, CHARANUM -1
	SIF ITEM:(CFLAG:(LOCAL+1):40) == 0
		ITEM:(CFLAG:(LOCAL+1):40) = 1
	SIF ITEM:(CFLAG:(LOCAL+1):41) == 0
		ITEM:(CFLAG:(LOCAL+1):41) = 1
	SIF ITEM:(CFLAG:(LOCAL+1):42) == 0
		ITEM:(CFLAG:(LOCAL+1):42) = 1

	SIF ITEM:(CFLAG:(LOCAL+1):43) == 0
		ITEM:(CFLAG:(LOCAL+1):43) = 1
	FOR LOCAL:1, 600, 700
		SIF EQUIP:(LOCAL+1):(LOCAL:1)
			ITEM:(LOCAL:1) = 1
	NEXT
NEXT

;방위력 감쇠 회피
FLAG:41 = 1
;탐색 할당량 설정
CALL RESEARCH_QUOTA

;초기화 처리
CALL UPDATE
BEGIN SHOP



;모드 선택(게임 시작 시 외에, 주회 시에도 호출된다)
@MODE_SELECT(인계플래그=0, 인계인원=0)
#DIM 인계플래그
#DIM 인계인원
$MASTER_LOOP
FONTBOLD
SETCOLORBYNAME CYAN
PRINTL ◆게임 모드 선택
FONTREGULAR
RESETCOLOR
FOR COUNT, 1 ,모드카운트
	PRINTL 
	IF 인계플래그 == 1 && ((모드네임:COUNT == "SOLO" && 인계인원 > 1) || 모드네임:COUNT == "SANDBOX")
		SETCOLOR 105,105,105
		PRINTPLAINFORM [{COUNT,2,RIGHT}] 【%"【"+모드네임:COUNT+"】",70,LEFT%】
		RESETCOLOR
	ELSE
		PRINTFORM [{COUNT,2,RIGHT}] %"【"+모드네임:COUNT+"】",70,LEFT%
	ENDIF
	PRINT 
	PRINTL
	SELECTCASE 모드네임:COUNT
		CASE "NORMAL"
			LOCALS= 　여러 명의 캐릭터로 적의 전멸을 목표로 하는 일반적인 모드입니다.
		CASE "SOLO"
			IF 인계플래그 == 1 && 인계인원 > 1
				LOCALS= 　※※인계인원이 오로지 １명 뿐이여야만 선택할 수 있습니다.※※
			ELSE
				LOCALS= 　１명의 캐릭터로 도전하는 심플 모드입니다. 간이 플레이용
			ENDIF
		CASE "HARDCORE"
			;구체적으로는 각종 할당량＆적 파라미터가 엄격하며, 주회 시에 히든 보스가 출현한다
			LOCALS= 　상급자를 위한 난이도 높은 챌린지 모드입니다.
		CASE "SURVIVAL"
			LOCALS= 　무한히 출현하는 보스를 상대로 가능한 한 저항하는 모드입니다.　난이도는 NORMAL 수준
		CASE "FREEPLAY"
			LOCALS= 　보스 격파 기한이 존재하지 않습니다.　난이도는 NORMAL 수준, 클리어 후 주회는 불가
		CASE "SANDBOX"
			IF 인계플래그 == 1
				LOCALS= 　※※주회차로는 선택할 수 없습니다.※※
			ELSE
				LOCALS= 　게임 오버도 엔딩도 발생하지 않습니다.　난이도는 NORMAL 수준, 실적 해제 없음
			ENDIF
		CASE "INSTANT"
			LOCALS= 　플레이 도중에 캐릭터의 신규 작성 및 은퇴가 가능한 모드입니다.
		CASEELSE
			LOCALS=%모드네임:COUNT% #ERROR#
	ENDSELECT
	PRINTBUTTON "　　　　┗"+LOCALS+"　　　",COUNT
	PRINTL
NEXT
;PRINT [99] REVERSE	
;PRINTPLAIN 촉수가 되어 당신을 습격해오는 여자아이를 격퇴해 가는 모드입니다.
;PRINTL 
[IF_DEBUG]
	PRINTL 
	PRINT [-1] ☆구상 색상　
	PRINTPLAIN 　구상의 색상을 비교, 작성하는 디버그 모드입니다.　구상 제작자용
	PRINTL 
	PRINTL 
	PRINT [-2] ☆커스텀　
	PRINTPLAIN 　게임 모드 옵션을 다양하게 다룰 수 있는 디버그 모드입니다.　게임 모드 제작자용
	PRINTL 
[ENDIF]
PRINTL 
IF 인계플래그 == 1
	PRINT [100] 인계 선택으로 돌아간다
ELSE
	PRINT [100] 타이틀로 돌아간다　　　　　　 
	PRINT [200] 글로벌 컨피그 편집　　　　　　　　　　　　
	PRINT [300] 게임 설명
ENDIF
PRINTL 
$INPUT_LOOP_MODE
INPUT
IF INRANGE(RESULT, 1, 모드카운트-1)
	IF 인계플래그 == 1 && ((모드네임:RESULT == "SOLO" && 인계인원 > 1) || 모드네임:RESULT == "SANDBOX")
		CLEARLINE 1
		GOTO INPUT_LOOP_MODE
	ENDIF
	FLAG:0 = 모드옵션:RESULT
	PRINTFORML %모드네임:RESULT% 모드로 플레이합니다.
;ELSEIF RESULT == 4
;	FLAG:0 = 11
;	PRINTL REVERSE 모드로 플레이합니다.
[IF_DEBUG]
ELSEIF RESULT == -1
	ADDVOIDCHARA
	CALL KOJO_COLOR_SETTING
	DELALLCHARA
	GOTO MASTER_LOOP
ELSEIF RESULT == -2
	PRINTL ☆커스텀 모드로 플레이합니다.
	CALL GAME_OPTION_CUSTUM
[ENDIF]
ELSEIF RESULT == 100
	RETURN 999
ELSEIF RESULT == 200 && 인계플래그 == 0
	FLAG:801 = GLOBAL:11
	FLAG:802 = GLOBAL:12
	FLAG:803 = GLOBAL:13
	FLAG:804 = GLOBAL:14
	FLAG:805 = GLOBAL:15
	;글로벌 업데이트
	CALL UPDATE_GLOBAL
	CALL CONFIG("mainmenu")
	GOTO MASTER_LOOP
ELSEIF RESULT == 300 && 인계플래그 == 0
	CALL TUTORIAL
	GOTO MASTER_LOOP
ELSE
	CLEARLINE 1
	GOTO INPUT_LOOP_MODE
ENDIF

@SET_LIMIT_DAY
;보스 1마리 당 제한 일수
;　13日：HARDCORE 모드
;　12日：SOLO 모드
;　11日：NORMAL 모드, INSTANT 모드 (기준치)
;　 9日：SANDBOX 모드, FREEPLAY 모드, SURVIVAL 모드
[SKIPSTART]
CALL GAME_MODE_CHECK
SELECTCASE RESULT
	CASE MODE_HARDCORE
		FLAG:2 = 13
	CASE MODE_SOLO
		FLAG:2 = 12
	CASE MODE_NORMAL, MODE_INSTANT
		FLAG:2 = 11
	CASE MODE_SANDBOX, MODE_FREEPLAY, MODE_SURVIVAL
		FLAG:2 = 9
	CASEELSE
		CALL BOSS_LIMIT_TIME_CUSTUM
ENDSELECT
[SKIPEND]
FLAG:2 = 11
SIF GAME_OPTION_CHECK_F(OPTION_hardcore)
	FLAG:2 += 2
SIF GAME_OPTION_CHECK_F(OPTION_solo)
	FLAG:2 += 1
SIF GAME_OPTION_CHECK_F(OPTION_endless) || GAME_OPTION_CHECK_F(OPTION_제한시간없음)
	FLAG:2 -= 2
SIF GAME_OPTION_CHECK_F(OPTION_가입은퇴있음)
	FLAG:2 -= 1
SIF GAME_OPTION_CHECK_F(OPTION_스탯저하있음)
	FLAG:2 += 1

;솔로 모드 이외의 모드에서, 기본 인원에서 증감한 경우 제한 일수가 변동된다 (많을수록 엄격하다)
SIF !GAME_OPTION_CHECK_F(OPTION_solo)
	FLAG:2 -= (CHARANUM-1) - 3

FLAG:1 = 0
;모든 보스 제한 일수 설정
;(보스 촉수 총 마릿수*１마리 당 제한 일수)＋(최종 보스 촉수 총 마릿수*10) 로 산출
SIF !GAME_OPTION_CHECK_F(OPTION_endless)
	FLAG:1 = (FLAG:3 * FLAG:2) + (FLAG:4 * 10)

@TUTORIAL
$TUTORIAL
PRINTL **********************************************
PRINTL 
PRINTL 튜토리얼 메뉴
PRINTL 
PRINTL [0] 게임을 하는 방법과 힌트
PRINTL [1] 캐릭터가 붙잡혀 버린다면
PRINTL [2] 자금에 대하여
PRINTL [3] 게임성을 변화시키는 옵션
PRINTL [4] 그래도 좀 어렵다고 느껴지신다면 (스포일러 주의)
PRINTL [999] 돌아간다
$INPUT_LOOP_TUTORIAL
INPUT
IF RESULT == 0
	PRINTL **********************************************
	PRINTL 
	PRINTL 게임을 하는 방법과 힌트:
	PRINTL 
	PRINTL 기본적으로 플레이어 캐릭터를 육성하여 보스를 격파하고 전멸시키는 것이 이 게임의 목표입니다.
	PRINTL 보스의 전투력은 막강하며, 대부분 플레이어 캐릭터보다 레벨도 더 높기 때문에
	PRINTL 단련을 쌓고 나서 싸움을 거는 것이 기본이 됩니다.
	PRINTL 그 때는 능력을 특화시키는 것도 좋지만 약점을 보완하는 것도 중요합니다.
	PRINTL 
	PRINTL 게다가, 전투 중에 체력·기력이 줄어들 수록 전투도 불리해집니다(보스 역시 약화되어 갑니다).
	PRINTL 너무 무리해서 쫓지 말고, 여유가 있을 때 철수하는 마음가짐도 필요합니다.
;	PRINTL 
;	PRINTL 덧붙여 REVERSE 모드는 보스를 조작해 여자 아이들을 격파해 간다는
;	PRINTL 평소와는 반대의 내용으로 되어 있습니다.
	PRINTW 
	GOTO TUTORIAL
ELSEIF RESULT == 1
	PRINTL **********************************************
	PRINTL 
	PRINTL 캐릭터가 붙잡혀 버린다면:
	PRINTL 
	PRINTL 보스와의 전투에서 패배해 버린다면 결국에는 붙잡혀 유폐되고 맙니다.
	PRINTL 아무리 마법소녀라 할지라도, 그대로 뒀다가는 몸과 마음이 모두 갉아먹혀 굴복하게 될 것입니다.
	PRINTL 
	PRINTL 그러나 만일 캐릭터가 유폐되어 버렸다 해도, 아직 포기하기에는 이릅니다.
	PRINTL 잡힌 캐릭터의 레벨이 높을수록 제한 일수에도 여유가 생기므로 그 사이에 대책을 세웁시다.
	PRINTL 또한 경험치를 많이 얻을 수 있기 때문에, 구할 수만 있다면 남들보다 뒤처진 캐릭터도 유폐를 통해 만회할 수도 있습니다.
	PRINTL 유폐된 상태에서 여러 가지 방법으로 상황을 되감아보는 것도 이 게임의 묘미 중 하나라고 할 수 있습니다.
	PRINTL 
	PRINTL 또한 SOLO 모드에서는 패배 조건이 다릅니다.
	PRINTL 촉수에게 잡히더라도 일수 경과에 따라 자동으로 유폐에서 탈출하는 대신,
	PRINTL 상태가 악화되어 세뇌/흑화 상태가 되어 버리면 게임 오버가 됩니다.
	PRINTW 
	GOTO TUTORIAL
ELSEIF RESULT == 2
	PRINTL **********************************************
	PRINTL 
	PRINTL 자금에 대하여:
	PRINTL 
	PRINTL 게임을 유리하게 만드는 요소 중 하나가 바로 자금입니다.
	PRINTL 장비품인 코스튬이나 설비 레벨의 강화 등에는 자금이 필요합니다.
	PRINTL 자금은 보스를 격파했을 때의 보너스로 입수할 수 있으며,
	PRINTL 특별 활동을 함으로써 자금을 직접 벌 수도 있습니다.
	PRINTL 
	PRINTL 어떤 특별 활동을 선택하느냐에 따라 입수 금액에 영향을 주는 능력치가 달라집니다.
	PRINTL 예를 들어 체력에 자신이 있다면 통상 아르바이트, 지성에 자신이 있다면 연구 조수를 선택하는 등……
	PRINTL 캐릭터의 주된 능력에 맞는 특별 활동을 찾아내는 것이 키포인트입니다.
	PRINTW 
	GOTO TUTORIAL
ELSEIF RESULT == 3
	PRINTL **********************************************
	PRINTL 
	PRINTL 게임성을 변화시키는 옵션:
	PRINTL 
	PRINTL 메인 메뉴에서 호출할 수 있는 컨피그 설정에는
	PRINTL 게임성을 변화시키는 옵션이 몇 가지 존재합니다.
	PRINTL 이러한 설정들은 임의로 변경할 수 있습니다.
	PRINTL 
	PRINTL 유폐 촉수 조우율 향상　　　 캐릭터를 유폐한 보스와의 조우율이 오릅니다.　　　　　　　 (난이도: 감소)
	PRINTL 색적 타겟 기능　　　　　　　한 번 조우한 보스와의 조우율이 오릅니다.　　　　 　　 　　(난이도: 감소)
	PRINTL 자동 풀어내기　　　 　　　　구속되었을 때 자동으로「풀어낸다」를 사용합니다. 　　　 　(난이도: 감소)
	PRINTL 착정 강화 기능　　　　　　　보스의 정액에서 파워를 흡수합니다.　　　　　　　　　　　　(난이도: 감소)
	;원문은 返り血機能 피가 튐 기능
	PRINTL 촉수혈 기능　　　　　　　　 보스 촉수를 격파하며 묻은 피가 몸을 더 음란하게 만듭니다. (난이도: 상승)
	PRINTL 최종 보스 강화 기능　　　　 최종 보스의 HP가 크게 늘어납니다.　　　　　　 　　　　　  (난이도: 상승)
	;원문은 力尽きても変身が解けない
	PRINTL 강제 변신 해제 무효　　　　 기력이 0이 되어도 변신이 풀리지 않게 됩니다.　　　　　　 (난이도: 대감소)
	PRINTL 
	PRINTL 딸 육성　　　　　　　　　　 임신하여 태어난 딸을 동료로 삼을 수 있습니다.
	PRINTL 남자 캐릭터 패배 시 여체화　남자 캐릭터가 촉수에게 패배하면 여자로 TS화됩니다.
	PRINTL 항복 커맨드　　　　　　 　　일부러 지고 싶을 때의 항복 커맨드를 사용 가능하게 합니다.
	PRINTL 묘상화 시 임신 셔틀화　　　 묘상화를 취득한 경우에만 세뇌/흑화 하기가 어려워집니다.
	;원문은 陥落せずに取り込まれる
	PRINTL 세뇌/흑화 없음　　　　　    촉수에게 붙잡힐 시 세뇌/흑화하지 않고 무조건 거두어져 사망합니다.
	PRINTW 
	GOTO TUTORIAL
ELSEIF RESULT == 4
	PRINTL **********************************************
	PRINTL 
	PRINTL 게임이 좀 어렵다고 느껴지신다면
	PRINTL 
	PRINTL 먼저 사용 설명서나 자료 파일을 읽어봅시다.
	PRINTL 특히 각각의 ERB 파일에는 게임에 대한 모든 시스템, 능력, 조건, 히든 요소 등이 제시되고 있으므로
	PRINTL 궁금하신 분들은 스포일러를 감안하여 직접 찾아보는 방법도 있겠습니다.
	PRINTL 
	PRINTL 초기 세트의 캐릭터는 기본적으로 강한 편이므로,
	;特装戦隊 특장전대 -> 무장전대
	PRINTL 무장 전대나 매지컬 헌터로 플레이하면 편하게 진행할 수 있습니다.
	PRINTL 옵션으로 착정 공격 기능이나 구출 커맨드 기능을 켜면
	PRINTL 플레이 폭이 넓어져 난이도가 떨어집니다.
	PRINTL 
	PRINTL SOLO 모드는 시간이 지나도 방위력이 저하되지 않으므로,
	PRINTL 단련과 출격에 집중할 수 있습니다.
	PRINTL 
	PRINTL [1] 도저히 못 하겠다는 분들을 위한 팁 (약스포 주의)
	PRINTL [99] 돌아간다
	$NETABARE
	INPUT
	IF RESULT == 99
		GOTO TUTORIAL
	ELSEIF RESULT != 1
		GOTO NETABARE
	ENDIF
	PRINTL 
	PRINTL 어떻게 해야 할지 도저히 모르겠는 사람들을 위한 약간의 스포일러
	PRINTL 
	PRINTL 　＊　민첩과 지성을 먼저 단련하자. 그 다음으로는 공격력이 좋다. (각 전투 스타일에 따라 조정할 것)
	PRINTL 　＊　전투 스타일을 활용해 자신의 스테이터스에 맞는 공격 방법을 선택하자.
	PRINTL 　＊　단련 시설 레벨을 먼저 올리자. 단련 효율이 올라 일수를 절약할 수 있다.
	PRINTL 　＊　보스와의 레벨차가 4 정도만 돼도 여유롭게 이길 수 있다. 특히 첫 번째 보스는 단련 없이도 쓰러뜨릴 수 있다.
	PRINTL 　＊　전투 지원은 매우 강력하며, 출격 시에 지원하면 생환율이 눈에 띄게 높아진다.
	PRINTL 　＊　"거리를벌린다" 또는 "관찰한다" 커맨드로 부지런히 회복하자.
	PRINTL 　＊　적에게 구속당했을 때에는 [EX방] 을 조합한다면 "풀어낸다" 또는 "떼어낸다" 의 성공 확률이 급격히 오른다.
	PRINTW 
	GOTO TUTORIAL
ELSEIF RESULT == 999
	RETURN 999
ELSE
	GOTO INPUT_LOOP_TUTORIAL
ENDIF

@SAVEINFO
VARSET LOCALS
GETTIME
CALL GAME_MODE_CHECK
SELECTCASE RESULT
	CASE 0 TO 모드카운트-1
		LOCALS = %모드네임:RESULT%モード
	CASEELSE
		LOCALS = ☆커스텀 모드
ENDSELECT
CALL TENTACLE_SURVIVE, "NUM"
LOCAL = RESULT
IF FLAG:64 > 0
	LOCALS:1 = ★GAMECLEAR
ELSEIF ENEMY_TYPE_CHECK_F("BOSS") == 1
	LOCALS:1 = {FLAG:3 - LOCAL + 1}마리쨰
ELSE
	LOCALS:1 = {FLAG:3 + LOCAL}마리쨰
ENDIF

LOCALS:2 = {1000 + GAMEBASE_VERSION}
SUBSTRING LOCALS:2, 1, 3
LOCALS:2 = {GAMEBASE_VERSION / 1000}.%RESULTS%

LOCALS:3 = {DAY}일차

IF FLAG:64 > 0
	PUTFORM %LOCALS,14,LEFT% %LOCALS:3,7,RIGHT% %LOCALS:1,12,RIGHT%    ver%LOCALS:2%
ELSE
	PUTFORM %LOCALS,14,LEFT% %LOCALS:3,7,RIGHT% %LOCALS:1,6,RIGHT%섬멸 중    ver%LOCALS:2%
ENDIF


;**********************************************************
@HEROINE_PRESET
$INPUT_LOOP_CON_HEAD
PRINTL
PRINTL
PRINTL
DRAWLINE
PRINTL
FONTBOLD
SETCOLORBYNAME CYAN
PRINTL ◆캐릭터 데이터 확인
FONTREGULAR
RESETCOLOR
FOR LOCAL,1,CHARANUM
	PRINTFORML 　[{19+LOCAL}] ●%NAME:LOCAL,20,LEFT% %(CSTR:LOCAL:0!="" && CSTR:LOCAL:0!=CALLNAME:LOCAL) ? "《"+CSTR:LOCAL:0+"》" # ""%
NEXT
;PRINTL  [20] 히로인의 스테이터스를 확인한다
SIF CHARANUM >= 2
	PRINTL  [30] 캐릭터에게 상관 관계를 설정한다
PRINTL  
PRINTL  
FONTBOLD
SETCOLORBYNAME CYAN
PRINTL ◆게임 시작　(※컨피그는 시작 후에도 개별로 변경할 수 있습니다)
FONTREGULAR
RESETCOLOR
PRINTL 　★주회차 플레이어용
PRINTL 　[0] 글로벌 컨피그를 인계하여 시작
PRINTL 　　┗[10] 편집
PRINTL  
PRINTL 　★신규 플레이어용 컨피그 세트
PRINTL 　┣[1]「기본 세트 」　	 로 시작：초기부터 존재해 온「소녀 vs 촉수」로 요소를 좁힙니다.
PRINTL 　┣[2]「음옥(淫獄)세트」　로 시작：잡몹적과의 전투나 캐릭터 흑화 등의 추가 옵션이 ON됩니다.
PRINTL 　┗[3]「악질 시민 세트」　로 시작：악질 시민들이 활성화되며, 치안 악화 시 감금이나 강간 전투가 발생합니다.
PRINTL  
$INPUT_LOOP_CON
INPUT
IF RESULT == 10
	CALL CONFIG("mainmenu")
	GOTO INPUT_LOOP_CON_HEAD
ELSEIF INRANGE(RESULT,20,29)
	CALL SHOW_STATUS_CHARA_SELECT(RESULT-19)
	GOTO INPUT_LOOP_CON_HEAD
ELSEIF RESULT == 30 && CHARANUM>=2
	;궁합 데이터의 컨버트
	CALL CONVERT_RELATION
	$LOOP_SETTING_99_0
	DRAWLINE
	PRINTL 상관관계 설정
	PRINTL 
	FOR LOCAL, 0, CHARANUM
		SIF LOCAL == MASTER
			CONTINUE
		VARSET LOCALS
		IF CALLNAME:LOCAL == "범용 캐릭터"
			PRINTFORML [{LOCAL}] 미설정
		ELSE
			LOCALS = [{LOCAL}] %NAME:LOCAL%
			SIF CFLAG:LOCAL:34 > 0
				LOCALS:1 = 실제나이:{BASE:LOCAL:실제나이}
			PRINTFORM %LOCALS, 38, LEFT%　%LOCALS:1, 10, LEFT%　
			IF ISFEMALE(LOCAL)
				PRINT ♀　
			ELSE
				PRINT ♂　
			ENDIF
			IF CFLAG:LOCAL:0 == 상태_유폐
				PRINT  / 처음부터 유폐되어 있다
			ELSEIF CFLAG:LOCAL:0 == 상태_세뇌
				PRINT  / 처음부터 세뇌되어 있다
			ELSEIF CFLAG:LOCAL:0 == 상태_흑화
				PRINT  / 처음부터 흑화되어 있다
			ELSEIF CFLAG:LOCAL:0 == 상태_악질감금
				PRINT  / 처음부터 감금되어 있다
			ELSEIF CFLAG:LOCAL:0 == 상태_사망
				PRINT  / 고인
			ENDIF
			PRINTL 
		ENDIF
	NEXT
	PRINTL [99] 결정
	$INPUT_LOOP_99_0
	INPUT
	IF RESULT > 0 && RESULT <= CHARANUM
		LOCAL:99 = RESULT
		PRINTFORML 상대 캐릭터를 선택해 주십시오.
		FOR LOCAL, 0, CHARANUM
			SIF LOCAL == MASTER
				CONTINUE
			SIF LOCAL == LOCAL:99
				SETCOLOR 105,105,105
			IF CALLNAME:LOCAL == "범용 캐릭터"
				PRINTFORML [{LOCAL}] 미설정
			ELSE
				LOCALS = [{LOCAL}] %NAME:LOCAL%
				IF CFLAG:LOCAL:34 > 0
					LOCALS:1 = 실제나이:{BASE:LOCAL:실제나이}
				ELSE
					LOCALS:1 =
				ENDIF
				PRINTFORM %LOCALS, 38, LEFT%　%LOCALS:1, 10, LEFT%　
				IF ISFEMALE(LOCAL)
					PRINT ♀　
				ELSE
					PRINT ♂　
				ENDIF
				IF CFLAG:LOCAL:0 == 상태_유폐
					PRINT  / 처음부터 유폐되어 있다
				ELSEIF CFLAG:LOCAL:0 == 상태_세뇌
					PRINT  / 처음부터 세뇌되어 있다
				ELSEIF CFLAG:LOCAL:0 == 상태_흑화
					PRINT  / 처음부터 흑화되어 있다
				ELSEIF CFLAG:LOCAL:0 == 상태_악질감금
					PRINT  / 처음부터 감금되어 있다
				ELSEIF CFLAG:LOCAL:0 == 상태_사망
					PRINT  / 고인
				ENDIF
				PRINTL 
			ENDIF
			RESETCOLOR
		NEXT
		PRINTL [99] 돌아간다
		$INPUT_LOOP_99_1
		INPUT
		IF RESULT > 0 && RESULT <= CHARANUM && RESULT != LOCAL:99
			CALL SET_RELATION, LOCAL:99, RESULT
		ELSEIF RESULT != 99
			GOTO INPUT_LOOP_99_1
		ENDIF
		GOTO LOOP_SETTING_99_0
	ELSEIF RESULT == 99
		;상관관계 전체 탐색
		CALL CHECK_ALL_RELATION
	ELSE
		GOTO INPUT_LOOP_99_0
	ENDIF
	GOTO INPUT_LOOP_CON_HEAD
ELSEIF RESULT < 0 || RESULT > 3
	GOTO INPUT_LOOP_CON
ELSE
	LOCAL=RESULT
	;선택한 프리셋에 맞춰 컨피그 설정
	CALL CONFIG_INIT(LOCAL)
ENDIF



