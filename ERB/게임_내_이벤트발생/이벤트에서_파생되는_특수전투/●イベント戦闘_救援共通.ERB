;---------------------------------------------------------------------
;救出イベントの実行(RAID_HANTEIから派生)
@RAID_RESCUE
#DIM CCOUNT


;全員非戦闘員ならイベント終了
LOCAL = 0
FOR CCOUNT, 1, CHARANUM
	SIF CFLAG:CCOUNT:999 == 0
		CONTINUE
	;能動的に動けない場合は除外
	SIF TALENT:CCOUNT:번식자루 > 0 || TALENT:CCOUNT:사지결손 > 0
		CONTINUE

	SIF CFLAG:CCOUNT:0 == 상태_건강 && CFLAG:CCOUNT:100 != 예정_출격 && CFLAG:CCOUNT:100 != 예정_방위 && TALENT:CCOUNT:변신능력 != -1 && CHECK_PREGNANT_F(CCOUNT) == 0 && CFLAG:CCOUNT:99 < 50
		LOCAL ++
NEXT
SIF LOCAL < 1
	RETURN

DRAWLINE
FLAG:43 = 0


;造件を満たすキャラからターゲットを選別
$LOOP
	;このターン出撃していないターゲットを狙う
	LOCAL = RAND:(CHARANUM -1) + 1
	;ターゲットの条件判定
	IF (CFLAG:LOCAL:100 == 예정_출격 || CFLAG:LOCAL:100 == 예정_방위 || CFLAG:LOCAL:0 != 상태_건강 || CFLAG:LOCAL:999 == 0 || TALENT:LOCAL:변신능력 == -1) && LOCAL:1 < 999
		LOCAL:1 ++
		GOTO LOOP
	;見つからなかった場合
	ELSEIF LOCAL:1 >= 999
		FLAG:45 = 0
		PRINTFORML %CALLNAME:LOCAL%は何か嫌な気配を感じたが、気のせいだったようだ…
		PRINTW 
		RETURN
	ENDIF

TARGET = LOCAL

;●発生イベントの種別決定
IF FLAG:999 
	;指定済み
ELSE
	;とりあえず２番以降からランダムとしておく
	FLAG:45 = RAND(2, 6)
ENDIF


LOCAL:1 = 0
LOCAL:2 = 0
PRINTL 
PRINTL 
PRINTL 
;デバッグ中はエフェクトを簡略化
IF FLAG:999 == 0
	REPEAT 30
		IF LOCAL:2 == 0
			LOCAL:1 += 50
			SIF LOCAL:1 == 250
				LOCAL:2 = 1
		ELSE
			LOCAL:1 -= 50
			SIF LOCAL:1 == 0
				LOCAL:2 = 0
		ENDIF
		FONTBOLD
		SETCOLOR LOCAL:1,LOCAL:1,0
		PRINTL 　　　―――――　　　ＥＭＥＲＧＥＮＣＹ　！！　　―――――
		PRINTL 
		PRINTL 
		PRINTL 
		TINPUTS 100,"WARNING",0,""
		CLEARLINE 5
		SIF RESULTS != "WARNING"
			BREAK
	REND
ENDIF
SETCOLOR 250,250,0
PRINTL 　　　―――――　　　ＥＭＥＲＧＥＮＣＹ　！！　　―――――
PRINTL 
PRINTL 
PRINTL 
RESETCOLOR
FONTREGULAR

;狙われたキャラはわずかに체력, 기력が回復する
LOCAL = MAXBASE:체력 / 10 + 500
BASE:체력 = LIMIT(LOCAL + BASE:체력, 0, MAXBASE:체력)

LOCAL = MAXBASE:기력 / 100 + 500
BASE:기력 = LIMIT(LOCAL + BASE:기력, 0, MAXBASE:기력)

PRINTL 
	IF TIME == 0
		IF CFLAG:100 == 예정_단련
			PRINT 鍛錬を終えて街に出ていた
		ELSEIF CFLAG:100 == 예정_휴식
			PRINT 息抜きがてらに街に出ていた
		ELSEIF CFLAG:100 == 예정_활동
			PRINT 특별 활동を終えて帰り仕度していた
		ELSEIF CFLAG:100 == 예정_지원
			PRINT 전투 지원を終えて街に出ていた
		ENDIF
		PRINTFORML %PRINT_CALLNAME(TARGET)%のもとに、緊急の連絡が入った。
	ELSE
		IF CFLAG:100 == 예정_단련
			PRINT 鍛錬を終えたばかりの
		ELSEIF CFLAG:100 == 예정_휴식
			PRINT 自室に居た
		ELSEIF CFLAG:100 == 예정_활동
			PRINT 특별 활동を終えて戻った
		ELSEIF CFLAG:100 == 예정_지원
			PRINT 전투 지원を行っていた
		ELSEIF CFLAG:100 == 107
			PRINT 정보 수집を終えて戻った
		ELSEIF CFLAG:100 == 108
			PRINT 自由行動を終えて戻った
		ENDIF

		PRINTFORML %PRINT_CALLNAME(TARGET)%のもとに、緊急の連絡が入った。
	ENDIF

	;ここで個別イベント호출
	TRYCCALLFORM EVENT_BATTLE_SITUATION_{FLAG:45}
	CATCH
		PRINTL 
		PRINTL #####     ERROR !!     #####
		PRINTFORML 存在しないイベントが選択されました。（EVENT_BATTLE_SITUATION_{FLAG:45}）
		IF FLAG:999
			PRINTL 存在するイベントを選択してください
		ELSE
			PRINTL 原因究明のため、掲示板にできるだけ詳細な発生状況を報告してください
		ENDIF
		PRINTW 
		PRINTL イベントを強制スキップして無理やりゲームを続行します
		PRINTW 
		RETURN
	ENDCATCH
	;右クリックに備えてフォースウェイト
	FORCEWAIT
	PRINTL 
	PRINTL どうする？
	PRINTL （보스 촉수と撤退不可の戦闘になります。助けに行かなくてもペナルティはありません）
	PRINTL  　[0]助けに行く　　[1]助けに行かない
	$INPUT_LOOP_1
	INPUT
	IF RESULT == 0
		PRINTL 
		CALLFORM EVENT_BATTLE_RESCUE_{FLAG:45}
	ELSEIF RESULT == 1
		;助けに行かない場合、イベント中断
		PRINTL 
		CALLFORM EVENT_BATTLE_ABANDON_{FLAG:45}
		PRINTW 
		FLAG:45 = 0
		RETURN
	ELSE
		GOTO INPUT_LOOP_1
	ENDIF

;●イベント内で状態異常等になるかもしれないため、
;　TRAIN先頭で行われている初期化の一部を先に済ませておく
;TCVARの手動初期化
VARSET TCVARn
;衣装の耐久度を설정
CALL CLOTH_BATTLE_SETHP
;衣装の情報を更新
CALL REFRESH_CLOTH_DATA


PRINTW 
CALL DOT_AFTER, 1
PRINTL 
CALLFORM EVENT_BATTLE_EXEC_{FLAG:45}

;RESULT == 1なら↑内でエンカウント演出表示済み
IF RESULT != 1
	CALL ENCOUNT_BOSS
ENDIF



SIF FLAG:999 == 1
	PRINTFORMW エンカウント番号 == {RESULT}
IF RESULT != 1
	PRINTL 
	PRINTL #####     ERROR !!     #####
	PRINTL 보스襲撃イベント不発バグです。原因究明のため、
	PRINTL 掲示板にできるだけ詳細な発生状況を報告してください
	PRINTW 
	PRINTL イベントを強制スキップして無理やりゲームを続行します
	PRINTW 
	RETURN
ELSEIF ENEMY_TYPE_CHECK_F("AKUOTI")
	PRINTL 
	PRINTL #####     ERROR !!     #####
	PRINTL ENEMY_TYPE_CHECK_F("AKUOTI")TRUEバグです。原因究明のため、
	PRINTL 掲示板にできるだけ詳細な発生状況を報告してください
	PRINTW 
	PRINTL イベントを強制スキップして無理やりゲームを続行します
	PRINTW 
	RETURN
ENDIF


BEGIN TRAIN





@RAID_MISSION_SUCCESS
CALL DOT_AFTER, 1
PRINTL 
TRYCALLFORM EVENT_BATTLE_SUCCESS_{FLAG:45}

@RAID_MISSION_FAILURE
CALL DOT_AFTER, 1
PRINTL 
TRYCALLFORM EVENT_BATTLE_FAILURE_{FLAG:45}
