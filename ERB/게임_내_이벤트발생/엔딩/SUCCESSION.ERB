;**********************************************************
;주회의 데이터 인계에 관한 처리
@SUCCESSION,ARG
#DIM NOWLINECOUNT
#DIM POINTS
#DIM USED
#DIM HAVE_DAUGHTER
#DIM NOWPAGE
#DIM PAGEMAX
#DIM LCOUNT
#DIM CCOUNT, 3
#DIM LMONEY
POINTS = 0
USED = 0
HAVE_DAUGHTER = 0
NOWPAGE = 1
PAGEMAX = 3
FLAG:64 = 0
VARSET LOCAL
FOR CCOUNT, 0, CHARANUM
	SIF CCOUNT == MASTER
		CONTINUE
	;딸이 있는지를 판정
	SIF CFLAG:CCOUNT:231
		HAVE_DAUGHTER ++
	;변신 상태를 푼다
	SIF CFLAG:CCOUNT:1 > 0
		CALL TRANSFORM, 0, CCOUNT
NEXT
PRINTW …
PRINTW ……
PRINTW ………
CALL LB
PRINTL +-+-+☆★☆　주회차 포인트 획득　☆★☆+-+-+
CALL SHORTLINE
PRINTFORML 주회수 누계(인계 횟수)　　　　　　　　　　+{FLAG:854 - 1}
POINTS += FLAG:854 - 1
IF GAME_MODE_CHECK_F() == MODE_SOLO
	PRINTFORML SOLO 클리어　　　　　　　　　　　　　　　 +2
	POINTS += 2
ELSEIF GAME_MODE_CHECK_F() == MODE_NORMAL
	PRINTFORML NORMAL 클리어　　　　　　　　　　　　　　 +5
	POINTS += 5
ELSEIF GAME_MODE_CHECK_F() == MODE_HARDCORE
	PRINTFORML HARDCORE 클리어　　　　　　　　　　　　　 +7
	POINTS += 7
ELSEIF GAME_MODE_CHECK_F() == MODE_INSTANT
	PRINTFORML INSTANT 클리어　　　　　　　　　　　　　　+5
	POINTS += 5
ELSE
	PRINTFORML 게임 클리어　　　　　　　　　　　　　　　 +2
	POINTS += 2
ENDIF
IF ARG == 6
	PRINTFORML 종합 Ｓ랭크　　　　　　　　　　　　　　　 +8
	POINTS += 8
ELSEIF ARG == 5
	PRINTFORML 종합 Ａ랭크　　　　　　　　　　　　　　　 +5
	POINTS += 5
ELSEIF ARG == 4
	PRINTFORML 종합 Ｂ랭크　　　　　　　　　　　　　　　 +3
	POINTS += 3
ELSEIF ARG == 3
	PRINTFORML 종합 Ｃ랭크　　　　　　　　　　　　　　　 +2
	POINTS += 2
ELSEIF ARG == 2
	PRINTFORML 종합 Ｄ랭크　　　　　　　　　　　　　　　 +1
	POINTS += 1
ELSEIF ARG == 1
	PRINTFORML 종합 Ｅ랭크　　　　　　　　　　　　　　　 +0
ENDIF
;패자(覇者)의 증표
IF GLOBAL:211
	PRINTFORML 패자(覇者)의 증표　　　　　　　　　　　　 +5
	POINTS += 5
ENDIF
;엉터리 대마왕
IF GLOBAL:212
	PRINTFORML 엉터리 대마왕　　　　　　　　　　　　　　 +5
	POINTS += 5
ENDIF
;eraGVT 마스터
IF GLOBAL:213
	PRINTFORML eraGVT 마스터　　　　　　　　　　　　　　 +5
	POINTS += 5
ENDIF
;ENDLESS 보스 격파 수
IF GLOBAL:114 >= 8
	IF GLOBAL:114 >= 60
		LOCAL = 12
	ELSEIF GLOBAL:114 >= 50
		LOCAL = 8
	ELSEIF GLOBAL:114 >= 40
		LOCAL = 6
	ELSEIF GLOBAL:114 >= 30
		LOCAL = 4
	ELSEIF GLOBAL:114 >= 20
		LOCAL = 2
	ELSE
		LOCAL = 1
	ENDIF
	PRINTFORML ENDLESS 격파 수 보너스 　　　　　　　　　 +{LOCAL}
	POINTS += LOCAL
ENDIF
LOCAL = 0
;마법(물리)소녀
SIF GLOBAL:220
	LOCAL += 10
;근육이 세상을 구한다
SIF GLOBAL:221
	LOCAL += 10
;탱커 누가 키웠냐
SIF GLOBAL:222
	LOCAL += 10
;움직이는 성채
SIF GLOBAL:223
	LOCAL += 10
;위타천
SIF GLOBAL:224
	LOCAL += 10
;준족순격，뒤따를 자 없으니
SIF GLOBAL:225
	LOCAL += 10
;저런 데이터는 없었다고？！
SIF GLOBAL:226
	LOCAL += 10
;전 L입니다
SIF GLOBAL:227
	LOCAL += 10
;불사신의 전사
SIF GLOBAL:228
	LOCAL += 20
;Lion Heart
SIF GLOBAL:229
	LOCAL += 20
;절륜 마법소녀
SIF GLOBAL:230
	LOCAL += 20
;그랜드 마스터
SIF GLOBAL:231
	LOCAL += 10
;전원 집중포화！
SIF GLOBAL:232
	LOCAL += 10
;백발백중 명 사 수
SIF GLOBAL:233
	LOCAL += 10
;성에 눈뜨다
SIF GLOBAL:240
	LOCAL += 15
;만져지기만 해도・・・
SIF GLOBAL:241
	LOCAL += 10
;요염한 향연
SIF GLOBAL:242
	LOCAL += 15
;음계 거주자
SIF GLOBAL:243
	LOCAL += 20
;쌍방구현
SIF GLOBAL:244
	LOCAL += 20
;마법젖소녀
SIF GLOBAL:245
	LOCAL += 20
;자위 중독 말기
SIF GLOBAL:246
	LOCAL += 20
;정액에 진심으로 빠지다
SIF GLOBAL:247
	LOCAL += 20
;생애 첫 출산
SIF GLOBAL:250
	LOCAL += 10
;신출내기 마망
SIF GLOBAL:251
	LOCAL += 10
;암흑 베이비 붐
SIF GLOBAL:252
	LOCAL += 10
;THE QUEEN
SIF GLOBAL:253
	LOCAL += 20
;행복가족계획
SIF GLOBAL:254
	LOCAL += 10
;기생 조커
SIF GLOBAL:255
	LOCAL += 10
;매지컬☆아이돌 육성 계획
SIF GLOBAL:256
	LOCAL += 10
;ＡＶ 여배우
SIF GLOBAL:257
	LOCAL += 10
;잠자는 촉수 속의 공주님
SIF GLOBAL:258
	LOCAL += 10
;기가 막히는 자린고비
SIF GLOBAL:259
	LOCAL += 10
;촉수 따위에겐 지지 않아！
SIF GLOBAL:260
	LOCAL += 10
;불굴의 투지
SIF GLOBAL:261
	LOCAL += 10
;음울(淫鬱) 허니문
SIF GLOBAL:262
	LOCAL += 20
;파괴신의 일격
SIF GLOBAL:263
	LOCAL += 10
;처녀 빗치 애널광
SIF GLOBAL:264
	LOCAL += 10
;NEXT GENERATION
SIF GLOBAL:265
	LOCAL += 10
;퍼펙트 킬
SIF GLOBAL:266
	LOCAL += 20
;기적의 역전승
SIF GLOBAL:267
	LOCAL += 10
;Tactical Order
SIF GLOBAL:268
	LOCAL += 10
;배틀 코스튬 마니아
SIF GLOBAL:269
	LOCAL += 5
;역대 최강 VS 현대 최강
SIF GLOBAL:270
	LOCAL += 20
;얼른 도와줄게！
SIF GLOBAL:271
	LOCAL += 5
;정신 차리세요！
SIF GLOBAL:272
	LOCAL += 5
;난 지금까지 무엇을…？
SIF GLOBAL:273
	LOCAL += 5
;패션 리더
SIF GLOBAL:274
	LOCAL += 5

PRINTFORML 실적 달성에 따른 주회 보너스　　　　　　　+{LOCAL}*0.1
LOCAL /= 10	
POINTS += LOCAL
CALL SHORTLINE
PRINTFORM 합계　　　　　　　　　　　　　　　　　　　{POINTS}pts.
POINTS = MAX(POINTS,GLOBAL:115)
PRINTFORML 　(역대 최고 {POINTS}pts.)


PRINTL 
PRINTW [PRESS ENTER]
CLEARLINE 1
;----------------------------------------------------------------------------------------------------------------------------------
;인계 선택 처리 개시
LMONEY = 0
;시설
REPEAT FLAG:50
	SIF COUNT == 0
		CONTINUE
	LMONEY += 5000 * COUNT + 5000
REND
REPEAT FLAG:51
	SIF COUNT == 0
		CONTINUE
	LMONEY += 1000 * COUNT + 1000
REND
REPEAT FLAG:52
	LMONEY += 10000 * COUNT + 10000
REND
SIF (FLAG:53 & 관엽식물)
	LMONEY += 500
SIF (FLAG:53 & 질좋은침대)
	LMONEY += 12000
SIF (FLAG:53 & 최고급침대)
	LMONEY += 95000
SIF (FLAG:53 & 안마의자)
	LMONEY += 25800
SIF (FLAG:53 & 목욕설비)
	LMONEY += 50000
SIF (FLAG:53 & 게임코너)
	LMONEY += 100000
SIF (FLAG:53 & 대욕탕)
	LMONEY += 200000
SIF (FLAG:53 & 미용온천)
	LMONEY += 500000
SIF (FLAG:53 & 뷰티살롱)
	LMONEY += 1500000
SIF (FLAG:53 & 마음이편안해지는풍경화)
	LMONEY += 4500
SIF (FLAG:53 & 세련된오브제)
	LMONEY += 8500
SIF (FLAG:53 & 호화로운분수)
	LMONEY += 100000

FOR LOCAL,21,31
	LOCAL:LOCAL = FLAG:(880+LOCAL)
NEXT
REDRAW 0
VARSET LOCAL
$MASTER_LOOP
NOWLINECOUNT = LINECOUNT
PRINTFORML 데이터 인계　　　　　　　　　　 　　　　　　　 남은 주회차 포인트 　({POINTS - USED}pts.)
DRAWLINE
PRINTFORML 입수한 주회차 포인트를 소비하여 다음 주회에 데이터를 인계받을 수 있습니다.
PRINTFORML 이름이나 종족 등, 게임 시작 시 캐릭터가 가지고 있던 데이터는 자동으로 인계됩니다.
PRINTFORML 인계할 항목을 선택해 주십시오.
PRINTL 
IF NOWPAGE > 1
	PRINTPLAIN  주회차용 옵션　　　　　　　 
	PRINTL  [0] 이 페이지의 항목을 모두 해제
	PRINTL 
ENDIF
IF NOWPAGE == 1
	PRINTFORML  [0] 인계 캐릭터 선택　　　　　　　　　현재 {LOCAL:10}인
	PRINTL 
	IF LOCAL:11
		PRINTL  [1] 성(性) 성장 인계　　　　　　　　　(선택 완료)
	ELSE
		SIF POINTS < 4
			SETCOLOR 105,105,105
		PRINTL  [1] 성(性) 성장 인계　　　　　　　　　-4
		RESETCOLOR
	ENDIF
	IF LOCAL:12
		PRINTL  [2] 딸 인계　　　　　　　　　　　　　 (선택 완료)
	ELSE
		SIF POINTS < 15 || HAVE_DAUGHTER == 0
			SETCOLOR 105,105,105
		PRINTL  [2] 딸 인계　　　　　　　　　　　　　 -15
		RESETCOLOR
	ENDIF
	IF GLOBAL:213
		IF LOCAL:13
			PRINTL  [3] 레벨 인계　　　　　　　　　　　　 (선택 완료)
		ELSE
			SIF POINTS < 30
				SETCOLOR 105,105,105
			PRINTL  [3] 레벨 인계　　　　　　　　　　　　 -30
			RESETCOLOR
		ENDIF
	ELSE
		SETCOLOR 105,105,105
		PRINTL  [3] ？？？
		RESETCOLOR
	ENDIF
	IF LOCAL:14
		PRINTL  [4] 전투기술 인계　　　　　　　　　　 (선택 완료)
	ELSE
		SIF POINTS < 8
			SETCOLOR 105,105,105
		PRINTL  [4] 전투기술 인계　　　　　　　　　　 -8
		RESETCOLOR
	ENDIF
	IF LOCAL:15 == 1
		PRINTL  [5] 자금 인계　　　　　　　　　　　　 25％
	ELSEIF LOCAL:15 == 2
		PRINTL  [5] 자금 인계　　　　　　　　　　　　 50％
	ELSEIF LOCAL:15 == 3
		PRINTL  [5] 자금 인계　　　　　　　　　　　　 75％
	ELSEIF LOCAL:15 == 4
		PRINTL  [5] 자금 인계　　　　　　　　　　　　 100％
	ELSEIF LOCAL:15 == 5
		PRINTL  [5] 자금 인계　　　　　　　　　　　　 100％＋시설 레벨 인계
	ELSE
		PRINTL  [5] 자금 인계　　　　　　　　　　　　 없음
	ENDIF
	IF LOCAL:16
		PRINTFORML  [6] 기초 스탯 보너스 증가　　　　　　　　　　　 {LOCAL:16,5,RIGHT} 포인트
	ELSE
		PRINTFORML  [6] 기초 스탯 보너스 증가　　　　　　 없음
	ENDIF
	IF LOCAL:17
		PRINTFORML  [7] 초기 방위력 증가　　　　　　　　　　　　　　{LOCAL:17*500,5,RIGHT} 포인트
	ELSE
		PRINTFORML  [7] 초기 방위력 증가　　　　　　　　　없음
	ENDIF
	IF LOCAL:18
		PRINTL  [8] 촉수의 조각＆연구Lv 인계　　　　　(선택 완료)
	ELSE
		SIF POINTS < 4 || FLAG:200 == 0
			SETCOLOR 105,105,105
		PRINTL  [8] 촉수의 조각＆연구Lv 인계　　　　　-4
		RESETCOLOR
	ENDIF
	IF LOCAL:19
		PRINTL  [9] 의상 인계　　　　　　　　　　　　 (선택 완료)
	ELSE
		SIF POINTS < 6
			SETCOLOR 105,105,105
		PRINTL  [9] 의상 인계　　　　　　　　　　　　 -6
		RESETCOLOR
	ENDIF
	IF LOCAL:20
		PRINTL [10] 매료경험 인계　　　　　　　　　　 (선택 완료)
	ELSE
		SIF POINTS < 6
			SETCOLOR 105,105,105
		PRINTL [10] 매료경험 인계　　　　　　　　　　 -6
		RESETCOLOR
	ENDIF
ELSEIF NOWPAGE == 2
	IF LOCAL:21
		PRINTL [11] 야간 자동 회복 UP　　　　　　　　 (선택 완료)
	ELSE
		SIF POINTS < 8
			SETCOLOR 105,105,105
		PRINTL [11] 야간 자동 회복 UP　　　　　　　　 -8
		RESETCOLOR
	ENDIF
	IF LOCAL:22
		PRINTL [12] 적의 구속 공격 선택률 DOWN　　　　(선택 완료)
	ELSE
		SIF (LOCAL:27 == 0 && POINTS < 6) || (LOCAL:27 && POINTS < 4)
			SETCOLOR 105,105,105
		PRINTL [12] 적의 구속 공격 선택률 DOWN　　　　-6
		RESETCOLOR
	ENDIF
	IF LOCAL:23
		PRINTL [13] 풀어내기 확률 UP　　　　　　　　　(선택 완료)
	ELSE
		SIF (LOCAL:28 == 0 && POINTS < 6) || (LOCAL:28 && POINTS < 1)
			SETCOLOR 105,105,105
		PRINTL [13] 풀어내기 확률 UP　　　　　　　　　-6
		RESETCOLOR
	ENDIF
	IF LOCAL:24
		PRINTL [14] 세뇌/흑화 쉬워짐　　　　　　　　　(선택 완료)
	ELSE
		SIF POINTS < 3
			SETCOLOR 105,105,105
		PRINTL [14] 세뇌/흑화 쉬워짐　　　　　　　　　-3
		RESETCOLOR
	ENDIF
	IF LOCAL:25
		PRINTL [15] 각인에 의한 기죽음 무효　　　　　 (선택 완료)
	ELSE
		SIF (LOCAL:30 == 0 && POINTS < 8) || (LOCAL:30 && POINTS < 6)
			SETCOLOR 105,105,105
		PRINTL [15] 각인에 의한 기죽음 무효　　　　　 -8
		RESETCOLOR
	ENDIF
	IF LOCAL:26
		PRINTL [16] 일수 제한 해제　　　　　　　　　　(선택 완료)
	ELSE
		SIF POINTS < 30
			SETCOLOR 105,105,105
		PRINTL [16] 일수 제한 해제　　　　　　　　　　-30
		RESETCOLOR
	ENDIF
	IF LOCAL:27
		PRINTL [17] 적의 구속 공격 선택률 UP　　　　　(선택 완료)
	ELSE
		SIF POINTS < 2 && LOCAL:22 == 0
			SETCOLOR 105,105,105
		PRINTL [17] 적의 구속 공격 선택률 UP　　　　　-2
		RESETCOLOR
	ENDIF
	IF LOCAL:28
		PRINTL [18] 풀어내기 확률 DOWN　　　　　　　　(선택 완료)
	ELSE
		SIF POINTS < 2 && LOCAL:23 == 0
			SETCOLOR 105,105,105
		PRINTL [18] 풀어내기 확률 DOWN　　　　　　　　-2
		RESETCOLOR
	ENDIF
	IF LOCAL:29
		PRINTL [19] 임신 확률 UP　　　　　　　　　　　(선택 완료)
	ELSE
		SIF POINTS < 3
			SETCOLOR 105,105,105
		PRINTL [19] 임신 확률 UP　　　　　　　　　　　-3
		RESETCOLOR
	ENDIF
	IF LOCAL:30
		PRINTL [20] 각인에 의한 기죽음 확률 ２배　　　(선택 완료)
	ELSE
		SIF POINTS < 2 && LOCAL:25 == 0
			SETCOLOR 105,105,105
		PRINTL [20] 각인에 의한 기죽음 확률 ２배　　　-2
		RESETCOLOR
	ENDIF
ELSEIF NOWPAGE == 3
	IF LOCAL:31
		PRINTL [21] 인기도 초기화　　　　　　　　　　 (선택 완료)
	ELSE
		SIF POINTS < 8
			SETCOLOR 105,105,105
		PRINTL [21] 인기도 초기화　　　　　　　　　　 무료
		RESETCOLOR
	ENDIF
ENDIF
DRAWLINE
IF NOWPAGE > 1
	PRINT [100] 앞 페이지로　　　　　
ELSE
	PRINT                            
ENDIF
PRINTPLAINFORM PAGE < {NOWPAGE}/{PAGEMAX} >　　　　
SIF NOWPAGE < PAGEMAX
	PRINT [200] 뒷 페이지로
PRINTL 
PRINTL [999] 인계를 개시한다
$INPUT_LOOP
INPUT
;인계 캐릭터 선택
IF RESULT == 0 && NOWPAGE == 1
	NOWLINECOUNT = LINECOUNT
	$PRINT_LOOP_1
	LOCAL:10 = 0
	CALL SHORTLINE
	PRINTFORML 다음 회차에 인계할 캐릭터를 선택해 주십시오.
	PRINTL 
	FOR CCOUNT, 0, CHARANUM
		SIF CCOUNT == MASTER
			CONTINUE
		SIF CFLAG:CCOUNT:231 && LOCAL:12 == 0
			SETCOLOR 105,105,105
		PRINTFORM  [{CCOUNT}] %NAME:CCOUNT,42,LEFT%　　　
		SIF CCOUNT < 10
			PRINT  
		IF CFLAG:CCOUNT:0 == 999
			PRINT ○인계한다
			LOCAL:10 ++
		ELSE
			PRINT ×인계하지 않는다
		ENDIF
		IF CFLAG:CCOUNT:231
			PRINT 　(딸)　
		ELSE
			PRINT 　　　　
		ENDIF
		PRINTL 
		RESETCOLOR
	NEXT
	PRINTL  [100] 모든 캐릭터 선택
	CALL SHORTLINE
	PRINTL [999] 선택 완료
	$INPUT_LOOP_1
	INPUT
	IF RESULT <= CHARANUM -1 && RESULT != MASTER && (CFLAG:RESULT:231 == 0 || LOCAL:12)
		IF CFLAG:RESULT:0 == 999
			CFLAG:RESULT:0 = 0
		ELSE
			CFLAG:RESULT:0 = 999
		ENDIF
		CLEARLINE LINECOUNT - NOWLINECOUNT
		GOTO PRINT_LOOP_1
	ELSEIF RESULT == 100
		FOR CCOUNT, 0, CHARANUM
			SIF CCOUNT == MASTER
				CONTINUE
			SIF CFLAG:CCOUNT:231 == 0 || LOCAL:12
				CFLAG:CCOUNT:0 = 999
		NEXT
		CLEARLINE LINECOUNT - NOWLINECOUNT
		GOTO PRINT_LOOP_1
	ELSEIF RESULT == 999
		CALL LB
		GOTO MASTER_LOOP
	ELSE
		CLEARLINE 1
		GOTO INPUT_LOOP_1
	ENDIF
;성(性)성장 인계
ELSEIF RESULT == 1 && (LOCAL:11 || POINTS >= 4) && NOWPAGE == 1
	IF LOCAL:11
		LOCAL:11 = 0
		POINTS += 4
	ELSE
		LOCAL:11 = 1
		POINTS -= 4
	ENDIF
;딸 인계
ELSEIF RESULT == 2 && (LOCAL:12 || POINTS >= 15) && HAVE_DAUGHTER && NOWPAGE == 1
	IF LOCAL:12
		LOCAL:12 = 0
		POINTS += 15
		FOR CCOUNT, 0, CHARANUM
			IF CFLAG:CCOUNT:0 == 999 && CFLAG:CCOUNT:231
				CFLAG:CCOUNT:0 = 0
				LOCAL:10 --
			ENDIF
		NEXT
	ELSE
		LOCAL:12 = 1
		POINTS -= 15
	ENDIF
;레벨 인계
ELSEIF RESULT == 3 && (LOCAL:13 || POINTS >= 30) && GLOBAL:213 && NOWPAGE == 1
	IF LOCAL:13
		LOCAL:13 = 0
		POINTS += 30
	ELSE
		LOCAL:13 = 1
		POINTS -= 30
	ENDIF
;전투기술 인계
ELSEIF RESULT == 4 && (LOCAL:14 || POINTS >= 8) && NOWPAGE == 1
	IF LOCAL:14
		LOCAL:14 = 0
		POINTS += 8
	ELSE
		LOCAL:14 = 1
		POINTS -= 8
	ENDIF
;자금 인계
ELSEIF RESULT == 5 && NOWPAGE == 1
	NOWLINECOUNT = LINECOUNT
	$PRINT_LOOP_2
	LOCAL = LOCAL:15 * 4
	CALL SHORTLINE
	PRINTFORML 인계할 자금 금액을 선택해 주십시오.　　　　　　남은 주회차 포인트 ({POINTS - USED}pts.)
	PRINTL 
	PRINTFORML  [0]  인계하지 않는다
	IF LOCAL:15 == 1
		PRINTFORML  [1]  25％ 인계한다　{MONEY * 25 / 100,10,RIGHT}＄　　　　　　　　　　 (선택 완료)
	ELSE
		SIF POINTS + LOCAL < 4
			SETCOLOR 105,105,105
		PRINTFORML  [1]  25％ 인계한다　{MONEY * 25 / 100,10,RIGHT}＄　　　　　　　　　　 -4
		RESETCOLOR
	ENDIF
	IF LOCAL:15 == 2
		PRINTFORML  [2]  50％ 인계한다　{MONEY * 50 / 100,10,RIGHT}＄　　　　　　　　　　 (선택 완료)
	ELSE
		SIF POINTS + LOCAL < 8
			SETCOLOR 105,105,105
		PRINTFORML  [2]  50％ 인계한다　{MONEY * 50 / 100,10,RIGHT}＄　　　　　　　　　　 -8
		RESETCOLOR
	ENDIF
	IF LOCAL:15 == 3
		PRINTFORML  [3]  75％ 인계한다　{MONEY * 75 / 100,10,RIGHT}＄　　　　　　　　　　 (선택 완료)
	ELSE
		SIF POINTS + LOCAL < 12
			SETCOLOR 105,105,105
		PRINTFORML  [3]  75％ 인계한다　{MONEY * 75 / 100,10,RIGHT}＄　　　　　　　　　　 -12
		RESETCOLOR
	ENDIF
	IF LOCAL:15 == 4
		PRINTFORML  [4] 100％ 인계한다　{MONEY,10,RIGHT}＄　　　　　　　　　　 (선택 완료)
	ELSE
		SIF POINTS + LOCAL < 16
			SETCOLOR 105,105,105
		PRINTFORML  [4] 100％ 인계한다　{MONEY,10,RIGHT}＄　　　　　　　　　　 -16
		RESETCOLOR
	ENDIF
	IF LOCAL:15 == 5
		PRINTFORML  [5] 100％ 인계한다　{MONEY - LMONEY,10,RIGHT}＄＋시설 레벨 인계　　　(선택 완료)
	ELSE
		SIF POINTS + LOCAL < 16
			SETCOLOR 105,105,105
		PRINTFORML  [5] 100％ 인계한다　{MONEY - LMONEY,10,RIGHT}＄＋시설 레벨 인계　　　-16
		RESETCOLOR
	ENDIF
	CALL SHORTLINE
	PRINTL [999] 선택 완료
	$INPUT_LOOP_2
	INPUT
	IF RESULT == 0
		POINTS += LOCAL:15 * 4
		LOCAL:15 = 0
	ELSEIF RESULT == 1 && (LOCAL:15 == 1 || POINTS + LOCAL >= 4)
		IF LOCAL:15 != 1
			SIF LOCAL:15 >= 1
				POINTS += LOCAL:15 * 4
			LOCAL:15 = 1
			POINTS -= 4
		ELSE
			LOCAL:15 = 0
			POINTS += 4
		ENDIF
		CLEARLINE LINECOUNT - NOWLINECOUNT
		GOTO PRINT_LOOP_2
	ELSEIF RESULT == 2 && (LOCAL:15 == 2 || POINTS + LOCAL >= 8)
		IF LOCAL:15 != 2
			SIF LOCAL:15 >= 1
				POINTS += LOCAL:15 * 4
			LOCAL:15 = 2
			POINTS -= 8
		ELSE
			LOCAL:15 = 0
			POINTS += 8
		ENDIF
		CLEARLINE LINECOUNT - NOWLINECOUNT
		GOTO PRINT_LOOP_2
	ELSEIF RESULT == 3 && (LOCAL:15 == 3 || POINTS + LOCAL >= 12)
		IF LOCAL:15 != 3
			SIF LOCAL:15 >= 1
				POINTS += LOCAL:15 * 4
			LOCAL:15 = 3
			POINTS -= 12
		ELSE
			LOCAL:15 = 0
			POINTS += 12
		ENDIF
		CLEARLINE LINECOUNT - NOWLINECOUNT
		GOTO PRINT_LOOP_2
	ELSEIF RESULT == 4 && (LOCAL:15 == 4 || POINTS + LOCAL >= 16)
		IF LOCAL:15 == 5
			LOCAL:15 = 4
		ELSEIF LOCAL:15 != 4
			SIF LOCAL:15 >= 1
				POINTS += LOCAL:15 * 4
			LOCAL:15 = 4
			POINTS -= 16
		ELSE
			LOCAL:15 = 0
			POINTS += 16
		ENDIF
		CLEARLINE LINECOUNT - NOWLINECOUNT
		GOTO PRINT_LOOP_2
	ELSEIF RESULT == 5 && (LOCAL:15 == 5 || POINTS + LOCAL >= 16)
		IF LOCAL:15 == 4
			LOCAL:15 = 5
		ELSEIF LOCAL:15 != 5
			SIF LOCAL:15 >= 1
				POINTS += LOCAL:15 * 4
			LOCAL:15 = 5
			POINTS -= 16
		ELSE
			LOCAL:15 = 0
			POINTS += 16
		ENDIF
		CLEARLINE LINECOUNT - NOWLINECOUNT
		GOTO PRINT_LOOP_2
	ELSEIF RESULT == 999
		CALL LB
		GOTO MASTER_LOOP
	ELSE
		CLEARLINE 1
		GOTO INPUT_LOOP_2
	ENDIF
;기초치 보너스 증가
ELSEIF RESULT == 6 && NOWPAGE == 1
	NOWLINECOUNT = LINECOUNT
	$PRINT_LOOP_3
	CALL SHORTLINE
	PRINTFORML 캐릭터 메이킹에서 사용할 기초치 보너스를 20포인트까지 획득할 수 있습니다.
	PRINTFORML 주회차 포인트를 할당해 주십시오.　　　　　　　 남은 주회차 포인트 ({POINTS - USED}pts.)
	PRINTL 
	PRINTFORML 　기초치 보너스：{LOCAL:16}
	PRINTL 
	PRINTFORM  [0] 초기화한다　　　　　　　　　　
	SIF LOCAL:16 < 1
		SETCOLOR 105,105,105
	PRINTFORM [1] −１　　 
	RESETCOLOR
	SIF POINTS < 2 || LOCAL:16 >= 20
		SETCOLOR 105,105,105
	PRINTFORML [10] ＋１
	RESETCOLOR
	CALL SHORTLINE
	PRINTL [999] 선택 완료
	$INPUT_LOOP_3
	INPUT
	IF RESULT == 0
		POINTS += LOCAL:16 * 2
		LOCAL:16 = 0
		GOTO PRINT_LOOP_3
	ELSEIF RESULT == 1 && LOCAL:16
		LOCAL:16 -= 1
		POINTS += 2
		CLEARLINE LINECOUNT - NOWLINECOUNT
		GOTO PRINT_LOOP_3
	ELSEIF RESULT == 10 && POINTS >= 2 && LOCAL:16 < 20
		LOCAL:16 += 1
		POINTS -= 2
		CLEARLINE LINECOUNT - NOWLINECOUNT
		GOTO PRINT_LOOP_3
	ELSEIF RESULT == 999
		CALL LB
		GOTO MASTER_LOOP
	ELSE
		CLEARLINE 1
		GOTO INPUT_LOOP_3
	ENDIF
;초기 방위력 증가
ELSEIF RESULT == 7 && NOWPAGE == 1
	NOWLINECOUNT = LINECOUNT
	$PRINT_LOOP_4
	CALL SHORTLINE
	PRINTFORML 초기 방위력을 5000포인트까지 강화할 수 있습니다.
	PRINTFORML 주회차 포인트를 할당해 주십시오.　　　　　　　 남은 주회차 포인트 ({POINTS - USED}pts.)
	PRINTL 
	PRINTFORML 　방위력 강화：{LOCAL:17*500}
	PRINTL 
	PRINTFORM  [0] 초기화한다　　　　　　　　　　
	SIF LOCAL:17 < 1
		SETCOLOR 105,105,105
	PRINTFORM [1] −５００　　 
	RESETCOLOR
	SIF POINTS < 1 || LOCAL:17 >= 10
		SETCOLOR 105,105,105
	PRINTFORML [10] ＋５００
	RESETCOLOR
	CALL SHORTLINE
	PRINTL [999] 선택 완료
	$INPUT_LOOP_4
	INPUT
	IF RESULT == 0
		POINTS += LOCAL:17
		LOCAL:17 = 0
		GOTO PRINT_LOOP_4
	ELSEIF RESULT == 1 && LOCAL:17
		LOCAL:17 -= 1
		POINTS += 1
		CLEARLINE LINECOUNT - NOWLINECOUNT
		GOTO PRINT_LOOP_4
	ELSEIF RESULT == 10 && POINTS >= 1 && LOCAL:17 < 10
		LOCAL:17 += 1
		POINTS -= 1
		CLEARLINE LINECOUNT - NOWLINECOUNT
		GOTO PRINT_LOOP_4
	ELSEIF RESULT == 999
		CALL LB
		GOTO MASTER_LOOP
	ELSE
		CLEARLINE 1
		GOTO INPUT_LOOP_4
	ENDIF
;촉수의 조각 인계
ELSEIF RESULT == 8 && (LOCAL:18 || POINTS >= 4) && FLAG:200 && NOWPAGE == 1
	IF LOCAL:18
		LOCAL:18 = 0
		POINTS += 4
	ELSE
		LOCAL:18 = 1
		POINTS -= 4
	ENDIF
;의상 인계
ELSEIF RESULT == 9 && (LOCAL:19 || POINTS >= 6) && NOWPAGE == 1
	IF LOCAL:19
		LOCAL:19 = 0
		POINTS += 6
	ELSE
		LOCAL:19 = 1
		POINTS -= 6
	ENDIF
;매료경험 인계
ELSEIF RESULT == 10 && (LOCAL:20 || POINTS >= 6) && NOWPAGE == 1
	IF LOCAL:20
		LOCAL:20 = 0
		POINTS += 6
	ELSE
		LOCAL:20 = 1
		POINTS -= 6
	ENDIF
;이 페이지의 항목을 모두 해제한다 (2페이지)
ELSEIF RESULT == 0 && NOWPAGE == 2
	IF LOCAL:21
		LOCAL:21 = 0
		POINTS += 8
	ENDIF
	IF LOCAL:22
		LOCAL:22 = 0
		POINTS += 6
	ENDIF
	IF LOCAL:23
		LOCAL:23 = 0
		POINTS += 6
	ENDIF
	IF LOCAL:24
		LOCAL:24 = 0
		POINTS += 3
	ENDIF
	IF LOCAL:25
		LOCAL:25 = 0
		POINTS += 8
	ENDIF
	IF LOCAL:26
		LOCAL:26 = 0
		POINTS += 30
	ENDIF
	IF LOCAL:27
		LOCAL:27 = 0
		POINTS += 2
	ENDIF
	IF LOCAL:28
		LOCAL:28 = 0
		POINTS += 2
	ENDIF
	IF LOCAL:29
		LOCAL:29 = 0
		POINTS += 3
	ENDIF
	IF LOCAL:30
		LOCAL:30 = 0
		POINTS += 2
	ENDIF
;야간 자동 회복 증가
ELSEIF RESULT == 11 && (LOCAL:21 || POINTS >= 8) && NOWPAGE == 2
	IF LOCAL:21
		LOCAL:21 = 0
		POINTS += 8
	ELSE
		LOCAL:21 = 1
		POINTS -= 8
	ENDIF
;구속 공격 선택률 DOWN
ELSEIF RESULT == 12 && (LOCAL:22 || POINTS >= 6 || (LOCAL:27 && POINTS >= 4)) && NOWPAGE == 2
	IF LOCAL:22
		LOCAL:22 = 0
		POINTS += 6
	ELSE
		IF LOCAL:27
			LOCAL:27 = 0
			POINTS += 2
		ENDIF
		LOCAL:22 = 1
		POINTS -= 6
	ENDIF
;풀어내기 확률 UP
ELSEIF RESULT == 13 && (LOCAL:23 || POINTS >= 6 || (LOCAL:28 && POINTS >= 4)) && NOWPAGE == 2
	IF LOCAL:23
		LOCAL:23 = 0
		POINTS += 6
	ELSE
		IF LOCAL:28
			LOCAL:28 = 0
			POINTS += 2
		ENDIF
		LOCAL:23 = 1
		POINTS -= 6
	ENDIF
;세뇌/흑화 쉬워짐
ELSEIF RESULT == 14 && (LOCAL:24 || POINTS >= 3) && NOWPAGE == 2
	IF LOCAL:24
		LOCAL:24 = 0
		POINTS += 3
	ELSE
		LOCAL:24 = 1
		POINTS -= 3
	ENDIF
;각인에 의한 기죽음 무효
ELSEIF RESULT == 15 && (LOCAL:25 || POINTS >= 8 || (LOCAL:30 && POINTS >= 6)) && NOWPAGE == 2
	IF LOCAL:25
		LOCAL:25 = 0
		POINTS += 8
	ELSE
		IF LOCAL:30
			LOCAL:30 = 0
			POINTS += 2
		ENDIF
		LOCAL:25 = 1
		POINTS -= 8
	ENDIF
;일수 제한 해제
ELSEIF RESULT == 16 && (LOCAL:26 || POINTS >= 30) && NOWPAGE == 2
	IF LOCAL:26
		LOCAL:26 = 0
		POINTS += 30
	ELSE
		LOCAL:26 = 1
		POINTS -= 30
	ENDIF
;구속 공격 선택률 UP
ELSEIF RESULT == 17 && (LOCAL:27 || POINTS >= 2 || LOCAL:22) && NOWPAGE == 2
	IF LOCAL:27
		LOCAL:27 = 0
		POINTS += 2
	ELSE
		IF LOCAL:22
			LOCAL:22 = 0
			POINTS += 6
		ENDIF
		LOCAL:27 = 1
		POINTS -= 2
	ENDIF
;풀어내기 확률 DOWN
ELSEIF RESULT == 18 && (LOCAL:28 || POINTS >= 2 || LOCAL:23) && NOWPAGE == 2
	IF LOCAL:28
		LOCAL:28 = 0
		POINTS += 2
	ELSE
		IF LOCAL:23
			LOCAL:23 = 0
			POINTS += 6
		ENDIF
		LOCAL:28 = 1
		POINTS -= 2
	ENDIF
;임신 확률 증가
ELSEIF RESULT == 19&& (LOCAL:29 || POINTS >= 3) && NOWPAGE == 2
	IF LOCAL:29
		LOCAL:29 = 0
		POINTS += 3
	ELSE
		LOCAL:29 = 1
		POINTS -= 3
	ENDIF
;각인에 의한 기죽음 확률 2배
ELSEIF RESULT == 20 && (LOCAL:30 || POINTS >= 2 || LOCAL:25) && NOWPAGE == 2
	IF LOCAL:30
		LOCAL:30 = 0
		POINTS += 2
	ELSE
		IF LOCAL:25
			LOCAL:25 = 0
			POINTS += 8
		ENDIF
		LOCAL:30 = 1
		POINTS -= 2
	ENDIF
ELSEIF RESULT == 0 && NOWPAGE == 3
	LOCAL:31 = 0
ELSEIF RESULT == 21 && NOWPAGE == 3
	IF LOCAL:31
		LOCAL:31 = 0
	ELSE
		LOCAL:31 = 1
	ENDIF
;앞 페이지로
ELSEIF RESULT == 100 && NOWPAGE > 1
	NOWPAGE --
	GOTO MASTER_LOOP
;뒷 페이지로
ELSEIF RESULT == 200 && NOWPAGE < PAGEMAX
	NOWPAGE ++
	GOTO MASTER_LOOP
;인계를 개시한다
ELSEIF RESULT == 999
	IF LOCAL:10 == 0
		PRINTL 현재 인계 인원이 0인으로 되어 있습니다.
		PRINTL 캐릭터를 인계받지 않고 다음 회차를 시작해도 괜찮겠습니까？
		PRINTL  [0] 네
		PRINTL  [1] 아니오
		$INPUT_LOOP_5
		INPUT
		IF RESULT == 0
		ELSEIF RESULT == 1
			CALL LB
			GOTO MASTER_LOOP
		ELSE
			CLEARLINE 1
			GOTO INPUT_LOOP_5
		ENDIF
	ENDIF

	;게임 모드 선택
	CALL LB
	PRINTL 다음 회차를 시작함에 앞서, 게임 모드를 다시 선택하실 수가 있습니다.
	PRINTW 
	CALL MODE_SELECT(1, LOCAL:10)
	SIF RESULT == 999
		GOTO MASTER_LOOP
	;플래그 초기화 처리
	DAY = 0
	DAY:1 = 0
	TIME = 0
	FLAG:250 = 0
	FLAG:251 = 0
	FLAG:252 = 0
	FLAG:253 = 0
	FLAG:254 = 0
	FLAG:255 = 0	
	FOR LOCAL,1,5
		FLAG:LOCAL = 0
	NEXT
	FOR LOCAL,9,22
		FLAG:LOCAL = 0
	NEXT
	FOR LOCAL,41,64
		SIF (LOCAL:15 != 5 || LOCAL < 50 || LOCAL > 53) && (LOCAL:18 == 0 || LOCAL != 54)
			FLAG:LOCAL = 0
	NEXT
	FOR LOCAL,65,112
		FLAG:LOCAL = 0
	NEXT
	FOR LOCAL,300,800
		FLAG:LOCAL = 0
	NEXT
	FOR LOCAL,900,999
		FLAG:LOCAL = 0
	NEXT
	;주회 요소 플래그를 세운다
	FOR LOCAL,21,31
		;제한 해제
		FLAG:(880+LOCAL) = LOCAL:LOCAL
	NEXT
	SIF FLAG:906
		SETBIT FLAG:0,OPTION_제한시간없음

	;단련 시설 레벨
	IF LOCAL:15 != 5
		FLAG:50 = 1
		FLAG:51 = 1
		FLAG:52 = 0
		FLAG:53 = 0
	ENDIF
	;보스 촉수, 최종 보스 촉수 총 마릿수
	CALL GET_BOSS_ERB_NUM
	FLAG:3 = RESULT
	;Ｋ촉수만 카운트
	FLAG:4 = 1

	;보스 촉수, 최종 보스 촉수 플래그 설정
	REPEAT FLAG:3
		SETBIT FLAG:100, COUNT
	REND
	FLAG:101 = 0

	;인계 개시
	FOR CCOUNT, 0, CHARANUM
		SIF CCOUNT == MASTER
			CONTINUE
		SIF CHARANUM - CCOUNT == 0
			BREAK

		IF CFLAG:CCOUNT:0 == 999
			TALENT:CCOUNT:초기경험설정불가 = 1
			;성(性)성장 인계하지 않는다
			IF LOCAL:11 == 0
				FOR LOCAL,0,25
					ABL:CCOUNT:LOCAL = CSVABL_F(NO:CCOUNT,LOCAL)
				NEXT
				FOR LOCAL,10,58
					EXP:CCOUNT:LOCAL = CSVEXP_F(NO:CCOUNT,LOCAL)
				NEXT
				FOR LOCAL,0,5
					MARK:CCOUNT:LOCAL = CSVMARK_F(NO:CCOUNT,LOCAL)
				NEXT
				FOR LOCAL,90,95
					MARK:CCOUNT:LOCAL = CSVMARK_F(NO:CCOUNT,LOCAL)
				NEXT
				FOR LOCAL,30,33
					CFLAG:CCOUNT:LOCAL = CSVCFLAG_F(NO:CCOUNT,LOCAL)
				NEXT
				FOR LOCAL,35,40
					CFLAG:CCOUNT:LOCAL = CSVCFLAG_F(NO:CCOUNT,LOCAL)
				NEXT
				FOR LOCAL,200,239
					CFLAG:CCOUNT:LOCAL = CSVCFLAG_F(NO:CCOUNT,LOCAL)
				NEXT
				;구상용 CFLAG는 성경험에 관련된 경우가 많기 때문에 여기서 초기화
				FOR LOCAL,250,999
					CFLAG:CCOUNT:LOCAL = CSVCFLAG_F(NO:CCOUNT,LOCAL)
				NEXT
				TALENT:CCOUNT:모유체질 = CSVTALENT_F(NO:CCOUNT,1)
				TALENT:CCOUNT:오줌싸개버릇 = CSVTALENT_F(NO:CCOUNT,2)
				TALENT:CCOUNT:촉수의포로 = CSVTALENT_F(NO:CCOUNT,150)
				TALENT:CCOUNT:음란 = CSVTALENT_F(NO:CCOUNT,151)
				TALENT:CCOUNT:붕괴 = CSVTALENT_F(NO:CCOUNT,152)
				TALENT:CCOUNT:음핵 = CSVTALENT_F(NO:CCOUNT,153)
				TALENT:CCOUNT:음호 = CSVTALENT_F(NO:CCOUNT,154)
				TALENT:CCOUNT:음고 = CSVTALENT_F(NO:CCOUNT,155)
				TALENT:CCOUNT:음유 = CSVTALENT_F(NO:CCOUNT,156)
				TALENT:CCOUNT:임신 = CSVTALENT_F(NO:CCOUNT,159)
				TALENT:CCOUNT:기생 = CSVTALENT_F(NO:CCOUNT,160)
				TALENT:CCOUNT:묘상화 = CSVTALENT_F(NO:CCOUNT,161)
				TALENT:CCOUNT:전락 = CSVTALENT_F(NO:CCOUNT,162)
				TALENT:CCOUNT:공생 = CSVTALENT_F(NO:CCOUNT,163)
				TALENT:CCOUNT:배란이상 = CSVTALENT_F(NO:CCOUNT,164)
				;고유 캐릭터라면 CSV에 맞춘다
				IF TALENT:CCOUNT:고유캐릭터 > 0
					TALENT:CCOUNT:미성숙 = CSVTALENT_F(NO:CCOUNT,4)
					TALENT:CCOUNT:후타나리 = CSVTALENT_F(NO:CCOUNT,300)
				;고유 캐릭터 이외의 경우
				ELSE
					SIF TALENT:CCOUNT:후타나리 != 3 && TALENT:CCOUNT:후타나리 != -3
						TALENT:CCOUNT:후타나리 = CSVTALENT_F(NO:CCOUNT,300)
				ENDIF
				;사정과 모유의 최대치는 사정중독과 모유중독으로 변동한다
				MAXBASE:CCOUNT:사정 = CSVBASE_F(NO:CCOUNT, 20)
				MAXBASE:CCOUNT:모유 = CSVBASE_F(NO:CCOUNT, 21)
				;촉수혈 등으로 변화된 민감・둔감을 원래대로 되돌린다
				SIF TALENT:CCOUNT:Ｖ민감 > 9
					TALENT:CCOUNT:Ｖ민감 -= 10
				SIF TALENT:CCOUNT:Ａ민감 > 9
					TALENT:CCOUNT:Ａ민감 -= 10
				SIF TALENT:CCOUNT:Ｂ민감 > 9
					TALENT:CCOUNT:Ｂ민감 -= 10
				SIF TALENT:CCOUNT:Ｃ민감 > 9
					TALENT:CCOUNT:Ｃ민감 -= 10
				SIF TALENT:CCOUNT:Ｖ둔감 < -8
					TALENT:CCOUNT:Ｖ둔감 += 10
				SIF TALENT:CCOUNT:Ａ둔감 < -8
					TALENT:CCOUNT:Ａ둔감 += 10
				SIF TALENT:CCOUNT:Ｂ둔감 < -8
					TALENT:CCOUNT:Ｂ둔감 += 10
				SIF TALENT:CCOUNT:Ｃ둔감 < -8
					TALENT:CCOUNT:Ｃ둔감 += 10

				;처녀를 빼앗긴 경우에는 처녀로 돌려낸다
				SIF TALENT:CCOUNT:처녀 == -1
					TALENT:CCOUNT:처녀 = 1
				;청순파가 없어진 경우에는 청순파로 되돌린다
				SIF TALENT:CCOUNT:청순파 == -1
					TALENT:CCOUNT:청순파 = 1
				;변신 시 비처녀를 소거
				TALENT:CCOUNT:변신시비처녀 = 0

				;소질 기본값의 읽어들이기
				IF BASE:CCOUNT:체격기본값 < 0
					TALENT:CCOUNT:왜소함 = 1
				ELSEIF BASE:CCOUNT:체격기본값 > 0
					TALENT:CCOUNT:장신 = 1
				ENDIF
				IF BASE:CCOUNT:가슴사이즈기본값 == -2
					TALENT:CCOUNT:빈유 = 2
				ELSEIF BASE:CCOUNT:가슴사이즈기본값 == -1
					TALENT:CCOUNT:빈유 = 1
				ELSEIF BASE:CCOUNT:가슴사이즈기본값 == 3
					TALENT:CCOUNT:거유 = 3
				ELSEIF BASE:CCOUNT:가슴사이즈기본값 == 2
					TALENT:CCOUNT:거유 = 2
				ELSEIF BASE:CCOUNT:가슴사이즈기본값 == 1
					TALENT:CCOUNT:거유 = 1
				ENDIF
				TALENT:CCOUNT:변신시체격변동 = MAXBASE:CCOUNT:체격기본값
				TALENT:CCOUNT:변신시가슴사이즈변동 = MAXBASE:CCOUNT:가슴사이즈기본값
				TALENT:CCOUNT:외관 = BASE:CCOUNT:외관기본값
				TALENT:CCOUNT:변신시외관 = MAXBASE:CCOUNT:외관기본값

				SIF BASE:CCOUNT:성별기본값 == 2
					TALENT:CCOUNT:후타나리 = 3
				SIF MAXBASE:CCOUNT:성별기본값 == 2
					TALENT:CCOUNT:변신시후타나리 = 3

				SIF BASE:CCOUNT:성별기본값 == 3
					TALENT:CCOUNT:오토코노코 = 1
				SIF MAXBASE:CCOUNT:성별기본값 == 3
					TALENT:CCOUNT:변신시오토코노코 = 1

				;ＴＳ관련 처리
				;♂/＊⇒♀/＊　원래의 성별이 여체화되어 있는 경우
				IF TALENT:CCOUNT:성별변화 % 10 == 1
					;원래의 성별을 남자로 되돌린다
					TALENT:CCOUNT:남자 = 1
					TALENT:CCOUNT:처녀 = 0
					TALENT:CCOUNT:모유체질 = 0
					TALENT:CCOUNT:백보지 = 0
					TALENT:CCOUNT:Ｖ민감 = 0
					TALENT:CCOUNT:Ｖ둔감 = 0
					TALENT:CCOUNT:젖기쉬움 = 0
					TALENT:CCOUNT:젖기어려움 = 0
					TALENT:CCOUNT:묘상화 = 0
					TALENT:CCOUNT:후타나리 = 0
					SIF TALENT:CCOUNT:교제상대 == 2 || TALENT:CCOUNT:교제상대 == 4 || TALENT:CCOUNT:교제상대 == 5
						TALENT:CCOUNT:교제상대 = 0
					TALENT:CCOUNT:악세서리 = 0
					TALENT:CCOUNT:외관 = 0
					;프로필 재설정
					CALL GENERATE_CHAR_SIZE, CCOUNT, 0
					BASE:CCOUNT:신장 = RESULT:2
					BASE:CCOUNT:체중 = RESULT:3
					BASE:CCOUNT:가슴둘레 = RESULT:4
					BASE:CCOUNT:허리둘레 = RESULT:5
					BASE:CCOUNT:골반둘레 = RESULT:6
					BASE:CCOUNT:가슴무게 = RESULT:7
				ENDIF
				;＊/♂⇒＊/♀　변신 시의 성별이 여체화되어 있는 경우
				IF TALENT:CCOUNT:성별변화 / 10 == 1
					;프로필 재설정
					CALL GENERATE_CHAR_SIZE, CCOUNT, 1
					MAXBASE:CCOUNT:신장 = RESULT:2
					MAXBASE:CCOUNT:체중 = RESULT:3
					MAXBASE:CCOUNT:가슴둘레 = RESULT:4
					MAXBASE:CCOUNT:허리둘레 = RESULT:5
					MAXBASE:CCOUNT:골반둘레 = RESULT:6
					MAXBASE:CCOUNT:가슴무게 = RESULT:7
				ENDIF
				;변신시ＴＳ의 부활
				SIF TALENT:CCOUNT:변신시ＴＳ == -1
					TALENT:CCOUNT:변신시ＴＳ = 1
				;성별변화 정보를 리셋
				TALENT:CCOUNT:성별변화 = 0
				SIF TALENT:CCOUNT:여체수용 > 0
					SWAP TALENT:남성기피,TALENT:여성기피
				TALENT:CCOUNT:여체수용 = 0
				;초기 경험 설정을 허가한다
				TALENT:CCOUNT:초기경험설정불가 = 0
			ENDIF
			;레벨 인계하지 않는다
			IF LOCAL:13 == 0
				ABL:CCOUNT:레벨 = CSVABL_F(NO:CCOUNT,50)
				;연줄(커넥션)
				FOR LOCAL,120,124
					CFLAG:CCOUNT:LOCAL = CSVCFLAG_F(NO:CCOUNT,LOCAL)
				NEXT
			ENDIF
			;전투기술 인계하지 않는다
			IF LOCAL:14 == 0
				FOR LOCAL,30,33
					ABL:CCOUNT:LOCAL = CSVABL_F(NO:CCOUNT,LOCAL)
				NEXT
				FOR LOCAL,5,8
					EXP:CCOUNT:LOCAL = CSVEXP_F(NO:CCOUNT,LOCAL)
				NEXT
			;전투 기술이 5 이상일 경우 레벨과 전투 경험을 5에 맞춘다
			ELSE 
				FOR LOCAL,30,33
					IF ABL:CCOUNT:LOCAL > 4
						ABL:CCOUNT:LOCAL = 5
						EXP:CCOUNT:(LOCAL - 25) = 300
					ENDIF
				NEXT
			ENDIF
			;의상 인계하지 않는다
			IF LOCAL:19 == 0
				FOR LOCAL,40,44
					CFLAG:CCOUNT:LOCAL = CSVCFLAG_F(NO:CCOUNT,LOCAL)
				NEXT
				FOR LOCAL,600,700
					;EQUIP:CCOUNT:LOCAL = 0
					EQUIP:CCOUNT:LOCAL = CSVEQUIP_F(NO:CCOUNT,LOCAL)
				NEXT
			;의상을 인계하는 경우 장비가 없다면 -1로 해 둔다
			ELSE
				SIF CFLAG:CCOUNT:40 == 0
					CFLAG:CCOUNT:40 = -1
				SIF CFLAG:CCOUNT:41 == 0
					CFLAG:CCOUNT:41 = -1
				SIF CFLAG:CCOUNT:42 == 0
					CFLAG:CCOUNT:42 = -1
			ENDIF
			;매료경험 인계하지 않는다
			IF LOCAL:20 == 0
				EXP:CCOUNT:58 = CSVCFLAG_F(NO:CCOUNT,58)
			;매료경험를 인계할 경우, 100을 초과한 분은 25%만 인계
			ELSEIF EXP:CCOUNT:58 > 100
				EXP:CCOUNT:58 = 100 + (EXP:CCOUNT:58 - 100) / 4
			ENDIF

			;반드시 초기화하는 항목
			FOR LOCAL,0,53
				;프로필은 초기화하지 않는다
				SIF LOCAL >= 40 && LOCAL <48
					CONTINUE
				;모유와 사정은 성(性)성장 인계 쪽에서 처리한다
				SIF LOCAL == 20 || LOCAL == 21
					CONTINUE
				BASE:CCOUNT:LOCAL = CSVBASE_F(NO:CCOUNT,LOCAL)
				MAXBASE:CCOUNT:LOCAL = BASE:CCOUNT:LOCAL
			NEXT
			FOR LOCAL,0,3
				EXP:CCOUNT:LOCAL = CSVEXP_F(NO:CCOUNT,LOCAL)
			NEXT
			FOR LOCAL,0,51
				JUEL:CCOUNT:LOCAL = CSVJUEL_F(NO:CCOUNT,LOCAL)
			NEXT
			CFLAG:CCOUNT:0 = CSVCFLAG_F(NO:CCOUNT,0)
			FOR LOCAL,20,32
				CFLAG:CCOUNT:LOCAL = CSVCFLAG_F(NO:CCOUNT,LOCAL)
			NEXT
			FOR LOCAL,99,119
				CFLAG:CCOUNT:LOCAL = CSVCFLAG_F(NO:CCOUNT,LOCAL)
			NEXT
			FOR LOCAL,90,95
				MARK:CCOUNT:LOCAL = CSVMARK_F(NO:CCOUNT,LOCAL)
			NEXT
			CFLAG:CCOUNT:999 = 0
			;딸의 경우, 기초치에 수정을 가한다
			IF CFLAG:CCOUNT:231
				BASE:CCOUNT:0 += CFLAG:CCOUNT:60
				BASE:CCOUNT:1 += CFLAG:CCOUNT:61
				BASE:CCOUNT:2 += CFLAG:CCOUNT:62
				BASE:CCOUNT:50 += CFLAG:CCOUNT:60
				BASE:CCOUNT:51 += CFLAG:CCOUNT:61
				BASE:CCOUNT:52 += CFLAG:CCOUNT:62
				BASE:CCOUNT:10 += CFLAG:CCOUNT:63
				BASE:CCOUNT:11 += CFLAG:CCOUNT:64
				BASE:CCOUNT:12 += CFLAG:CCOUNT:65
				BASE:CCOUNT:13 += CFLAG:CCOUNT:66
			ENDIF
			;범용 캐릭터 체력 등의 BASE 값 계산
			IF NO:CCOUNT == 0
				CALL SEIKAKU_CHECK,"GET_TALENT_VALUE",CCOUNT
				LOCAL = RESULT
				LOCAL:1 = 0
				LOCAL:2 = 0
				FOR LOCAL:3, 0, 7
					LOCAL:1 = LOCAL:3
					SIF LOCAL:3 > 2
						LOCAL:1 = LOCAL:3 + 7
					LOCAL:2 = BASE:CCOUNT:(LOCAL:1)
					;성격 보정
					BASE:CCOUNT:(LOCAL:1) = SEIKAKU_HOSEI_F(LOCAL, LOCAL:1, LOCAL:2)
					MAXBASE:CCOUNT:(LOCAL:1) = BASE:CCOUNT:(LOCAL:1)
				NEXT
			ENDIF
			;변신 중인 경우, 변신을 해제한다
			CALL TRANSFORM, 0, CCOUNT

			;사정 게이지, 모유 게이지 초기화
			BASE:CCOUNT:사정 = 0
			BASE:CCOUNT:모유 = 0
		ELSE
			;소거할 예정인 캐릭터를 맨 뒤로 가져간다
			FOR CCOUNT:1, CCOUNT, CHARANUM - 1
				SIF CCOUNT:1 == MASTER
					CONTINUE
				SWAPCHARA CCOUNT:1, CCOUNT:1 + 1
				;RELATION의 교체
				FOR CCOUNT:2, 0, CHARANUM
					SIF LOCAL == MASTER
						CONTINUE
					SWAP RELATION:(CCOUNT:2):(CCOUNT:1), RELATION:(CCOUNT:2):(CCOUNT:1 + 1)
				NEXT
			NEXT
			;소거할 예정인 캐릭터의 RELATION을 양쪽 모두 초기화
			FOR CCOUNT:1, 0, CHARANUM
				SIF CCOUNT:1 == MASTER
					CONTINUE
				RELATION:(CHARANUM - 1):(CCOUNT:1) = 0
				RELATION:(CCOUNT:1):(CHARANUM - 1) = 0
			NEXT
			;소거 처리
			DELCHARA CHARANUM - 1
			CCOUNT --
			CONTINUE
		ENDIF
	NEXT
	;자금 인계
	IF LOCAL:15 == 5
		MONEY = MONEY - LMONEY + 1000
	ELSE
		MONEY = MONEY * 25 * LOCAL:15 / 100 + 1000
	ENDIF
	;초기 방위력 증가
	FLAG:852 = 5000 + LOCAL:17 * 500
	;촉수의 조각 인계 없음
	IF LOCAL:18 == 0
		FLAG:200 = 0
		FLAG:54 = 0
	ENDIF
	;의상 인계 없음
	IF LOCAL:19 == 0
		FOR LOCAL,101,200
			ITEM:LOCAL = 0
		NEXT
		FOR LOCAL,301,400
			ITEM:LOCAL = 0
		NEXT
		FOR LOCAL,401,500
			ITEM:LOCAL = 0
		NEXT
		FOR LOCAL,500,600
			ITEM:LOCAL = 0
		NEXT
		FOR LOCAL,600,700
			ITEM:LOCAL = 0
		NEXT
	ENDIF
	;인기도 초기화가 있을 경우
	SIF LOCAL:31 == 1
		FLAG:853 = 0
	;각종 주회차 특전 플래그 처리
	FOR LCOUNT, 21,31
		IF LOCAL:LCOUNT > 0
			FLAG:(880+LCOUNT) = 1
		ELSE
			FLAG:(880+LCOUNT) = 0
		ENDIF
	NEXT

	IF GAME_OPTION_CHECK_F(OPTION_solo)
		LOCAL = 1
	ELSE
		;인계받은 인원수를 LOCAL에 대입
		LOCAL = LOCAL:10
		;인계 인원이 3인 미만이면 3인을 기본값으로 한다
		SIF LOCAL < 3
			LOCAL = 3
		IF (GLOBAL:100 + GLOBAL:101 + GLOBAL:102) > 0 && LOCAL < 7
			PRINTL 
			PRINTFORML 주회차 클리어 보너스：
			PRINTFORML 게임 시작 시의 인원수를 늘리거나 줄일 수 있습니다.
			SIF LOCAL:10 < 3
				PRINTFORML [-1] １명 줄인다　　　　　　　　　 （{LOCAL - 1}인으로 시작）
			PRINTFORML [ 0] 기본 인원수　　　　　　　　　 （{LOCAL}인으로 시작）
			SIF LOCAL:10 < 6
				PRINTFORML [ 1] １명 늘린다　　　　　　　　　 （{LOCAL + 1}인으로 시작）
			SIF LOCAL:10 < 5
				PRINTFORML [ 2] ２명 늘린다　　　　　　　　　 （{LOCAL + 2}인으로 시작）
			SIF LOCAL:10 < 4
				PRINTFORML [ 3] ３명 늘린다　　　　　　　　　 （{LOCAL + 3}인으로 시작）
			$INPUT_LOOP_CHARA_NUM
			INPUT
			IF RESULT >= 0 && RESULT <= 3 && LOCAL:10 + RESULT < 7
				LOCAL = LOCAL + RESULT
				PRINTFORML {LOCAL}인으로 시작합니다.
			ELSEIF RESULT == -1 && LOCAL:10 < 3
				LOCAL = 2
				PRINTFORML {LOCAL}인으로 시작합니다.
			ELSE
				GOTO INPUT_LOOP_CHARA_NUM
			ENDIF
		ENDIF
	ENDIF
	LOCAL = LOCAL - LOCAL:10
	IF LOCAL > 0
		PRINTFORMW 신규 캐릭터 {LOCAL}인의 작성을 마쳤습니다.
		REPEAT LOCAL
			ADDCHARA 0
			FLAG:8 ++
		REND
	ENDIF
	PRINTL 

	;제한 일수 설정
	CALL SET_LIMIT_DAY

	;수련Ｐ 처리
	FOR CCOUNT, 0, CHARANUM
		SIF CCOUNT == MASTER
			CONTINUE
		;사용한 보너스만큼 뺄셈
		JUEL:CCOUNT:수련P -= CFLAG:CCOUNT:50 * 10
		JUEL:CCOUNT:수련P -= CFLAG:CCOUNT:51 * 10
		JUEL:CCOUNT:수련P -= CFLAG:CCOUNT:52 * 10
		JUEL:CCOUNT:수련P -= CFLAG:CCOUNT:53 * 10
		JUEL:CCOUNT:수련P -= CFLAG:CCOUNT:54 * 10
		JUEL:CCOUNT:수련P -= CFLAG:CCOUNT:55 * 10
		JUEL:CCOUNT:수련P -= CFLAG:CCOUNT:56 * 10
		SIF TALENT:CCOUNT:근거리특기
			JUEL:CCOUNT:수련P -= 150
		SIF TALENT:CCOUNT:중거리특기
			JUEL:CCOUNT:수련P -= 150
		SIF TALENT:CCOUNT:원거리특기
			JUEL:CCOUNT:수련P -= 150
		SIF TALENT:CCOUNT:공중전특기
			JUEL:CCOUNT:수련P -= 150
		SIF TALENT:CCOUNT:회복빠름
			JUEL:CCOUNT:수련P -= 150
		SIF TALENT:CCOUNT:Ｃ결계
			JUEL:CCOUNT:수련P -= 50
		SIF TALENT:CCOUNT:Ｖ결계
			JUEL:CCOUNT:수련P -= 50
		SIF TALENT:CCOUNT:Ａ결계
			JUEL:CCOUNT:수련P -= 50
		SIF TALENT:CCOUNT:Ｂ결계
			JUEL:CCOUNT:수련P -= 50
		SIF TALENT:CCOUNT:피임결계
			JUEL:CCOUNT:수련P -= 50
		SIF TALENT:CCOUNT:소문을좋아하는친구
			JUEL:CCOUNT:수련P -= 50
		SIF TALENT:CCOUNT:경찰관계자 == 1
			JUEL:CCOUNT:수련P -= 100
		SIF TALENT:CCOUNT:경찰관계자 == 2
			JUEL:CCOUNT:수련P -= 200
		SIF TALENT:CCOUNT:정보원
			JUEL:CCOUNT:수련P -= 150
		SIF TALENT:CCOUNT:유복한친정
			JUEL:CCOUNT:수련P -= 100

		SIF TALENT:CCOUNT:근거리서툼
			JUEL:CCOUNT:수련P += 50
		SIF TALENT:CCOUNT:중거리서툼
			JUEL:CCOUNT:수련P += 50
		SIF TALENT:CCOUNT:원거리서툼
			JUEL:CCOUNT:수련P += 50
		SIF TALENT:CCOUNT:공중전서툼
			JUEL:CCOUNT:수련P += 50
		SIF TALENT:CCOUNT:회복느림
			JUEL:CCOUNT:수련P += 50


		SIF JUEL:CCOUNT:수련P < 0
			JUEL:CCOUNT:수련P = 0

		JUEL:CCOUNT:수련P += LOCAL:16 * 10
	NEXT

	;TARGET을 존재하는 캐릭터로 설정
	FOR LOCAL,0,CHARANUM
		SIF LOCAL == MASTER
			CONTINUE
		TARGET = LOCAL
	NEXT

	;캐릭터 메이킹으로 날아간다
	CALL CHARA_MAKE_MAIN, LOCAL:16
	PRINTL 

	;전용 구상의 번호를 설정, 결계 내구력을 완쾌
	FOR CCOUNT, 0, CHARANUM
		SIF CCOUNT == MASTER
			CONTINUE
		SIF CFLAG:CCOUNT:6 == 0 || CFLAG:CCOUNT:6 >= 100
			CFLAG:CCOUNT:6 = NO:CCOUNT
		;부위 결계 설정
		CALL BASEUP_CAL_SHIELD, CCOUNT
	NEXT

	;구상의 초기 설정 호출 등
	FOR LOCAL,0,CHARANUM
		SIF LOCAL == MASTER
			CONTINUE

		TARGET = LOCAL
		SIF CFLAG:0 != 상태_건강 && CFLAG:20 == 0 && CFLAG:21 == 0
			CFLAG:21 = RAND:7 + 1

		CALL MESSAGE_FIRST

		SIF LOCAL <= 파티인원최대치
			CFLAG:999 = 1
	NEXT
	;초기 세트 캐릭터, CSV 캐릭터용 의상 획득 처리
	FOR LOCAL, 0, CHARANUM -1
		SIF ITEM:(CFLAG:(LOCAL+1):40) == 0
			ITEM:(CFLAG:(LOCAL+1):40) = 1
		SIF ITEM:(CFLAG:(LOCAL+1):41) == 0
			ITEM:(CFLAG:(LOCAL+1):41) = 1
		SIF ITEM:(CFLAG:(LOCAL+1):42) == 0
			ITEM:(CFLAG:(LOCAL+1):42) = 1

		SIF ITEM:(CFLAG:(LOCAL+1):43) == 0
			ITEM:(CFLAG:(LOCAL+1):43) = 1
		FOR LOCAL:1, 600, 700
			SIF EQUIP:(LOCAL+1):(LOCAL:1)
				ITEM:(LOCAL:1) = 1
		NEXT
	NEXT

	;컨피그 프리셋 설정
	CALL HEROINE_PRESET

	;방위력 감쇠(減衰) 회피
	FLAG:41 = 1
	;탐색 할당량 설정
	CALL RESEARCH_QUOTA

	;플래시 뉴스용 문자열 초기화
	SAVESTR:20 =
	SAVESTR:21 =
	SAVESTR:22 =
	SAVESTR:23 =
	SAVESTR:24 =
	SAVESTR:25 =
	SAVESTR:26 =

	FLAG:64 = -1
	BEGIN SHOP
ELSE
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF
CLEARLINE LINECOUNT - NOWLINECOUNT
GOTO MASTER_LOOP












