




;**********************************************


@SHOW_SHOP
#DIM CCOUNT
#DIM 일괄일시TARGET = 0,0




printl
printl
printl
printl
printl
printl
printl

;캐릭터 총 인원수가 3인 이상일 때, CHARANUM_ACTIVE가 기존의 TARGET을 덮어쓰는 현상이 발생하여 임시로 막았습니다.
;TARGET=MIN(TARGET,CHARANUM_ACTIVE())

SIF CHARANUM_ACTIVE()==1
	FLAG:9 = 0

;일괄일시TARGET:0 랜덤으로 뽑힌 건강한 TARGET
;일괄일시TARGET:1 본래의 TARGET
IF FLAG:9 && CHARANUM_ACTIVE() > 0
	$HITOKUCHI_LOOP
	일괄일시TARGET:0 = RAND:(CHARANUM -1) + 1
	SIF CFLAG:(일괄일시TARGET:0):0 != 상태_건강 || CFLAG:(일괄일시TARGET:0):999 == 0
		GOTO HITOKUCHI_LOOP
	일괄일시TARGET:1 = TARGET
ENDIF

;인기도 상한과 하한을 설정
FLAG:853 = LIMIT(FLAG:853, -100, 100)
DRAWLINE
;플래시 뉴스 갱신
CALL FLASHNEWS
FLAG:60 = 0

;도중 로드용
FLAG:799 = 0

;살아 남은 보스의 수를 본다
CALL TENTACLE_SURVIVE, "NUM"
LOCAL = RESULT

IF TIME == 0
	LOCALS = 낮
	IF FLAG:999 == 1
		SETBGCOLOR 0,0,40
	ELSE
		SETBGCOLOR 10,10,0
	ENDIF
ELSEIF TIME == 1
	LOCALS = 밤
	IF FLAG:999 == 1
		SETBGCOLOR 0,0,40
	ELSE
		SETBGCOLOR 0,0,5
	ENDIF
ENDIF




;**********************************************************
;여기서부터 SHOP 화면 표시 부분

DRAWLINE
PRINTFORM INTERMISSION {DAY}일 째　　
FONTBOLD
SELECTCASE DAY % 7
CASE 0
	PRINT (日)
CASE 1
	PRINT (月)
CASE 2
	PRINT (火)
CASE 3
	PRINT (水)
CASE 4
	PRINT (木)
CASE 5
	PRINT (金)
CASE 6
	PRINT (土)
ENDSELECT
FONTREGULAR
IF TIME == 0
	SETCOLOR 230,100,0
ELSE
	SETCOLOR 0,30,230
ENDIF
PRINTFORM  %LOCALS%
RESETCOLOR
;일수 제한 해제 효과 (주회 인계 or FREEPLAY 모드)
IF CHECK_GAMEOVER_F() == 1
	PRINTL 
ELSEIF GAME_OPTION_CHECK_F(OPTION_제한시간없음)
	PRINTFORML （완전 섬멸까지 ∞일）
ELSEIF ENEMY_TYPE_CHECK_F("BOSS") == 1
	IF GAME_OPTION_CHECK_F(OPTION_endless)
		PRINTFORML （{FLAG:3 - LOCAL + 1}번째 적 섬멸까지 {(FLAG:3 - LOCAL + 1) * FLAG:2 - DAY + DAY:1}일 / 완전 섬멸까지 ∞일）
	ELSE
		PRINTFORML （{FLAG:3 - LOCAL + 1}번째 적 섬멸까지 {(FLAG:3 - LOCAL + 1) * FLAG:2 - DAY + DAY:1}일 / 완전 섬멸까지 {FLAG:1 - DAY + DAY:1}일）
	ENDIF
;최종 보스 출현 후
ELSEIF GET_LASTBOSS_PHASE_F() >= 1
	IF GAME_OPTION_CHECK_F(OPTION_endless)
		PRINTFORML （완전 섬멸까지 ∞일）
	ELSE
		PRINTFORML （완전 섬멸까지 {FLAG:1 - DAY + DAY:1}일）
	ENDIF
ENDIF



;자금
PRINTFORM 자금：{MONEY}＄　
;방위력
IF FLAG:852 == 0
	SETCOLOR 120,0,0
ELSEIF FLAG:852 < 1250
	SETCOLOR 250,0,0
ELSEIF FLAG:852 < 2500
	SETCOLOR 250,250,100
ELSEIF FLAG:852 < 10000
	SETCOLOR 0,250,60
ELSEIF FLAG:852 < 20000
	SETCOLOR 0,200,255
ELSE
	SETCOLOR 200,0,255
ENDIF
PRINTFORM 방위력：{FLAG:852}　　
;인기도
IF FLAG:853 <= -75
	SETCOLOR 180,0,0
ELSEIF FLAG:853 <= -50
	SETCOLOR 250,50,50
ELSEIF FLAG:853 < 0
	SETCOLOR 250,200,200
ELSEIF FLAG:853 < 50
	SETCOLOR 200,250,200
ELSEIF FLAG:853 < 75
	SETCOLOR 100,250,150
ELSE
	SETCOLOR 100,250,200
ENDIF
PRINTFORM 인기도：{FLAG:853}
PRINTL 

;탐색 상황에 따라 대응 커맨드로 색상 변경
IF FLAG:47 >= FLAG:46
	SETCOLOR 255,125,125
ELSE
	SETCOLOR 255,180,0
ENDIF
DRAWLINE
PRINTFORML 【탐색 상황】
PRINTFORM 　
;정보가 부족할 시
IF FLAG:47 < FLAG:46
	PRINTFORM 탐색도 {PERCENT_CAL_F(FLAG:47, FLAG:46)}％ ({FLAG:47}/{FLAG:46})
;정보 수집이 끝났을 시
ELSE
	IF FLAG:100 == 0
		PRINTFORM 　　최종 보스 촉수와 교전 중!
	ELSE
		PRINTFORM 　　보스 촉수의 흔적 확인!
	ENDIF
	IF FLAG:47 >= FLAG:46
		IF TIME == 0
			LOCAL:1 = 40
		ELSEIF TIME == 1
			LOCAL:1 = 50
		ENDIF
		IF GET_LASTBOSS_PHASE_F() >= 1
			LOCAL:1 += 50
		ELSE
			LOCAL:1 += FLAG:3 * 3
		ENDIF
		LOCAL:1 = LOCAL:1 * FLAG:47 / FLAG:46
		FOR LOCAL,0, CHARANUM
			SIF CONFIG_CHECK_BALANCE_F(1) > 0 && FLAG:18 > 0 && (CFLAG:LOCAL:100 == 예정_출격 || CFLAG:LOCAL:100 == 예정_방위)
				LOCAL:1 += 4
		NEXT
		SIF LOCAL:1 > 95
			LOCAL:1 = 95
		PRINTFORM (포착률 {LOCAL:1}％)　
		SETCOLOR 255,255,225
		;IF CONFIG_CHECK_BALANCE_F(1) > 0 && FLAG:18 > 0
		;	PRINT 追跡対象：
		;	FLAG:11 = FLAG:18
		;	SAVESTR:13 = BOSS
		;	CALL TENTACLE_ACCESS, "GETNAME"
		;	PRINTFORM %RESULTS%
		;ENDIF
	ENDIF

ENDIF
CALL SHOP_PRINT_SITUATION()
RESETCOLOR


;캐릭터의 스테이터스
SIF FLAG:9
	TARGET = 일괄일시TARGET:0

CALL SHOW_STATUS_BASE_SHOP

SIF FLAG:9
	TARGET = 일괄일시TARGET:1

;일괄 설정용
IF FLAG:9 == 1
	LOCAL:1 = 1
	FONTBOLD
	SETCOLOR 255, 150, 0
ELSE
	SETCOLOR 120, 60, 0
ENDIF
IF CHARANUM_SAFE() >= 2 && FLAG:63 == 0
	PRINTFORM [90] 일괄 설정
ELSE
	PRINT 　　　　　　
ENDIF
RESETCOLOR
FONTREGULAR

PRINTPLAIN                          

IF (CHARANUM > 2 && CHARANUM_SAFE()>0) || FLAG:63
	IF FLAG:63 && TARGET != 0
		IF CFLAG:TARGET:999 == 0 && CHARANUM_ACTIVE() < 파티인원최대치
			IF CFLAG:TARGET:0 == 상태_출산직전
				PRINT 편성 불가(특별병동에 입원 중)
			ELSEIF CFLAG:TARGET:0 == 상태_육아중
				PRINT 편성 불가(특별병동에서 육아 중)
			ELSE
				PRINT [100] 파티에 추가한다
			ENDIF
		ELSEIF CFLAG:TARGET:999
			PRINT [100] 파티에서 제외한다
		ENDIF
	ELSE
			PRINT 　　　　　　　　　　　
	ENDIF

	IF FLAG:63
		FONTBOLD
		SETCOLOR 250,180,50
		PRINT [50] 파티 편성을 완료한다　　
	ELSE
		PRINT [50] 파티 편성　　 
	ENDIF
	SIF FLAG:63 == 0
		PRINTPLAIN  
	FONTREGULAR
	RESETCOLOR
ELSE
	PRINTPLAIN                
ENDIF
IF FLAG:63 != 0 || CHARANUM_PLAYABLE()==CHARANUM_SAFE_PARTYCHECK()
	;;편성 중에는 항상 전체 표시이므로 버튼을 꺼내지 않는다.　평소에는 액티브 캐릭터 전원 편성 중이라면 꺼내지 않는다.
ELSEIF CHARANUM_SAFE_PARTYCHECK(0) && FLAG:61
	PRINT     [60] ▲조작 캐릭터만
ELSEIF CHARANUM_SAFE_PARTYCHECK(0)
	PRINT         [60] ▼모두 표시
ENDIF



;;각 캐릭터의 행동 예정 표시
;IF FLAG:63 == 0
;	CALL SHOP_ACTION_PLANDISP(0)
;ELSE
;	PRINTL
;	PRINTL
;ENDIF


;SIF FLAG:63 == 0
	PRINTL 
DRAWLINE


;인포메이션
CALL SHOP_INFOMATION



;지문: SHOP 화면에서의 한 마디 메시지 호출용
SIF FLAG:9
	TARGET = 일괄일시TARGET:0
IF CFLAG:TARGET:0 == 상태_건강 && FLAG:63 == 0 && TARGET != MASTER
	LOCAL:2=LINECOUNT
	CALL MESSAGE_HITOKUTI_SHOP
	REPEAT 3-(LINECOUNT-LOCAL:2)
		PRINTL
	REND
ELSE
	PRINTL
	PRINTL
	PRINTL
ENDIF
SIF FLAG:9
	TARGET = 일괄일시TARGET:1

IF FLAG:63 == 0
	PRINTL [100] 행동 개시
	PRINTL 
ELSE
	IF TARGET != 0
		PRINT 선택 중인 캐릭터：
		SETCOLOR 250,180,50
		PRINTPLAINFORM [{TARGET}] %CALLNAME:TARGET%
		RESETCOLOR
		PRINTL 　⇒　
		PRINTL （위치를 바꿀 대상 캐릭터를 선택해 주십시오.）
		PRINTL （선택 중인 캐릭터를 다시 선택하면 선택을 해제합니다.）
		PRINTL 
	ELSE
		PRINTFORML 선택 중인 캐릭터：없음
		PRINTL （배치를 변경할 캐릭터를 선택해 주십시오.）
		PRINTL 
		PRINTL 
		PRINTL 
		PRINTL 
	ENDIF
	IF TARGET != 0
		IF CFLAG:TARGET:999 == 0 && CHARANUM_ACTIVE() < 파티인원최대치
			PRINTL 
			IF CFLAG:TARGET:0 == 상태_출산직전
				PRINTL 해당 캐릭터는 특별병동에 입원 중이기 때문에, 파티에 추가할 수 없습니다.
			ELSEIF CFLAG:TARGET:0 == 상태_육아중
				PRINTL 해당 캐릭터는 특별병동에서 육아 중이기 때문에, 파티에 추가할 수 없습니다.
				PRINTL
				;PRINTL [100] 선택 중인 캐릭터를 파티에 추가한다
			ENDIF
		ELSEIF CFLAG:TARGET:999
			PRINTL 
			PRINTL
			;PRINTL [100] 선택 중인 캐릭터를 파티에서 제외한다
		ENDIF
	ENDIF
ENDIF
IF CHECK_GAMEOVER_F() == 0 && FLAG:63 == 0
	SETCOLOR 255, 170, 170
	PRINTFORM [101] 출격한다　　　 
	SETCOLOR 255, 255, 170
	PRINTFORM [102] 단련한다　　　 
	SETCOLOR 170, 170, 255
	PRINTFORM [103] 휴식한다　　　 
	SETCOLOR 170, 255, 255
	PRINTFORM [104] 특별 활동　　　 
	PRINTL 
	IF !GAME_OPTION_CHECK_F(OPTION_solo)
		SETCOLOR 255, 170, 255
		PRINTFORM [105] 거점 방위　　　
	ELSE
		SETCOLOR 105,105,105
		PRINTPLAIN [105] 거점 방위　　　
	ENDIF
	IF CHARANUM >= 3
		SETCOLOR 170, 255, 170
		PRINTFORM [106] 전투 지원　　　
	ELSE
		SETCOLOR 105,105,105
		PRINTPLAIN [106] 전투 지원　　　
	ENDIF
	SETCOLOR 255, 200, 50
	PRINTFORM [107] 정보 수집　　　
	SETCOLOR 50, 200, 255
	PRINTFORM [108] 자유 행동
	PRINTL 
ENDIF
RESETCOLOR
PRINTL 
PRINT [110] 스테이터스 표시　
IF CHECK_GAMEOVER_F() == 0 && FLAG:63 == 0
	PRINT [111] 캐릭터 강화    
	PRINT [112] 의상 설정　　　
	PRINTL [113] 메디컬 룸
	PRINTL [114] 커스터마이즈 
	PRINT [120] 의상 구입　　　
ENDIF
SIF FLAG:63 == 0
	PRINT [130] 상황 확인　　　

IF CHECK_GAMEOVER_F() == 0 && FLAG:63 == 0
	PRINT [150] 시설 확장　　　
	;안 될 경우
	IF CFLAG:TARGET:0 != 상태_건강 || TARGET == MASTER || FLAG:9 != 0
		SETCOLOR 70,70,70
		PRINT [160] 스케쥴 설정
		RESETCOLOR
	ELSE
		PRINT [160] 스케쥴 설정
	ENDIF
ENDIF
PRINTL 

;INSTANT 모드라면 은퇴&신인 가입을 가능하게
IF FLAG:63 == 0 && GAME_OPTION_CHECK_F(OPTION_가입은퇴있음)
	PRINT                   　[169] 은퇴자 명부
	PRINT 	    [170] 은퇴시킨다　　
	PRINTL [180] 신인을 가입시킨다   
ENDIF


PRINT [200] 세이브　　　　 
PRINT [300] 로드　　　　　
PRINT [700] 컨피그　　　　
PRINT [800] 트로피 확인　 

;출격 가능 체크
@SORTIE_CHECK
#DIM CCOUNT
LOCAL = 0
FOR CCOUNT, 0, CHARANUM
	SIF CCOUNT == MASTER
		CONTINUE
	SIF (CFLAG:CCOUNT:100 == 예정_출격 && BASE:CCOUNT:체력 > 강제휴식체력) || (CFLAG:CCOUNT:100 == 예정_방위 && BASE:CCOUNT:체력 > 강제휴식체력)
		LOCAL += 1
NEXT
RETURN LOCAL

@USERSHOP
#DIM CCOUNT
IF RESULT == 100 && (TARGET != 0 && FLAG:63 && CFLAG:TARGET:0 == 상태_건강)
	IF CFLAG:TARGET:999 == 0 && CHARANUM_ACTIVE() < 파티인원최대치
		CFLAG:TARGET:999 = 1
	ELSEIF CFLAG:TARGET:999
		CFLAG:TARGET:999 = 0
		CFLAG:TARGET:100 = 예정_휴식
		;SIF CHARANUM_SAFE_PARTYCHECK(0)
		;	FLAG:61 = 1
	ENDIF
	TARGET = 0
ELSEIF RESULT == 100 && FLAG:63 == 0
	CALL SORTIE_CHECK
	IF FLAG:40 == 0 && CHECK_GAMEOVER_F() == 0
		DRAWLINE
		PRINTL 
		PRINTL 각 캐릭터의 행동 예정은 다음과 같습니다.
		PRINTL 
		CALL SHOP_ACTION_PLANDISP(1)
		PRINTL 
		PRINTL 이 설정으로 행동을 개시하시겠습니까?
		PRINTL ※ 행동 예정을 설정하지 않은 캐릭터는 자동으로 휴식합니다.
		CALL SORTIE_CHECK
		IF RESULT == 0 && !GAME_OPTION_CHECK_F(OPTION_solo)
			PRINTL ※ 현재 출격, 방위를 행할 캐릭터가 없어 방위력이 저하합니다.
		ENDIF
		DRAWLINE
		PRINTL [0] 아니오
		PRINTL [1] 네
		PRINTL [9] 네 (다음부터 확인하지 않는다)
		$INPUT_LOOP
		INPUT
		IF RESULT == 9
			FLAG:40 = 1
			CALL SORTIE_CHECK
			;주의를 한 번에 끝낸 경우
			IF RESULT == 0 && !GAME_OPTION_CHECK_F(OPTION_solo)
				FLAG:40 = 2
			ENDIF
			CALL SHOP_ACTION
			ELSEIF RESULT < 0 || RESULT > 9
		ELSEIF RESULT == 0
		ELSEIF RESULT == 1
			CALL SHOP_ACTION
		ENDIF
	;출격 혹은 방위할 수 있는 캐릭터가 없다
	ELSEIF FLAG:40 == 1 && RESULT == 0 && !GAME_OPTION_CHECK_F(OPTION_solo) && CHECK_GAMEOVER_F() == 0
		DRAWLINE
		PRINTL 
		PRINTL 출격・방위를 행할 예정인 캐릭터가 한 명도 없습니다.
		PRINTL 
		CALL SHOP_ACTION_PLANDISP(1)
		PRINTL 
		PRINTL 이대로 행동을 개시하시겠습니까?
		PRINTL ※ 현재 출격, 방위를 행할 캐릭터가 없어 방위력이 저하합니다.
		DRAWLINE
		PRINTL [0] 아니오
		PRINTL [1] 네
		PRINTL [9] 네 (다음부터 확인하지 않는다)
		$INPUT_LOOP_1
		INPUT
		IF RESULT == 9
			FLAG:40 = 2
			CALL SHOP_ACTION
		ELSEIF RESULT < 0 || RESULT > 9
		ELSEIF RESULT == 0
		ELSEIF RESULT == 1
			CALL SHOP_ACTION
		ENDIF
	ELSE
		CALL SHOP_ACTION
	ENDIF
;출격한다
ELSEIF RESULT == 예정_출격 && (CHECK_GAMEOVER_F() == 0 && TARGET != MASTER && (CFLAG:0 == 상태_건강 || FLAG:9) && FLAG:63 == 0)
	IF FLAG:9 == 0
		PRINTL 
		;임신 중에는 출격할 수 없다
		IF CHECK_PREGNANT_F(TARGET)
			PRINTFORML %CALLNAME:TARGET%은(는) 임신 중이기에 출격할 수 없습니다.
			PRINTL
		;FEAT 효과로 인한 커맨드 제한
		ELSEIF TALENT:밤악마의귀족 > 0 && TIME == 0
			SETCOLOR 255,128,0
			PRINTFORML %CALLNAME:TARGET%은(는) [밤악마의귀족] 효과로 인해 낮에 외출할 수 없습니다!
			RESETCOLOR
			PRINTL 
		ELSE
			CFLAG:100 = 예정_출격
			PRINTFORML %CALLNAME:TARGET%은(는) 출격하기로 했습니다.
		ENDIF
	ELSE
		FOR CCOUNT, 0, CHARANUM
			SIF CCOUNT == MASTER
				CONTINUE
			SIF CFLAG:CCOUNT:999 == 0
				CONTINUE
			SIF GROUPMATCH(CFLAG:CCOUNT:0, 상태_유폐, 상태_세뇌, 상태_악질감금, 상태_사망)
				CONTINUE
			PRINTL 
			;임신 중에는 출격할 수 없다
			IF CHECK_PREGNANT_F(CCOUNT) > 0
				PRINTFORML %CALLNAME:CCOUNT%은(는) 임신 중이기에 출격할 수 없습니다.
			;FEAT 효과로 인한 커맨드 제한
			ELSEIF TALENT:CCOUNT:밤악마의귀족 > 0 && TIME == 0
				SETCOLOR 255,128,0
				PRINTFORML %CALLNAME:CCOUNT%は[밤악마의귀족]の効果で外出は出撃できません！
				RESETCOLOR
			ELSE
				CFLAG:CCOUNT:100 = 예정_출격
				PRINTFORML %CALLNAME:CCOUNT%은(는) 출격하기로 했습니다.
			ENDIF
		NEXT
	ENDIF
	PRINTW 
;단련한다
ELSEIF RESULT == 예정_단련 && (CHECK_GAMEOVER_F() == 0 && TARGET != MASTER && (CFLAG:0 == 상태_건강 || FLAG:9) && FLAG:63 == 0)
	IF FLAG:9 == 0
		PRINTL 
		CFLAG:100 = 예정_단련
		PRINTFORML %CALLNAME%은(는) 단련하기로 했습니다.
	ELSE
		FOR CCOUNT, 0, CHARANUM
			SIF CCOUNT == MASTER
				CONTINUE
			SIF CFLAG:CCOUNT:999 == 0
				CONTINUE
			SIF GROUPMATCH(CFLAG:CCOUNT:0, 상태_유폐, 상태_세뇌, 상태_악질감금, 상태_사망)
				CONTINUE
			PRINTL 
			CFLAG:CCOUNT:100 = 예정_단련
			PRINTFORML %CALLNAME:CCOUNT%은(는) 단련하기로 했습니다.
		NEXT
	ENDIF
	PRINTW 
;휴식한다
ELSEIF RESULT == 예정_휴식 && (CHECK_GAMEOVER_F() == 0 && TARGET != MASTER && (CFLAG:0 == 상태_건강 || FLAG:9) && FLAG:63 == 0)
	IF FLAG:9 == 0
		PRINTL 
		CFLAG:100 = 예정_휴식
		PRINTFORML %CALLNAME%은(는) 휴식하기로 했습니다.
	ELSE
		FOR CCOUNT, 0, CHARANUM
			SIF CCOUNT == MASTER
				CONTINUE
			SIF CFLAG:CCOUNT:999 == 0
				CONTINUE
			SIF GROUPMATCH(CFLAG:CCOUNT:0, 상태_유폐, 상태_세뇌, 상태_악질감금, 상태_사망)
				CONTINUE
			PRINTL 
			CFLAG:CCOUNT:100 = 예정_휴식
			PRINTFORML %CALLNAME:CCOUNT%은(는) 휴식하기로 했습니다.
		NEXT
	ENDIF
	PRINTW 
;특별 활동
ELSEIF RESULT == 예정_활동 && (CHECK_GAMEOVER_F() == 0 && TARGET != MASTER && (CFLAG:0 == 상태_건강 || FLAG:9) && FLAG:63 == 0)
	IF FLAG:9 == 0
		;FEAT 효과로 인한 커맨드 제한
		IF TALENT:밤악마의귀족 > 0 && TIME == 0
			SETCOLOR 255,128,0
			PRINTFORML %CALLNAME:TARGET%은(는) [밤악마의귀족] 효과로 인해 낮에 외출할 수 없습니다!
			RESETCOLOR
			PRINTL 
		ELSEIF TALENT:타고난전사 > 0
			SETCOLOR 255,128,0
			PRINTFORML %CALLNAME%은(는) [타고난전사] 효과로 인해 특별 활동에 참여할 수 없습니다!
			RESETCOLOR
			PRINTL 
		ELSE
			PRINTL 
			CFLAG:100 = 예정_활동
			PRINTFORML %CALLNAME%은(는) 자금을 벌기로 했습니다.
		ENDIF
	ELSE
		FOR CCOUNT, 0, CHARANUM
			SIF CCOUNT == MASTER
				CONTINUE
			SIF CFLAG:CCOUNT:999 == 0
				CONTINUE
			SIF GROUPMATCH(CFLAG:CCOUNT:0, 상태_유폐, 상태_세뇌, 상태_악질감금, 상태_사망)
				CONTINUE
			PRINTL 
			;FEAT 효과로 인한 커맨드 제한
			IF TALENT:CCOUNT:밤악마의귀족 > 0 && TIME == 0
				SETCOLOR 255,128,0
				PRINTFORML %CALLNAME:CCOUNT%은(는) [밤악마의귀족] 효과로 인해 낮에 외출할 수 없습니다!
				RESETCOLOR
			ELSEIF TALENT:CCOUNT:타고난전사 > 0
				SETCOLOR 255,128,0
				PRINTFORML %CALLNAME:CCOUNT%은(는) [타고난전사] 효과로 인해 특별 활동에 참여할 수 없습니다!
				RESETCOLOR
			ELSE
				CFLAG:CCOUNT:100 = 예정_활동
				PRINTFORML %CALLNAME:CCOUNT%은(는) 자금을 벌기로 했습니다.
			ENDIF
		NEXT
	ENDIF
	PRINTW 
ELSEIF RESULT == 예정_방위 && (!GAME_OPTION_CHECK_F(OPTION_solo) && CHECK_GAMEOVER_F() == 0 && TARGET != MASTER && (CFLAG:0 == 상태_건강 || FLAG:9) && FLAG:63 == 0)
	IF FLAG:9 == 0
		PRINTL 
		;임신 중에는 출격할 수 없다
		IF CHECK_PREGNANT_F(TARGET) > 0
			PRINTL %CALLNAME:TARGET%은(는) 임신 중이기에 방위에 참가할 수 없습니다.
			PRINTL
		;FEAT 효과로 인한 커맨드 제한
		ELSEIF TALENT:밤악마의귀족 > 0 && TIME == 0
			SETCOLOR 255,128,0
			PRINTFORML %CALLNAME:TARGET%은(는) [밤악마의귀족] 효과로 인해 낮에 외출할 수 없습니다!
			RESETCOLOR
			PRINTL 
		ELSE
			PRINTFORML %CALLNAME%은(는) 거점을 방위하기로 했습니다.
			CFLAG:100 = 예정_방위
		ENDIF
	ELSE
		FOR CCOUNT, 0, CHARANUM
			SIF CCOUNT == MASTER
				CONTINUE
			SIF CFLAG:CCOUNT:999 == 0
				CONTINUE
			SIF GROUPMATCH(CFLAG:CCOUNT:0, 상태_유폐, 상태_세뇌, 상태_악질감금, 상태_사망)
				CONTINUE
			PRINTL 
			;임신 중에는 출격할 수 없다
			IF CHECK_PREGNANT_F(CCOUNT) > 0
				PRINTFORML %CALLNAME:CCOUNT%은(는) 임신 중이기에 방위에 참가할 수 없습니다.
			;FEAT 효과로 인한 커맨드 제한
			ELSEIF TALENT:CCOUNT:밤악마의귀족 > 0 && TIME == 0
				SETCOLOR 255,128,0
				PRINTFORML %CALLNAME:CCOUNT%은(는) [밤악마의귀족] 효과로 인해 낮에 외출할 수 없습니다!
				RESETCOLOR
			ELSE
				CFLAG:CCOUNT:100 = 예정_방위
				PRINTFORML %CALLNAME:CCOUNT%은(는) 거점을 방위하기로 했습니다.
			ENDIF
		NEXT
	ENDIF
	PRINTW 
;전투 지원
ELSEIF RESULT == 예정_지원 && (CHARANUM >= 3 && CHECK_GAMEOVER_F() == 0 && TARGET != MASTER && (CFLAG:0 == 상태_건강 || FLAG:9) && FLAG:63 == 0)
	IF FLAG:9 == 0
		IF TALENT:타고난전사 > 0
			SETCOLOR 255,128,0
			PRINTFORML %CALLNAME:TARGET%은(는) [타고난전사] 효과로 인해 전투 지원에 참여할 수 없습니다!
			RESETCOLOR
			PRINTL 
		ELSE
			PRINTL 
			CFLAG:100 = 예정_지원
			;마지막 부분의 원문은 徹する(てっする), "다른 것은 하지 않고 그것에만 구애되다(関わる)"
			PRINTFORML %CALLNAME:TARGET%은(는) 후방에서 전투 지원에 전념하기로 했습니다.
		ENDIF
	ELSE
		FOR CCOUNT, 0, CHARANUM
			SIF CCOUNT == MASTER
				CONTINUE
			SIF CFLAG:CCOUNT:999 == 0
				CONTINUE
			SIF GROUPMATCH(CFLAG:CCOUNT:0, 상태_유폐, 상태_세뇌, 상태_악질감금, 상태_사망)
				CONTINUE
			PRINTL 
			IF TALENT:CCOUNT:타고난전사 > 0
				SETCOLOR 255,128,0
				PRINTFORML %CALLNAME:CCOUNT%은(는) [타고난전사] 효과로 인해 전투 지원에 참여할 수 없습니다!
				RESETCOLOR
			ELSE
				CFLAG:CCOUNT:100 = 예정_지원
				PRINTFORML %CALLNAME:CCOUNT%은(는) 후방에서 전투 지원에 전념하기로 했습니다.
			ENDIF
		NEXT
	ENDIF
	PRINTW 
;정보 수집
ELSEIF RESULT == 예정_정보 && (CHECK_GAMEOVER_F() == 0 && TARGET != MASTER && (CFLAG:0 == 상태_건강 || FLAG:9) && FLAG:63 == 0)
	IF FLAG:9 == 0
		;FEAT 효과로 인한 커맨드 제한
		IF TALENT:밤악마의귀족 > 0 && TIME == 0
			SETCOLOR 255,128,0
			PRINTFORML %CALLNAME:TARGET%은(는) [밤악마의귀족] 효과로 인해 낮에 외출할 수 없습니다!
			RESETCOLOR
			PRINTL 
		ELSE
			PRINTL 
			CFLAG:100 = 예정_정보
			PRINTFORML %CALLNAME%은(는) 후방에서 정보 수집에 전념하기로 했습니다.
		ENDIF
	ELSE
		FOR CCOUNT, 0, CHARANUM
			SIF CCOUNT == MASTER
				CONTINUE
			SIF CFLAG:CCOUNT:999 == 0
				CONTINUE
			SIF GROUPMATCH(CFLAG:CCOUNT:0, 상태_유폐, 상태_세뇌, 상태_악질감금, 상태_사망)
				CONTINUE
			PRINTL 
			;FEAT 효과로 인한 커맨드 제한
			IF TALENT:CCOUNT:밤악마의귀족 > 0 && TIME == 0
				SETCOLOR 255,128,0
				PRINTFORML %CALLNAME:CCOUNT%은(는) [밤악마의귀족] 효과로 인해 낮에 외출할 수 없습니다!
				RESETCOLOR
			ELSE
				CFLAG:CCOUNT:100 = 예정_정보
				PRINTFORML %CALLNAME:CCOUNT%은(는) 후방에서 정보 수집에 전념하기로 했습니다.
			ENDIF
		NEXT
	ENDIF
	PRINTW 
;자유 활동
ELSEIF RESULT == 예정_자유 && (CHECK_GAMEOVER_F() == 0 && TARGET != MASTER && (CFLAG:0 == 상태_건강 || FLAG:9) && FLAG:63 == 0)
	IF FLAG:9 == 0
		;FEAT 효과로 인한 커맨드 제한
		IF TALENT:밤악마의귀족 > 0 && TIME == 0
			SETCOLOR 255,128,0
			PRINTFORML %CALLNAME:TARGET%은(는) [밤악마의귀족] 효과로 인해 낮에 외출할 수 없습니다!
			RESETCOLOR
			PRINTL 
		ELSE
			PRINTL 
			CFLAG:100 = 예정_자유
			PRINTFORML %CALLNAME%은(는) 자유롭게 행동하기로 했습니다.
		ENDIF
	ELSE
		FOR CCOUNT, 0, CHARANUM
			SIF CCOUNT == MASTER
				CONTINUE
			SIF CFLAG:CCOUNT:999 == 0
				CONTINUE
			SIF GROUPMATCH(CFLAG:CCOUNT:0, 상태_유폐, 상태_세뇌, 상태_악질감금, 상태_사망)
				CONTINUE
			PRINTL 
			;FEAT 효과로 인한 커맨드 제한
			IF TALENT:CCOUNT:밤악마의귀족 > 0 && TIME == 0
				SETCOLOR 255,128,0
				PRINTFORML %CALLNAME:CCOUNT%은(는) [밤악마의귀족] 효과로 인해 낮에 외출할 수 없습니다!
				RESETCOLOR
			ELSE
				CFLAG:CCOUNT:100 = 예정_자유
				PRINTFORML %CALLNAME:CCOUNT%은(는) 자유롭게 행동하기로 했습니다.
			ENDIF
		NEXT
	ENDIF
	PRINTW 
;스테이터스 표시
ELSEIF RESULT == 110
	IF CHARANUM >= 3
		WHILE RESULT != 999
			PRINTL 
			PRINTL 【스테이터스 표시】
			IF TARGET == 0
				CALL CHARA_LIST, 2,1
			ELSE
				RESULT = TARGET
			ENDIF

			IF RESULT != 999
				CALL SHOW_STATUS_CHARA_SELECT, RESULT
			ENDIF
		WEND
	ELSE
		PRINTL 
		PRINTL 【스테이터스 표시】
		TARGET = 1
		CALL SHOW_STATUS_CHARA_SELECT, TARGET
	ENDIF
	SIF FLAG:63
		TARGET = 0
;캐릭터 강화
ELSEIF RESULT == 111 && (CHECK_GAMEOVER_F() == 0 && CHARANUM_ACTIVE() && FLAG:63 == 0)
	IF CHARANUM >= 3
		WHILE RESULT != 999
			PRINTL 
			PRINTL 【캐릭터 강화】
			CALL CHARA_LIST, 1,2
			SIF RESULT != 999
				CALL CHARA_POWERUP, RESULT
		WEND
	ELSE
		PRINTL 
		PRINTL 【캐릭터 강화】
		TARGET = 1
		CALL CHARA_POWERUP, TARGET
	ENDIF
;의상 설정
ELSEIF RESULT == 112 && (CHECK_GAMEOVER_F() == 0 && CHARANUM_ACTIVE() && FLAG:63 == 0)
	CALL CLOTH_WEAR
;메디컬 룸
ELSEIF RESULT == 113 && (CHECK_GAMEOVER_F() == 0 && FLAG:63 == 0 && FLAG:63 == 0)
	CALL DRUG_PREPARATION
;커스터마이즈
ELSEIF RESULT == 114 && (CHECK_GAMEOVER_F() == 0 && FLAG:63 == 0 && FLAG:63 == 0)
	IF CHARANUM >= 3
		WHILE RESULT != 999
			PRINTL 
			PRINTL 【무기 커스터마이즈】
			CALL CHARA_LIST, 1,0
			SIF RESULT != 999
				CALL WEAPON_CUSTOMIZE, RESULT
		WEND
	ELSE
		PRINTL 
		PRINTL 【무기 커스터마이즈】
		TARGET = 1
		CALL WEAPON_CUSTOMIZE, TARGET
	ENDIF
;의상 구입 
ELSEIF RESULT == 120 && (CHECK_GAMEOVER_F() == 0 && FLAG:63 == 0 && FLAG:63 == 0)
	CALL SHOW_SHOP_CLOTH
;상황 확인
ELSEIF RESULT == 130 && FLAG:63 == 0
	CALL SHOW_STATUS_SENMETU

;시설 확장
ELSEIF RESULT == 150 && (CHECK_GAMEOVER_F() == 0 && FLAG:63 == 0)
	CALL HOME_ENHANCING
;스케쥴 설정
ELSEIF RESULT == 160 && FLAG:63 == 0
	IF CFLAG:0 != 상태_건강 || TARGET == MASTER || FLAG:9
		PRINTW 스케쥴을 설정할 캐릭터가 선택되지 않았습니다.
	ELSE
		CALL SCHEDULE
	ENDIF

;여기서부터 INSTANT 모드용 은퇴&신인 가입 처리 호출
;은퇴자 명부
ELSEIF RESULT == 169 && (GAME_OPTION_CHECK_F(OPTION_가입은퇴있음) && FLAG:63 == 0)
	CALL CHAR_INTAI_LIST
;은퇴시킨다
ELSEIF RESULT == 170 && (CHARANUM > 1 && GAME_OPTION_CHECK_F(OPTION_가입은퇴있음) && FLAG:63 == 0)
	CALL CHARA_INTAI
;신인을 가입시킨다
ELSEIF RESULT == 180 && (CHARANUM <= 등록최대인원 - 1 && GAME_OPTION_CHECK_F(OPTION_가입은퇴있음) && FLAG:63 == 0)
	CALL TSUIKAYOUSEI_NORMAL
;여기까지 INSTANT 모드용 은퇴&신인 가입 처리 호출


;세이브
ELSEIF RESULT == 200
	SAVEGAME
;로드
ELSEIF RESULT == 300
	LOADGAME

;디버그 모드 전환(숨김 커맨드 400)
ELSEIF RESULT == 400
	;이름 변경이나 캐릭터 정렬용으로 비어있다
	;DEBUG
	IF FLAG:999 == 0
		PRINTL 
		PRINTL 
		PRINTL DEBUG ON
		FLAG:999 = 1
		SETBGCOLOR 0,0,40
	ELSEIF FLAG:999 == 1
		PRINTL 
		PRINTL 
		PRINTL DEBUG OFF
		FLAG:999 = 0
		RESETBGCOLOR
	ENDIF
;컨피그
ELSEIF RESULT == 700
	CALL CONFIG
;트로피 확인
ELSEIF RESULT == 800
	CALL SHOW_TROPHY
;일괄 설정
ELSEIF RESULT == 90 && (CHARANUM_SAFE() >= 2 && FLAG:63 == 0)
	SIF TARGET == MASTER
		TARGET = RAND:(CHARANUM-1) + 1
	IF FLAG:9 == 0
		PRINTL 일괄 설정 모드로 변경하였습니다.
		FLAG:9 = 1
	ENDIF
;파티 편성
ELSEIF RESULT == 50 && (CHARANUM_PLAYABLE() > 0 || FLAG:63)
	IF FLAG:63
		PRINTL 파티 편성 모드를 해제합니다.
		FLAG:63 = 0
		FLAG:9 = 1
		SIF TARGET == MASTER
			TARGET = RAND:(CHARANUM-1) + 1
		;FLAG:61 = 0
	ELSE
		CALL SET_PARTYMEMBER
		PRINTL 파티 편성 모드로 이행합니다.
		FLAG:63 = 1
		;SIF CHARANUM_SAFE_PARTYCHECK(0)
		;	FLAG:61 = 1
		FLAG:9 = 0

		;FOR CCOUNT, 0, CHARANUM
			;CFLAG:CCOUNT:100 = 103
		;NEXT
		CALL SET_PARTYMEMBER
		TARGET = 0
	ENDIF
;캐릭터 표시 전환
ELSEIF RESULT == 60 && CHARANUM_SAFE_PARTYCHECK(0)
	IF FLAG:61
		PRINTL 캐릭터를 모두 표시합니다.
		FLAG:61 = 0
	ELSE
		PRINTL 파티 인원만 표시합니다.
		FLAG:61 = 1
	ENDIF
;파티 편성 모드가 아닐 때 표시된 캐릭터 선택 시
ELSEIF (RESULT >= 1 && RESULT <= CHARANUM -1) && (CFLAG:RESULT:999 != 0 && FLAG:63 == 0)
	FLAG:9 = 0
	IF CHARANUM > RESULT
		IF CFLAG:RESULT:0 == 상태_건강
			TARGET = RESULT
			PRINTL 
			PRINTFORML 조작 캐릭터를 %CALLNAME:TARGET%(으)로 변경했습니다.
			PRINTL 
		ENDIF
	ENDIF
ELSEIF (RESULT >= 1 && RESULT <= CHARANUM -1) && FLAG:63
	IF GROUPMATCH(CFLAG:RESULT:0, 상태_유폐, 상태_세뇌, 상태_흑화, 상태_사망)
	ELSEIF TARGET == 0
		TARGET = RESULT
	ELSEIF TARGET == RESULT
		TARGET = 0
	ELSEIF CFLAG:TARGET:0 == 상태_출산직전
		PRINTFORML %CALLNAME:TARGET%은(는) 특별병동에 입원 중이기 때문에 교체할 수 없습니다.
		;PRINTW 
		TARGET = 0
	ELSEIF CFLAG:TARGET:0 == 상태_육아중
		PRINTFORML %CALLNAME:TARGET%은(는) 특별병동에서 육아 중이므로 교체할 수 없습니다.
		;PRINTW 
		TARGET = 0
	ELSEIF CFLAG:RESULT:0 == 상태_출산직전
		PRINTFORML %CALLNAME:RESULT%은(는) 특별병동에 입원 중이기 때문에 교체할 수 없습니다.
		;PRINTW 
		TARGET = 0
	ELSEIF CFLAG:RESULT:0 == 상태_육아중
		PRINTFORML %CALLNAME:RESULT%은(는) 특별병동에서 육아 중이므로 교체할 수 없습니다.
		;PRINTW 
		TARGET = 0
	ELSE
		;캐릭터를 교체한다
		;SETCOLOR 250,180,50
		;PRINTFORM 　[{TARGET}] %CALLNAME:TARGET%
		;RESETCOLOR
		;PRINT 와(과)
		;SETCOLOR 250,180,50
		;PRINTFORM [{RESULT}] %CALLNAME:RESULT%
		;RESETCOLOR
		;PRINTL 의 배치를 서로 바꿨다!
		;PRINTW 
		;RELATION
		FOR LOCAL, 0, CHARANUM
			SIF LOCAL == MASTER
				CONTINUE
			SWAP RELATION:LOCAL:TARGET, RELATION:LOCAL:RESULT
		NEXT
		SWAP CFLAG:TARGET:999, CFLAG:RESULT:999
		SWAPCHARA TARGET,RESULT
		SIF CFLAG:TARGET:999 == 0
			CFLAG:TARGET:100 = 예정_휴식
		SIF CFLAG:RESULT:999 == 0
			CFLAG:RESULT:100 = 예정_휴식
		TARGET = 0
	ENDIF
ELSE
ENDIF



@SHOP_PRINT_SITUATION()
#DIM CCOUNT
LOCAL = 0
LOCAL:1 = 1
;촉수 섬멸 상황
PRINTL 
LOCAL = FLAG:11
IF GET_LASTBOSS_PHASE_F() == 0
	PRINTFORM 　　　
	SAVESTR:13 = BOSS
	REPEAT FLAG:3
		CALL TENTACLE_SURVIVE_CHECK, LOCAL:1
		IF RESULT == 0
			SETCOLOR 96,96,96
			SIF !GETBIT(FLAG:0,4)
				PRINT [―――]
		ELSE
			FLAG:11 = RESULT
			;색적 타겟 기능 ON일 경우
			IF CONFIG_CHECK_BALANCE_F(1) > 0
				;타겟
				IF FLAG:18 == FLAG:11
					;탐색 상황에 따라 대응 커맨드로 색상 변경
					IF FLAG:47 >= FLAG:46
						SETCOLOR 255,125,125
					ELSE
						SETCOLOR 255,180,0
					ENDIF
					PRINT [[[
					CALL TENTACLE_ACCESS, "NAME"
					PRINT ]]]
				;타겟 이외
				ELSE
					IF FLAG:47 >= FLAG:46
						SETCOLOR 185,85,85
					ELSE
						SETCOLOR 65,65,155
					ENDIF
					PRINT [
					CALL TENTACLE_ACCESS, "NAME"
					PRINT ]
				ENDIF
			;색적 타겟 기능 OFF일 경우
			ELSE
				FLAG:11 = RESULT
				;탐색 상황에 따라 대응 커맨드로 색상 변경
				IF FLAG:47 >= FLAG:46
					SETCOLOR 255,125,125
				ELSE
					SETCOLOR 255,180,0
				ENDIF
				PRINT [
				CALL TENTACLE_ACCESS, "NAME"
				PRINT ]
			ENDIF
		ENDIF
		LOCAL:1 *=2
	REND
ELSEIF GET_LASTBOSS_PHASE_F() >= 1
	PRINTFORM 　　　
	REPEAT FLAG:4
		CALL TENTACLE_SURVIVE_CHECK, LOCAL:1
		IF RESULT == 0
			SETCOLOR 96,96,96
			PRINT [―――]
		ELSE
			FLAG:11 = RESULT
			;색적 타겟 기능 ON일 경우
			IF CONFIG_CHECK_BALANCE_F(1) > 0
				;타겟
				IF FLAG:18 == FLAG:11
					;탐색 상황에 따라 대응 커맨드로 색상 변경
					IF FLAG:47 >= FLAG:46
						SETCOLOR 255,125,125
					ELSE
						SETCOLOR 255,180,0
					ENDIF
					PRINT [[[
					CALL TENTACLE_ACCESS, "NAME"
					PRINT ]]]
				;타겟 이외
				ELSE
					IF FLAG:47 >= FLAG:46
						SETCOLOR 185,85,85
					ELSE
						SETCOLOR 65,65,155
					ENDIF
					PRINT [
					CALL TENTACLE_ACCESS, "NAME"
					PRINT ]
				ENDIF
			;색적 타겟 기능 OFF일 경우
			ELSE
				FLAG:11 = RESULT
				;탐색 상황에 따라 대응 커맨드로 색상 변경
				IF FLAG:47 >= FLAG:46
					SETCOLOR 255,125,125
				ELSE
					SETCOLOR 255,180,0
				ENDIF
				PRINT [
				CALL TENTACLE_ACCESS, "NAME"
				PRINT ]
			ENDIF
		ENDIF
		LOCAL:1 *=2
	REND
ENDIF
FLAG:11 = LOCAL

;세뇌/흑화 상태인 캐릭터 확인
LOCAL = 0
IF CHARANUM_DARKEN()>=4
	PRINTL
	PRINT 　　 
ENDIF
FOR CCOUNT, 1, CHARANUM
	IF CFLAG:CCOUNT:0 == 상태_세뇌	
		SETCOLOR 255,125,185
		PRINTFORM  [세:%CALLNAME:CCOUNT%]
		LOCAL++
	ELSEIF CFLAG:CCOUNT:0 == 상태_흑화
		SETCOLOR 255,125,255
		PRINTFORM  [흑:%CALLNAME:CCOUNT%]
		LOCAL++
	ENDIF
NEXT
PRINTL 
LOCAL = 0
FOR CCOUNT, 1, CHARANUM
	IF CFLAG:CCOUNT:0 == 상태_유폐 || CFLAG:CCOUNT:0 == 상태_악질감금
		IF LOCAL == 0
			PRINTFORM 　　 
		ENDIF
		SETCOLOR 155,65,125
		PRINTFORM 【❤%CALLNAME:CCOUNT% 실종 중】
		LOCAL++
	ENDIF
NEXT
SIF LOCAL > 0
	PRINTL


;--------------------------------------------------
;상황 표시
@SHOW_STATUS_SENMETU
#DIM CCOUNT

LOCAL = 0
LOCAL:1 = 1
;촉수 섬멸 상황
PRINTL 
LOCAL = FLAG:11
IF ENEMY_TYPE_CHECK_F("BOSS") == 1
	PRINTFORML 현재 활동 중인 %STR:2502%
	SAVESTR:13 = BOSS
	DRAWLINE
	REPEAT FLAG:3
		CALL TENTACLE_SURVIVE_CHECK, LOCAL:1
		IF RESULT > 0
			FLAG:11 = RESULT
			PRINT [
			CALL TENTACLE_ACCESS, "NAME"
			PRINT ]
		ENDIF
		LOCAL:1 *=2
	REND
ELSEIF GET_LASTBOSS_PHASE_F() >= 1
	PRINTFORML 현재 활동 중인 %STR:2503%
	DRAWLINE
	REPEAT FLAG:4
		CALL TENTACLE_SURVIVE_CHECK, LOCAL:1
		IF RESULT > 0
			FLAG:11 = RESULT
			PRINT [
			CALL TENTACLE_ACCESS, "NAME"
			PRINT ]
		ENDIF
		LOCAL:1 *=2
	REND
ENDIF
FLAG:11 = LOCAL
PRINTL 
DRAWLINE
PRINTW 

;입원/육아 중인 캐릭터 확인
LOCAL = 0
FOR CCOUNT, 0, CHARANUM
	SIF CCOUNT == MASTER
		CONTINUE
	SIF CFLAG:CCOUNT:0 == 상태_출산직전 || CFLAG:CCOUNT:0 == 상태_육아중
		LOCAL ++
NEXT
IF LOCAL
	PRINTFORML 입원/육아 중인 캐릭터
	DRAWLINE
	FOR CCOUNT, 0, CHARANUM
		SIF CCOUNT == MASTER
			CONTINUE

		IF CFLAG:CCOUNT:0 == 상태_출산직전
			PRINTFORM  %CALLNAME:CCOUNT%：특별병동에 입원 중　
		ELSEIF CFLAG:CCOUNT:0 == 상태_육아중
			PRINTFORM  %CALLNAME:CCOUNT%：육아 중　
		ENDIF
		;임신 상태
		SIF (CHECK_PREGNANT_F(CCOUNT) > 0 && CFLAG:CCOUNT:0 == 상태_출산직전) || (CHECK_PREGNANT_F(CCOUNT) > 0 && CFLAG:CCOUNT:0 == 상태_육아중)
			PRINT [임신 중]
		SIF CFLAG:CCOUNT:0 == 상태_출산직전 || CFLAG:CCOUNT:0 == 상태_육아중
			PRINTL 
	NEXT
	DRAWLINE
	PRINTW 
ENDIF

;유폐 중인 캐릭터 확인
PRINTFORML 유폐 중인 캐릭터
DRAWLINE
LOCAL = 0
FOR CCOUNT, 0, CHARANUM
	SIF CCOUNT == MASTER
		CONTINUE
	
	IF CFLAG:CCOUNT:0 == 상태_유폐
		;유폐 일수 계산
		LOCAL:1 = 0
		SIF CFLAG:CCOUNT:31 > 0
			LOCAL:1 = CFLAG:CCOUNT:31 / 2
		
		PRINTFORM  %CALLNAME:CCOUNT%：
		IF (CFLAG:CCOUNT:20 == 2)
			FOR LOCAL:2, 0, CHARANUM
				RESULT = 0
				SIF CFLAG:CCOUNT:21 == CFLAG:(LOCAL:2):240
					RESULT = LOCAL:2
				SIF RESULT
					BREAK
			NEXT
			PRINTFORM %PRINT_TRANSCALLNAME(RESULT)%
		ELSE
			CALL TENTACLE_ACCESS_PRISON, CCOUNT, "NAME"
		ENDIF
		PRINTFORM 에 의해 유폐 중 {CFLAG:CCOUNT:31 / 2}일째　
		;임신 상태
		SIF CHECK_PREGNANT_F(CCOUNT) > 0
			PRINT [임신 중]　
		SIF CFLAG:CCOUNT:220 && CONFIG_CHECK_OTHER_F(0) == 0
			PRINTFORM 육아 중인 새끼 촉수：{CFLAG:CCOUNT:220}
		PRINTL 
		LOCAL += 1
	ENDIF
NEXT
SIF LOCAL == 0
	PRINTL 없음
DRAWLINE
PRINTW 

;감금 중인 캐릭터
PRINTFORML 납치 감금 중인 캐릭터
DRAWLINE
LOCAL = 0
FOR CCOUNT, 0, CHARANUM
	SIF CCOUNT == MASTER
		CONTINUE
	
	IF CFLAG:CCOUNT:0 == 상태_악질감금
		;감금 일수 계산
		LOCAL:1 = 0
		SIF CFLAG:CCOUNT:70 > 0
			LOCAL:1 = CFLAG:CCOUNT:70 / 2
		PRINTFORM %PRINT_TRANSCALLNAME(RESULT)%：폐건물에 납치 감금 중 {CFLAG:CCOUNT:70 / 2}일째　
		;임신 상태
		SIF CHECK_PREGNANT_F(CCOUNT) > 0
			PRINT [임신 중]　
		SIF TALENT:CCOUNT:사지결손 > 0
			PRINT [생오나홀]
		PRINTL 
		LOCAL += 1
	ENDIF
NEXT
SIF LOCAL == 0
	PRINTL 없음
DRAWLINE
PRINTW 

;세뇌/흑화 상태인 캐릭터 확인
PRINTFORML 세뇌/흑화 상태인 캐릭터
DRAWLINE
LOCAL = 0
FOR CCOUNT, 0, CHARANUM
	SIF CCOUNT == MASTER
		CONTINUE
	
	IF CFLAG:CCOUNT:0 == 상태_세뇌
		PRINTFORM  %CALLNAME:CCOUNT%：세뇌 (
		IF CFLAG:CCOUNT:20 < 2
			CALL TENTACLE_ACCESS_PRISON, CCOUNT, "NAME"
		ELSEIF CFLAG:CCOUNT:20 == 2
			PRINTFORM %PRINT_TRANSCALLNAME(CFLAG:CCOUNT:21)%
		ENDIF
		PRINT ）　
	ELSEIF CFLAG:CCOUNT:0 == 상태_흑화
		PRINTFORM  %CALLNAME:CCOUNT%：흑화　
	ENDIF
	;임신 상태
	SIF (CHECK_PREGNANT_F(CCOUNT) > 0 && CFLAG:CCOUNT:0 == 상태_세뇌) || (CHECK_PREGNANT_F(CCOUNT) > 0 && CFLAG:CCOUNT:0 == 상태_흑화)
		PRINT [임신 중]
	IF CFLAG:CCOUNT:0 == 상태_세뇌 || CFLAG:CCOUNT:0 == 상태_흑화
		PRINTL 
		LOCAL += 1
	ENDIF
NEXT
SIF LOCAL == 0
	PRINTL 없음
DRAWLINE
PRINTW 

;거두어진 캐릭터
IF !GAME_OPTION_CHECK_F(OPTION_solo)
	PRINTFORML 거두어진 캐릭터
	DRAWLINE
	LOCAL = 0
	FOR CCOUNT, 0, CHARANUM
		SIF CCOUNT == MASTER
			CONTINUE

		SIF CFLAG:CCOUNT:0 == 상태_사망
			PRINTFORM  %CALLNAME:CCOUNT%：
		;묘상화 상태
		IF TALENT:CCOUNT:번식자루 && CFLAG:CCOUNT:0 == 상태_사망
			PRINT 묘상화
		ELSEIF CFLAG:CCOUNT:0 == 상태_사망
			PRINT 거두어짐
		ENDIF
		IF CFLAG:CCOUNT:0 == 상태_사망
			PRINTL 
			LOCAL += 1
		ENDIF
	NEXT
	SIF LOCAL == 0
		PRINTL 없음
	DRAWLINE
	PRINTW 
ENDIF

;새끼 촉수의 수
LOCAL = FLAG:44
FOR CCOUNT,0,CHARANUM
	LOCAL -= CFLAG:CCOUNT:220
NEXT
IF LOCAL
	PRINTFORML 활동 중인 새끼 촉수의 총 마릿수
	DRAWLINE
	PRINTFORML  {LOCAL}마리
	DRAWLINE
	PRINTW 
ENDIF

;--------------------------------------------------
;시설 확장
@HOME_ENHANCING
$INPUT_LOOP
PRINTL 
DRAWLINE
PRINTL 시설을 확장합니다.
PRINTFORML 자금：{MONEY}＄
CALL SHORTLINE
IF FLAG:53 != 0
	PRINTFORML 보유하고 있는 여가 시설
	CALL SHORTLINE
	SIF (FLAG:53 & 관엽식물)
		PRINTFORML 　관엽식물　　　　　　  　　  　매 행동마다 피로도를 아주 약간 회복
	SIF (FLAG:53 & 마음이편안해지는풍경화)
		PRINTFORML 　마음이 편안해지는 풍경화　　　매 행동마다 피로도를 아주 약간 회복
	SIF (FLAG:53 & 세련된오브제)
		PRINTFORML 　세련된 오브제　　　　　　　　 매 행동마다 피로도를 아주 약간 회복
	SIF (FLAG:53 & 호화로운분수)
		PRINTFORML 　호화로운 분수　　　　　　　　 매 행동마다 피로도를 아주 약간 회복
	SIF (FLAG:53 & 질좋은침대) && (FLAG:53 & 최고급침대) == 0
		PRINTFORML 　질 좋은 침대　　　　　　　　　야간 휴식의 피로도 회복량이 증가
	SIF (FLAG:53 & 최고급침대)
		PRINTFORML 　최고급 침대　　　　　　　　　 야간 휴식의 피로도 회복량이 크게 증가
	SIF (FLAG:53 & 안마의자)
		PRINTFORML 　안마 의자　　　　　　　　　　 매 행동마다 피로도를 조금 회복
	SIF (FLAG:53 & 목욕설비)
		PRINTFORML 　목욕 설비　　　　　　　　　　 단련 후 피로도를 조금 회복
	SIF (FLAG:53 & 게임코너)
		PRINTFORML 　게임 코너　　　　　　　　　　 낮 휴식의 피로도 회복량이 증가
	SIF (FLAG:53 & 대욕탕) && (FLAG:53 & 미용온천) == 0
		PRINTFORML 　대욕탕　　　　　　　　　　　　모든 휴식의 피로도 회복량이 증가
	SIF (FLAG:53 & 미용온천)
		PRINTFORML 　미용 온천　　　　　　　　　　 모든 휴식의 피로도 회복량이 크게 증가
	SIF (FLAG:53 & 뷰티살롱)
		PRINTFORML 　뷰티살롱　　　　　　　　　　　모든 휴식의 피로도 회복량이 매우 크게 증가
ELSE
	PRINTL 무엇을 하시겠습니까?
ENDIF
CALL SHORTLINE
PRINTFORML [0] 단련 설비 레벨업：현재 설비 레벨：Lv.\@FLAG:50 > 4 ? MAX # {FLAG:50}\@
PRINTFORML [1] 휴식 설비 레벨업：현재 설비 레벨：Lv.\@FLAG:51 > 4 ? MAX # {FLAG:51}\@
PRINTFORML [2] 방위 설비 레벨업：현재 설비 레벨：Lv.\@FLAG:52 > 4 ? MAX # {FLAG:52}\@
;원문은 リラクゼーション設備 '휴게 설비' 이나, 위의 '휴식 설비' 와 겹치므로 여가 시설로 바꿨음
PRINTFORML [3] 여가 시설을 추가한다
PRINTFORML [4] 촉수 연구 레벨업：현재 연구 레벨：Lv.\@FLAG:54 > 4 ? MAX # {FLAG:54}\@
PRINTL [999] 돌아간다
$INPUT_LOOP_1
INPUT
IF RESULT == 999
ELSEIF RESULT == 0
	IF FLAG:50 >= 5
		PRINTW 더 이상 설비 레벨을 올릴 수 없습니다.
		CLEARLINE 2
		GOTO INPUT_LOOP_1
	ELSE
		PRINTL 
		PRINTFORML 단련 설비의 레벨업에는 {5000 * FLAG:50 + 5000}＄만큼 필요합니다.
		PRINTL 단련 설비의 레벨을 올리시겠습니까?
		
		PRINTL [0] 네
		PRINTL [1] 아니오
		$INPUT_LOOP_SELECT
		INPUT
		IF RESULT == 0
			IF MONEY < 5000 * FLAG:50 + 5000
				PRINTW 소지 자금이 부족합니다.
			ELSE
				MONEY -= 5000 * FLAG:50 + 5000
				FLAG:50 += 1
				PRINTW 설비 레벨이 올랐습니다!
			ENDIF
		ELSEIF RESULT == 1
		ELSE
			PRINTW 올바른 값을 입력해 주십시오.
			CLEARLINE 2
			GOTO INPUT_LOOP_SELECT
		ENDIF
	ENDIF
	PRINTL 
	GOTO INPUT_LOOP
ELSEIF RESULT == 1
	IF FLAG:51 >= 5
		PRINTW 더 이상 설비 레벨을 올릴 수 없습니다.
		CLEARLINE 2
		GOTO INPUT_LOOP_1
	ELSE
		PRINTL 
		PRINTFORML 휴식 설비의 레벨업에는 {1000 * FLAG:51 + 1000}＄만큼 필요합니다.
		PRINTL 휴식 설비의 레벨을 올리시겠습니까?
		
		PRINTL [0] 네
		PRINTL [1] 아니오
		$INPUT_LOOP_SELECT_1
		INPUT
		IF RESULT == 0
			IF MONEY < 1000 * FLAG:51 + 1000
				PRINTW 소지 자금이 부족합니다.
			ELSE
				MONEY -= 1000 * FLAG:51 + 1000
				FLAG:51 += 1
				PRINTW 설비 레벨이 올랐습니다!
			ENDIF
		ELSEIF RESULT == 1
		ELSE
			PRINTW 올바른 값을 입력해 주십시오.
			CLEARLINE 2
			GOTO INPUT_LOOP_SELECT_1
		ENDIF
	ENDIF
	PRINTL 
	GOTO INPUT_LOOP
ELSEIF RESULT == 2
	IF FLAG:52 >= 5
		PRINTW 더 이상 설비 레벨을 올릴 수 없습니다.
		CLEARLINE 2
		GOTO INPUT_LOOP_1
	ELSE
		PRINTL 
		PRINTFORML 방위 설비의 레벨업에는 {10000 * FLAG:52 + 10000}＄만큼 필요합니다.
		PRINTL 방위 설비의 레벨을 올리시겠습니까?
		
		PRINTL [0] 네
		PRINTL [1] 아니오
		$INPUT_LOOP_SELECT_2
		INPUT
		IF RESULT == 0
			IF MONEY < 10000 * FLAG:52 + 10000
				PRINTL 소지 자금이 부족합니다.
			ELSE
				MONEY -= 10000 * FLAG:52 + 10000
				FLAG:52 += 1
				PRINTW 설비 레벨이 올랐습니다!
			ENDIF
		ELSEIF RESULT == 1
		ELSE
			PRINTW 올바른 값을 입력해 주십시오.
			CLEARLINE 2
			GOTO INPUT_LOOP_SELECT_2
		ENDIF
	ENDIF
	PRINTL 
	GOTO INPUT_LOOP
ELSEIF RESULT == 3
	$INPUT_LOOP_SELECT_4
	IF FLAG:53 == 4095
		PRINTW 더 이상 추가할 수 있는 시설이 없습니다.
		GOTO INPUT_LOOP
	ENDIF

	PRINTL 
	DRAWLINE
	PRINTL 여가 시설을 추가합니다.
	PRINTFORML 자금：{MONEY}＄
	;PRINTFORML 　＊　카탈로그　＊
	CALL SHORTLINE
	SIF (FLAG:53 & 관엽식물) == 0
		PRINTFORML [0] 관엽식물　　　　　　  　　　　　 500＄　매 행동마다 피로도를 아주 약간 회복
	SIF (FLAG:53 & 마음이편안해지는풍경화) == 0 && (FLAG:53 & 관엽식물)
		PRINTFORML [0] 마음이 편안해지는 풍경화　　　  4500＄　매 행동마다 피로도를 아주 약간 회복
	SIF (FLAG:53 & 세련된오브제) == 0 && (FLAG:53 & 마음이편안해지는풍경화)
		PRINTFORML [0] 세련된 오브제　　　　　　　　　 8500＄　매 행동마다 피로도를 아주 약간 회복
	SIF (FLAG:53 & 호화로운분수) == 0 && (FLAG:53 & 세련된오브제)
		PRINTFORML [0] 호화로운 분수　　　　　　　　 100000＄　매 행동마다 피로도를 아주 약간 회복
	SIF (FLAG:53 & 질좋은침대) == 0
		PRINTFORML [1] 질 좋은 침대　　　　　　　　　 12000＄　야간 휴식의 피로도 회복량이 증가
	SIF (FLAG:53 & 최고급침대) == 0 && (FLAG:53 & 질좋은침대)
		PRINTFORML [1] 최고급 침대　　　　　　　　　　95000＄　야간 휴식의 피로도 회복량이 크게 증가
	SIF (FLAG:53 & 안마의자) == 0
		PRINTFORML [2] 안마 의자　　　　　　　　　　　25800＄　매 행동마다 피로도를 조금 회복
	SIF (FLAG:53 & 목욕설비) == 0
		PRINTFORML [3] 목욕 설비　　　　　　　　　　　50000＄　단련 후 피로도를 조금 회복
	SIF (FLAG:53 & 게임코너) == 0
		PRINTFORML [4] 게임 코너　　　　　　　　　　 100000＄　낮 휴식의 피로도 회복량이 증가
	SIF (FLAG:53 & 대욕탕) == 0
		PRINTFORML [5] 대욕탕　　　　　　　　　　　　200000＄　모든 휴식의 피로도 회복량이 증가
	SIF (FLAG:53 & 미용온천) == 0 && (FLAG:53 & 대욕탕)
		PRINTFORML [5] 미용 온천　　　　　　　　　　 500000＄　모든 휴식의 피로도 회복량이 크게 증가
	SIF (FLAG:53 & 뷰티살롱) == 0
		PRINTFORML [6] 뷰티살롱　　　　　　　　　　 1500000＄　모든 휴식의 피로도 회복량이 매우 크게 증가
	PRINTFORML [99] 돌아간다
	$INPUT_LOOP_SELECT_3
	INPUT
	IF RESULT == 0 && (FLAG:53 & 관엽식물) == 0
		IF MONEY >= 500
			MONEY -= 500
			FLAG:53 |= 관엽식물
			PRINTL 관엽식물을 구입하여 거점에 설치했다!
			PRINTW (휴식을 취하지 않을 때에도 피로도가 아주 약간씩 회복됩니다.)
			GOTO INPUT_LOOP_SELECT_4
		ENDIF
	ELSEIF RESULT == 0 && (FLAG:53 & 마음이편안해지는풍경화) == 0 && (FLAG:53 & 관엽식물)
		IF MONEY >= 4500
			MONEY -= 4500
			FLAG:53 |= 마음이편안해지는풍경화
			PRINTL 마음이 편안해지는 풍경화를 구입하여 거점에 설치했다!
			PRINTW (휴식을 취하지 않을 때에도 피로도가 아주 약간씩 회복됩니다.)
			GOTO INPUT_LOOP_SELECT_4
		ENDIF
	ELSEIF RESULT == 0 && (FLAG:53 & 세련된오브제) == 0 && (FLAG:53 & 마음이편안해지는풍경화)
		IF MONEY >= 8500
			MONEY -= 8500
			FLAG:53 |= 세련된오브제
			PRINTL 세련된 오브제를 구입하여 거점에 설치했다!
			PRINTW (휴식을 취하지 않을 때에도 피로도가 아주 약간씩 회복됩니다.)
			GOTO INPUT_LOOP_SELECT_4
		ENDIF
	ELSEIF RESULT == 0 && (FLAG:53 & 호화로운분수) == 0 && (FLAG:53 & 세련된오브제)
		IF MONEY >= 100000
			MONEY -= 100000
			FLAG:53 |= 호화로운분수
			PRINTL 호화로운 분수를 구입하여 거점에 설치했다!
			PRINTW (휴식을 취하지 않을 때에도 피로도가 아주 약간씩 회복됩니다.)
			GOTO INPUT_LOOP_SELECT_4
		ENDIF
	ELSEIF RESULT == 1 && (FLAG:53 & 질좋은침대) == 0
		IF MONEY >= 12000
			MONEY -= 12000
			FLAG:53 |= 질좋은침대
			PRINTL 질 좋은 침대를 구입하여 방 안에 놓았다!
			PRINTW (야간 휴식의 피로도 회복량이 증가합니다.)
			GOTO INPUT_LOOP_SELECT_4
		ENDIF
	ELSEIF RESULT == 1 && (FLAG:53 & 최고급침대) == 0 && (FLAG:53 & 질좋은침대)
		IF MONEY >= 95000
			MONEY -= 95000
			FLAG:53 |= 최고급침대
			PRINTL 방 안에 있던 침대를 최고급 침대로 교체했다!
			PRINTW (야간 휴식의 피로도 회복량이 크게 증가합니다.)
			GOTO INPUT_LOOP_SELECT_4
		ENDIF
	ELSEIF RESULT == 2 && (FLAG:53 & 안마의자) == 0
		IF MONEY >= 25800
			MONEY -= 25800
			FLAG:53 |= 안마의자
			PRINTL 안마 의자를 구입하여 거점에 설치했다!
			PRINTW (휴식을 취하지 않을 때에도 피로도가 조금 회복됩니다.)
		ENDIF
	ELSEIF RESULT == 3 && (FLAG:53 & 목욕설비) == 0
		IF MONEY >= 50000
			MONEY -= 50000
			FLAG:53 |= 목욕설비
			;원문은 入浴設備にシャワーを追加した！ 샤워기 설치인데... 처음 ERH 번역할 때 목욕설비라고 오역해 버림
			;이참에 그냥 거점 목욕탕을 확장한 셈 쳐버렸다. 애초에 거점에 목욕탕이나 샤워실이 있던가?
			PRINTL 거점 내 목욕 시설에 샤워부스, 전신욕조 등의 설비를 추가하였다!
			PRINTW (단련 후에 피로도가 조금 회복됩니다.)
			GOTO INPUT_LOOP_SELECT_4
		ENDIF
	ELSEIF RESULT == 4 && (FLAG:53 & 게임코너) == 0
		IF MONEY >= 100000
			MONEY -= 100000
			FLAG:53 |= 게임코너
			PRINTL 거점 내에 게임 코너를 개설했다!
			PRINTW (낮 휴식의 피로도 회복량이 크게 증가합니다.)
			GOTO INPUT_LOOP_SELECT_4
		ENDIF
	ELSEIF RESULT == 5 && (FLAG:53 & 대욕탕) == 0
		IF MONEY >= 200000
			MONEY -= 200000
			FLAG:53 |= 대욕탕
			PRINTL 거점 내 욕탕 시설을 개수하여, 널찍한 대욕탕이 추가되었다!
			PRINTW (모든 휴식의 피로도 회복량이 증가합니다.)
			GOTO INPUT_LOOP_SELECT_4
		ENDIF
	ELSEIF RESULT == 5 && (FLAG:53 & 미용온천) == 0 && (FLAG:53 & 대욕탕)
		IF MONEY >= 500000
			MONEY -= 500000
			FLAG:53 |= 미용온천
			;원문은 美肌効果のある温泉を引いた！
			PRINTL 거점 내 대욕탕에 피부 미용 효과가 있는 온천을 뒀다!
			PRINTL 찜질방은 덤이라구요?
			PRINTW (모든 휴식의 피로도 회복량이 크게 증가합니다.)
			GOTO INPUT_LOOP_SELECT_4
		ENDIF
	ELSEIF RESULT == 6 && (FLAG:53 & 뷰티살롱) == 0
		IF MONEY >= 1500000
			MONEY -= 1500000
			FLAG:53 |= 뷰티살롱
			PRINTL 멤버 전용의 뷰티살롱을 개설했다!
			PRINTW (모든 휴식의 피로도 회복량이 매우 크게 증가합니다.)
			GOTO INPUT_LOOP_SELECT_4
		ENDIF
	ELSEIF RESULT == 99
		GOTO INPUT_LOOP
	ELSE
		PRINTW 올바른 값을 입력해 주십시오.
		CLEARLINE 2
		GOTO INPUT_LOOP_SELECT_3
	ENDIF
	PRINTL 
	;GOTO INPUT_LOOP
	;GOTO INPUT_LOOP_SELECT_4
	PRINTW 소지 자금이 부족합니다.
	CLEARLINE 3
	GOTO INPUT_LOOP_SELECT_3
ELSEIF RESULT == 4
	IF FLAG:54 >= 5
		PRINTW 더 이상 연구 레벨을 올릴 수 없습니다.
		CLEARLINE 2
		GOTO INPUT_LOOP_1
	ELSE
		PRINTL 
		PRINTFORML 연구 레벨업으로 촉수 슈트의 커스터마이즈가 추가됩니다.
		PRINTFORML 연구 레벨업에는 촉수의 조각이 {5 * FLAG:54 + 5}개만큼 필요합니다.
		PRINTFORML 레벨업을 하시겠습니까?　　(촉수의 조각 소지 수:{FLAG:200})
		
		PRINTL [0] 네
		PRINTL [1] 아니오
		$INPUT_LOOP_SELECT_5
		INPUT
		IF RESULT == 0
			IF FLAG:200 < 5 * FLAG:54 + 5
				PRINTW 촉수의 조각이 부족합니다.
			ELSE
				FLAG:200 -= 5 * FLAG:54 + 5
				FLAG:54 += 1
				PRINTW 연구 레벨이 올랐습니다!
			ENDIF
		ELSEIF RESULT == 1
		ELSE
			PRINTW 올바른 값을 입력해 주십시오.
			CLEARLINE 2
			GOTO INPUT_LOOP_SELECT_5
		ENDIF
	ENDIF
	PRINTL 
	GOTO INPUT_LOOP
ELSE
	PRINTW 올바른 값을 입력해 주십시오.
	CLEARLINE 2
	GOTO INPUT_LOOP_1
ENDIF

;--------------------------------------------------
;이름을 변경할 캐릭터를 고른다
@NAME_CHANGE_SELECT
CALL CHARA_LIST
IF RESULT == 999
ELSE
	CALL NAME_CHANGE, RESULT
ENDIF
PRINTL 

@AFTER_RESCUED,ARG
#DIM LCOUNT
LCOUNT = TARGET
TARGET = ARG

;이벤트 의상 그대로 유폐되어 있으면 해제한다
CALL EVENT_BATTLE_RESET_COSTUME(TARGET)


PRINTL 
PRINTFORML %CALLNAME%은(는) 치료를 받고 있습니다・・・
CALL TRANSFORM, 0
;행동 예정을 리셋
CFLAG:ARG:100 = 예정_휴식
;음문의 진행도가 저하
;전락하면 저하하지 않는다
CALL TATTOO_ACCESS, "PROGRESS_VAR"
SIF TALENT:전락 != 1
	RESULT = RESULT * 7 / 10
IF RESULT == 0
	FLAG:32 = 0
ELSE
	CALL SAVE_TATTOO, RESULT
ENDIF

;흑화 상태라면 외모 복귀 처리
SIF GETBIT(CFLAG:ARG:80, 2) == 1
	CALL RECOVER_CORRUPTION, ARG

IF CHECK_PREGNANT_F(ARG) > 0
	PRINTFORML %CALLNAME:ARG%은(는) 임신했기 때문에, 긴급히 특별병동으로 옮겨졌습니다・・・
	PRINTL 
	CFLAG:ARG:0 = 상태_출산직전
	CALL SET_PARTYMEMBER
ELSEIF TALENT:ARG:번식자루 == 1
	PRINTL 완전히 의식불명이고, 언제 회복될지도 모릅니다・・・
	PRINTL 
	PRINTL 
	CFLAG:ARG:0 = 0
	CALL RECOVER_TO_PARTY, ARG
ELSEIF TALENT:ARG:사지결손 == 1
	PRINTW 전혀 움직일 수가 없어, 앞으로 평소와 같은 생활은 어렵겠습니다・・・
	PRINTW 혹시나 촉수를 이용한 연구의 성과로 복원할 수 있을지도...?
	PRINTL 
	CFLAG:ARG:0 = 0
	CALL RECOVER_TO_PARTY, ARG
ELSEIF TALENT:ARG:전락 > 0
	PRINTL 묘한 말을 하거나, 기묘한 이름을 대거나 하고 있습니다.
	PRINTL 눈앞에 있는 것은 정말 동일인일까요…
	PRINTL 
	CFLAG:ARG:0 = 0
	CALL RECOVER_TO_PARTY, ARG
ELSE
	PRINTFORML 다행히 생명에는 지장이 없는 것 같습니다・・・
	PRINTL 
	CFLAG:ARG:0 = 상태_건강
	CALL RECOVER_TO_PARTY, ARG
ENDIF
TARGET = LCOUNT

;음문의 진행/회복
@INMON_RECOVERY,ARG
#DIM LCOUNT
#DIM CCOUNT
LOCAL:2 = TARGET
TARGET = ARG
;구출 후에도 진행될 경우
IF CONFIG_CHECK_MANIAC_F(9) == 1
	IF CFLAG:32 > 0 && CFLAG:0 == 상태_건강 && TALENT:촉수의포로 == 1
		;오염도가 상승한다
		RESULT:1 = 2 + RAND:(LIMIT(SQRT(100 - CFLAG:32 / 10000000000000000),1,8)) + RAND:(MIN(ABL:욕망 + 1,5)) + RAND:(MIN(ABL:촉수중독 + 1,5))
		RESULT:1 = RESULT:1 * 3 / 4
		CALL TATTOO_ACCESS, "PROGRESS_VAR"
		CALL SAVE_TATTOO, LIMIT(RESULT + RESULT:1,0,100)
		PRINTFORML %CALLNAME%의 음문이 {RESULT:1}％ 진행됐습니다・・・（{CFLAG:32 / 10000000000000000}％）
		PRINTW 

		;음문의 진행이 오염도(얼마나 함락에 가까워지고 있는지 나타내는 내부값)와 연동되어 있는 경우, 기본값으로 함락 흑화한다
		;★이 흑화 옵션 확인 안 했는데 괜찮나?
		;
		CALL TATTOO_ACCESS, "PROGRESS_VAR"
		IF RESULT >= 100 && CONFIG_CHECK_MANIAC_F(10) == 1
			;살아남은 보스 중 랜덤으로 지배자 선택
			CALL CLEARRANDCHOOSE
			IF ENEMY_TYPE_CHECK_F("BOSS") == 1
				CALL GET_BOSS_ERB_NUM
			ELSE
				CALL GET_LASTBOSS_ERB_NUM
			ENDIF
			FOR LCOUNT, 0, RESULT
				;히든 보스는 대상에서 제외
				SIF LCOUNT == 1 && GET_LASTBOSS_PHASE_F() >= 2
					CONTINUE
				CALL TENTACLE_BITVALUE, LCOUNT + 1
				CALL TENTACLE_SURVIVE_CHECK, RESULT
				SIF RESULT > 0
					CALL ADDRANDCHOOSE, RESULT
			NEXT
			CFLAG:20 = FLAG:10
			CFLAG:21 = RANDCHOOSE_F()

			;전투 능력이 있는 캐릭터라면 반드시 흑화
			IF TALENT:변신능력 >= 0
				CFLAG:0 = 상태_흑화
				;변신 가능 캐릭터라면 겉옷이 변경된다
				SIF TALENT:변신능력 == 1
					CFLAG:41 = 401
				;＠중국판　전락 장비
				IF TALENT:전락 == 1
					;변신능력이 있다면 음박의 사슬(淫縛の鎖)
					IF TALENT:변신능력 == 1
						CFLAG:41 = 402
					;변신능력이 없다면 촉수의 신부／no inner
					ELSE
						CFLAG:40 = 196
						CFLAG:42 = 0
					ENDIF
				ENDIF
				;함락경험 가산
				EXP:함락경험 += 1

				;지문: 흑화하고 말았다
				CALL MESSAGE_SHOP_AKUOTI
				;외모를 변경한다
				CALL CORRUPT_CHANGE_LOOKS_MAIN, TARGET
				;SOLO모드에서 캐릭터가 세뇌/흑화된다면 게임 오버
				IF GAME_OPTION_CHECK_F(OPTION_solo)
					LOCAL:1 = 0
					FOR LOCAL,0,CHARANUM
						SIF CFLAG:LOCAL:0 == 상태_세뇌 || CFLAG:LOCAL:0 == 상태_흑화
							LOCAL:1 ++
					NEXT
					IF (CHARANUM - LOCAL:1) < 2
						CALL ENDING_4
						DRAWLINE
						RETURN
					ENDIF
				ENDIF
				
				PRINTFORML %STR:2500%의 첨병이 되어 버린 %CALLNAME%에게 사악한 힘이 흘러든다・・・
				PRINTL 
				;흑화할 시 대량의 경험치를 얻는다
				CALL TENTACLE_LEVEL
				IF CFLAG:0 == 상태_세뇌
					LOCAL = 20 * (5 + ABL:레벨) + RAND:25
				ELSEIF CFLAG:0 == 상태_흑화
					LOCAL = 40 * (5 + ABL:레벨) + RAND:50
				ENDIF
				SIF LOCAL < 50
					LOCAL = 50
				CALL GET_EXP,min(LOCAL,750)
				;조우율 상승 효과 리셋
				CFLAG:23 = 0
			;전투 능력이 없는 경우
			;ＴＳ할 수 없는 남자＆양성평등이 OFF라면 즉시 사망
			ELSEIF ISHOLE(TARGET) == 0 && TALENT:변신시TS < 1 && CONFIG_CHECK_PRISON_F(0) == 0
				CFLAG:0 = 상태_사망

				;지문: 스스로 유폐된다
				CALL MESSAGE_SHOP_PRISON
				PRINTW 
				;지문: 즉사
				PRINTFORML ……그러나 남자였던 %PRINT_TRANSCALLNAME(TARGET)%은(는), 그대로 숨통이 끊겨 %STR:2500%에게 거두어져 버린 것 같다・・・
				PRINTW 
				PRINTFORML %PRINT_TRANSCALLNAME(TARGET)%을(를) 잃었습니다.
				PRINTW 

				;첫 유폐라면 이상경험을 붙인다
				SIF EXP:유폐경험 == 0
					EXP:이상경험 += 1

					EXP:유폐경험 += 1
				;조우율 상승 효과 리셋
				CFLAG:23 = 0
			;여성 또는 여성화가 가능하면 유폐
			ELSE
				CFLAG:0 = 상태_유폐

				;지문: 스스로 유폐된다
				CALL MESSAGE_SHOP_PRISON

				;첫 유폐라면 이상경험을 붙인다
				SIF EXP:유폐경험 == 0
				EXP:이상경험 += 1
				;유폐경험 가산
				EXP:유폐경험 += 1
				;조우율 상승 효과 리셋
				CFLAG:23 = 0
			ENDIF
			PRINTW 
		ENDIF
		DRAWLINE
	ENDIF
;음문 회복
ELSE
	TARGET = ARG
	IF CFLAG:32 > 0
		CALL TATTOO_ACCESS, "PROGRESS_VAR"
		RESULT = RESULT * 9 / 10
		IF RESULT == 0
			CFLAG:32 = 0
		ELSE
			CALL SAVE_TATTOO, RESULT
		ENDIF
	ENDIF
ENDIF
TARGET = LOCAL:2

@SET_PARTYMEMBER
#DIM CCOUNT



;파티 캐릭터 자동 관리
SIF CHARANUM < 3 && CHARANUM_SAFE_PARTYCHECK(0) == 0
	FLAG:63 = 0
FOR CCOUNT, 0, CHARANUM
	SIF CCOUNT == MASTER
		CONTINUE

	IF GAME_OPTION_CHECK_F(OPTION_solo)	|| CHECK_GAMEOVER_F() == 1
		;솔로 모드라면 임신 중이어도 싸운다. 보테배 마법소녀ｃ！　게임 오버 모드에서는 일부러 표시하지 않는다
	ELSEIF CHECK_PREGNANT_F(CCOUNT) > 0 && CFLAG:CCOUNT:100 == 예정_출격 && CFLAG:CCOUNT:0 < 1
		PRINTFORML %CALLNAME:CCOUNT%은(는) 임신을 했기 때문에 출격할 수 없게 됐습니다.
		PRINTW 
		CFLAG:CCOUNT:100 = 예정_휴식
	ELSEIF CHECK_PREGNANT_F(CCOUNT) > 0 && CFLAG:CCOUNT:100 == 예정_방위
		PRINTFORML %CALLNAME:CCOUNT%은(는) 임신을 했기 때문에 방위를 할 수 없게 됐습니다.
		PRINTW 
		CFLAG:CCOUNT:100 = 예정_휴식
	ENDIF
	;이탈한 캐릭터를 맨 뒤로 이동
	IF (CFLAG:CCOUNT:0 != 상태_건강) && CCOUNT < CHARANUM -1 && CFLAG:CCOUNT:999
		CFLAG:CCOUNT:999 = 0
		CALL SHIFTBACK_CHARA,CCOUNT
	ELSEIF CFLAG:CCOUNT:0 != 상태_건강 && CFLAG:CCOUNT:999
		CFLAG:CCOUNT:999 = 0
	ENDIF
NEXT


@SHIFTBACK_CHARA,ARG
;ARG번째 캐릭터를 리스트 맨 끝으로 이동한다
VARSET LOCAL
FOR LOCAL, ARG, CHARANUM -1
	SIF LOCAL == MASTER
		CONTINUE
	;RELATION
	FOR LOCAL:1, 0, CHARANUM
		SIF LOCAL:1 == MASTER
			CONTINUE
		SWAP RELATION:(LOCAL:1):LOCAL, RELATION:(LOCAL:1):(LOCAL + 1)
	NEXT
	SWAPCHARA LOCAL,LOCAL + 1
NEXT


@SHIFTFOWARD_CHARA,ARG
;ARG번째 캐릭터를 파티 맨 앞부분으로 이동한다
VARSET LOCAL
FOR LOCAL, 0, CHARANUM
	SIF CFLAG:LOCAL:999
		LOCAL:1 = LOCAL
NEXT
FOR LOCAL, ARG, LOCAL:1, -1
	SIF LOCAL == MASTER
		CONTINUE
	;RELATION
	FOR LOCAL:1, 0, CHARANUM
		SIF LOCAL:1 == MASTER
			CONTINUE
		SWAP RELATION:(LOCAL:1):LOCAL, RELATION:(LOCAL:1):(LOCAL - 1)
	NEXT
	SWAPCHARA LOCAL,LOCAL - 1
NEXT



@RECOVER_TO_PARTY,ARG
;ARG번째 캐릭터를 파티에 복귀시킨다
CFLAG:ARG:0 = 상태_건강
;파티 인원이 최대 인원수 미만이라면 자동으로 파티에 추가한다
IF CHARANUM_ACTIVE() < 파티인원최대치 && CFLAG:ARG:999 == 0
	CFLAG:ARG:999 = 1
	PRINTL 
	IF TALENT:ARG:번식자루 == 1
		PRINTFORML %CALLNAME:ARG%은(는) 생명 감시 기능이 있는 자신의 방으로 돌아왔습니다.
		PRINTFORML 익숙한 환경이 이성을 되찾는 데 도움이 되었으면 좋겠다만...
	ELSEIF TALENT:ARG:사지결손 == 1
		PRINTFORML %CALLNAME:ARG%이(가) 파티 멤버에 추가되었습니다.
		PRINTFORML 하지만, 지금의 %CALLNAME:ARG%에게 맡길 수 있는 일은 후방 지원 뿐입니다...
	ELSE
		PRINTFORML %CALLNAME:ARG%이(가) 파티 멤버에 추가되었습니다.
	ENDIF

	PRINTW 
ELSE
	PRINTL 
	PRINTFORML %CALLNAME:ARG%이(가) 대기 멤버에 합류했습니다.
	PRINTW 
ENDIF

